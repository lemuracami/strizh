
#Область ПрограммныйИнтерфейс
// Проверить и установить изначальные параметры МА.
// проверка необходимости первоначально заполнения БД
// Возвращаемое значение: 
//  Булево
Функция ПроверитьИУстановитьИзначальныеПараметрыМА() Экспорт
	Если Не ЗначениеЗаполнено(Константы.ИдентификаторМП.Получить()) Тогда
		Возврат ПроизвестиПервоначальноеЗаполнениеДанных();
	Иначе
		Возврат Истина;	
	КонецеСли;	
КонецФункции
#КонецОбласти


#Область СлужебныеПроцедурыИФункции
// Произвести первоначальное заполнение данных.
// 
// Возвращаемое значение:
// Булево 
Функция ПроизвестиПервоначальноеЗаполнениеДанных()
	Попытка
		Константы.ИдентификаторМП.Установить(Строка(Новый УникальныйИдентификатор()));
		ЗаполнитьПланыОбмена();
		ЗаполнитьВстроенныеАдресаОбмена();
		Возврат Истина;
	Исключение
		Возврат Ложь;	
	КонецПопытки;		
КонецФункции

// Заполнить планы обмена.
Процедура ЗаполнитьПланыОбмена()
	//основной обмен
	Если Не ЗначениеЗаполнено(Константы.УзелПланаОбмена.Получить()) Тогда
		ОбъектЭУ = ПланыОбмена.ОбменСЦУСтатусыЗаказа.ЭтотУзел().ПолучитьОбъект();
		ОбъектЭУ.Наименование = "Смартфон";
		ОбъектЭУ.Записать();
		
		ЦУзел = ПланыОбмена.ОбменСЦУСтатусыЗаказа.НайтиПоКоду("000000002");
		Если ЦУзел.Пустая() Тогда
			ЦУзел = ПланыОбмена.ОбменСЦУСтатусыЗаказа.СоздатьУзел();
			ЦУзел.Наименование = "ЦБ";
			ЦУзел.Код = "000000002";
			ЦУзел.Записать();
		КонецеСли;	
		
		Константы.УзелПланаОбмена.Установить(ЦУзел.Ссылка);
	КонецеСли;	
	
	//обмен с сервером ККМ
	Если Не ЗначениеЗаполнено(Константы.УзелПланаОбменаСерверККМ.Получить()) Тогда
		ОбъектЭУ = ПланыОбмена.ОбменСЦУКассовыйСервер.ЭтотУзел().ПолучитьОбъект();
		ОбъектЭУ.Наименование = "Смартфон";
		ОбъектЭУ.Записать();
		
		ЦУзел = ПланыОбмена.ОбменСЦУКассовыйСервер.НайтиПоКоду("000000002");
		Если ЦУзел.Пустая() Тогда
			ЦУзел = ПланыОбмена.ОбменСЦУКассовыйСервер.СоздатьУзел();
			ЦУзел.Наименование = "ЦБ";
			ЦУзел.Код = "000000002";
			ЦУзел.Записать();
		КонецеСли;	
		
		Константы.УзелПланаОбменаСерверККМ.Установить(ЦУзел.Ссылка);
	КонецеСли;	
КонецПроцедуры	

// Заполнить встроенные адреса обмена.
Процедура ЗаполнитьВстроенныеАдресаОбмена()
	НабЗаписей = РегистрыСведений.АдресаОбменаСЦУ.СоздатьНаборЗаписей();
	НабЗаписей.Отбор.Терминал.Установить(Справочники.РегиональныеТерминалы.ПустаяСсылка());
	НабЗаписей.Отбор.ВстроенныйАдрес.Установить(Истина);
	
	Нов = НабЗаписей.Добавить();
	Нов.Адрес = "192.168.1.107";
	Нов.Приоритет = 1;
	Нов.ВстроенныйАдрес = Истина;
	
	Нов = НабЗаписей.Добавить();
	Нов.Адрес = "192.168.5.9";
	Нов.Приоритет = 1;
	Нов.ВстроенныйАдрес = Истина;
	
	НабЗаписей.Записать();
КонецПроцедуры
#КонецОбласти	
