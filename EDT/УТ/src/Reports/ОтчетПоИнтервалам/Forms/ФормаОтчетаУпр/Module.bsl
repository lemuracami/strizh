&НаКлиенте
Процедура ПриОткрытии(Отказ)
	УстановитьЗаголовок();
	УстановитьВидимостьОтборов();
КонецПроцедуры

&НаКлиенте
Процедура Сформировать(Команда)
	Если ЗначениеЗаполнено(Отчет.ПериодОтчета) И (Отчет.ВариантОтчета <= 1) Тогда
		СкомпоноватьРезультат();
		Результат.ПоказатьУровеньГруппировокСтрок(1);
	ИначеЕсли Отчет.ВариантОтчета = 2 Тогда
		СформироватьОтчетПоПериоду(Ложь);
	ИначеЕсли Отчет.ВариантОтчета = 3 Тогда
		СформироватьОтчетПоПериоду(Истина);
	Иначе
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Не указан период отчета!";
		Сообщение.Поле = "Отчет.ПериодОтчета";
		Сообщение.Сообщить();
	КонецЕсли;	
КонецПроцедуры

&НаКлиенте
Процедура ПартнерНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ОткрытьФорму("Справочник.Контрагенты.Форма.ФормаВыбораВладельцаТоваров",,Элемент);
КонецПроцедуры

&НаКлиенте
Процедура ПартнерАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ПараметрыПолученияДанных.Отбор.Вставить("ВладельцыТоваров");
	ДанныеВыбора = ПолучитьДанныеВыбора(Тип("СправочникСсылка.Контрагенты"),ПараметрыПолученияДанных);
КонецПроцедуры

&НаКлиенте
Процедура ВариантОтчетаПриИзменении(Элемент)
	УстановитьЗаголовок();
	УстановитьВидимостьОтборов();
	ПоказатьОжиданиеФормированияОтчета()
КонецПроцедуры

&НаКлиенте
Процедура ПериодОтчетаПриИзменении(Элемент)
	ПоказатьОжиданиеФормированияОтчета()
КонецПроцедуры

&НаКлиенте
Процедура ПартнерПриИзменении(Элемент)
	ПоказатьОжиданиеФормированияОтчета()
КонецПроцедуры

&НаКлиенте
Процедура ПричинаПриИзменении(Элемент)
	ПоказатьОжиданиеФормированияОтчета()
КонецПроцедуры

&НаКлиенте
Процедура ВодительПриИзменении(Элемент)
	ПоказатьОжиданиеФормированияОтчета()
КонецПроцедуры

&НаКлиенте
Процедура СтатусПриИзменении(Элемент)
	ПоказатьОжиданиеФормированияОтчета()
КонецПроцедуры

&НаКлиенте
Процедура ТерминалПриИзменении(Элемент)
	ПоказатьОжиданиеФормированияОтчета();
КонецПроцедуры

&НаКлиенте
Процедура УбратьЗаборыПриИзменении(Элемент)
	ПоказатьОжиданиеФормированияОтчета();
КонецПроцедуры
&НаКлиенте
Процедура ИнтервалОпозданияПриИзменении(Элемент)
	ПоказатьОжиданиеФормированияОтчета();
КонецПроцедуры


&НаКлиенте
Процедура ПоказатьОжиданиеФормированияОтчета()
	Элементы.Результат.ОтображениеСостояния.Видимость = Истина;
	Элементы.Результат.ОтображениеСостояния.ДополнительныйРежимОтображения = ДополнительныйРежимОтображения.Неактуальность;
	Элементы.Результат.ОтображениеСостояния.Текст = "Отчет не сформирован. Нажмите ""Сформировать"" для получения отчета"
КонецПроцедуры	

&НаСервере
Процедура УстановитьЗаголовок()
	
	Если Отчет.ВариантОтчета = 0 Тогда
		Заголовок = "Отчет по интервалам (По экипажам)";	
	ИначеЕсли Отчет.ВариантОтчета = 1 Тогда
		Заголовок = "Отчет по интервалам (По партнерам)";
	ИначеЕсли Отчет.ВариантОтчета = 2 Тогда
		Заголовок = "Отчет по интервалам (По дням)";
	ИначеЕсли Отчет.ВариантОтчета = 3 Тогда
		Заголовок = "Отчет по интервалам (По месяцам)";
	КонецЕсли;	
	
КонецПроцедуры	

&НаСервере
Процедура УстановитьВидимостьОтборов()
	Элементы.Причина.Видимость = Не Отчет.ВариантОтчета;
	Элементы.Статус.Видимость  = Не Отчет.ВариантОтчета;
	Элементы.УбратьЗаборы.Видимость = Отчет.ВариантОтчета;
КонецПроцедуры	

&НаСервере
Процедура СформироватьОтчетПоПериоду(ПоМесяцам = Ложь)

	ОтчетОбъект = РеквизитФормыВЗначение("Отчет");
	
	Макет = ОтчетОбъект.ПолучитьМакет("МакетПоПериоду");
	
	ОбластьДеньЗаголовок = Макет.ПолучитьОбласть("День_Заголовок");
	ОбластьДеньСтрока = Макет.ПолучитьОбласть("День_Строка");
	ОбластьДеньПодвал = Макет.ПолучитьОбласть("День_Подвал");
	
	Результат.Очистить();
	
	//основа - запрос из СКД отчёта
	//доработки:
	//- добавлены втВремяПечатиРазблюдовки
	//- в результирующием запросе итоги по дате рейса и рейсу
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	РейсЗаказы.Ссылка КАК Ссылка,
	|	РейсЗаказы.Ссылка.ТерминалДоставки КАК ТерминалДоставки,
	|	ВЫБОР
	|		КОГДА РейсЗаказы.Заказ ССЫЛКА Документ.РеализацияТоваровУслуг
	|			ТОГДА ""Доставка""
	|		ИНАЧЕ ""Забор""
	|	КОНЕЦ КАК ТипЗаказа,
	|	РейсЗаказы.Заказ КАК Заказ,
	|	ВЫБОР
	|		КОГДА РейсЗаказы.Заказ ССЫЛКА Документ.РеализацияТоваровУслуг
	|			ТОГДА ВЫРАЗИТЬ(РейсЗаказы.Заказ КАК Документ.РеализацияТоваровУслуг).Номер
	|		ИНАЧЕ ВЫРАЗИТЬ(РейсЗаказы.Заказ КАК Документ.ЗаборТовара).Номер
	|	КОНЕЦ КАК НомерЗаказаСтриж,
	|	ВЫБОР
	|		КОГДА РейсЗаказы.Заказ ССЫЛКА Документ.РеализацияТоваровУслуг
	|			ТОГДА ВЫРАЗИТЬ(РейсЗаказы.Заказ КАК Документ.РеализацияТоваровУслуг).НомерВнешнегоЗаказа
	|		ИНАЧЕ ВЫРАЗИТЬ(РейсЗаказы.Заказ КАК Документ.ЗаборТовара).НомерВнешнегоЗаказа
	|	КОНЕЦ КАК НомерЗаказаИМ,
	|	ВЫБОР
	|		КОГДА РейсЗаказы.Заказ ССЫЛКА Документ.РеализацияТоваровУслуг
	|				И ВЫРАЗИТЬ(РейсЗаказы.Заказ КАК Документ.РеализацияТоваровУслуг).ВладелецТовара.Родитель.ОсновнойМагазин = ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
	|			ТОГДА ВЫРАЗИТЬ(РейсЗаказы.Заказ КАК Документ.РеализацияТоваровУслуг).ВладелецТовара
	|		КОГДА РейсЗаказы.Заказ ССЫЛКА Документ.РеализацияТоваровУслуг
	|				И ВЫРАЗИТЬ(РейсЗаказы.Заказ КАК Документ.РеализацияТоваровУслуг).ВладелецТовара.Родитель.ОсновнойМагазин <> ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
	|			ТОГДА ВЫРАЗИТЬ(РейсЗаказы.Заказ КАК Документ.РеализацияТоваровУслуг).ВладелецТовара.Родитель.ОсновнойМагазин
	|		КОГДА РейсЗаказы.Заказ ССЫЛКА Документ.ЗаборТовара
	|				И ВЫРАЗИТЬ(РейсЗаказы.Заказ КАК Документ.ЗаборТовара).Контрагент.Родитель.ОсновнойМагазин = ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
	|			ТОГДА ВЫРАЗИТЬ(РейсЗаказы.Заказ КАК Документ.ЗаборТовара).Контрагент
	|		КОГДА РейсЗаказы.Заказ ССЫЛКА Документ.ЗаборТовара
	|				И ВЫРАЗИТЬ(РейсЗаказы.Заказ КАК Документ.ЗаборТовара).Контрагент.Родитель.ОсновнойМагазин <> ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
	|			ТОГДА ВЫРАЗИТЬ(РейсЗаказы.Заказ КАК Документ.ЗаборТовара).Контрагент.Родитель.ОсновнойМагазин
	|	КОНЕЦ КАК Партнер,
	|	ВЫБОР
	|		КОГДА РейсЗаказы.Заказ ССЫЛКА Документ.РеализацияТоваровУслуг
	|			ТОГДА ВЫРАЗИТЬ(РейсЗаказы.Заказ КАК Документ.РеализацияТоваровУслуг).ВладелецТовара
	|		ИНАЧЕ ВЫРАЗИТЬ(РейсЗаказы.Заказ КАК Документ.ЗаборТовара).Контрагент
	|	КОНЕЦ КАК Магазин,
	|	ВЫБОР
	|		КОГДА РейсЗаказы.Заказ ССЫЛКА Документ.РеализацияТоваровУслуг
	|			ТОГДА ЕСТЬNULL(ПараметрыТарифаЗаказаСрезПоследних.ЗонаТарификации, ЗНАЧЕНИЕ(Справочник.ГруппыРайонов.ПустаяСсылка))
	|		ИНАЧЕ ВЫРАЗИТЬ(РейсЗаказы.Заказ КАК Документ.ЗаборТовара).ЗонаТарификации
	|	КОНЕЦ КАК Зона,
	|	ВЫБОР
	|		КОГДА РейсЗаказы.Заказ ССЫЛКА Документ.РеализацияТоваровУслуг
	|			ТОГДА ВЫРАЗИТЬ(РейсЗаказы.Заказ КАК Документ.РеализацияТоваровУслуг).АдресДоставки
	|		ИНАЧЕ ВЫРАЗИТЬ(РейсЗаказы.Заказ КАК Документ.ЗаборТовара).АдресДоставки
	|	КОНЕЦ КАК Адрес,
	|	ВЫБОР
	|		КОГДА РейсЗаказы.Заказ ССЫЛКА Документ.РеализацияТоваровУслуг
	|			ТОГДА ДОБАВИТЬКДАТЕ(ДАТАВРЕМЯ(1, 1, 1), СЕКУНДА, РАЗНОСТЬДАТ(НАЧАЛОПЕРИОДА(новаМестнаяДоставка.ВремяПрибытияС, ДЕНЬ), новаМестнаяДоставка.ВремяПрибытияС, СЕКУНДА))
	|		ИНАЧЕ ВЫРАЗИТЬ(РейсЗаказы.Заказ КАК Документ.ЗаборТовара).ВремяДоставкиС
	|	КОНЕЦ КАК ИнтервалС,
	|	ВЫБОР
	|		КОГДА РейсЗаказы.Заказ ССЫЛКА Документ.РеализацияТоваровУслуг
	|			ТОГДА ДОБАВИТЬКДАТЕ(ДАТАВРЕМЯ(1, 1, 1), СЕКУНДА, РАЗНОСТЬДАТ(НАЧАЛОПЕРИОДА(новаМестнаяДоставка.ВремяПрибытияПо, ДЕНЬ), новаМестнаяДоставка.ВремяПрибытияПо, СЕКУНДА))
	|		ИНАЧЕ ВЫРАЗИТЬ(РейсЗаказы.Заказ КАК Документ.ЗаборТовара).ВремяДоставкиПо
	|	КОНЕЦ КАК ИнтервалПо,
	|	ВЫБОР
	|		КОГДА РейсЗаказы.Заказ ССЫЛКА Документ.РеализацияТоваровУслуг
	|			ТОГДА НАЧАЛОПЕРИОДА(ВЫРАЗИТЬ(РейсЗаказы.Заказ КАК Документ.РеализацияТоваровУслуг).Дата, ДЕНЬ)
	|		ИНАЧЕ НАЧАЛОПЕРИОДА(ВЫРАЗИТЬ(РейсЗаказы.Заказ КАК Документ.ЗаборТовара).Дата, ДЕНЬ)
	|	КОНЕЦ КАК ДатаДоставки
	|ПОМЕСТИТЬ втРейсы
	|ИЗ
	|	Документ.Рейс.Заказы КАК РейсЗаказы
	|		ЛЕВОЕ СОЕДИНЕНИЕ БизнесПроцесс.новаМестнаяДоставка КАК новаМестнаяДоставка
	|		ПО (ВЫРАЗИТЬ(РейсЗаказы.Заказ КАК Документ.РеализацияТоваровУслуг).Номер = новаМестнаяДоставка.Номер)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПараметрыТарифаЗаказа.СрезПоследних КАК ПараметрыТарифаЗаказаСрезПоследних
	|		ПО РейсЗаказы.Заказ = ПараметрыТарифаЗаказаСрезПоследних.Заказ
	|ГДЕ
	|	НЕ РейсЗаказы.УдаленИзРейса
	|	И РейсЗаказы.Ссылка.ДатаРейса МЕЖДУ &ДатаНачала И &ДатаОкончания
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	КонтрольВремениСрезПоследних.Рейс КАК Рейс,
	|	КонтрольВремениСрезПоследних.Период КАК Период
	|ПОМЕСТИТЬ втВремяПечатиМаршрутногоЛиста
	|ИЗ
	|	РегистрСведений.КонтрольВремени.СрезПоследних(
	|			,
	|			Рейс В
	|					(ВЫБРАТЬ
	|						втРейсы.Ссылка
	|					ИЗ
	|						втРейсы)
	|				И Событие = ЗНАЧЕНИЕ(Справочник.СобытияКонтроляВремени.РаспечаткаМаршрутногоЛиста)) КАК КонтрольВремениСрезПоследних
	|ГДЕ
	|	КонтрольВремениСрезПоследних.ОкончаниеСобытия
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	КонтрольВремениСрезПоследних.Рейс КАК Рейс,
	|	КонтрольВремениСрезПоследних.Период КАК Период
	|ПОМЕСТИТЬ втВремяПечатиРазблюдовки
	|ИЗ
	|	РегистрСведений.КонтрольВремени.СрезПоследних(
	|			,
	|			Рейс В
	|					(ВЫБРАТЬ
	|						втРейсы.Ссылка
	|					ИЗ
	|						втРейсы)
	|				И Событие = ЗНАЧЕНИЕ(Справочник.СобытияКонтроляВремени.ОкончаниеЗагрузкиВАвто)) КАК КонтрольВремениСрезПоследних
	|ГДЕ
	|	КонтрольВремениСрезПоследних.ОкончаниеСобытия
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА &ПоМесяцам = ИСТИНА
	|			ТОГДА НАЧАЛОПЕРИОДА(втРейсы.Ссылка.ДатаРейса, МЕСЯЦ)
	|		ИНАЧЕ втРейсы.Ссылка.ДатаРейса
	|	КОНЕЦ КАК ДатаРейса,
	|	втРейсы.Ссылка КАК Ссылка,
	|	ПривязкаМашинКРейсамСрезПоследних.Транспорт КАК Транспорт,
	|	ПривязкаМашинКРейсамСрезПоследних.Водитель КАК Водитель,
	|	ПривязкаМашинКРейсамСрезПоследних.Экспедитор КАК Экспедитор,
	|	втВремяПечатиРазблюдовки.Период КАК ВремяПечатиРазблюдовки,
	|	втВремяПечатиМаршрутногоЛиста.Период КАК ВремяПечатиМЛ,
	|	втРейсы.Заказ КАК Заказ,
	|	втРейсы.Заказ.Номер КАК НомерЗаказа,
	|	втРейсы.ТипЗаказа КАК ТипЗаказа,
	|	втРейсы.НомерЗаказаСтриж КАК НомерЗаказаСтриж,
	|	втРейсы.НомерЗаказаИМ КАК НомерЗаказаИМ,
	|	втРейсы.Партнер КАК Партнер,
	|	втРейсы.Магазин КАК Магазин,
	|	втРейсы.Зона КАК Зона,
	|	втРейсы.Адрес КАК Адрес,
	|	втРейсы.ИнтервалС КАК ИнтервалС,
	|	втРейсы.ИнтервалПо КАК ИнтервалПо,
	|	ДОБАВИТЬКДАТЕ(ДАТАВРЕМЯ(1, 1, 1), СЕКУНДА, РАЗНОСТЬДАТ(НАЧАЛОПЕРИОДА(ОперативныеСтатусыЗаказовПоДаннымМПСрезПоследних.Регистратор.ДатаДоставки, ДЕНЬ), ОперативныеСтатусыЗаказовПоДаннымМПСрезПоследних.Регистратор.ДатаДоставки, СЕКУНДА)) КАК ВремяМП,
	|	ДОБАВИТЬКДАТЕ(ДАТАВРЕМЯ(1, 1, 1), СЕКУНДА, РАЗНОСТЬДАТ(НАЧАЛОПЕРИОДА(ОперативныеСтатусыЗаказовПоДаннымМПСрезПоследних.Регистратор.ДатаВыгрузки, ДЕНЬ), ОперативныеСтатусыЗаказовПоДаннымМПСрезПоследних.Регистратор.ДатаВыгрузки, СЕКУНДА)) КАК ВремяСтатус,
	|	втРейсы.ТерминалДоставки КАК ТерминалДоставки,
	|	ВЫБОР
	|		КОГДА ОперативныеСтатусыЗаказовПоДаннымМПСрезПоследних.ПричинаНеВыполнения = ЗНАЧЕНИЕ(Справочник.ПричиныНеВыполненияДоставки.ПустаяСсылка)
	|			ТОГДА ""Доставлен""
	|		КОГДА ОперативныеСтатусыЗаказовПоДаннымМПСрезПоследних.ПричинаНеВыполнения В (ЗНАЧЕНИЕ(Справочник.ПричиныНеВыполненияДоставки.ОтказКлиентаБезЗаезда), ЗНАЧЕНИЕ(Справочник.ПричиныНеВыполненияДоставки.ОтказКлиентаСЗаездом))
	|			ТОГДА ""Отказ""
	|		КОГДА ОперативныеСтатусыЗаказовПоДаннымМПСрезПоследних.ПричинаНеВыполнения В (ЗНАЧЕНИЕ(Справочник.ПричиныНеВыполненияДоставки.ПереносДоставки), ЗНАЧЕНИЕ(Справочник.ПричиныНеВыполненияДоставки.ПереносНаВторуюОчередь), ЗНАЧЕНИЕ(Справочник.ПричиныНеВыполненияДоставки.ПереносСЗаездом))
	|			ТОГДА ""Перенос""
	|	КОНЕЦ КАК Статус,
	|	ОперативныеСтатусыЗаказовПоДаннымМПСрезПоследних.ПричинаОтказаПереноса КАК Причина,
	|	ВЫБОР
	|		КОГДА втРейсы.Заказ ССЫЛКА Документ.РеализацияТоваровУслуг
	|				И втРейсы.Зона В (ЗНАЧЕНИЕ(Справочник.ГруппыРайонов.Москва), &ГруппаРайоновМКАД, ЗНАЧЕНИЕ(Справочник.ГруппыРайонов.ДоКАД), &ГруппаРайоновКАД)
	|				И ДОБАВИТЬКДАТЕ(ДАТАВРЕМЯ(1, 1, 1), СЕКУНДА, РАЗНОСТЬДАТ(НАЧАЛОПЕРИОДА(ОперативныеСтатусыЗаказовПоДаннымМПСрезПоследних.Регистратор.ДатаДоставки, ДЕНЬ), ОперативныеСтатусыЗаказовПоДаннымМПСрезПоследних.Регистратор.ДатаДоставки, СЕКУНДА)) < ДОБАВИТЬКДАТЕ(втРейсы.ИнтервалС, МИНУТА, -&Интервал)
	|			ТОГДА 1
	|		КОГДА втРейсы.Заказ ССЫЛКА Документ.ЗаборТовара
	|				И ДОБАВИТЬКДАТЕ(ДАТАВРЕМЯ(1, 1, 1), СЕКУНДА, РАЗНОСТЬДАТ(НАЧАЛОПЕРИОДА(ОперативныеСтатусыЗаказовПоДаннымМПСрезПоследних.Регистратор.ДатаДоставки, ДЕНЬ), ОперативныеСтатусыЗаказовПоДаннымМПСрезПоследних.Регистратор.ДатаДоставки, СЕКУНДА)) < ДОБАВИТЬКДАТЕ(втРейсы.ИнтервалС, МИНУТА, -&Интервал)
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК Раньше,
	|	ВЫБОР
	|		КОГДА втРейсы.Заказ ССЫЛКА Документ.РеализацияТоваровУслуг
	|				И втРейсы.Зона В (ЗНАЧЕНИЕ(Справочник.ГруппыРайонов.Москва), &ГруппаРайоновМКАД, ЗНАЧЕНИЕ(Справочник.ГруппыРайонов.ДоКАД), &ГруппаРайоновКАД)
	|				И ДОБАВИТЬКДАТЕ(ДАТАВРЕМЯ(1, 1, 1), СЕКУНДА, РАЗНОСТЬДАТ(НАЧАЛОПЕРИОДА(ОперативныеСтатусыЗаказовПоДаннымМПСрезПоследних.Регистратор.ДатаДоставки, ДЕНЬ), ОперативныеСтатусыЗаказовПоДаннымМПСрезПоследних.Регистратор.ДатаДоставки, СЕКУНДА)) > ДОБАВИТЬКДАТЕ(втРейсы.ИнтервалПо, МИНУТА, &Интервал)
	|			ТОГДА 1
	|		КОГДА втРейсы.Заказ ССЫЛКА Документ.ЗаборТовара
	|				И ДОБАВИТЬКДАТЕ(ДАТАВРЕМЯ(1, 1, 1), СЕКУНДА, РАЗНОСТЬДАТ(НАЧАЛОПЕРИОДА(ОперативныеСтатусыЗаказовПоДаннымМПСрезПоследних.Регистратор.ДатаДоставки, ДЕНЬ), ОперативныеСтатусыЗаказовПоДаннымМПСрезПоследних.Регистратор.ДатаДоставки, СЕКУНДА)) > ДОБАВИТЬКДАТЕ(втРейсы.ИнтервалПо, МИНУТА, &Интервал)
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК Опоздание,
	|	1 КАК КоличествоЗаказов,
	|	втРейсы.ДатаДоставки КАК ДатаДоставки
	|ПОМЕСТИТЬ ВТ_Общая
	|ИЗ
	|	втРейсы КАК втРейсы
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПривязкаМашинКРейсам.СрезПоследних(
	|				,
	|				Рейс В
	|					(ВЫБРАТЬ
	|						втРейсы.Ссылка
	|					ИЗ
	|						втРейсы)) КАК ПривязкаМашинКРейсамСрезПоследних
	|		ПО втРейсы.Ссылка = ПривязкаМашинКРейсамСрезПоследних.Рейс
	|		ЛЕВОЕ СОЕДИНЕНИЕ втВремяПечатиМаршрутногоЛиста КАК втВремяПечатиМаршрутногоЛиста
	|		ПО втРейсы.Ссылка = втВремяПечатиМаршрутногоЛиста.Рейс
	|		ЛЕВОЕ СОЕДИНЕНИЕ втВремяПечатиРазблюдовки КАК втВремяПечатиРазблюдовки
	|		ПО втРейсы.Ссылка = втВремяПечатиРазблюдовки.Рейс
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОперативныеСтатусыЗаказовПоДаннымМП.СрезПоследних(
	|				,
	|				Заказ В
	|					(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|						втРейсы.Заказ
	|					ИЗ
	|						втРейсы)) КАК ОперативныеСтатусыЗаказовПоДаннымМПСрезПоследних
	|		ПО втРейсы.Заказ = ОперативныеСтатусыЗаказовПоДаннымМПСрезПоследних.Заказ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_Общая.ДатаРейса КАК ДатаРейса,
	|	ВТ_Общая.Водитель КАК ВодительЭкспедитор,
	|	ВТ_Общая.КоличествоЗаказов КАК КоличествоЗаказов,
	|	ВТ_Общая.Раньше КАК Раньше,
	|	ВТ_Общая.Опоздание КАК Опоздание,
	|	ВТ_Общая.НомерЗаказаСтриж КАК НомерЗаказаСтриж,
	|	ВТ_Общая.ИнтервалС КАК ИнтервалС,
	|	ВТ_Общая.ИнтервалПо КАК ИнтервалПо,
	|	ВТ_Общая.ВремяМП КАК ВремяМП,
	|	ВЫБОР
	|		КОГДА ВТ_Общая.Раньше = 1
	|			ТОГДА РАЗНОСТЬДАТ(ВТ_Общая.ВремяМП, ВТ_Общая.ИнтервалС, МИНУТА)
	|		КОГДА ВТ_Общая.Опоздание = 1
	|			ТОГДА РАЗНОСТЬДАТ(ВТ_Общая.ИнтервалПо, ВТ_Общая.ВремяМП, МИНУТА)
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК РазницаМинут,
	|	ВТ_Общая.Зона КАК Зона,
	|	ВТ_Общая.ВремяПечатиМЛ КАК ВремяПечатиМЛ,
	|	ВТ_Общая.ВремяПечатиРазблюдовки КАК ВремяПечатиРазблюдовки,
	|	ВТ_Общая.ТерминалДоставки КАК ТерминалДоставки,
	|	ВТ_Общая.Заказ КАК Заказ,
	|	ВТ_Общая.Заказ.ВладелецТовара КАК ВладелецТовара
	|ПОМЕСТИТЬ ВТ_Финал
	|ИЗ
	|	ВТ_Общая КАК ВТ_Общая
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВТ_Общая.ДатаРейса,
	|	ВТ_Общая.Экспедитор,
	|	ВТ_Общая.КоличествоЗаказов,
	|	ВТ_Общая.Раньше,
	|	ВТ_Общая.Опоздание,
	|	ВТ_Общая.НомерЗаказаСтриж,
	|	ВТ_Общая.ИнтервалС,
	|	ВТ_Общая.ИнтервалПо,
	|	ВТ_Общая.ВремяМП,
	|	ВЫБОР
	|		КОГДА ВТ_Общая.Раньше = 1
	|			ТОГДА РАЗНОСТЬДАТ(ВТ_Общая.ВремяМП, ВТ_Общая.ИнтервалС, МИНУТА)
	|		КОГДА ВТ_Общая.Опоздание = 1
	|			ТОГДА РАЗНОСТЬДАТ(ВТ_Общая.ИнтервалПо, ВТ_Общая.ВремяМП, МИНУТА)
	|		ИНАЧЕ 0
	|	КОНЕЦ,
	|	ВТ_Общая.Зона,
	|	ВТ_Общая.ВремяПечатиМЛ,
	|	ВТ_Общая.ВремяПечатиРазблюдовки,
	|	ВТ_Общая.ТерминалДоставки,
	|	ВТ_Общая.Заказ,
	|	ВТ_Общая.Заказ.ВладелецТовара
	|ИЗ
	|	ВТ_Общая КАК ВТ_Общая
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_Финал.ДатаРейса КАК ДатаРейса,
	|	ВТ_Финал.ВодительЭкспедитор КАК ВодительЭкспедитор,
	|	ВТ_Финал.КоличествоЗаказов КАК КоличествоЗаказов,
	|	ВТ_Финал.Раньше КАК Раньше,
	|	ВТ_Финал.Опоздание КАК Опоздание,
	|	ВТ_Финал.НомерЗаказаСтриж КАК НомерЗаказа,
	|	ВТ_Финал.ИнтервалС КАК ИнтервалС,
	|	ВТ_Финал.ИнтервалПо КАК ИнтервалПо,
	|	ВТ_Финал.ВремяМП КАК ВремяМП,
	|	ВТ_Финал.РазницаМинут КАК РазницаМинут,
	|	ВТ_Финал.Зона КАК Зона,
	|	ВТ_Финал.ВремяПечатиМЛ КАК ВремяПечатиМЛ,
	|	ВТ_Финал.ВремяПечатиРазблюдовки КАК ВремяПечатиРазблюдовки,
	|	ВТ_Финал.ТерминалДоставки КАК ТерминалДоставки,
	|	ВТ_Финал.Заказ КАК Заказ,
	|	ВТ_Финал.ВладелецТовара КАК ВладелецТовара
	|ИЗ
	|	ВТ_Финал КАК ВТ_Финал
	|
	|УПОРЯДОЧИТЬ ПО
	|	ДатаРейса,
	|	ВТ_Финал.ВодительЭкспедитор.Наименование,
	|	ВТ_Финал.Заказ.Дата
	|ИТОГИ
	|	СУММА(КоличествоЗаказов),
	|	СУММА(Раньше),
	|	СУММА(Опоздание),
	|	МИНИМУМ(ВремяПечатиМЛ),
	|	МИНИМУМ(ВремяПечатиРазблюдовки),
	|	МИНИМУМ(ТерминалДоставки)
	|ПО
	|	ДатаРейса,
	|	ВодительЭкспедитор";
	
	Запрос.УстановитьПараметр("ГруппаРайоновКАД", Справочники.ГруппыРайонов.НайтиПоКоду("00000000052"));
	Запрос.УстановитьПараметр("ГруппаРайоновМКАД", Справочники.ГруппыРайонов.НайтиПоКоду("00000000030"));
	Запрос.УстановитьПараметр("ДатаНачала", Отчет.ПериодОтчета.ДатаНачала);
	Запрос.УстановитьПараметр("ДатаОкончания", Отчет.ПериодОтчета.ДатаОкончания);
	Запрос.УстановитьПараметр("Интервал", Отчет.Интервал);
	Запрос.УстановитьПараметр("ПоМесяцам", ПоМесяцам);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДатаРейса = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		
	Пока ВыборкаДатаРейса.Следующий() Цикл
	
		ВыборкаВодительЭкспедитор = ВыборкаДатаРейса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		
		Пока ВыборкаВодительЭкспедитор.Следующий() Цикл
						
			Если НЕ Отчет.Водитель.Пустая() Тогда
				Если ВыборкаВодительЭкспедитор.ВодительЭкспедитор <> Отчет.Водитель Тогда
					Продолжить;
				КонецЕсли; 
			КонецЕсли;			
			
			Если НЕ Отчет.Терминал.Пустая() Тогда
				Если ВыборкаВодительЭкспедитор.ТерминалДоставки <> Отчет.Терминал Тогда
					Продолжить;
				КонецЕсли; 
			КонецЕсли; 
			
			
			//выводим шапку таблицы
			ОбластьДеньЗаголовок.Параметры.Заполнить(ВыборкаВодительЭкспедитор);
			ОбластьДеньЗаголовок.Параметры.ПроцентРаньше = Формат(ВыборкаВодительЭкспедитор.Раньше / ВыборкаВодительЭкспедитор.КоличествоЗаказов * 100, "ЧЦ=15; ЧДЦ=2");
			ОбластьДеньЗаголовок.Параметры.ПроцентОпоздание = Формат(ВыборкаВодительЭкспедитор.Опоздание / ВыборкаВодительЭкспедитор.КоличествоЗаказов * 100, "ЧЦ=15; ЧДЦ=2");
			ОбластьДеньЗаголовок.Параметры.КоличествоЗаказов = ВыборкаВодительЭкспедитор.КоличествоЗаказов;
			Результат.Вывести(ОбластьДеньЗаголовок);
			
			ВыборкаДетальныеЗаписи = ВыборкаВодительЭкспедитор.Выбрать();
	
			Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
				
				Если НЕ ЗначениеЗаполнено(ВыборкаДетальныеЗаписи.Раньше)
					И НЕ ЗначениеЗаполнено(ВыборкаДетальныеЗаписи.Опоздание) Тогда
					Продолжить;
				КонецЕсли; 
								
				Если НЕ Отчет.Партнер.Пустая() Тогда
					Если ВыборкаДетальныеЗаписи.ВладелецТовара <> Отчет.Партнер Тогда
						Продолжить;
					КонецЕсли; 
				КонецЕсли;  
				
				Если Отчет.УбратьЗаборы Тогда
					Если ТипЗнч(ВыборкаДетальныеЗаписи.Заказ) = Тип("ДокументСсылка.ЗаборТовара") Тогда
						Продолжить;
					КонецЕсли; 
				КонецЕсли; 
				
				ОбластьДеньСтрока.Параметры.НомерЗаказаРаньше	= Неопределено;
				ОбластьДеньСтрока.Параметры.ИнтервалРаньше		= Неопределено;	
				ОбластьДеньСтрока.Параметры.ВремяМПРаньше		= Неопределено;	
				ОбластьДеньСтрока.Параметры.РазницаРаньше		= Неопределено;	
				ОбластьДеньСтрока.Параметры.ЗонаРаньше			= Неопределено;
								
				ОбластьДеньСтрока.Параметры.НомерЗаказаПозже	= Неопределено;
				ОбластьДеньСтрока.Параметры.ИнтервалПозже		= Неопределено;	
				ОбластьДеньСтрока.Параметры.ВремяМППозже		= Неопределено;	
				ОбластьДеньСтрока.Параметры.РазницаПозже		= Неопределено;	
				ОбластьДеньСтрока.Параметры.ЗонаПозже			= Неопределено;
				ОбластьДеньСтрока.Параметры.Водитель			= Неопределено;
				ОбластьДеньСтрока.Параметры.Экспедитор			= Неопределено;
								
				Если ЗначениеЗаполнено(ВыборкаДетальныеЗаписи.Раньше) Тогда
					ОбластьДеньСтрока.Параметры.НомерЗаказаРаньше	= ВыборкаДетальныеЗаписи.НомерЗаказа;
					ОбластьДеньСтрока.Параметры.ИнтервалРаньше		= ""+Формат(ВыборкаДетальныеЗаписи.ИнтервалС,"ДЛФ=T")+" - "+Формат(ВыборкаДетальныеЗаписи.ИнтервалПо,"ДЛФ=T");	
					ОбластьДеньСтрока.Параметры.ВремяМПРаньше		= ВыборкаДетальныеЗаписи.ВремяМП;	
					ОбластьДеньСтрока.Параметры.РазницаРаньше		= ВыборкаДетальныеЗаписи.РазницаМинут;	
					ОбластьДеньСтрока.Параметры.ЗонаРаньше			= ВыборкаДетальныеЗаписи.Зона;						
				КонецЕсли;
				
				Если ЗначениеЗаполнено(ВыборкаДетальныеЗаписи.Опоздание) Тогда
					ОбластьДеньСтрока.Параметры.НомерЗаказаПозже	= ВыборкаДетальныеЗаписи.НомерЗаказа;
					ОбластьДеньСтрока.Параметры.ИнтервалПозже		= ""+Формат(ВыборкаДетальныеЗаписи.ИнтервалС,"ДЛФ=T")+" - "+Формат(ВыборкаДетальныеЗаписи.ИнтервалПо,"ДЛФ=T");	
					ОбластьДеньСтрока.Параметры.ВремяМППозже		= ВыборкаДетальныеЗаписи.ВремяМП;	
					ОбластьДеньСтрока.Параметры.РазницаПозже		= ВыборкаДетальныеЗаписи.РазницаМинут;	
					ОбластьДеньСтрока.Параметры.ЗонаПозже			= ВыборкаДетальныеЗаписи.Зона;
				КонецЕсли;
				
				ОбластьДеньСтрока.Параметры.КоличествоЗаказов = ВыборкаВодительЭкспедитор.КоличествоЗаказов;
				ОбластьДеньСтрока.Параметры.ВремяПечатиМЛ = ВыборкаВодительЭкспедитор.ВремяПечатиМЛ;
				ОбластьДеньСтрока.Параметры.ВремяПечатиРазблюдовки = ВыборкаВодительЭкспедитор.ВремяПечатиРазблюдовки;
				
				Если ТипЗнч(ВыборкаДетальныеЗаписи.ВодительЭкспедитор) = Тип("СправочникСсылка.новаВодители") Тогда
					ОбластьДеньСтрока.Параметры.Водитель = ВыборкаВодительЭкспедитор.ВодительЭкспедитор;
				ИначеЕсли ТипЗнч(ВыборкаДетальныеЗаписи.ВодительЭкспедитор) = Тип("СправочникСсылка.новаЭкспедиторы") Тогда
					ОбластьДеньСтрока.Параметры.Экспедитор = ВыборкаВодительЭкспедитор.ВодительЭкспедитор;
				КонецЕсли; 
				
				Результат.Вывести(ОбластьДеньСтрока);
				
			КонецЦикла;
			
		КонецЦикла;
		
		Результат.Вывести(ОбластьДеньПодвал);
		
	КонецЦикла;
		
	Элементы.Результат.ОтображениеСостояния.Видимость = Ложь;
	Элементы.Результат.ОтображениеСостояния.ДополнительныйРежимОтображения = ДополнительныйРежимОтображения.НеИспользовать;
	
КонецПроцедуры // ()
 

