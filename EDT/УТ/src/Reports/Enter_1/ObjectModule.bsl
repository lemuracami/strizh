Процедура Сформировать(Таб, НачПериода, КонПериода, Контрагент) Экспорт
	Таб.Очистить();
	Зап = Новый Запрос;
	Зап.Текст = "ВЫБРАТЬ
	            |	ЗакрытыеЗаказы.Реализация.НомерВнешнегоЗаказа КАК НомерЗаказа,
	            |	ЗакрытиеЗаказовТовары.НомерСтроки КАК НомерСтроки,
	            |	ЗакрытиеЗаказовТовары.Номенклатура.Наименование КАК Наименование,
	            |	ЗакрытиеЗаказовТовары.Номенклатура.Артикул КАК Артикул,
	            |	ЗакрытиеЗаказовТовары.Количество,
	            |	ВЫБОР
	            |		КОГДА ЗакрытыеЗаказы.СтатусИнтернетМагазина = ЗНАЧЕНИЕ(Справочник.СтатусЗаказаИнтернетМагазина.ЗаказЗакрыт)
	            |			ТОГДА ВЫБОР
	            |					КОГДА ЗакрытыеЗаказы.ПричинаНевыполнения В (ЗНАЧЕНИЕ(Справочник.ПричиныНевыполненияДоставки.ПереносСЗаездом), ЗНАЧЕНИЕ(Справочник.ПричиныНевыполненияДоставки.ОтказКлиентаСЗаездом))
	            |						ТОГДА ""Отказ""
	            |					ИНАЧЕ ""Доставлено""
	            |				КОНЕЦ
	            |		ИНАЧЕ ""Отказ""
	            |	КОНЕЦ КАК Статус,
	            |	ВЫБОР
	            |		КОГДА ЗакрытыеЗаказы.ТипОплаты = ЗНАЧЕНИЕ(Справочник.ТипыОплат.Наличные)
	            |			ТОГДА (ЗакрытиеЗаказовТовары.Количество - ЗакрытиеЗаказовТовары.КоличествоВозвращено) * ЗакрытиеЗаказовТовары.Цена
	            |		ИНАЧЕ 0
	            |	КОНЕЦ КАК Сумма
	            |ИЗ
	            |	РегистрНакопления.ЗакрытыеЗаказы КАК ЗакрытыеЗаказы
	            |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗакрытиеЗаказов.Товары КАК ЗакрытиеЗаказовТовары
	            |		ПО ЗакрытыеЗаказы.Регистратор.Ссылка = ЗакрытиеЗаказовТовары.Ссылка.Ссылка
	            |			И ЗакрытыеЗаказы.Реализация.Ссылка = ЗакрытиеЗаказовТовары.Реализация.Ссылка
	            |ГДЕ
	            |	ЗакрытыеЗаказы.Период МЕЖДУ &НачПериода И &КонПериода
	            |	И ЗакрытыеЗаказы.ТипЗаказа В (ЗНАЧЕНИЕ(Перечисление.ТипыЗаказов.Доставка), ЗНАЧЕНИЕ(Перечисление.ТипыЗаказов.Самовывоз))
	            |	И ЗакрытыеЗаказы.ИнтернетМагазин = &ИнтернетМагазин
	            |
	            |ОБЪЕДИНИТЬ ВСЕ
	            |
	            |ВЫБРАТЬ
	            |	ЗакрытыеЗаказы.Реализация.НомерВнешнегоЗаказа,
	            |	1000000,
	            |	""Услуга доставки"",
	            |	""----"",
	            |	1,
	            |	ВЫБОР
	            |		КОГДА ЗакрытыеЗаказы.СтатусИнтернетМагазина = ЗНАЧЕНИЕ(Справочник.СтатусЗаказаИнтернетМагазина.ЗаказЗакрыт)
	            |			ТОГДА ВЫБОР
	            |					КОГДА ЗакрытыеЗаказы.ПричинаНевыполнения В (ЗНАЧЕНИЕ(Справочник.ПричиныНевыполненияДоставки.ПереносСЗаездом), ЗНАЧЕНИЕ(Справочник.ПричиныНевыполненияДоставки.ОтказКлиентаСЗаездом))
	            |						ТОГДА ""Доставлено""
	            |					ИНАЧЕ ""Отказ""
	            |				КОНЕЦ
	            |		ИНАЧЕ ""Отказ""
	            |	КОНЕЦ,
	            |	ВЫБОР
	            |		КОГДА ЗакрытыеЗаказы.ТипОплаты = ЗНАЧЕНИЕ(Справочник.ТипыОплат.Наличные)
	            |			ТОГДА ВЫБОР
	            |					КОГДА ЗакрытыеЗаказы.СтатусИнтернетМагазина = ЗНАЧЕНИЕ(Справочник.СтатусЗаказаИнтернетМагазина.ЗаказЗакрыт)
	            |						ТОГДА ЗакрытыеЗаказы.СуммаДоставкиДоМКАД + ЗакрытыеЗаказы.СуммаДоставкиЗаМКАД
	            |					ИНАЧЕ 0
	            |				КОНЕЦ
	            |		ИНАЧЕ 0
	            |	КОНЕЦ
	            |ИЗ
	            |	РегистрНакопления.ЗакрытыеЗаказы КАК ЗакрытыеЗаказы
	            |ГДЕ
	            |	ЗакрытыеЗаказы.Период МЕЖДУ &НачПериода И &КонПериода
	            |	И ЗакрытыеЗаказы.ТипЗаказа В (ЗНАЧЕНИЕ(Перечисление.ТипыЗаказов.Доставка), ЗНАЧЕНИЕ(Перечисление.ТипыЗаказов.Самовывоз))
	            |	И ЗакрытыеЗаказы.ИнтернетМагазин = &ИнтернетМагазин
	            |
	            |УПОРЯДОЧИТЬ ПО
	            |	НомерЗаказа,
	            |	НомерСтроки";
	Зап.УстановитьПараметр("НачПериода", НачалоДня(НачПериода));			
	Зап.УстановитьПараметр("КонПериода", КонецДня(КонПериода));			
	Зап.УстановитьПараметр("ИнтернетМагазин", Контрагент);			
	
	Рез = Зап.Выполнить().Выгрузить();
	
	//Рез.Сортировать("НомерЗаказа, НомерСтроки");
	
	Мак = ПолучитьМакет("Макет");
	ОблШ = Мак.ПолучитьОбласть("Шапка");
	ОблСтр = Мак.ПолучитьОбласть("Строка");
	Таб.Вывести(ОблШ);
	
	Линия = Новый Линия(ТипЛинииЯчейкиТабличногоДокумента.Сплошная, 2);
	
	
	ТекЗак = Неопределено;
	
	Сч = 2;
	Для Каждого Тек Из Рез Цикл
		ЗаполнитьЗначенияСвойств(ОблСтр.параметры, Тек);
		Таб.Вывести(ОблСтр);
		
		Если ТекЗак = Неопределено Или ТекЗак <> Тек.НомерЗаказа Тогда
			Обл = Таб.Область("R" + Формат(Сч, "ЧГ=") + "C2:R" + Формат(Сч, "ЧГ=") + "C7");
			Обл.ГраницаСверху = Линия;
			ТекЗак = Тек.НомерЗаказа;
		КонецЕСли;	
		Сч = Сч + 1;
	КонецЦикла;
	
	Сч = Сч -1;
	
	Обл = Таб.Область("R" + Формат(Сч, "ЧГ=") + "C2:R" + Формат(Сч, "ЧГ=") + "C7");
	Обл.ГраницаСнизу = Линия;

	Таб.ПолеСлева = 3;
	Таб.ПолеСнизу = 3;
	Таб.ПолеСверху = 3;
	Таб.ПолеСправа = 3;
	Таб.Автомасштаб = Истина;
	Таб.ТолькоПросмотр = 1;
	Таб.ИмяПараметровПечати = "Энтер_1";
КонецПроцедуры	