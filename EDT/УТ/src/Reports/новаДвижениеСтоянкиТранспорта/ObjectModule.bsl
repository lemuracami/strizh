
Перем ПараметрыОткрытия Экспорт;
Перем Форма Экспорт;
Перем соЗапросыДанных;
Перем ВыполненоЗапросов;
Перем КСЛ;
Перем тзДвижение;

Процедура ПодключитьКСЛ()
	#Если Клиент Тогда
		КСЛ = НоваСерверКСЛ.Подключиться(новаКонтекст);
	#Иначе
		КСЛ = НоваСерверКСЛ.Подключиться();
	#КонецЕсли
КонецПроцедуры

Функция ПолучитьПараметрыПоУмолчанию() Экспорт
	стПараметры = Новый Структура;
	стПараметры.Вставить("ДатаНачала", НачалоМесяца(ТекущаяДата()));
	стПараметры.Вставить("ДатаОкончания", КонецМесяца(ТекущаяДата()));
	стПараметры.Вставить("Транспорт", Справочники.новаТранспорт.ПустаяСсылка());
	стПараметры.Вставить("ИспользоватьКарту", Истина);
	
	Возврат стПараметры;
КонецФункции

Процедура НачатьФормирование() Экспорт
	соЗапросыДанных = Новый Соответствие;
	ВыполненоЗапросов = 0;
КонецПроцедуры

Процедура НачатьПолучениеДанных(ИмяИсточникаДанных, стПараметры) Экспорт
	Если ИмяИсточникаДанных = "новаДвижениеСтоянкиТранспорта" Тогда
		//Если Не стПараметры.Свойство("Транспорт") Тогда
		//	ВызватьИсключение("Не указан транспорт!");
		//КонецЕсли;
		
		//Если Не ЗначениеЗаполнено(стПараметры.Транспорт) Тогда
		//	ВызватьИсключение("Не указан транспорт!");
		//КонецЕсли;
		
		ПодключитьКСЛ();
		
		ЗапросДанных = КСЛ.Мониторинг_Отчеты_ДвижениеСтоянки();
		
		Если стПараметры.Свойство("Транспорт") Тогда
			Если ЗначениеЗаполнено(стПараметры.Транспорт) Тогда
				ЗапросДанных.Параметры.Транспорт.Ид = СокрЛП(стПараметры.Транспорт.УникальныйИдентификатор());
			КонецЕсли;
		КонецЕсли;
		
		Если стПараметры.Свойство("МинимальнаяДлительностьСтоянки") Тогда
			Если ЗначениеЗаполнено(стПараметры.МинимальнаяДлительностьСтоянки) Тогда
				ЗапросДанных.Параметры.МинимальнаяДлительностьСтоянки = стПараметры.МинимальнаяДлительностьСтоянки * 60;
			КонецЕсли;
		КонецЕсли;
		
		Если стПараметры.Свойство("ВыводитьТолькоСтоянки") Тогда
			ЗапросДанных.Параметры.ВыводитьТолькоСтоянки = стПараметры.ВыводитьТолькоСтоянки;
		КонецЕсли;
		
		ЗапросДанных.Параметры.ДатаНачала = стПараметры.ДатаНачала;
		ЗапросДанных.Параметры.ДатаОкончания = стПараметры.ДатаОкончания;
		
		соЗапросыДанных.Вставить(ИмяИсточникаДанных, ЗапросДанных);
		ЗапросДанных.ПриВыполнении(ЭтотОбъект, "ДанныеПолучены");
		
		ЗапросДанных.Выполнить();
	КонецЕсли;
КонецПроцедуры

Процедура ДанныеПолучены(ЗапросДанных, Ошибка) Экспорт
	ВыполненоЗапросов = ВыполненоЗапросов + 1;
	
	Если ВыполненоЗапросов = соЗапросыДанных.Количество() Тогда
		Форма.ВсеДанныеПолучены();
	КонецЕсли;
КонецПроцедуры

Функция ПолучитьДанные(ИмяИсточникаДанных, стПараметры) Экспорт
	Если ИмяИсточникаДанных = "новаДвижениеСтоянкиТранспорта" Тогда
		
		ЗапросДанных = соЗапросыДанных[ИмяИсточникаДанных];
		
		Если ЗапросДанных.Ошибка <> Неопределено Тогда
			ВызватьИсключение ЗапросДанных.Ошибка.ВСтроку();
		КонецЕсли;
		
		ЗапросДанных.ОтключитьВсеОбработчики();
		
		тзДвижение = Новый ТаблицаЗначений;
		тзДвижение.Колонки.Добавить("Начало", Новый ОписаниеТипов("Дата"));
		тзДвижение.Колонки.Добавить("Окончание", Новый ОписаниеТипов("Дата"));
		тзДвижение.Колонки.Добавить("Длительность", Новый ОписаниеТипов("Число"));
		тзДвижение.Колонки.Добавить("Пробег", Новый ОписаниеТипов("Число"));
		тзДвижение.Колонки.Добавить("СредняяСкорость", Новый ОписаниеТипов("Число"));
		тзДвижение.Колонки.Добавить("Широта", Новый ОписаниеТипов("Число"));
		тзДвижение.Колонки.Добавить("Долгота", Новый ОписаниеТипов("Число"));
		тзДвижение.Колонки.Добавить("Место", Новый ОписаниеТипов("Строка"));
		тзДвижение.Колонки.Добавить("Транспорт", Новый ОписаниеТипов("СправочникСсылка.новаТранспорт"));
		
		мТранспорт = Справочники.новаТранспорт;
		
		Результат = ЗапросДанных.ПолучитьРезультат();
		КоличествоСтрок = Результат.Количество - 1;
		Для ъ = 0 По КоличествоСтрок Цикл
			Строка = Результат.Получить(ъ);
			стрДвижение = тзДвижение.Добавить();
			
			стрДвижение.Начало = Строка.Начало;
			стрДвижение.Окончание = Строка.Окончание;
			стрДвижение.Длительность = Строка.Длительность / 3600;
			стрДвижение.Пробег = Строка.Пробег;
			стрДвижение.СредняяСкорость = Строка.СредняяСкорость;
			
			стрДвижение.Широта = Строка.Широта;
			стрДвижение.Долгота = Строка.Долгота;
			стрДвижение.Место = Строка.Место;
			
			стрДвижение.Транспорт = мТранспорт.ПолучитьСсылку(Новый УникальныйИдентификатор(Строка.Транспорт));
		КонецЦикла;
		
		Возврат тзДвижение;
	КонецЕсли;
КонецФункции

Процедура Расшифровать(Элемент, Расшифровка, ОбработкаРасшифровки) Экспорт
	стПараметры = Новый Структура("ДатаНачала, ДатаОкончания");
	
	Место = Форма.ПолучитьРасшифровку("Место");
	стрМесто = тзДвижение.Найти(Место, "Место");
	Если стрМесто <> Неопределено Тогда
		Форма.ПоказатьМестоНаКарте(стрМесто.Широта, стрМесто.Долгота);
	КонецЕсли;
	
	Дата = Форма.ПолучитьРасшифровку("Дата");
	Если Дата = Неопределено Тогда
		стПараметры.ДатаНачала = НачалоДня(Форма.ПолучитьРасшифровку("ДатаНачала"));
		стПараметры.ДатаОкончания = КонецДня(Форма.ПолучитьРасшифровку("ДатаОкончания"));
	Иначе
		стПараметры.ДатаНачала = НачалоДня(Дата);
		стПараметры.ДатаОкончания = КонецДня(Дата);
	КонецЕсли;
	#Если Клиент Тогда
	сзРасшифровки = новаОтчетыСКД.ПолучитьРасшифровкиПоПараметрам(стПараметры, новаКонтекст);
	выбРасшифровка = Форма.ВыбратьИзМеню(сзРасшифровки, Элемент);
	Если выбРасшифровка = Неопределено Тогда Возврат; КонецЕсли;
	новаОтчетыСКД.Расшифровать(выбРасшифровка.Значение, стПараметры);
	#КонецЕсли
КонецПроцедуры

Процедура СформироватьНаКарте(ПолеКарты, стПараметры) Экспорт
	
	ПолеКарты.УдалитьВсеОбъекты();
	
	Если не стПараметры.Свойство("Транспорт") Тогда
		
		#Если Клиент Тогда
			Сообщить("Не указан транспорт!");
		#КонецЕсли
		Возврат;
	КонецЕсли;
	
	ПодключитьКСЛ();
	
	Трек = КСЛ.Мониторинг_Карты_ТрекМониторинга();
	Трек.Транспорт.Ид = СокрЛП(стПараметры.Транспорт.УникальныйИдентификатор());
	Трек.ВремяНачала = стПараметры.ДатаНачала;
	Трек.ВремяОкончания = стПараметры.ДатаОкончания;
	ПолеКарты.ДобавитьОбъект(Трек);
	Трек.ЗагрузитьДанные();
	
	Для Каждого стрДвижение Из тзДвижение Цикл
		Если стрДвижение.СредняяСкорость <> 0 Тогда Продолжить; КонецЕсли;
		
		Точка = КСЛ.Доставка_Карты_ОтображениеТочкиМаршрута();
		Точка.Факт = Истина;
		Точка.Координаты.Широта = стрДвижение.Широта;
		Точка.Координаты.Долгота = стрДвижение.Долгота;
		Точка.Заголовок = стрДвижение.Место;
		Точка.ВремяПосещения = стрДвижение.Начало;
		Точка.ДлительностьСтоянки = стрДвижение.Длительность * 3600; 
		Точка.ПорядокРисования = 10;
		
		ПолеКарты.ДобавитьОбъект(Точка);
	КонецЦикла;
	
КонецПроцедуры