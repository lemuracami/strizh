
// Предопределенное свойство. Содержит коллекцию свойств печатной формы.
//
Перем СвойстваПечатнойФормы Экспорт; // <Структура>

// Предопределенная функция. Формирует печатную форму в указанном документе.
//
// Параметры:
//  Форма             <Форма>
//  ТабличныйДокумент <ТабличныйДокумент>
//  Заголовок         <Строка>
//
// Возвращаемое значение:
//  <Булево>: Истина, если печатную форму удалось сформировать.
//
Функция ВывестиНаПечать(Форма, ТабличныйДокумент, Заголовок) Экспорт
	
	Если ТипЗнч(Форма) = Тип("Форма") Тогда
		ИсточникМаршрута = Форма.ИсточникМаршрута;
	Иначе
		ИсточникМаршрута = Обработки.новаИсточникМаршрутРейса.Создать();
		ИсточникМаршрута.НачатьРедактирование(Форма.Ссылка, Форма.ЭтотОбъект);
	КонецЕсли;
	
	КоличествоЕдиницПеревозки = 0;
	КоличествоВес = 0;
	
	мсДоставки = ИсточникМаршрута.ПолучитьДоставкиВПорядкеВыполнения(Форма.Ссылка);
	Для Каждого Доставка Из мсДоставки Цикл
		КоличествоЕдиницПеревозки = КоличествоЕдиницПеревозки + Доставка.Груз.КоличествоЕдиницПеревозки;
		КоличествоВес = КоличествоВес + Доставка.Груз.Вес;
	КонецЦикла;
	
	Макет = ПолучитьМакет("ПогрузочныйЛист");
	
	Шапка = Макет.ПолучитьОбласть("Шапка");
	Шапка.Параметры.Заголовок = "Детальный план на "+Формат(Владелец.Дата, "ДФ=dd.MM.yyyy");
	Шапка.Параметры.Водитель = Владелец.Водитель;
	Шапка.Параметры.Транспорт = Владелец.Транспорт;
	Шапка.Параметры.Расстояние = Строка(Владелец.РасстояниеПлан)+" км";
	Шапка.Параметры.КоличествоЗаказов = Строка(мсДоставки.Количество())+" зак";
	Шапка.Параметры.КоличествоМестВес = Строка(КоличествоЕдиницПеревозки)+"/"+Строка(КоличествоВес);
	ТабличныйДокумент.Вывести(Шапка);
	
	Строка = Макет.ПолучитьОбласть("Строка");
	ъ = 1;
	Для Каждого Доставка Из мсДоставки Цикл
		Строка.Параметры.Номер = ъ;
		Строка.Параметры.КолМест = Доставка.Груз.КоличествоЕдиницПеревозки;
		Строка.Параметры.Вес = Доставка.Груз.Вес;
		Строка.Параметры.НомерЗаказа = Доставка.Номер;
		Строка.Параметры.Клиент = Доставка.ОсновнойКонтрагент;
		Строка.Параметры.Адрес = Доставка.ОсновнаяТочка.Адрес;
		Строка.Параметры.Примечание = Доставка.Комментарий;
		ТабличныйДокумент.Вывести(Строка);
		
		ъ = ъ + 1;
	КонецЦикла;
	
	Подвал = Макет.ПолучитьОбласть("Подвал");
	ТабличныйДокумент.Вывести(Подвал);
	
	// Зададим параметры макета
	ТабличныйДокумент.ПолеСверху              = 0;
	ТабличныйДокумент.ПолеСлева               = 0;
	ТабличныйДокумент.ПолеСнизу               = 0;
	ТабличныйДокумент.ПолеСправа              = 0;
	ТабличныйДокумент.РазмерКолонтитулаСверху = 0;
	ТабличныйДокумент.РазмерКолонтитулаСнизу  = 0;
	ТабличныйДокумент.АвтоМасштаб             = Истина;
	ТабличныйДокумент.ОриентацияСтраницы      = ОриентацияСтраницы.Портрет;
	ТабличныйДокумент.ИмяПараметровПечати = "ПараметрыПечатиПогрузочногоЛиста";
	
	Возврат Истина;
КонецФункции

СвойстваПечатнойФормы = Новый Структура("Представление", "Погрузочный лист");