
// Предопределенное свойство. Содержит коллекцию свойств печатной формы.
//
Перем СвойстваПечатнойФормы Экспорт; // <Структура>

// Предопределенная функция. Формирует печатную форму в указанном документе.
//
// Параметры:
//  Форма             <Форма>
//  ТабличныйДокумент <ТабличныйДокумент>
//  Заголовок         <Строка>
//
// Возвращаемое значение:
//  <Булево>: Истина, если печатную форму удалось сформировать.
//
Функция ВывестиНаПечать(Форма, ТабличныйДокумент, Заголовок) Экспорт
	ТабДокумент = ТабличныйДокумент;
	Макет = ПолучитьМакет("ЛистСборки");
	
	НомерНаПечать = Владелец.Номер;
	ТекстЗаголовка = 
	"Сборка " + " № " + НомерНаПечать
	+ " от " + Формат(Владелец.Дата, "ДФ='дд ММММ гггг'");
	ТекстВремени = "Время формирования: " + Формат(ТекущаяДата(),"ДЛФ=T");

	ТабДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_Лист_Сборки_Рейс_По_Заявкам_На_Склад_По_машине";
	
	ОбластьМакета = Макет.ПолучитьОбласть("Заголовок");
	ОбластьМакета.Параметры.ТекстЗаголовка = ТекстЗаголовка;
	ОбластьМакета.Параметры.ТекстВремени = ТекстВремени;
	ТабДокумент.Вывести(ОбластьМакета);
	
	ОбластьМакета = Макет.ПолучитьОбласть("Автомобиль");
	ТипАвто = ""+Владелец.Рейс.Транспорт.Марка+", гос. номер "+Владелец.Рейс.Транспорт.НомерГосударственнойРегистрации+", водитель "+Владелец.Рейс.Водитель;//+", ходка "+Строка(ПолучитьНомерХодки());
	ОбластьМакета.Параметры.ТипАвто = ТипАвто;
	ТабДокумент.Вывести(ОбластьМакета);
	ОбластьМакета = Макет.ПолучитьОбласть("ШапкаСборки");
	ТабДокумент.Вывести(ОбластьМакета);
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	новаСборкаНаСкладеСостав.МестнаяДоставка.Ссылка КАК ЗаявкаСклад,
	|	новаСборкаНаСкладеСостав.МестнаяДоставка.Инкассация КАК Инкассация,
	|	новаСборкаНаСкладеСостав.МестнаяДоставка.Факт КАК Факт,
	|	новаСборкаНаСкладеСостав.МестнаяДоставка.СуммаИнкассацииЗаПредыдущиеПоставки КАК СуммаИнкассацииЗаПредыдущиеПоставки,
	|	новаСборкаНаСкладеСостав.МестнаяДоставка.ВремяПрибытияС КАК ДоставкаС,
	|	новаСборкаНаСкладеСостав.МестнаяДоставка.ВремяПрибытияПо КАК ДоставкаДо,
	|	новаСборкаНаСкладеСостав.МестнаяДоставка.Грузополучатель КАК Контрагент,
	|	новаСборкаНаСкладеСостав.МестнаяДоставка.ТочкаПрибытия КАК ТорговаяТочка
	|ИЗ
	|	Документ.новаСборкаНаСкладе.Состав КАК новаСборкаНаСкладеСостав
	|ГДЕ
	|	новаСборкаНаСкладеСостав.Ссылка = &Владелец";
	
	Запрос.УстановитьПараметр("Владелец",Владелец.Ссылка);
	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	
	НомСтроки = 1;
	ОбластьМакета = Макет.ПолучитьОбласть("ШапкаТаблицы");
	ТабДокумент.Вывести(ОбластьМакета);
	
	Пока Выборка.Следующий() Цикл
		ОбластьМакета = Макет.ПолучитьОбласть("Строка");
		ОбластьМакета.Параметры.Заполнить(Выборка);
		Если Выборка.ДоставкаС = НачалоДня(Выборка.ДоставкаС) Тогда
			ОбластьМакета.Параметры.ДоставкаС = "";
		КонецЕсли;
		Если Выборка.ДоставкаДо = НачалоДня(Выборка.ДоставкаДо) Тогда
			ОбластьМакета.Параметры.ДоставкаДо = "";
		КонецЕсли;
		
		Если Выборка.Инкассация Тогда
			СтрокаИнкассация = "Инкассация!";
			Если Выборка.Факт Тогда
				СтрокаИнкассация = СтрокаИнкассация+", факт.";
			КонецЕсли;
			Если Выборка.СуммаИнкассацииЗаПредыдущиеПоставки > 0 Тогда
				СтрокаИнкассация = СтрокаИнкассация+", за пред. "+ Выборка.СуммаИнкассацииЗаПредыдущиеПоставки;
			КонецЕсли;	
			ОбластьМакета.Параметры.Инкассация = СтрокаИнкассация;
		Иначе
			ОбластьМакета.Параметры.Инкассация ="";
		КонецЕсли;	
		ОбластьМакета.Параметры.НомСтроки = НомСтроки;
		
	    ТабДокумент.Вывести(ОбластьМакета);
		НомСтроки = НомСтроки +1;
	КонецЦикла;
	
	ОбластьМакета = Макет.ПолучитьОбласть("Подписи");
	ТабДокумент.Вывести(ОбластьМакета);
	
	//СписокЗаявок = Новый СписокЗначений;
	СписокЗаявок = Результат.Выгрузить().ВыгрузитьКолонку("ЗаявкаСклад");
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	новаГрузСпецификация.Товар КАК Номенклатура,
	|	СУММА(новаГрузСпецификация.Количество) КАК Количество,
	|	СУММА(новаГрузСпецификация.Вес) КАК Вес,
	|	новаГрузСпецификация.Серия КАК ДатаРозлива,
	|	новаГрузСпецификация.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	новаМестнаяДоставка.Грузоотправитель КАК Склад
	|ИЗ
	|	БизнесПроцесс.новаМестнаяДоставка КАК новаМестнаяДоставка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.новаГруз.Спецификация КАК новаГрузСпецификация
	|		ПО новаГрузСпецификация.Ссылка = новаМестнаяДоставка.Груз
	|ГДЕ
	|	новаМестнаяДоставка.Ссылка В(&СписокЗаявок)
	|
	|СГРУППИРОВАТЬ ПО
	|	новаМестнаяДоставка.Грузоотправитель,
	|	новаГрузСпецификация.Товар,
	|	новаГрузСпецификация.ЕдиницаИзмерения,
	|	новаГрузСпецификация.Серия
	|ИТОГИ
	|	СУММА(Количество),
	|	СУММА(Вес)
	|ПО
	|   ОБЩИЕ,
	|	Склад,
	|	Товар,
	|	ЕдиницаИзмерения,
	|	Серия";
	
	
	Запрос.УстановитьПараметр("СписокЗаявок",СписокЗаявок);
	Запрос.УстановитьПараметр("Дата",Владелец.Дата);
	Результат = Запрос.Выполнить();
	
	УровеньИтоги = Результат.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	УровеньИтоги.Следующий();
	ИтогоВес = УровеньИтоги.Вес;
	ИтогоКоличество = УровеньИтоги.Количество;
	
	УровеньСклад = УровеньИтоги.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	НомСтроки = 0;
	Пока УровеньСклад.Следующий() Цикл
		НомСтроки = НомСтроки + 1;
		КоличествоПоЗаявке = УровеньСклад.Количество;
		ОбластьМакета = Макет.ПолучитьОбласть("МестоХранения");
		ОбластьМакета.Параметры.Склад = УровеньСклад.Склад;
		ТабДокумент.Вывести(ОбластьМакета);
		
		ОбластьМакета = Макет.ПолучитьОбласть("ШапкаТовара");
		ТабДокумент.Вывести(ОбластьМакета);
		
		ИтогоВесПоСкладу = УровеньСклад.Вес;
		ИтогоКолвоПоСкладу = УровеньСклад.Количество;
		
		УровеньНом = УровеньСклад.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		НомерСтроки = 1;
		Пока УровеньНом.Следующий() Цикл
			УровеньЕд = УровеньНом.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			
			Пока УровеньЕд.Следующий() Цикл
				УровеньДата = УровеньЕд.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
				Пока УровеньДата.Следующий() Цикл
					ОбластьМакета = Макет.ПолучитьОбласть("СтрокаТовара");	
					ОбластьМакета.Параметры.Заполнить(УровеньДата);
					Если УровеньДата.ЕдиницаИзмерения <> NULL Тогда
						Областьмакета.Параметры.Коэффициент = УровеньДата.ЕдиницаИзмерения.Коэффициент;
					КонецЕсли;
					ОбластьМакета.Параметры.НомерСтроки = НомерСтроки;
					ОбластьМакета.Параметры.ДатаРозлива = Лев(УровеньДата.ДатаРозлива,10);
					ТабДокумент.Вывести(ОбластьМакета);
					НомерСтроки = НомерСтроки +1;
				КонецЦикла;
				
			КонецЦикла;
			
		КонецЦикла;
		ТабДокумент.Вывести(Макет.ПолучитьОбласть("Подписи"));
		
		ОбластьМакета = Макет.ПолучитьОбласть("Подвал");	
		ОбластьМакета.Параметры.ПечВсего = ИтогоКолвоПоСкладу;
		ТабДокумент.Вывести(ОбластьМакета);
	КонецЦикла;
	
	Если НомСтроки = 0 Тогда Возврат Ложь; КонецЕсли;

	ТабДокумент.Вывести(Макет.ПолучитьОбласть("Подписи"));
	ОбластьМакета = Макет.ПолучитьОбласть("ОбщийПодвал");	
	ОбластьМакета.Параметры.ВсегоКоличество = ИтогоКоличество;
	ОбластьМакета.Параметры.ВсегоВес = ИтогоВес;
	ТабДокумент.Вывести(ОбластьМакета);

	ТабДокумент.ПолеСверху              = 0;
	ТабДокумент.ПолеСлева               = 0;
	ТабДокумент.ПолеСнизу               = 0;
	ТабДокумент.ПолеСправа              = 0;
	ТабДокумент.РазмерКолонтитулаСверху = 0;
	ТабДокумент.РазмерКолонтитулаСнизу  = 0;
	ТабДокумент.АвтоМасштаб             = Истина;
	ТабДокумент.ОриентацияСтраницы      = ОриентацияСтраницы.Портрет;
	ТабДокумент.ОтображатьЗаголовки = Ложь;
	ТабДокумент.ОтображатьСетку     = Ложь;
	ТабДокумент.Защита              = Истина;
	ТабДокумент.ТолькоПросмотр      = Истина;
	
	Возврат Истина;
	
КонецФункции

СвойстваПечатнойФормы = Новый Структура("Представление", "Лист сборки по машине");