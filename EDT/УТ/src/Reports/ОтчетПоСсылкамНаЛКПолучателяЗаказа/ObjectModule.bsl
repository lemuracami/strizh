
Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	НастройкиКомпоновщика = КомпоновщикНастроек.ПолучитьНастройки();
	
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
	МакетКомпоновки = КомпоновщикМакета.Выполнить(СхемаКомпоновкиДанных, НастройкиКомпоновщика, ДанныеРасшифровки);

	ПроцессорКомпоновки = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновки.Инициализировать(МакетКомпоновки,, ДанныеРасшифровки, Истина);

	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент;
	ПроцессорВывода.УстановитьДокумент(ДокументРезультат);
	
	ПроцессорВывода.Вывести(ПроцессорКомпоновки);
	
	КоличествоСтрок = ДокументРезультат.ВысотаТаблицы;
	КоличествоКолонок = ДокументРезультат.ШиринаТаблицы;
	
	КоличествоСтрокПоиска = Мин(КоличествоСтрок, 10);
	
	НомерСтрокиСсылка = 0;
	НомерКолонкиСсылка = 0;
	
	//ищем ячейку Ссылка
	Для НомерСтроки = 1 По КоличествоСтрокПоиска Цикл
		Для НомерКолонки = 1 По КоличествоКолонок Цикл
			Область = ДокументРезультат.Область(НомерСтроки, НомерКолонки, НомерСтроки, НомерКолонки);
			Если Область.Текст = "Ссылка" Тогда
				НомерСтрокиСсылка = НомерСтроки;
				НомерКолонкиСсылка = НомерКолонки;
				Прервать;
			КонецЕсли;
		КонецЦикла;
		Если НомерСтрокиСсылка Тогда
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Если НомерСтрокиСсылка Тогда
		НомерСтрокиСсылка = НомерСтрокиСсылка + 1;
		Для НомерСтроки = НомерСтрокиСсылка По КоличествоСтрок Цикл
			Область = ДокументРезультат.Область(НомерСтроки, НомерКолонкиСсылка, НомерСтроки, НомерКолонкиСсылка);
			ЗначениеРасшифровки = Область.Текст;
			Область.Гиперссылка = Найти(ЗначениеРасшифровки, "http://") Или Найти(ЗначениеРасшифровки, "https://") Или Найти(ЗначениеРасшифровки, "www.");
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры
