
&НаСервере
Функция ПолучитьЗначенияРасшифровки(Расшифровка)
	
	ЗначенияРасшифровки = Новый Массив;
	
	Данные = ПолучитьИзВременногоХранилища(ДанныеРасшифровки);
	
	ЭлементРасшифровки = Данные.Элементы[Расшифровка];
	
	Если ТипЗнч(ЭлементРасшифровки) = Тип("ЭлементРасшифровкиКомпоновкиДанныхПоля") Тогда
		ПоляРасшифровки = ЭлементРасшифровки.ПолучитьПоля();
		Для Каждого Поле Из ПоляРасшифровки Цикл
			ЗначенияРасшифровки.Добавить(Поле.Значение);
		КонецЦикла;
	КонецЕсли;
	
	Возврат ЗначенияРасшифровки;
	
КонецФункции

&НаКлиенте
Процедура РезультатОбработкаРасшифровки(Элемент, Расшифровка, СтандартнаяОбработка, ДополнительныеПараметры)
	
	Если ТипЗнч(Расшифровка) = Тип("ИдентификаторРасшифровкиКомпоновкиДанных") Тогда
		ЗначенияРасшифровки = ПолучитьЗначенияРасшифровки(Расшифровка);
		Если ЗначенияРасшифровки.Количество() Тогда
			ЗначениеРасшифровки = ЗначенияРасшифровки[0];
			ТипЗначения = ТипЗнч(ЗначениеРасшифровки);
			Если ТипЗначения = Тип("Строка") Тогда
				
				Если Найти(ЗначениеРасшифровки, "http://") Или Найти(ЗначениеРасшифровки, "https://") Или Найти(ЗначениеРасшифровки, "www.") Тогда
					СтандартнаяОбработка = Ложь;
					ЗапуститьПриложение(ЗначениеРасшифровки);
				КонецЕсли;
				
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры
