
// Предопределенное свойство. Содержит коллекцию свойств печатной формы.
//
Перем СвойстваПечатнойФормы Экспорт; // <Структура>

// Предопределенная функция. Формирует печатную форму в указанном документе.
//
// Параметры:
//  Форма             <Форма>
//  ТабличныйДокумент <ТабличныйДокумент>
//  Заголовок         <Строка>
//
// Возвращаемое значение:
//  <Булево>: Истина, если печатную форму удалось сформировать.
//
Функция ВывестиНаПечать(Форма, ТабличныйДокумент, Заголовок) Экспорт
	Если ТипЗнч(Форма) = Тип("Форма") Тогда
		ИсточникМаршрута = Форма.ИсточникМаршрута;
	Иначе
		ИсточникМаршрута = Обработки.новаИсточникМаршрутРейса.Создать();
		ИсточникМаршрута.НачатьРедактирование(Форма.Ссылка, Форма.ЭтотОбъект);
	КонецЕсли;
	
	Заголовок = СокрЛП(Форма.Ссылка) + " - Развозка";
	
	СкладОМ = Справочники.новаСклады.НайтиПоНаименованию("Склад Отдела Магазинов", Истина);
	СкладОС = Справочники.новаСклады.НайтиПоНаименованию("Основной склад", Истина);
	
	тзРазвозка = Новый ТаблицаЗначений;
	тзРазвозка.Колонки.Добавить("Номер");
	тзРазвозка.Колонки.Добавить("ОМ");
	тзРазвозка.Колонки.Добавить("ОС");
	тзРазвозка.Колонки.Добавить("Контрагент");
	тзРазвозка.Колонки.Добавить("Адрес");
	тзРазвозка.Колонки.Добавить("Сумма");
	тзРазвозка.Колонки.Добавить("Комментарий");
	тзРазвозка.Колонки.Добавить("Коробки");
	тзРазвозка.Колонки.Добавить("ПеревезеноПалетт");
	
	мсЗадания = ИсточникМаршрута.ПолучитьДоставкиВПорядкеВыполнения(Форма.Ссылка);
	Для Каждого Задание Из мсЗадания Цикл
		стрРазвозка = тзРазвозка.Добавить();
		стрРазвозка.Номер = Задание.Номер; 
		стрРазвозка.ОС = ?(Задание.Грузоотправитель = СкладОС, 1, 0);
		стрРазвозка.Контрагент = Задание.ОсновнойКонтрагент;
		стрРазвозка.Адрес = Задание.ОсновнаяТочка;
		стрРазвозка.Сумма = Задание.Груз.Стоимость;
		стрРазвозка.Комментарий = Задание.Комментарий + ?(ПустаяСтрока(Задание.Груз.Описание), "", " " + Задание.Груз.Описание);
		стрРазвозка.Коробки = Задание.Груз.КоличествоЕдиницПеревозки;
		стрРазвозка.ПеревезеноПалетт = Задание.Груз.ПеревезеноПалетт;
	КонецЦикла;
	
	Макет = ПолучитьМакет("ФормаРазвозки");
	
	Шапка = Макет.ПолучитьОбласть("Шапка");
	Шапка.Параметры.Водитель = Владелец.Водитель;
	Шапка.Параметры.Экспедитор = Владелец.Экспедитор;
	Шапка.Параметры.Дата = Владелец.ДатаНачала;
	
	ТабличныйДокумент.Вывести(Шапка);
	
	ИтогоСумма = 0;
	ИтогоКоробки = 0;
	
	Строка = Макет.ПолучитьОбласть("Строка");
	Для Каждого стрРазвозка Из тзРазвозка Цикл
		Контрагент = стрРазвозка.Контрагент;
		стрРазвозка.Контрагент = СокрЛП(Контрагент.Наименование);
		
		Адрес = стрРазвозка.Адрес;
		стрРазвозка.Адрес = СокрЛП(Адрес.Наименование);
		
		Строка.Параметры.Заполнить(стрРазвозка);
		ТабличныйДокумент.Вывести(Строка);
		
		ИтогоСумма = ИтогоСумма + стрРазвозка.Сумма;
		ИтогоКоробки = ИтогоКоробки + стрРазвозка.Коробки;
	КонецЦикла;
	
	Подвал = Макет.ПолучитьОбласть("Подвал");
	Подвал.Параметры.Сумма = ИтогоСумма;
	Подвал.Параметры.Коробки = ИтогоКоробки;
	ТабличныйДокумент.Вывести(Подвал);
	
	// Зададим параметры макета
	ТабличныйДокумент.ПолеСверху              = 0;
	ТабличныйДокумент.ПолеСлева               = 0;
	ТабличныйДокумент.ПолеСнизу               = 0;
	ТабличныйДокумент.ПолеСправа              = 0;
	ТабличныйДокумент.РазмерКолонтитулаСверху = 0;
	ТабличныйДокумент.РазмерКолонтитулаСнизу  = 0;
	ТабличныйДокумент.АвтоМасштаб             = Истина;
	ТабличныйДокумент.ОриентацияСтраницы      = ОриентацияСтраницы.Ландшафт;
	ТабличныйДокумент.ИмяПараметровПечати = "ПараметрыПечатиФормыРазвозки";
	
	Возврат Истина;
КонецФункции

СвойстваПечатнойФормы = Новый Структура("Представление", "Развозка");