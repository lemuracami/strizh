
&НаСервере
Функция ПолучитьИмяОтбора(ИдентификаторПользовательскойНастройки, КоллекцияЭлементовОтбора)
	
	Для Каждого ЭлементКоллекции Из КоллекцияЭлементовОтбора Цикл
		Если ЭлементКоллекции.ИдентификаторПользовательскойНастройки = ИдентификаторПользовательскойНастройки Тогда
			Возврат ЭлементКоллекции.ЛевоеЗначение;
		КонецЕсли;
	КонецЦикла;
	
КонецФункции

&НаСервере
Функция ПолучитьПараметрыОтчетаПоЗаказам()
	
	ПользовательскиеНастройкиИсточник = Отчет.КомпоновщикНастроек.ПользовательскиеНастройки;
	КоллекцияЭлементовОтбораИсточник = Отчет.КомпоновщикНастроек.Настройки.Отбор.Элементы;
	
	КомпоновщикНастроек = Отчеты.зпСогласованиеЧасовРаботПоЗаказам.Создать().КомпоновщикНастроек;
	
	ПользовательскиеНастройки = КомпоновщикНастроек.ПользовательскиеНастройки;
	КоллекцияЭлементовОтбора = КомпоновщикНастроек.Настройки.Отбор.Элементы;
	
	ТипЗначениеПараметраНастроекКомпоновкиДанных = Тип("ЗначениеПараметраНастроекКомпоновкиДанных");
	ТипЭлементОтбораКомпоновкиДанных = Тип("ЭлементОтбораКомпоновкиДанных");
	
	Для Каждого ЭлементНастройки Из ПользовательскиеНастройки.Элементы Цикл
		ТипЭлемента = ТипЗнч(ЭлементНастройки);
		Если ТипЭлемента = ТипЗначениеПараметраНастроекКомпоновкиДанных Тогда
			//простое копирование
			ИмяПараметра = Строка(ЭлементНастройки.Параметр);
			Для Каждого ЭлементНастройкиИсточник Из ПользовательскиеНастройкиИсточник.Элементы Цикл
				Если ТипЗнч(ЭлементНастройкиИсточник) = ТипЗначениеПараметраНастроекКомпоновкиДанных Тогда
					Если ИмяПараметра = Строка(ЭлементНастройкиИсточник.Параметр) Тогда
						ЭлементНастройки.Значение = ЭлементНастройкиИсточник.Значение;
						ЭлементНастройки.Использование = ЭлементНастройкиИсточник.Использование;
						Прервать;
					КонецЕсли;
				КонецЕсли;
			КонецЦикла;
		ИначеЕсли ТипЭлемента = ТипЭлементОтбораКомпоновкиДанных Тогда
			//сложное копирование
			ИмяОтбора = ПолучитьИмяОтбора(ЭлементНастройки.ИдентификаторПользовательскойНастройки, КоллекцияЭлементовОтбора);
			Для Каждого ЭлементНастройкиИсточник Из ПользовательскиеНастройкиИсточник.Элементы Цикл
				Если ТипЗнч(ЭлементНастройкиИсточник) = ТипЭлементОтбораКомпоновкиДанных Тогда
					Если ИмяОтбора = ПолучитьИмяОтбора(ЭлементНастройкиИсточник.ИдентификаторПользовательскойНастройки, КоллекцияЭлементовОтбораИсточник) Тогда
						ЭлементНастройки.ПравоеЗначение = ЭлементНастройкиИсточник.ПравоеЗначение;
						ЭлементНастройки.ВидСравнения = ЭлементНастройкиИсточник.ВидСравнения;
						ЭлементНастройки.Использование = ЭлементНастройкиИсточник.Использование;
						Прервать;
					КонецЕсли;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Новый Структура("ПользовательскиеНастройки,СформироватьПриОткрытии", ПользовательскиеНастройки, Истина);
	
КонецФункции


&НаКлиенте
Процедура ОтчетПоЗаказам(Команда)
	
	ОткрытьФорму("Отчет.зпСогласованиеЧасовРаботПоЗаказам.ФормаОбъекта", ПолучитьПараметрыОтчетаПоЗаказам());
	
КонецПроцедуры
