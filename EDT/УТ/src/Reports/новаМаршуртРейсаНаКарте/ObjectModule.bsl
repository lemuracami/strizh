
// Предопределенное свойство. Содержит коллекцию свойств печатной формы.
//
Перем СвойстваПечатнойФормы Экспорт; // <Структура>

Перем КартинкаКарты Экспорт;

Перем КСЛ;

Процедура ПодключитьКСЛ()
	#Если Клиент Тогда
		КСЛ = новаСерверКСЛ.Подключиться(новаКонтекст);
	#Иначе
		КСЛ = новаСерверКСЛ.Подключиться();
	#КонецЕсли
КонецПроцедуры


// Предопределенная функция. Формирует печатную форму в указанном документе.
//
// Параметры:
//  Форма             <Форма>
//  ТабличныйДокумент <ТабличныйДокумент>
//  Заголовок         <Строка>
//
// Возвращаемое значение:
//  <Булево>: Истина, если печатную форму удалось сформировать.
//
Функция ВывестиНаПечать(Форма, ТабличныйДокумент, Заголовок) Экспорт
	Рейс = Форма.Ссылка;
	
	Заголовок = СокрЛП(Рейс) + " - маршрут";
	
	Макет = ПолучитьМакет("РейсНаКарте");
	
	Шапка = Макет.ПолучитьОбласть("Шапка");
	Шапка.Параметры.Рейс = Владелец;
	Шапка.Параметры.Водитель = Владелец.Водитель;
	//Шапка.Параметры.Экспедитор = Владелец.Экспедитор;
	Шапка.Параметры.Выезд = Владелец.ДатаНачала;
	
	ТабличныйДокумент.Вывести(Шапка);
	
	ОбластьКарты = Макет.ПолучитьОбласть("ОбластьКарты");
	
	Если КартинкаКарты = Неопределено Тогда
		
		ПодключитьКСЛ();
		
		ПолеКарты = КСЛ.ГрафическийИнтерфейс_Карты_ПолеКарты();
		ПолеКарты.Анимировать = Ложь;
		
		РейсНаКарте = КСЛ.Доставка_Карты_ОтображениеРейса();
		РейсНаКарте.Рейс.Ид = СокрЛП(Рейс.УникальныйИдентификатор());
		РейсНаКарте.ОтображатьФактическийТрек = Ложь;
		РейсНаКарте.ОтображатьТекущееПоложениеТранспорта = Ложь;
			
		ПолеКарты.ДобавитьОбъект(РейсНаКарте);
		
		ПолеКарты.УстановитьОптимальныйМасштаб();
		
		ШиринаКартинки = Цел(ОбластьКарты.Рисунки.Карта.Ширина * 96 / 25.4);
		ВысотаКартинки = Цел(ОбластьКарты.Рисунки.Карта.Высота * 96 / 25.4);
		
		ОбластьКарты.Рисунки.Карта.Картинка = Новый Картинка(Base64Значение(ПолеКарты.ПолучитьКартинкуBase64(ШиринаКартинки, ВысотаКартинки)), Истина);
	Иначе
		ОбластьКарты.Рисунки.Карта.Картинка = КартинкаКарты; 
		ОбластьКарты.Рисунки.Карта.РазмерКартинки = РазмерКартинки.РеальныйРазмер;
	КонецЕсли;
	
	ТабличныйДокумент.Вывести(ОбластьКарты);
		
	Подвал = Макет.ПолучитьОбласть("Подвал");
	ТабличныйДокумент.Вывести(Подвал);
	
	// Зададим параметры макета
	ТабличныйДокумент.ПолеСверху              = 0;
	ТабличныйДокумент.ПолеСлева               = 0;
	ТабличныйДокумент.ПолеСнизу               = 0;
	ТабличныйДокумент.ПолеСправа              = 0;
	ТабличныйДокумент.РазмерКолонтитулаСверху = 0;
	ТабличныйДокумент.РазмерКолонтитулаСнизу  = 0;
	ТабличныйДокумент.АвтоМасштаб             = Истина;
	ТабличныйДокумент.ОриентацияСтраницы      = ОриентацияСтраницы.Портрет;
	ТабличныйДокумент.ТолькоПросмотр      	  = Истина;
	ТабличныйДокумент.ИмяПараметровПечати 	  = "ПараметрыПечатиМаршрутаРейсаНаКарте";
	
	Возврат Истина;
КонецФункции

СвойстваПечатнойФормы = Новый Структура("Представление", "Маршрут на карте");