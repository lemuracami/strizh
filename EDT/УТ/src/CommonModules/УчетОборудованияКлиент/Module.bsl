
//CeHbKA #2995 08.04.2019
//Параметры:
//	СтатусОборудования = Перечисления.СтатусыЕдиницыОборудования;
//	СписокОборудования = Массив
//	ТекстВопроса = Строка
Процедура ПоказатьВопросОСменеСтатуса(СтатусОборудования, СписокОборудования, Источник = Неопределено) Экспорт

	СписокОборудованияТекст = "";
	
	Для каждого ТекОборудование Из СписокОборудования Цикл
		СписокОборудованияТекст = СписокОборудованияТекст + "• " + ТекОборудование + Символы.ПС;
	КонецЦикла; 
	
	Параметры = Новый Структура();
	Параметры.Вставить("СтатусОборудования", СтатусОборудования);
	Параметры.Вставить("СписокОборудования", СписокОборудования);
	
	Если Источник <> Неопределено Тогда
		Параметры.Вставить("Источник", Источник);
	КонецЕсли; 
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ПослеОтветаНаВопрос", ЭтотОбъект, Параметры);
	
	ПоказатьВопрос(ОписаниеОповещения,"Установить статус "" "+СтатусОборудования+" "" для оборудования "+Символы.ПС + СписокОборудованияТекст, РежимДиалогаВопрос.ДаНет,,,"Смена статуса оборудования");
	
КонецПроцедуры

Процедура ПоказатьВопросОСменеСтатусаСВыборомРегиона(СтатусОборудования, СписокОборудования, Источник = Неопределено, Регионы) Экспорт

	Параметры = Новый Структура();
	Параметры.Вставить("СтатусОборудования", СтатусОборудования);
	Параметры.Вставить("СписокОборудования", СписокОборудования);
	
	Если Источник <> Неопределено Тогда
		Параметры.Вставить("Источник", Источник);
	КонецЕсли; 
	
	Регионы.ПоказатьВыборЭлемента(Новый ОписаниеОповещения("ВыборРегионаЗавершение", ЭтотОбъект, Параметры), "Регион");
	
КонецПроцедуры

Процедура ВыборРегионаЗавершение(РезультатВыбора, ДополнительныеПараметры) Экспорт
	
	Если РезультатВыбора = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ДополнительныеПараметры.Вставить("Регион", РезультатВыбора.Значение);
	
	СписокОборудования = ДополнительныеПараметры.СписокОборудования;
	
	СписокОборудованияТекст = "";
	
	Для каждого ТекОборудование Из СписокОборудования Цикл
		СписокОборудованияТекст = СписокОборудованияТекст + "• " + ТекОборудование + Символы.ПС;
	КонецЦикла; 
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ПослеОтветаНаВопрос", ЭтотОбъект, ДополнительныеПараметры);
	
	ПоказатьВопрос(ОписаниеОповещения,"Установить статус "" "+ДополнительныеПараметры.СтатусОборудования+" "" для оборудования "+Символы.ПС + СписокОборудованияТекст, РежимДиалогаВопрос.ДаНет,,,"Смена статуса оборудования");
	
КонецПроцедуры

//Параметры:
//	Параметры - Структура
//	Параметры.СтатусОборудования = Перечисления.СтатусыЕдиницыОборудования;
//	Параметры.СписокОборудования = Массив
Процедура ПослеОтветаНаВопрос(Результат, Параметры) Экспорт 

	Если Результат <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли; 
	
	Регион = Неопределено;
	Параметры.Свойство("Регион", Регион);
	
	ДокументСсылка = УчетОборудованияСервер.СоздатьНовыйДокументУстановкаСтатусаОборудования(Параметры.СтатусОборудования, Параметры.СписокОборудования, Регион);
	
	Если НЕ ДокументСсылка.Пустая() Тогда
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Документ "+ДокументСсылка+" успешно создан и проведён";
		Сообщение.Сообщить(); 
	КонецЕсли; 
	
	Если Параметры.Свойство("Источник") Тогда
		УпрФорма = Параметры.Источник;
		//обновим динамический список на форме
		УпрФорма.Элементы.Список.Обновить();
	КонецЕсли; 
	
КонецПроцедуры // ()

Функция НайтиОборудование(Код) Экспорт

	СоответствиеОборудования = УчетОборудованияСервер.ПолучитьСоответствиеОборудованияНаСервере(Код);
	
	Если СоответствиеОборудования.Количество() = 0 Тогда
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Оборудование с кодом "+Код+" не найдено";
		Сообщение.Сообщить(); 
	КонецЕсли;	

	Возврат СоответствиеОборудования;
	
КонецФункции
 
//CeHbKA #2995 08.04.2019
