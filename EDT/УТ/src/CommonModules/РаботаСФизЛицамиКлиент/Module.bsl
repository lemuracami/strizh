
//CeHbKA 09.04.2019 #2989
Функция ВыделитьСлово(ИсходнаяСтрока) Экспорт
	
	Буфер = СокрЛ(ИсходнаяСтрока);
	ПозицияПослПробела = Найти(Буфер, " ");

	Если ПозицияПослПробела = 0 Тогда
		ИсходнаяСтрока = "";
		Возврат Буфер;
	КонецЕсли;
	
	ВыделенноеСлово = СокрЛП(Лев(Буфер, ПозицияПослПробела));
	ИсходнаяСтрока = Сред(ИсходнаяСтрока, ПозицияПослПробела + 1);
	
	Возврат ВыделенноеСлово;
	
КонецФункции

Процедура ЗаполнитьФИО(Форма) Экспорт
	
	ФИО = Строка(Форма.Объект.ФизЛицо);
	
	Форма.Фамилия  = ВыделитьСлово(ФИО);
	Форма.Имя      = ВыделитьСлово(ФИО);
	Форма.Отчество = ВыделитьСлово(ФИО);
	
КонецПроцедуры

Процедура ЗаполнитьПрочиеПараметры(Форма) Экспорт
	
	Форма.НомерЛичногоТелефона 	= Форма.Объект.ФизЛицо.НомерЛичногоТелефона;
	Форма.КомментарийФЛ			= Форма.Объект.ФизЛицо.Комментарий;
	
КонецПроцедуры	

Процедура ЗаполнитьНаименование(Форма) Экспорт
	
	ФИО = НаименованиеФИОВСтруктуру(Форма.Объект.Наименование);
	
	Форма.Объект.Наименование = ФИО.Фамилия + " " + ФИО.Имя + " " + ФИО.Отчество;
	
КонецПроцедуры
 

Функция ПроверитьФИО(Форма) Экспорт
		
	ТекстВопроса = "";
	
	Если ПустаяСтрока(Форма.Отчество) Тогда
		ТекстВопроса = "Отчество не заполнено. Продолжить запись?";
	КонецЕсли;
	
	Если НЕ Форма.Объект.Ссылка.Пустая() И ТекстВопроса = "" Тогда
		
		ФИО = НаименованиеФИОВСтруктуру(Строка(Форма.Объект.ФизЛицо));
		
		Если ФИОНеСовпадает(ФИО, Форма.Фамилия, Форма.Имя, Форма.Отчество) Тогда
			ТекстВопроса = "Есть изменения ФИО. Продолжить запись?";
		КонецЕсли; 
				
	КонецЕсли;
	
	Если ТекстВопроса <> "" Тогда
		
		ОписаниеОповещения = Новый ОписаниеОповещения("ПослеОтветаНаВопросОбИзмененииФИО", ЭтотОбъект, Форма);	
		ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет,, КодВозвратаДиалога.Да, "Проверка записи");
		
		Возврат Истина;
		
	КонецЕсли; 
	
	Форма.ПроверкаФИО = Ложь;
	
	Возврат Ложь;
	
КонецФункции

Функция ФИОНеСовпадает(СтароеФИО, Фамилия, Имя, Отчество) Экспорт

	СтараяФамилия  = СтароеФИО.Фамилия;
	СтароеИмя      = СтароеФИО.Имя;
	СтароеОтчество = СтароеФИО.Отчество;
	
	Возврат (СтароеИмя <> Имя ИЛИ СтараяФамилия <> Фамилия ИЛИ СтароеОтчество <> Отчество);		

КонецФункции // ()
 
Процедура ПослеОтветаНаВопросОбИзмененииФИО(Результат, Параметры) Экспорт
		
	Если Результат <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;	
	
	Форма = Параметры;
	Форма.ПроверкаФИО = Ложь;
	
	Форма.Записать();		
	
КонецПроцедуры

Процедура ФИОПриИзменении(Форма) Экспорт
	
	Форма.ПроверкаФИО		 = Истина;	
	Форма.Модифицированность = Истина;
	
КонецПроцедуры

Функция НаименованиеФИОВСтруктуру(Наименование) Экспорт

	ФИО = Наименование;
	
	СтруктураФИО = Новый Структура();
	
	СтруктураФИО.Вставить("Фамилия", ВыделитьСлово(ФИО));
	СтруктураФИО.Вставить("Имя", ВыделитьСлово(ФИО));
	СтруктураФИО.Вставить("Отчество", ВыделитьСлово(ФИО));		

	Возврат СтруктураФИО;
	
КонецФункции

Функция ПроверитьЗаполнениеПоляФормы(Форма, ИмяРеквизита) Экспорт 
	
	Если НЕ ЗначениеЗаполнено(Форма[ИмяРеквизита]) Тогда
		
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Поле "+ИмяРеквизита+" не заполнено";
		Сообщение.Поле = ИмяРеквизита;
		Сообщение.Сообщить();
		
		Возврат Истина;
		
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

Процедура СообщитьОднофамильцев(МассивОднофамильцев) Экспорт
	
	Для каждого Сотрудник из МассивОднофамильцев Цикл
		
		Если ТипЗнч(Сотрудник) = Тип("СправочникСсылка.новаВодители") Тогда
			ТекстСообщения = "Изменено наименование у водителя: " + Сотрудник;
		КонецЕсли;
		
		Если ТипЗнч(Сотрудник) = Тип("СправочникСсылка.новаЭкспедиторы") Тогда 
			ТекстСообщения = "Изменено наименование у экспедитора: " + Сотрудник;
		КонецЕсли;
		
		//Переварюха В.В. 2019.09.24 11-20 Задача № 3441
		Если ТипЗнч(Сотрудник) = Тип("СправочникСсылка.Сотрудники") Тогда 
			ТекстСообщения = "Изменено наименование у сотрудника: " + Сотрудник;
		КонецЕсли;
		//Переварюха В.В. 2019.09.24 11-20 Задача № 3441
		
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = ТекстСообщения;
		Сообщение.Сообщить(); 
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ДоступностьНаименованияИФИО(Форма) Экспорт
	
	НаименованиеИзменено = Форма.Объект.НаименованиеИзменено;
	ВозможностьРедактированияНаименования = Форма.ВозможностьРедактированияНаименования;
		
	Форма.Элементы.НаименованиеИзменено.Доступность = ВозможностьРедактированияНаименования И (НЕ Форма.Объект.ФизЛицо.Пустая());
	
	Форма.Элементы.Наименование.Доступность = НаименованиеИзменено И ВозможностьРедактированияНаименования;
	
	Форма.Элементы.Фамилия.Доступность 		= НЕ Форма.Элементы.Наименование.Доступность;
	Форма.Элементы.Имя.Доступность 			= НЕ Форма.Элементы.Наименование.Доступность;
	Форма.Элементы.Отчество.Доступность 	= НЕ Форма.Элементы.Наименование.Доступность;
	
КонецПроцедуры

Процедура ПроверитьСменуДатыРождения(ДатаРождения, Форма) Экспорт

	Если ТипЗнч(ДатаРождения) = Тип("Строка") Тогда
		СтрокаДата = СтрЗаменить(ДатаРождения, ".", " ");
		ДеньДаты = РаботаСФизЛицамиКлиент.ВыделитьСлово(СтрокаДата);
		МесяцДаты = РаботаСФизЛицамиКлиент.ВыделитьСлово(СтрокаДата);
		ГодДаты = РаботаСФизЛицамиКлиент.ВыделитьСлово(СтрокаДата);
		ДатаНовая = Дата(ГодДаты, МесяцДаты, ДеньДаты);	
	КонецЕсли; 	

	Форма.ДРПоменялось = Форма.ДатаРождения <> ДатаНовая;

	Форма.Модифицированность = Форма.ДРПоменялось;
	
	Форма.ДатаРождения = ДатаНовая;
	
КонецПроцедуры
 
//CeHbKA 09.04.2019 #2989

//Геннадий 10.06.2020 ->
Процедура ПроверитьСменуТелефона(НомерТелНовый, Форма) Экспорт

	Форма.НомерТелПоменялся 	= Форма.НомерЛичногоТелефона <> НомерТелНовый;
	Форма.Модифицированность 	= Форма.НомерТелПоменялся;
	Форма.НомерЛичногоТелефона  = НомерТелНовый;
	
КонецПроцедуры

Процедура ПроверитьСменуКомментария(КомментарийНовый, Форма) Экспорт

	Форма.КомментарийПоменялся 	= Форма.КомментарийФЛ <> КомментарийНовый;
	Форма.Модифицированность 	= Форма.КомментарийПоменялся;
	Форма.КомментарийФЛ 		= КомментарийНовый;
	
КонецПроцедуры
//Геннадий 10.06.2020 <-
