//Геннадий #4611 05.07.2021 ->
Функция ПолучитьГруппуФизическогоЛица(ФизЛицоФамилия) Экспорт
	
	ПерваяБукваФамилии = НРег(Лев(ФизЛицоФамилия, 1));
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	зпГруппыФизлиц.Ссылка КАК Ссылка,
		|	зпГруппыФизлиц.НачальнаяБукваФамилии КАК НачальнаяБукваФамилии,
		|	зпГруппыФизлиц.КонечнаяБукваФамилии КАК КонечнаяБукваФамилии
		|ИЗ
		|	Справочник.зпГруппыФизлиц КАК зпГруппыФизлиц
		|ГДЕ
		|	НЕ зпГруппыФизлиц.ПометкаУдаления";
	
	РезультатЗапроса = Запрос.Выполнить();
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		 НачальнаяБукваФамилии	= НРег(ВыборкаДетальныеЗаписи.НачальнаяБукваФамилии);
		 КонечнаяБукваФамилии	= НРег(ВыборкаДетальныеЗаписи.КонечнаяБукваФамилии);
		 
		 Если НачальнаяБукваФамилии <= ПерваяБукваФамилии И ПерваяБукваФамилии <= КонечнаяБукваФамилии Тогда
			Возврат ВыборкаДетальныеЗаписи.Ссылка; 
		 КонецЕсли;	 
			 
	КонецЦикла;
	
	Возврат Неопределено; 
	
КонецФункции
//Геннадий <-

Процедура РегламентЗагрузкаДанныхФизЛицИзЗУП() Экспорт
	
	//Асеев 09.02.2023 (Задача № 4985)>>>
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ДокументыУдостоверяющиеЛичность.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.ДокументыУдостоверяющиеЛичность КАК ДокументыУдостоверяющиеЛичность
	|ГДЕ
	|	ДокументыУдостоверяющиеЛичность.КодИМНС = ""21""";
	
	Результат = Запрос.Выполнить();
	Если Результат.Пустой() Тогда
		Возврат;
	КонецЕсли;
	Выборка = Результат.Выбрать();
	Выборка.Следующий();
	ПаспортРФ = Выборка.Ссылка;
	
	Запрос.УстановитьПараметр("ПаспортРФ", ПаспортРФ);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ФизическиеЛица.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ВТ_Физлица
	|ИЗ
	|	Справочник.ФизическиеЛица КАК ФизическиеЛица
	|ГДЕ
	|	ФизическиеЛица.Ссылка В ИЕРАРХИИ (ЗНАЧЕНИЕ(Справочник.ФизическиеЛица.Сотрудники))
	|	И НЕ ФизическиеЛица.ПометкаУдаления
	|	И НЕ ФизическиеЛица.ЭтоГруппа
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РаботникиОрганизации.ФизЛицо КАК ФизЛицо,
	|	МАКСИМУМ(РаботникиОрганизации.Период) КАК Период
	|ПОМЕСТИТЬ ВТ_ДатыПриема
	|ИЗ
	|	ВТ_Физлица КАК ВТ_Физлица
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.РаботникиОрганизации КАК РаботникиОрганизации
	|		ПО ВТ_Физлица.Ссылка = РаботникиОрганизации.ФизЛицо
	|			И (РаботникиОрганизации.Состояние = ЗНАЧЕНИЕ(Перечисление.СостояниеРаботникаОрганизации.ПринятНаРаботуВОрганизацию))
	|
	|СГРУППИРОВАТЬ ПО
	|	РаботникиОрганизации.ФизЛицо
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РаботникиОрганизации.ФизЛицо КАК ФизЛицо,
	|	РаботникиОрганизации.Организация КАК Организация
	|ПОМЕСТИТЬ ВТ_ФизлицаОрганизаций
	|ИЗ
	|	ВТ_ДатыПриема КАК ВТ_ДатыПриема
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.РаботникиОрганизации КАК РаботникиОрганизации
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РаботникиОрганизации КАК РаботникиОрганизации_Уволен
	|			ПО РаботникиОрганизации.Период < РаботникиОрганизации_Уволен.Период
	|				И РаботникиОрганизации.Организация = РаботникиОрганизации_Уволен.Организация
	|				И РаботникиОрганизации.ФизЛицо = РаботникиОрганизации_Уволен.ФизЛицо
	|				И (РаботникиОрганизации_Уволен.Состояние = ЗНАЧЕНИЕ(Перечисление.СостояниеРаботникаОрганизации.Уволен))
	|		ПО ВТ_ДатыПриема.Период = РаботникиОрганизации.Период
	|			И ВТ_ДатыПриема.ФизЛицо = РаботникиОрганизации.ФизЛицо
	|			И (РаботникиОрганизации.Состояние = ЗНАЧЕНИЕ(Перечисление.СостояниеРаботникаОрганизации.ПринятНаРаботуВОрганизацию))
	|ГДЕ
	|	РаботникиОрганизации_Уволен.Период ЕСТЬ NULL
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПаспортныеДанныеФизЛицСрезПоследних.ФизЛицо КАК ФизЛицо,
	|	ПаспортныеДанныеФизЛицСрезПоследних.ДокументСерия КАК Серия,
	|	ПаспортныеДанныеФизЛицСрезПоследних.ДокументНомер КАК Номер,
	|	ПаспортныеДанныеФизЛицСрезПоследних.ДокументДатаВыдачи КАК ДатаВыдачи,
	|	ПаспортныеДанныеФизЛицСрезПоследних.ДокументКемВыдан КАК КемВыдан,
	|	ПаспортныеДанныеФизЛицСрезПоследних.ДокументКодПодразделения КАК КодПодразделения,
	|	ПаспортныеДанныеФизЛицСрезПоследних.ДатаРегистрацииПоМестуЖительства КАК ДатаРегистрацииПоМестуЖительства
	|ПОМЕСТИТЬ ВТ_ПаспортныеДанные
	|ИЗ
	|	РегистрСведений.ПаспортныеДанныеФизЛиц.СрезПоследних(
	|			,
	|			ФизЛицо В
	|				(ВЫБРАТЬ
	|					ВТ_ФизлицаОрганизаций.ФизЛицо КАК ФизЛицо
	|				ИЗ
	|					ВТ_ФизлицаОрганизаций КАК ВТ_ФизлицаОрганизаций)) КАК ПаспортныеДанныеФизЛицСрезПоследних
	|ГДЕ
	|	ПаспортныеДанныеФизЛицСрезПоследних.ДокументВид = &ПаспортРФ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ФизлицаОрганизаций.ФизЛицо КАК ФизЛицо,
	|	ВТ_ФизлицаОрганизаций.ФизЛицо.Имя КАК ФизЛицоИмя,
	|	ВТ_ФизлицаОрганизаций.ФизЛицо.Фамилия КАК ФизЛицоФамилия,
	|	ВТ_ФизлицаОрганизаций.ФизЛицо.Отчество КАК ФизЛицоОтчество,
	|	ВТ_ФизлицаОрганизаций.ФизЛицо.ДатаРождения КАК ФизЛицоДатаРождения,
	|	ИсточникиДанныхБазы1С.Ссылка КАК БазаИсточник,
	|	ЕСТЬNULL(ВТ_ПаспортныеДанные.Серия, """") КАК ДокументСерия,
	|	ЕСТЬNULL(ВТ_ПаспортныеДанные.Номер, """") КАК ДокументНомер,
	|	ЕСТЬNULL(ВТ_ПаспортныеДанные.ДатаВыдачи, ДАТАВРЕМЯ(1, 1, 1)) КАК ДокументДатаВыдачи,
	|	ЕСТЬNULL(ВТ_ПаспортныеДанные.КемВыдан, """") КАК ДокументКемВыдан,
	|	ЕСТЬNULL(ВТ_ПаспортныеДанные.КодПодразделения, """") КАК ДокументКодПодразделения,
	|	ЕСТЬNULL(ВТ_ПаспортныеДанные.ДатаРегистрацииПоМестуЖительства, ДАТАВРЕМЯ(1, 1, 1)) КАК ДатаРегистрацииПоМестуЖительства
	|ИЗ
	|	ВТ_ФизлицаОрганизаций КАК ВТ_ФизлицаОрганизаций
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ИсточникиДанныхБазы1С КАК ИсточникиДанныхБазы1С
	|		ПО ВТ_ФизлицаОрганизаций.Организация = ИсточникиДанныхБазы1С.Владелец
	|			И (ИсточникиДанныхБазы1С.ТипКонфигурации = ЗНАЧЕНИЕ(Перечисление.ТипыКонфигураций1С.ЗарплатаИУправлениеПерсоналом_3_1))
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ПаспортныеДанные КАК ВТ_ПаспортныеДанные
	|		ПО ВТ_ФизлицаОрганизаций.ФизЛицо = ВТ_ПаспортныеДанные.ФизЛицо
	|ИТОГИ ПО
	|	БазаИсточник";
	
	Результат = Запрос.Выполнить();
	Если Результат.Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапросаЗУП =
	"ВЫБРАТЬ
	|	ДокументыФизическихЛицСрезПоследних.Физлицо.Имя КАК ФизЛицоИмя,
	|	ДокументыФизическихЛицСрезПоследних.Физлицо.Фамилия КАК ФизЛицоФамилия,
	|	ДокументыФизическихЛицСрезПоследних.Физлицо.Отчество КАК ФизЛицоОтчество,
	|	ДокументыФизическихЛицСрезПоследних.Физлицо.ДатаРождения КАК ФизЛицоДатаРождения,
	|	ДокументыФизическихЛицСрезПоследних.Серия КАК ДокументСерия,
	|	ДокументыФизическихЛицСрезПоследних.Номер КАК ДокументНомер,
	|	ДокументыФизическихЛицСрезПоследних.ДатаВыдачи КАК ДокументДатаВыдачи,
	|	ДокументыФизическихЛицСрезПоследних.КемВыдан КАК ДокументКемВыдан,
	|	ДокументыФизическихЛицСрезПоследних.КодПодразделения КАК ДокументКодПодразделения,
	|	ДокументыФизическихЛицСрезПоследних.Физлицо.ДатаРегистрации КАК ДатаРегистрацииПоМестуЖительства
	|ИЗ
	|	РегистрСведений.ДокументыФизическихЛиц.СрезПоследних(, ВидДокумента = ЗНАЧЕНИЕ(Справочник.ВидыДокументовФизическихЛиц.ПаспортРФ)) КАК ДокументыФизическихЛицСрезПоследних";
	
	Сейчас = ТекущаяДата();
	
	ВыборкаБаза = Результат.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаБаза.Следующий() Цикл
		
		СтруктураCOM = ПлатежиКомитентамСервер.ПолучитьСоединениеСБазой(ВыборкаБаза.БазаИсточник);
		
		Если СтруктураCOM = Неопределено Тогда
			Сообщить("Не удалось установить соединение для организации " + ВыборкаБаза.БазаИсточник.Владелец);
			Продолжить;
		КонецЕсли;
		
		Соединение = СтруктураCOM.Соединение;
		БазаЗуп = СтруктураCOM.БазаБух;
		
		Запрос = БазаЗуп.NewObject("Запрос");
		Запрос.Текст = ТекстЗапросаЗУП;
		ТаблицаЗУП = Запрос.Выполнить().Выгрузить();
		Для Каждого СтрокаЗУП Из ТаблицаЗУП Цикл
			СтрокаЗУП.ФизЛицоИмя = СокрЛП(СтрокаЗУП.ФизЛицоИмя);
			СтрокаЗУП.ФизЛицоФамилия = СокрЛП(СтрокаЗУП.ФизЛицоФамилия);
			СтрокаЗУП.ФизЛицоОтчество = СокрЛП(СтрокаЗУП.ФизЛицоОтчество);
		КонецЦикла;
		ТаблицаЗУП.Индексы.Добавить("ФизЛицоИмя,ФизЛицоФамилия,ФизЛицоОтчество,ФизЛицоДатаРождения");
		
		СтруктураПоискаФизЛица = БазаЗуп.NewObject("Структура", "ФизЛицоИмя,ФизЛицоФамилия,ФизЛицоОтчество,ФизЛицоДатаРождения");
		
		Выборка = ВыборкаБаза.Выбрать();
		Пока Выборка.Следующий() Цикл
			//ЗаполнитьЗначенияСвойств(СтруктураПоискаФизЛица, Выборка);
			СтруктураПоискаФизЛица.ФизЛицоИмя = СокрЛП(Выборка.ФизЛицоИмя);
			СтруктураПоискаФизЛица.ФизЛицоФамилия = СокрЛП(Выборка.ФизЛицоФамилия);
			СтруктураПоискаФизЛица.ФизЛицоОтчество = СокрЛП(Выборка.ФизЛицоОтчество);
			СтруктураПоискаФизЛица.ФизЛицоДатаРождения = Выборка.ФизЛицоДатаРождения;
			
			СтрокиЗУП = ТаблицаЗУП.НайтиСтроки(СтруктураПоискаФизЛица);
			Если СтрокиЗУП.Количество() Тогда
				СтрокаЗУП = СтрокиЗУП.Получить(0);
				Если Выборка.ДокументСерия = СтрокаЗУП.ДокументСерия И Выборка.ДокументНомер = СтрокаЗУП.ДокументНомер И Выборка.ДокументДатаВыдачи = СтрокаЗУП.ДокументДатаВыдачи
					И Выборка.ДокументКемВыдан = СтрокаЗУП.ДокументКемВыдан И Выборка.ДокументКодПодразделения = СтрокаЗУП.ДокументКодПодразделения И Выборка.ДатаРегистрацииПоМестуЖительства = СтрокаЗУП.ДатаРегистрацииПоМестуЖительства Тогда
					Продолжить;
				КонецЕсли;
				ЗаписьРегистра = РегистрыСведений.ПаспортныеДанныеФизЛиц.СоздатьМенеджерЗаписи();
				ЗаполнитьЗначенияСвойств(ЗаписьРегистра, СтрокаЗУП);
				ЗаписьРегистра.Период = Сейчас;
				ЗаписьРегистра.ФизЛицо = Выборка.ФизЛицо;
				ЗаписьРегистра.ДокументВид = ПаспортРФ;
				ЗаписьРегистра.Записать();
				//Сообщить("Записано");
			КонецЕсли;
		КонецЦикла;
		
		Соединение = Неопределено;
		БазаЗуп = Неопределено;
		СтруктураCOM = Неопределено;
		
	КонецЦикла;
	//Асеев 09.02.2023 (Задача № 4985)<<<
	
КонецПроцедуры	