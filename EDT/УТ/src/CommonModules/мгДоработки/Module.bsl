
Функция ПолучитьТекущийТариф(Дата, НачислениеУдержание) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	мгТарифыНадбавкиСрезПоследних.Тариф
	|ИЗ
	|	РегистрСведений.мгТарифыНадбавки.СрезПоследних(&Период, Начисление = &Начисление) КАК мгТарифыНадбавкиСрезПоследних";
	Запрос.УстановитьПараметр("Период", Дата);
	Запрос.УстановитьПараметр("Начисление", НачислениеУдержание);
	Результат = Запрос.Выполнить();
	Если не Результат.Пустой() Тогда
		Выборка = Результат.Выбрать();
		Выборка.Следующий();
		Возврат Выборка.Тариф;
	Иначе 
		Возврат 0
	КонецЕсли;
	
КонецФункции

//Полигон    ТаблицаЗначений: НомерСтроки, Х, У
//Точки        ТаблицаЗначений: НомерСтроки, Х, У
Функция ПроверитьТочкиЗапросом(Полигон, Точки) Экспорт
    Если Полигон.Количество() < 3 Тогда
        Возврат Ложь;
    КонецЕсли;
    
    Запрос = Новый Запрос;
    Запрос.УстановитьПараметр("Полигон", Полигон);
    Запрос.УстановитьПараметр("РазмерПолигона", Полигон.Количество());
    Запрос.УстановитьПараметр("Точки", Точки);
    Запрос.Текст =
        "ВЫБРАТЬ
        |    Полигон.НомерСтроки КАК НомерТочкиПолигона,
        |    Полигон.Х,
        |    Полигон.У
        |ПОМЕСТИТЬ Полигон
        |ИЗ
        |    &Полигон КАК Полигон
        |;
        |
        |////////////////////////////////////////////////////////////////////////////////
        |ВЫБРАТЬ
        |    Точки.НомерСтроки КАК НаименованиеТочки,
        |    Точки.Х КАК Х,
        |    Точки.У КАК У
        |ПОМЕСТИТЬ Точки
        |ИЗ
        |    &Точки КАК Точки
        |;
        |
        |////////////////////////////////////////////////////////////////////////////////
        |ВЫБРАТЬ
        |    Точки.НаименованиеТочки,
        |    Точки.Х КАК Точка_Х,
        |    Точки.У КАК Точка_У,
        |    Полигон.НомерТочкиПолигона,
        |    Полигон.Х КАК Полигон_Х,
        |    Полигон.У КАК Полигон_У,
        |    Полигон.Х - Точки.Х КАК Разница_Х,
        |    Полигон.У - Точки.У КАК Разница_У,
        |    ВЫБОР
        |        КОГДА Полигон.Х - Точки.Х < 0
        |                И Полигон.У - Точки.У < 0
        |            ТОГДА 2
        |        КОГДА Полигон.Х - Точки.Х < 0
        |                И Полигон.У - Точки.У >= 0
        |            ТОГДА 1
        |        КОГДА Полигон.Х - Точки.Х >= 0
        |                И Полигон.У - Точки.У < 0
        |            ТОГДА 3
        |        КОГДА Полигон.Х - Точки.Х >= 0
        |                И Полигон.У - Точки.У >= 0
        |            ТОГДА 0
        |    КОНЕЦ КАК К
        |ПОМЕСТИТЬ Таб
        |ИЗ
        |    Точки КАК Точки,
        |    Полигон КАК Полигон
        |
        |ИНДЕКСИРОВАТЬ ПО
        |    Точки.НаименованиеТочки,
        |    Полигон.НомерТочкиПолигона
        |;
        |
        |////////////////////////////////////////////////////////////////////////////////
        |ВЫБРАТЬ
        |    Таб.НаименованиеТочки,
        |    Таб.Точка_Х,
        |    Таб.Точка_У,
        |    Таб.НомерТочкиПолигона,
        |    Таб.Полигон_Х,
        |    Таб.Полигон_У,
        |    Таб.К КАК К,
        |    ТабПред.К КАК ПредК,
        |    Таб.К - ТабПред.К КАК ДельтаКу,
        |    ВЫБОР
        |        КОГДА Таб.К - ТабПред.К = -3
        |            ТОГДА 1
        |        КОГДА Таб.К - ТабПред.К = 3
        |            ТОГДА -1
        |        КОГДА Таб.К - ТабПред.К = -2
        |                И ТабПред.Разница_Х * Таб.Разница_У >= ТабПред.Разница_У * Таб.Разница_Х
        |            ТОГДА 1
        |        КОГДА Таб.К - ТабПред.К = 2
        |                И НЕ ТабПред.Разница_Х * Таб.Разница_У >= ТабПред.Разница_У * Таб.Разница_Х
        |            ТОГДА -1
        |        ИНАЧЕ 0
        |    КОНЕЦ КАК Результат
        |ПОМЕСТИТЬ Развернуто
        |ИЗ
        |    Таб КАК Таб
        |        ВНУТРЕННЕЕ СОЕДИНЕНИЕ Таб КАК ТабПред
        |        ПО Таб.НаименованиеТочки = ТабПред.НаименованиеТочки
        |            И (Таб.НомерТочкиПолигона = ТабПред.НомерТочкиПолигона + 1
        |                ИЛИ Таб.НомерТочкиПолигона = 1
        |                    И ТабПред.НомерТочкиПолигона = &РазмерПолигона)
        |;
        |
        |////////////////////////////////////////////////////////////////////////////////
        |ВЫБРАТЬ
        |    Развернуто.НаименованиеТочки,
        |    Развернуто.Точка_Х КАК Х,
        |    Развернуто.Точка_У КАК У,
        |    ВЫБОР
        |        КОГДА СУММА(Развернуто.Результат) = 0
        |            ТОГДА ЛОЖЬ
        |        ИНАЧЕ ИСТИНА
        |    КОНЕЦ КАК Результат
        |ИЗ
        |    Развернуто КАК Развернуто
        |
        |СГРУППИРОВАТЬ ПО
        |    Развернуто.НаименованиеТочки,
        |    Развернуто.Точка_Х,
        |    Развернуто.Точка_У";
    Возврат Запрос.Выполнить().Выгрузить();
КонецФункции

Функция ПолучитьОрганизациюСотрудника(Сотрудник) Экспорт
	Запрос = Новый Запрос;
	Запрос.Текст = 	
	"ВЫБРАТЬ
	|	РаботникиОрганизации.Сотрудник КАК Сотрудник,
	|	МАКСИМУМ(РаботникиОрганизации.Период) КАК Период
	|ПОМЕСТИТЬ вр_РаботникиОрганизации
	|ИЗ
	|	РегистрСведений.РаботникиОрганизации КАК РаботникиОрганизации
	|ГДЕ
	|	РаботникиОрганизации.Сотрудник = &Сотрудник
	|
	|СГРУППИРОВАТЬ ПО
	|	РаботникиОрганизации.Сотрудник
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	вр_РаботникиОрганизации.Сотрудник КАК Сотрудник,
	|	вр_РаботникиОрганизации.Период КАК Период,
	|	РаботникиОрганизации.Организация КАК Организация,
	|	РаботникиОрганизации.Состояние КАК Состояние,
	|	РаботникиОрганизации.Должность КАК Должность
	|ПОМЕСТИТЬ втРаботники
	|ИЗ
	|	вр_РаботникиОрганизации КАК вр_РаботникиОрганизации
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РаботникиОрганизации КАК РаботникиОрганизации
	|		ПО вр_РаботникиОрганизации.Сотрудник = РаботникиОрганизации.Сотрудник
	|			И вр_РаботникиОрганизации.Период = РаботникиОрганизации.Период
	|ГДЕ
	|	НЕ РаботникиОрганизации.Регистратор ССЫЛКА Документ.УвольнениеСРаботыИзОрганизация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МетодикаРасчетаЗППоФизЛицамСрезПоследних.Оклад,
	|	МетодикаРасчетаЗППоФизЛицамСрезПоследних.ОкладСумма,
	|	МетодикаРасчетаЗППоФизЛицамСрезПоследних.ФизЛицо КАК ФизЛицо
	|ПОМЕСТИТЬ втОклад
	|ИЗ
	|	РегистрСведений.МетодикаРасчетаЗППоФизЛицам.СрезПоследних(, ФизЛицо = &Сотрудник) КАК МетодикаРасчетаЗППоФизЛицамСрезПоследних
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ФизЛицо
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втРаботники.Организация,
	|	втОклад.Оклад,
	|	втОклад.ОкладСумма
	|ИЗ
	|	втРаботники КАК втРаботники
	|		ЛЕВОЕ СОЕДИНЕНИЕ втОклад КАК втОклад
	|		ПО втРаботники.Сотрудник = втОклад.ФизЛицо";
	Запрос.УстановитьПараметр("Сотрудник", Сотрудник);
	//Запрос.УстановитьПараметр("Уволен", ПредопределенноеЗначение("Перечисление.СостояниеРаботникаОрганизации.Уволен"));
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	СтруктураДанных = Новый Массив;
	Пока Выборка.Следующий() Цикл
		СтруктураДанных.Добавить(Новый Структура("Организация, ОкладСумма", Выборка.Организация, Выборка.ОкладСумма));
	КонецЦикла;
	
	Возврат СтруктураДанных;
	
КонецФункции