
Процедура ФормированиеАПП() Экспорт
	
	//Асеев 30.09.2024 (Задача № 5329)>>>
	//Асеев 30.09.2024 (Задача № 5329)<<<
	Сейчас = ТекущаяДата();
	
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Сегодня", НачалоДня(Сейчас));
	Запрос.УстановитьПараметр("ОкончаниеСегодня", КонецДня(Сейчас));
	Запрос.УстановитьПараметр("НачалоПериода", Сейчас - 300);//-5 мин
	Запрос.УстановитьПараметр("ОкончаниеПериода", Сейчас + 300);//+5 мин
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВЗ_Контрагенты.Контрагент КАК Контрагент,
	|	ВЗ_Контрагенты.Терминал КАК Терминал
	|ПОМЕСТИТЬ ВТ_Контрагенты
	|ИЗ
	|	(ВЫБРАТЬ
	|		ПараметрыКонтрагентовСрезПоследних.Контрагент КАК Контрагент,
	|		РегиональныеТерминалы.Ссылка КАК Терминал
	|	ИЗ
	|		РегистрСведений.ПараметрыКонтрагентов.СрезПоследних КАК ПараметрыКонтрагентовСрезПоследних
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.РегиональныеТерминалы КАК РегиональныеТерминалы
	|			ПО (ПараметрыКонтрагентовСрезПоследних.ФормироватьАПП)
	|				И (ПараметрыКонтрагентовСрезПоследних.ВремяФормированияАПП > ДАТАВРЕМЯ(1, 1, 1))
	|				И (ДОБАВИТЬКДАТЕ(&Сегодня, СЕКУНДА, РАЗНОСТЬДАТ(ДАТАВРЕМЯ(1, 1, 1), ПараметрыКонтрагентовСрезПоследних.ВремяФормированияАПП, СЕКУНДА)) МЕЖДУ &НачалоПериода И &ОкончаниеПериода)
	|				И (НЕ РегиональныеТерминалы.ПометкаУдаления)) КАК ВЗ_Контрагенты
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПриходЗаказовНаСклад КАК ПриходЗаказовНаСклад
	|		ПО (ПриходЗаказовНаСклад.Дата МЕЖДУ &Сегодня И &ОкончаниеСегодня)
	|			И ВЗ_Контрагенты.Контрагент = ПриходЗаказовНаСклад.Контрагент
	|			И ВЗ_Контрагенты.Терминал = ПриходЗаказовНаСклад.ТерминалПриема
	|			И (ПриходЗаказовНаСклад.Основание = ЗНАЧЕНИЕ(Перечисление.ТипыЗагрузкиДанных.Автоформирование))
	|			И (НЕ ПриходЗаказовНаСклад.ПометкаУдаления)
	|ГДЕ
	|	ПриходЗаказовНаСклад.Дата ЕСТЬ NULL
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_Контрагенты.Контрагент КАК Контрагент,
	|	ВТ_Контрагенты.Терминал КАК Терминал,
	|	РеализацияТоваровУслуг.Ссылка КАК Заказ
	|ПОМЕСТИТЬ ВТ_Заказы
	|ИЗ
	|	ВТ_Контрагенты КАК ВТ_Контрагенты
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
	|		ПО (РеализацияТоваровУслуг.ДатаПриемки МЕЖДУ &Сегодня И &ОкончаниеСегодня)
	|			И ВТ_Контрагенты.Контрагент = РеализацияТоваровУслуг.ВладелецТовара
	|			И ВТ_Контрагенты.Терминал = РеализацияТоваровУслуг.ТерминалПриема
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВТ_Заказы.Контрагент КАК Контрагент,
	|	ВТ_Заказы.Терминал КАК Терминал
	|ИЗ
	|	ВТ_Заказы КАК ВТ_Заказы
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_Заказы.Контрагент КАК Контрагент,
	|	ВТ_Заказы.Терминал КАК Терминал,
	|	РеализацияТоваровУслуг.НомерВнешнегоЗаказа КАК ВнешнийНомерЗаказа,
	|	РеализацияТоваровУслуг.Ссылка КАК Заказ,
	|	РеализацияТоваровУслуг.КоличествоМест КАК КоличествоМест
	|ИЗ
	|	ВТ_Заказы КАК ВТ_Заказы
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
	|		ПО ВТ_Заказы.Заказ = РеализацияТоваровУслуг.Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_Заказы.Контрагент КАК Контрагент,
	|	ВТ_Заказы.Терминал КАК Терминал,
	|	РеализацияТоваровУслуг.Ссылка КАК Заказ,
	|	РеализацияТоваровУслуг.НомерВнешнегоЗаказа КАК ВнешнийНомерЗаказа,
	|	РеализацияТоваровУслугМестаЗаказа.ШтрихкодМеста КАК Штрихкод
	|ИЗ
	|	ВТ_Заказы КАК ВТ_Заказы
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
	|		ПО ВТ_Заказы.Заказ = РеализацияТоваровУслуг.Ссылка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.РеализацияТоваровУслуг.МестаЗаказа КАК РеализацияТоваровУслугМестаЗаказа
	|		ПО ВТ_Заказы.Заказ = РеализацияТоваровУслугМестаЗаказа.Ссылка
	|			И (НЕ РеализацияТоваровУслугМестаЗаказа.МестоИсключено)";
	
	РезультатПакета = Запрос.ВыполнитьПакет();
	
	Результат = РезультатПакета[2];
	Если Результат.Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	Выборка = Результат.Выбрать();
	
	ТаблицаЗаказы = РезультатПакета[3].Выгрузить();
	ТаблицаШтрихкоды = РезультатПакета[4].Выгрузить();
	
	СтруктураКонтрагент = Новый Структура("Контрагент,Терминал");
	
	Пока Выборка.Следующий() Цикл
		
		ЗаполнитьЗначенияСвойств(СтруктураКонтрагент, Выборка);
		
		СтрокиЗаказов = ТаблицаЗаказы.НайтиСтроки(СтруктураКонтрагент);
		Если СтрокиЗаказов.Количество() Тогда
			
			ДокументПриход = Документы.ПриходЗаказовНаСклад.СоздатьДокумент();
			ДокументПриход.Контрагент = Выборка.Контрагент;
			ДокументПриход.Основание = Перечисления.ТипыЗагрузкиДанных.Автоформирование;
			ДокументПриход.ДатаВходящегоДокумента = Сейчас;
			ДокументПриход.Дата = Сейчас;
			ДокументПриход.ТерминалПриема = Выборка.Терминал;
			
			Для Каждого СтрокаЗаказа Из СтрокиЗаказов Цикл
				ЗаполнитьЗначенияСвойств(ДокументПриход.Заказы.Добавить(), СтрокаЗаказа);
			КонецЦикла;
			
			СтрокиШтрихкодов = ТаблицаШтрихкоды.НайтиСтроки(СтруктураКонтрагент);
			Для Каждого СтрокаШтрихкода Из СтрокиШтрихкодов Цикл
				ЗаполнитьЗначенияСвойств(ДокументПриход.ШтрихкодыПоМестам.Добавить(), СтрокаШтрихкода);
			КонецЦикла;
			
			ДокументПриход.Записать();
			
			Попытка
				ДокументПриход.Записать(РежимЗаписиДокумента.Проведение);
			Исключение
				pkv.ДобавитьВСписокОтложенногоПроведения(ДокументПриход.Ссылка, "Не удалось провести " + ДокументПриход + " ФормированиеАПП " + ОписаниеОшибки());
			КонецПопытки;
			
		КонецЕсли;
		
	КонецЦикла;
	
	
КонецПроцедуры	