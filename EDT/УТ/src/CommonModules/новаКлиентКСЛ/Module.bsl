
//Замена картинки на форме на ActiveX объект
//
//  Параметры:
//    Форма - <Форма> - форма приёмник ActiveX
//    Панель - <Панель> - панель формы приёмник ActiveX
//    Элемент - <ЭлементУправления> - элемент управления подменяемый картой
//
//  Возвращаемое значение:
//      ActiveX помещённый на формы
//
Функция ИнициализироватьОкноКСЛ(Форма, Панель = Неопределено, Элемент = Неопределено, Регистрировать = Истина) Экспорт
	
	Если Регистрировать Тогда
		новаКонтекст.СборщикМусора.ЗарегистрироватьОбъектТребующийОчистки(Форма);
	КонецЕсли;
	
	Если Элемент = Неопределено Тогда
		Элемент = Форма.ЭлементыФормы.Найти("КонтейнерОкнаУТЛ");
	КонецЕсли;
	Если Элемент = Неопределено Тогда
		Элемент = Форма.ЭлементыФормы.Найти("КонтейнерОкнаКСЛ");
	КонецЕсли;
	
	Попытка
		ОкноКСЛ = Форма.ЭлементыФормы.ДобавитьActiveX("UTL.GUIContainer", "ОкноКСЛ", Истина, Панель);
		ОкноКСЛ.Лево = Элемент.Лево;
		ОкноКСЛ.Верх = Элемент.Верх;
		ОкноКСЛ.Ширина = Элемент.Ширина;
		ОкноКСЛ.Высота = Элемент.Высота;
		
		новаРасширениеФорм.СкопироватьПривязку(ОкноКСЛ, Элемент, ГраницаЭлементаУправления.Право, ГраницаЭлементаУправления.Право);
		новаРасширениеФорм.СкопироватьПривязку(ОкноКСЛ, Элемент, ГраницаЭлементаУправления.Низ, ГраницаЭлементаУправления.Низ);
		новаРасширениеФорм.СкопироватьПривязку(ОкноКСЛ, Элемент, ГраницаЭлементаУправления.Лево, ГраницаЭлементаУправления.Лево);
		новаРасширениеФорм.СкопироватьПривязку(ОкноКСЛ, Элемент, ГраницаЭлементаУправления.Верх, ГраницаЭлементаУправления.Верх);
	Исключение
		//новаОбщиеПроцедуры.СообщитьОКритическойОшибке("И001", ОписаниеОшибки());
		ВызватьИсключение;
	КонецПопытки;
	
	Возврат ОкноКСЛ;
	
КонецФункции

//Замена картинки на форме на ActiveX объект
//
//  Параметры:
//    Форма - <Форма> - форма приёмник ActiveX
//    Панель - <Панель> - панель формы приёмник ActiveX
//    Элемент - <ЭлементУправления> - элемент управления подменяемый картой
//
//  Возвращаемое значение:
//    <Структура>
//      ОкноКСЛ - ActiveX помещённый на формы
//      ФормаКарты - Контейнер содержащий графические объекты КСЛ
//      ПолеКарты - Поле карты
//
Функция ИнициализироватьКарту(Форма, Панель = Неопределено, Элемент = Неопределено) Экспорт
	
	новаКонтекст.СборщикМусора.ЗарегистрироватьОбъектТребующийОчистки(Форма);
	
	Если Элемент = Неопределено Тогда
		Элемент = Форма.ЭлементыФормы.Найти("КонтейнерОкнаУТЛ");
	КонецЕсли;
	Если Элемент = Неопределено Тогда
		Элемент = Форма.ЭлементыФормы.Найти("КонтейнерОкнаКСЛ");
	КонецЕсли;
	
	Попытка
		ОкноКСЛ = Форма.ЭлементыФормы.ДобавитьActiveX("UTL.GUIContainer", "ОкноКСЛ", Истина, Панель);
		ОкноКСЛ.Лево = Элемент.Лево;
		ОкноКСЛ.Верх = Элемент.Верх;
		ОкноКСЛ.Ширина = Элемент.Ширина;
		ОкноКСЛ.Высота = Элемент.Высота;
		
		новаРасширениеФорм.СкопироватьПривязку(ОкноКСЛ, Элемент, ГраницаЭлементаУправления.Право, ГраницаЭлементаУправления.Право);
		новаРасширениеФорм.СкопироватьПривязку(ОкноКСЛ, Элемент, ГраницаЭлементаУправления.Низ, ГраницаЭлементаУправления.Низ);
		новаРасширениеФорм.СкопироватьПривязку(ОкноКСЛ, Элемент, ГраницаЭлементаУправления.Лево, ГраницаЭлементаУправления.Лево);
		новаРасширениеФорм.СкопироватьПривязку(ОкноКСЛ, Элемент, ГраницаЭлементаУправления.Верх, ГраницаЭлементаУправления.Верх);
		
		КСЛ = новаСерверКСЛ.Подключиться(новаКонтекст);
		
		ФормаКарты = КСЛ.ГрафическийИнтерфейс_Форма();
		ФормаКарты.Группа = КСЛ.ГрафическийИнтерфейс_ВертикальнаяГруппа();
		ФормаКарты.Группа.Высота = 1;
		ФормаКарты.Группа.Ширина = 1;
		
		ПолеКарты = КСЛ.ГрафическийИнтерфейс_Карты_ПолеКарты();
		ПолеКарты.Группа = ФормаКарты.Группа;
		
		ОкноКСЛ.Proxy.Форма = ФормаКарты;
		
		Элемент.Видимость = Ложь;
	Исключение
		//новаОбщиеПроцедуры.СообщитьОКритическойОшибке("И001", ОписаниеОшибки());
		ВызватьИсключение;
	КонецПопытки;
	
	Результат = Новый Структура;
	Результат.Вставить("ОкноКСЛ", ОкноКСЛ);
	Результат.Вставить("ФормаКарты", ФормаКарты);
	Результат.Вставить("ПолеКарты", ПолеКарты);
	
	Возврат Результат;
	
КонецФункции

//Замена картинки на сообщение об ошибке и кнопку Повоторить попытку подключения
//
//  Параметры:
//    Действие - действие для кнопки Повторить
//    Форма - <Форма> - форма приёмник элементов управления
//    Панель - <ЭлементыФормы.Панель> - панель приёмник элементов управления
//    Элемент - <ЭлементУправления> - элемент управления подменяемый элементами управления
//
Процедура ПоказатьОшибкуПодключения(Знач ПредставлениеОшибки, Действие, Форма, Панель = Неопределено, Элемент = Неопределено) Экспорт
	
	ПозицияСкобки = Найти(ПредставлениеОшибки, "}");
	Если ПозицияСкобки > 0 Тогда
		ПредставлениеОшибки = Сред(ПредставлениеОшибки, ПозицияСкобки + 3);
	КонецЕсли;
	
	Если Элемент = Неопределено Тогда
		Элемент = Форма.ЭлементыФормы.Найти("КонтейнерОкнаУТЛ");
	КонецЕсли;
	
	Лево = Элемент.Лево + (Элемент.Ширина - 364) /2;
	Лево = Макс(Лево, Элемент.Лево);
	
	Верх = Элемент.Верх + (Элемент.Высота - 64) /2;
	Верх = Макс(Верх, Элемент.Верх);
	
	Картинка = Форма.ЭлементыФормы.Найти("КартинкаОшибки");
	Если Картинка = Неопределено Тогда
		
		Картинка = Форма.ЭлементыФормы.Добавить(Тип("ПолеКартинки"), "КартинкаОшибки", Истина, Панель);
		Картинка.Лево = Лево;
		Картинка.Верх = Верх;
		Картинка.Ширина = 64;
		Картинка.Высота = 64;
		Картинка.Картинка = БиблиотекаКартинок.новаОшибка;
		
		Картинка.УстановитьПривязку(ГраницаЭлементаУправления.Лево, Элемент, ГраницаЭлементаУправления.Центр);
		Картинка.УстановитьПривязку(ГраницаЭлементаУправления.Верх, Элемент, ГраницаЭлементаУправления.Центр);
		Картинка.УстановитьПривязку(ГраницаЭлементаУправления.Право, Картинка, ГраницаЭлементаУправления.Лево);
		Картинка.УстановитьПривязку(ГраницаЭлементаУправления.Низ, Картинка, ГраницаЭлементаУправления.Верх);
	КонецЕсли;
	
	Надпись = Форма.ЭлементыФормы.Найти("ПовторПодключения");
	Если Надпись = Неопределено Тогда
		
		Надпись = Форма.ЭлементыФормы.Добавить(Тип("Надпись"), "ПовторПодключения", Истина, Панель);
		Надпись.Лево = Картинка.Лево + 68;
		Надпись.Верх = Картинка.Верх;
		Надпись.Ширина = Элемент.Лево + Элемент.Ширина - Надпись.Лево;
		Надпись.Высота = 15;
		Надпись.Заголовок = "Подключиться...";
		Надпись.Гиперссылка = Истина;
		Надпись.УстановитьДействие("Нажатие", Действие);
		
		Надпись.УстановитьПривязку(ГраницаЭлементаУправления.Лево, Картинка, ГраницаЭлементаУправления.Лево);
		Надпись.УстановитьПривязку(ГраницаЭлементаУправления.Верх, Картинка, ГраницаЭлементаУправления.Верх);
		Надпись.УстановитьПривязку(ГраницаЭлементаУправления.Право, Элемент, ГраницаЭлементаУправления.Право);
		Надпись.УстановитьПривязку(ГраницаЭлементаУправления.Низ, Надпись, ГраницаЭлементаУправления.Верх);
	КонецЕсли;
	
	Надпись = Форма.ЭлементыФормы.Найти("СообщениеОбОшибке");
	Если Надпись = Неопределено Тогда
		
		Надпись = Форма.ЭлементыФормы.Добавить(Тип("ПолеВвода"), "СообщениеОбОшибке", Истина, Панель);
		Надпись.Лево = Картинка.Лево + 68;
		Надпись.Верх = Картинка.Верх + 19;
		Надпись.Ширина = Элемент.Лево + Элемент.Ширина - Надпись.Лево;
		Надпись.Высота = Элемент.Верх + Элемент.Высота - Надпись.Верх;
		Надпись.МногострочныйРежим  = Истина;
		Надпись.Рамка = Новый Рамка(ТипРамкиЭлементаУправления.БезРамки);
		Надпись.ЦветФонаПоля = WindowsЦвета.ФонОкна;
		Надпись.ТолькоПросмотр = Истина;
		
		Надпись.УстановитьПривязку(ГраницаЭлементаУправления.Лево, Картинка, ГраницаЭлементаУправления.Лево);
		Надпись.УстановитьПривязку(ГраницаЭлементаУправления.Верх, Картинка, ГраницаЭлементаУправления.Верх);
		Надпись.УстановитьПривязку(ГраницаЭлементаУправления.Право, Элемент, ГраницаЭлементаУправления.Право);
		Надпись.УстановитьПривязку(ГраницаЭлементаУправления.Низ, Элемент, ГраницаЭлементаУправления.Низ);
	КонецЕсли;
	
	Надпись.Значение = "Сейчас корпоративный сервер логистики недоступен," + Символы.ПС + "Вы можете попробовать подключиться повторно," + Символы.ПС + "для этого перейдите по ссылке выше.
					   |Ошибка:
					   |" + ПредставлениеОшибки;
	
	Элемент.Видимость = Ложь;

КонецПроцедуры

//удаление сообщение об ошибке и кнопку Повоторить попытку подключения
//
//  Параметры:
//    Форма - <Форма> - форма приёмник элементов управления
//
Процедура СкрытьОшибкуПодключения(Форма) Экспорт
	
	УдаляемыйЭлемент = Форма.ЭлементыФормы.Найти("КартинкаОшибки");
	Если УдаляемыйЭлемент <> Неопределено Тогда
		Форма.ЭлементыФормы.Удалить(УдаляемыйЭлемент);
	КонецЕсли;
	УдаляемыйЭлемент = Форма.ЭлементыФормы.Найти("СообщениеОбОшибке");
	Если УдаляемыйЭлемент <> Неопределено Тогда
		Форма.ЭлементыФормы.Удалить(УдаляемыйЭлемент);
	КонецЕсли;
	УдаляемыйЭлемент = Форма.ЭлементыФормы.Найти("ПовторПодключения");
	Если УдаляемыйЭлемент <> Неопределено Тогда
		Форма.ЭлементыФормы.Удалить(УдаляемыйЭлемент);
	КонецЕсли;

КонецПроцедуры

//Открытие формы настройки карт
//
Процедура НастроитьКарты() Экспорт
	
	КСЛ = новаСерверКСЛ.Подключиться(новаКонтекст);
	ФормаНастройки = КСЛ.Карты_Настройки_Формы_ФормаНастройкиКарт();
	ФормаНастройки.Открыть();
	
КонецПроцедуры

//Установка настроек отображения рейсов
//
Процедура ОбновитьНастройкуРейса() Экспорт
	
	КСЛ = новаСерверКСЛ.Подключиться(новаКонтекст);
	
	Настройка = Константы.новаНастройкаОтображенияРейса.Получить();
	Если ЗначениеЗаполнено(Настройка) Тогда
		КСЛ.Клиент_КлиентУТЛ.НастройкаОтображенияРейса.Установить(Настройка);
	Иначе
		КСЛ.Клиент_КлиентУТЛ.НастройкаОтображенияРейса.УстановитьПоУмолчанию();
	КонецЕсли;
	
КонецПроцедуры
