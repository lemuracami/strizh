
//Асеев 30.09.2021 (Задача № 4665)>>>
Функция СоздатьПВЗ(СтруктураПВЗ) Экспорт
	
	ПунктПВЗ = Справочники.ПунктыПВЗ.НайтиПоРеквизиту("КодАдминки", СтруктураПВЗ.pickupPointID_Ч);
	Если ПунктПВЗ.Пустая() Тогда
		
		ПВЗ = Справочники.ПунктыПВЗ.СоздатьЭлемент();
		ПВЗ.КодАдминки = СтруктураПВЗ.pickupPointID_Ч;
		ПВЗ.КодПВЗ = СтруктураПВЗ.pnt_externalId;
		ПВЗ.Наименование = СтруктураПВЗ.pnt_name;
		ПВЗ.Адрес = СтруктураПВЗ.pnt_address;
		ПВЗ.Владелец = Справочники.Контрагенты.НайтиПоКоду("Shop_" + Формат(СтруктураПВЗ.pnt_shopId_Ч, "ЧН=0; ЧГ="));
		ПВЗ.Записать();
		
		ПунктПВЗ = ПВЗ.Ссылка;
	КонецЕсли;
	Возврат ПунктПВЗ;
	
КонецФункции

Функция ЗагрузитьДанныеПВЗ(ДатаЗапроса)
	
	Подключение = Евген.СоздатьПодключениеКИнтернетМагазину();
	
	ТекстЗапроса =
	"select
	|pnt.id AS pickupPointID,
	|pnt.externalId AS pnt_externalId,
	|pnt.name AS pnt_name,
	|pnt.address AS pnt_address,
	|pnt_stgs.internetShopID AS pnt_shopId
	|from
	|	pickuppoint pnt (NOLOCK)
	|inner join
	|	settings pnt_stgs (NOLOCK) on pnt.settingId = pnt_stgs.shopId
	|where
	|	pnt.modifyDate > '" + Евген.ДатаВSQL(ДатаЗапроса, Ложь) + "'
	|";
	
	RS = Евген.ЗапросКИнтернетМагазину(ТекстЗапроса, Подключение);
	
	ДанныеПВЗ = Евген.СоздатьТаблицу(RS, "pickupPointID_Ч,pnt_externalId,pnt_name,pnt_address,pnt_shopId_Ч"); 
	
	RS = 0;
	
	Если Не ДанныеПВЗ.Количество() Тогда
		Возврат Истина;
	КонецЕсли;
	
	РезультатВыполнения = Истина;
	
	СтруктураПВЗ = Новый Структура("pickupPointID_Ч,pnt_externalId,pnt_name,pnt_address,pnt_shopId_Ч");
	
	Для Каждого ПВЗ Из ДанныеПВЗ Цикл
		
		ЗаполнитьЗначенияСвойств(СтруктураПВЗ, ПВЗ);
		
		НачатьТранзакцию(РежимУправленияБлокировкойДанных.Управляемый);
		Попытка
			
			БлокировкаДанных = Новый БлокировкаДанных;
			
			ЭлементБлокировки = БлокировкаДанных.Добавить("Справочник.ПунктыПВЗ");
			ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
			
			БлокировкаДанных.Заблокировать();
			
			СоздатьПВЗ(СтруктураПВЗ);
			ЗафиксироватьТранзакцию();
		Исключение
			ОтменитьТранзакцию();
			РезультатВыполнения = Ложь;
		КонецПопытки;
		
	КонецЦикла;

	Возврат РезультатВыполнения;
	
КонецФункции
//Асеев 30.09.2021 (Задача № 4665)<<<

Процедура РегламентЗагрузкаДанныхПВЗ() Экспорт
	
	//Асеев 30.09.2021 (Задача № 4665)>>>
	ЗаписьРегистра = РегистрыСведений.ПараметрыРегламентныхЗаданий.СоздатьМенеджерЗаписи();
	ЗаписьРегистра.Ключ = "ПоследняяДатаЗагрузкиПВЗ";
	ЗаписьРегистра.Прочитать();
	
	
	Попытка
		ДатаЗапроса = Вычислить("'" + ЗаписьРегистра.Значение + "'");
	Исключение
		
		Запрос = Новый Запрос;
		Запрос.Текст =
		"ВЫБРАТЬ
		|	МылоОдминов.Мыло КАК Мыло
		|ИЗ
		|	РегистрСведений.МылоОдминов КАК МылоОдминов";
		lem.ОтправитьСообщение(Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Мыло"), "РегламентЗагрузкаДанныхПВЗ", "Ошибочное значение даты последней загрузки тарифов (РегистрСведений.ПараметрыРегламентныхЗаданий, Ключ=ПоследняяДатаЗагрузкиПВЗ)");
		
		Возврат;
	КонецПопытки;
	
	ДатаВызова = ТекущаяДата();
	
	//ДатаЗапроса = '20200101';
	//ДатаЗапроса = ТекущаяДата();
	
	Если ЗагрузитьДанныеПВЗ(ДатаЗапроса) Тогда
		
		ЗаписьРегистра = РегистрыСведений.ПараметрыРегламентныхЗаданий.СоздатьМенеджерЗаписи();
		ЗаписьРегистра.Ключ = "ПоследняяДатаЗагрузкиПВЗ";
		ЗаписьРегистра.Значение = Формат(ДатаВызова, "ДФ=yyyyMMddHHmmss");
		ЗаписьРегистра.Записать();
		
	КонецЕсли;
	
	//Асеев 30.09.2021 (Задача № 4665)<<<	
	
КонецПроцедуры	