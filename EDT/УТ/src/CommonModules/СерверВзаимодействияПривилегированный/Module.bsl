Функция ПолучитьЗначениеКонстанты(ИмяКонстанты) Экспорт
	Возврат Константы[ИмяКонстанты].Получить();
КонецФункции

Функция СоздатьОбсуждениеБезКонтекста() Экспорт
	
	НовоеОбсуждение = СистемаВзаимодействия.СоздатьОбсуждение();
	НовоеОбсуждение.Отображаемое = ЛОЖЬ;
	НовоеОбсуждение.Ключ = Строка(Новый УникальныйИдентификатор);
	НовоеОбсуждение.Записать();
	
	Константы.ИдентификаторОбсужденияСерверныхСообщенийБезКонтекста.Установить(Строка(НовоеОбсуждение.Идентификатор));
	
	Возврат НовоеОбсуждение.Идентификатор;
	
КонецФункции

Функция ОбсуждениеСуществует(ИдентификаторОбсуждения) Экспорт
	Возврат СистемаВзаимодействия.ПолучитьОбсуждение(ИдентификаторОбсуждения) <> неопределено;
КонецФункции

Процедура ДобавитьТекущегоПользователяВОбсуждение(ИдентификаторОбсуждения) Экспорт

	Обсуждение = СистемаВзаимодействия.ПолучитьОбсуждение(ИдентификаторОбсуждения);
	
	Если Обсуждение.Участники.Содержит(СистемаВзаимодействия.ИдентификаторТекущегоПользователя()) Тогда
		Возврат;
	КонецЕсли;
	
	Обсуждение.Участники.Добавить(СистемаВзаимодействия.ИдентификаторТекущегоПользователя());	
	Обсуждение.Записать();
	
КонецПроцедуры

Функция ПолучитьИдентификаторТекущегоПользователя() Экспорт
	Возврат СистемаВзаимодействия.ИдентификаторТекущегоПользователя();
КонецФункции

Процедура СоздатьСообщение(Параметры) Экспорт
	
	Если СистемаВзаимодействия.ИнформационнаяБазаЗарегистрирована() Тогда
		
		ИдентификаторОбсужденияСтрока = Константы.ИдентификаторОбсужденияСерверныхСообщенийБезКонтекста.Получить();
		ИдентификаторОбсуждения = Новый ИдентификаторОбсужденияСистемыВзаимодействия(ИдентификаторОбсужденияСтрока);
		
		Обсуждение = СистемаВзаимодействия.ПолучитьОбсуждение(ИдентификаторОбсуждения);
		
		ОтветственныйПользовательИБ = ПользователиИнформационнойБазы.НайтиПоИмени(Строка(Параметры.Ответственный));
		ИдентификаторПользователяСВ = СистемаВзаимодействия.ПолучитьИдентификаторПользователя(ОтветственныйПользовательИБ.УникальныйИдентификатор);
		
		НовоеСообщение = СистемаВзаимодействия.СоздатьСообщение(ИдентификаторОбсуждения);
		НовоеСообщение.Данные = Параметры.Ссылка;
		НовоеСообщение.Получатели.Добавить(ИдентификаторПользователяСВ);
		НовоеСообщение.Дата = ТекущаяДата();
		НовоеСообщение.Текст = "Записан документ " + Параметры.Ссылка;
		НовоеСообщение.Записать();
		
	КонецЕсли;
	
КонецПроцедуры