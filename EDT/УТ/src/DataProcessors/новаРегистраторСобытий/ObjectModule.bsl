
Процедура Очистить() Экспорт
	
	Сообщения.Очистить();
	КоличествоБП 			= 0;
	КоличествоДокументов 	= 0;
	КоличествоСправочников 	= 0;
	
КонецПроцедуры

Процедура Добавить(Знач Статус = Неопределено, ТипДействия = "", Сообщение = "", Объект = Неопределено, УИ_Док = "") Экспорт
	
	Статус = ?(Статус = Неопределено, СтатусСообщения.Обычное, Статус);
	
	НовСообщение = Сообщения.Добавить();
	НовСообщение.Дата   				= ТекущаяДата();
	НовСообщение.Тело   				= Сообщение;
	НовСообщение.Статус 				= Статус;
	НовСообщение.ТипДействия 			= ТипДействия;
	НовСообщение.СсылкаДляРасшифровки 	= Объект;
	НовСообщение.УИ_Док   				= УИ_Док;
	
КонецПроцедуры

Процедура УвеличитьКоличествоСправочников(Количество = 1) Экспорт
	
	КоличествоСправочников = КоличествоСправочников + Количество;
	
КонецПроцедуры

Процедура УвеличитьКоличествоДокументов(Количество = 1) Экспорт
	
	КоличествоДокументов = КоличествоДокументов + Количество;
	
КонецПроцедуры

Процедура УвеличитьКоличествоБП(Количество = 1) Экспорт
	
	КоличествоБП = КоличествоБП + Количество;
	
КонецПроцедуры

//Функции вывода сообщений 

Процедура ВывестиСообщенияВТабличныйДокумент()
	
	//Инициализация
	Результат  =Новый ТабличныйДокумент;
	
	Макет = ПолучитьМакет("ФорматТабличногоДокумента");
	ОбластьШапка       = Макет.ПолучитьОбласть("Шапка");
	ОбластьПодвал      = Макет.ПолучитьОбласть("Подвал");
	ОбластьИтог        = Макет.ПолучитьОбласть("ОбластьИтог");
	
	ОбластиПоСтатусу = нОВЫЙ Соответствие;
	ОбластиПоСтатусу.Вставить(СтатусСообщения.БезСтатуса, 	Макет.ПолучитьОбласть("БезСтатуса"));
	ОбластиПоСтатусу.Вставить(СтатусСообщения.Обычное, 	  	Макет.ПолучитьОбласть("Обычное"));
	ОбластиПоСтатусу.Вставить(СтатусСообщения.Информация, 	Макет.ПолучитьОбласть("Информация"));
	ОбластиПоСтатусу.Вставить(СтатусСообщения.Внимание,   	Макет.ПолучитьОбласть("Внимание"));
	ОбластиПоСтатусу.Вставить(СтатусСообщения.Важное, 		Макет.ПолучитьОбласть("Важное"));
	ОбластиПоСтатусу.Вставить(СтатусСообщения.ОченьВажное, 	Макет.ПолучитьОбласть("ОченьВажное"));
	
	// Вывод заголовка
	ОбластьШапка.Параметры.Заголовок = Заголовок;
	Результат.Вывести(ОбластьШапка);
	
	// Вывод сообщений
	Для каждого Сообщение Из Сообщения Цикл
		ОбластьСтрока = ОбластиПоСтатусу[Сообщение.Статус];
		
		ОбластьСтрока.Параметры.Дата		= Сообщение.Дата;
		ОбластьСтрока.Параметры.Тело 		= Сообщение.Тело;
		ОбластьСтрока.Параметры.ТипДействия = Сообщение.ТипДействия;
		ОбластьСтрока.Параметры.УИ_Док 		= Сообщение.УИ_Док;
		
		ОбластьСтрока.Параметры.Объект 		= Сообщение.СсылкаДляРасшифровки;
		
		Результат.Вывести(ОбластьСтрока);
	КонецЦикла;
	
	// Вывод подвала
	Результат.Вывести(ОбластьПодвал);
	
	ОбластьИтог.Параметры.Тело   = "Итого количество справочников: "+КоличествоСправочников
			+ "; документов : "+ КоличествоДокументов
			+ "; бизнес-процессов : "+ КоличествоБП;
	Результат.Вывести(ОбластьИтог);
	
	РезультирующийДокумент.Вывести(Результат);
	
#Если Клиент Тогда
	Если не ТихийРежим Тогда
		Результат.ТолькоПросмотр = Истина;
		Результат.Показать("Регистрация сообщений...");
	КонецЕсли;
#КонецЕсли
	
	Если не ПустаяСтрока(ИмяРезультирующегоФайла) Тогда
	    Результат.Вывод = ИспользованиеВывода.Разрешить;
		Результат.Записать(ИмяРезультирующегоФайла, ТипФайлаТабличногоДокумента.MXL);
	КонецЕсли;
	
	
КонецПроцедуры

Процедура ВывестиСообщенияВТекстовыйДокумент()
	
	//Инициализация
	Результат = Новый ТекстовыйДокумент;
	
	ТекстовоеПредставлениеСтатуса = нОВЫЙ Соответствие;
	ТекстовоеПредставлениеСтатуса.Вставить(СтатусСообщения.БезСтатуса, "");
	ТекстовоеПредставлениеСтатуса.Вставить(СтатусСообщения.Обычное, ">");
	ТекстовоеПредставлениеСтатуса.Вставить(СтатусСообщения.Информация, ">>");
	ТекстовоеПредставлениеСтатуса.Вставить(СтатусСообщения.Внимание, "!");
	ТекстовоеПредставлениеСтатуса.Вставить(СтатусСообщения.Важное, "!!");
	ТекстовоеПредставлениеСтатуса.Вставить(СтатусСообщения.ОченьВажное, "!!!");
	
	Макет = ПолучитьМакет("ФорматТекстовогоДокумента");
	ОбластьШапка       = Макет.ПолучитьОбласть("Шапка");
	ОбластьСтрока      = Макет.ПолучитьОбласть("Строка");
	ОбластьПодвал      = Макет.ПолучитьОбласть("Подвал");
	
	// Вывод заголовка
	ОбластьШапка.Параметры.Заголовок = Заголовок;
	Результат.Вывести(ОбластьШапка);
	
	// Вывод сообщений
	Для каждого Сообщение Из Сообщения Цикл
		ОбластьСтрока.Параметры.Дата   = Сообщение.Дата;
		ОбластьСтрока.Параметры.Статус = ТекстовоеПредставлениеСтатуса[Сообщение.Статус];
		ОбластьСтрока.Параметры.Тело   = Сообщение.Тело;
		
		Результат.Вывести(ОбластьСтрока);
	КонецЦикла;
	
	// Вывод подвала
	Результат.Вывести(ОбластьПодвал);
	
	РезультирующийДокумент.Вывести(Результат);
	
	Если не ТихийРежим Тогда
		Результат.Показать("Регистрация сообщений...");
	КонецЕсли;
	
	Если не ПустаяСтрока(ИмяРезультирующегоФайла) Тогда
		Результат.Записать(ИмяРезультирующегоФайла, КодировкаТекста.UTF8, Символы.ПС);
	КонецЕсли;
	
КонецПроцедуры

Процедура ВывестиСообщенияВЗаписьXML()
	
	ТекстовоеПредставлениеСтатуса = нОВЫЙ Соответствие;
	ТекстовоеПредставлениеСтатуса.Вставить(СтатусСообщения.БезСтатуса, "");
	ТекстовоеПредставлениеСтатуса.Вставить(СтатусСообщения.Обычное, "ordinary");
	ТекстовоеПредставлениеСтатуса.Вставить(СтатусСообщения.Информация, "information");
	ТекстовоеПредставлениеСтатуса.Вставить(СтатусСообщения.Внимание, "attention");
	ТекстовоеПредставлениеСтатуса.Вставить(СтатусСообщения.Важное, "important");
	ТекстовоеПредставлениеСтатуса.Вставить(СтатусСообщения.ОченьВажное, "very_important");
	
	ЗаписьXML = Новый ЗаписьXML;
	ЗаписьXML.УстановитьСтроку("UTF-8");
	ЗаписьXML.ЗаписатьНачалоЭлемента("log");
	
	ЗаписьXML.ЗаписатьНачалоЭлемента("title");
	ЗаписьXML.ЗаписатьТекст(Заголовок);
	ЗаписьXML.ЗаписатьКонецЭлемента();
	
	ЗаписьXML.ЗаписатьНачалоЭлемента("messages");
	Для каждого Сообщение Из Сообщения Цикл
		ЗаписьXML.ЗаписатьНачалоЭлемента("message");
		
		//Дата
		ЗаписьXML.ЗаписатьНачалоЭлемента("date");
		ЗаписьXML.ЗаписатьТекст(XMLСтрока(Сообщение.Дата));
		ЗаписьXML.ЗаписатьКонецЭлемента(); // date
		//Статус
		ЗаписьXML.ЗаписатьНачалоЭлемента("status");
		ЗаписьXML.ЗаписатьТекст(ТекстовоеПредставлениеСтатуса[Сообщение.Статус]);
		ЗаписьXML.ЗаписатьКонецЭлемента(); // status
		//Тело
		ЗаписьXML.ЗаписатьНачалоЭлемента("body");
		ЗаписьXML.ЗаписатьТекст(Сообщение.Тело);
		ЗаписьXML.ЗаписатьКонецЭлемента(); // body
		
		ЗаписьXML.ЗаписатьКонецЭлемента(); // message
	КонецЦикла;
	ЗаписьXML.ЗаписатьКонецЭлемента(); // messages
	ЗаписьXML.ЗаписатьКонецЭлемента(); // log
	
	Результат = ЗаписьXML.Закрыть();
	
	РезультирующийДокумент.ЗаписатьБезОбработки(Результат);
	
	Если не ПустаяСтрока(ИмяРезультирующегоФайла) Тогда
		Запись = Новый ЗаписьТекста(ИмяРезультирующегоФайла);
		Запись.Записать(Результат);
		Запись.Закрыть();
	КонецЕсли;
	
КонецПроцедуры

Процедура ВывестиСообщенияВОкноСообщений()
	
	Если не ТихийРежим Тогда
		// Вывод заголовка
		Сообщить(Заголовок, СтатусСообщения.БезСтатуса);
		
		// Вывод сообщений
		Для каждого Сообщение Из Сообщения Цикл
			Сообщить(Формат(Сообщение.Дата, "ДЛФ=DT") + Символы.Таб + Сообщение.Тело, Сообщение.Статус);
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры
                         
Процедура Вывести() Экспорт
	
	Если не ТихийРежим Тогда
		Если ТипЗнч(РезультирующийДокумент) = Тип("ТабличныйДокумент") Тогда
			ВывестиСообщенияВТабличныйДокумент();
		ИначеЕсли ТипЗнч(РезультирующийДокумент) = Тип("ТекстовыйДокумент") Тогда
			ВывестиСообщенияВТекстовыйДокумент();
		ИначеЕсли ТипЗнч(РезультирующийДокумент) = Тип("ЗаписьXML") Тогда
			ВывестиСообщенияВЗаписьXML();
		Иначе // Вывод в окно сообщений
			ВывестиСообщенияВОкноСообщений();
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Заголовок = "";
КоличествоЗагруженныхСправочников   = 0;
КоличествоЗагруженныхДокументов		= 0;

РезультирующийДокумент  = Новый ТабличныйДокумент;
ИмяРезультирующегоФайла = "";

ТихийРежим = Ложь;

Сообщения = Новый ТаблицаЗначений;
Сообщения.Колонки.Добавить("Дата", 			Новый ОписаниеТипов("Дата", , , Новый КвалификаторыДаты(ЧастиДаты.ДатаВремя)));
Сообщения.Колонки.Добавить("Тело", 			Новый ОписаниеТипов("Строка", , Новый КвалификаторыСтроки));
Сообщения.Колонки.Добавить("Статус", 		Новый ОписаниеТипов("СтатусСообщения"));
Сообщения.Колонки.Добавить("ТипДействия", 	Новый ОписаниеТипов("Строка", , Новый КвалификаторыСтроки));
Сообщения.Колонки.Добавить("УИ_Док", 		Новый ОписаниеТипов("Строка", , Новый КвалификаторыСтроки));
Сообщения.Колонки.Добавить("СсылкаДляРасшифровки");
