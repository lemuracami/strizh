
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	НомерСтроки = 0;
	
	Параметры.Свойство("АдресВХранилищеСтруктураХраненияБазыДанных",АдресВХранилищеСтруктураХраненияБазыДанных);
	Параметры.Свойство("ПросматриваемыйТекстИсходный",ПросматриваемыйТекстИсходный);
	Параметры.Свойство("НомерСтроки",НомерСтроки);
	Параметры.Свойство("РазмерШрифта",РазмерШрифта);
	
	Заголовок = СтрЗаменить(НСтр("ru = '№ %1 Просмотр запроса/текста'") ,"%1",Строка(НомерСтроки));
	
КонецПроцедуры

&НаКлиенте
Процедура ПросмотрСтруктурыХраненияБазыДанных(Команда)
	
	Если ПустаяСтрока( АдресВХранилищеСтруктураХраненияБазыДанных) ИЛИ НЕ ЭтоАдресВременногоХранилища(АдресВХранилищеСтруктураХраненияБазыДанных) Тогда
		Попытка 
			АдресВХранилищеСтруктураХраненияБазыДанных = ВладелецФормы.АдресВХранилищеСтруктураХраненияБазыДанных ;
		Исключение
		КонецПопытки;
	КонецЕсли;
	
	Состояние(Нстр("ru = 'Формируется структура хранения базы данных. Ждите...'"));

	Если НЕ ЭтоАдресВременногоХранилища(АдресВХранилищеСтруктураХраненияБазыДанных) Тогда
		ПолучитьОписаниеХраненияБазыДанныхНаСервере();
		ОбновитьАдресХранилищаВладельцаСтруктураХраненияБД();
	КонецЕсли;
	
	ОткрытьФорму("ВнешняяОбработка.ПросмотрЛоговТехнологическогоЖурнала.Форма.ФормаПросмотраСтруктурыХраненияБазыДанных",Новый Структура("АдресВХранилищеСтруктураХраненияБазыДанных",АдресВХранилищеСтруктураХраненияБазыДанных));
	
	Состояние();
	
КонецПроцедуры

&НаКлиенте 
Процедура ОбновитьАдресХранилищаВладельцаСтруктураХраненияБД()
	
	Если НЕ ПустаяСтрока(АдресВХранилищеСтруктураХраненияБазыДанных) Тогда
		Попытка 
			ВладелецФормы.АдресВХранилищеСтруктураХраненияБазыДанных = АдресВХранилищеСтруктураХраненияБазыДанных;
		Исключение
		КонецПопытки;
	КонецЕсли;
	
КонецПроцедуры


&НаСервере 
Процедура ПолучитьОписаниеХраненияБазыДанныхНаСервере()
	АдресВХранилищеСтруктураХраненияБазыДанных = ПолучитьОбъектОбработки().ПолучитьОписаниеХраненияБД();
КонецПроцедуры

&НаСервере 
Функция ПолучитьОбъектОбработки()
	Возврат РеквизитФормыВЗначение("Объект");
КонецФункции

&НаКлиенте
Процедура ИсходныйТекст(Команда)
	
	Если ВладелецФормы<>Неопределено Тогда
		ПросматриваемыйТекст = ПолучитьФорматированныйЗапросИлиТекстНаСервере(ПросматриваемыйТекстИсходный,Истина,РазмерШрифта);
	Иначе	 
		ПоказатьСообщениеПользователю(НСтр("ru = 'Форма-владелец была закрыта! Операция невозможна!'"));
	КонецЕсли;
	
КонецПроцедуры

&НаСервере 
Функция ПолучитьФорматированныйЗапросИлиТекстНаСервере(ТекущийТекстЗапроса , НуженФорматированныйПросмотр,РазмерШрифта)
	Возврат ПолучитьОбъектОбработки().ПолучитьФорматированныйЗапросИлиТекстНаСервере(ТекущийТекстЗапроса , НуженФорматированныйПросмотр,РазмерШрифта);
КонецФункции


&НаКлиенте
Процедура ПриОткрытии(Отказ)
	ИсходныйТекст(Неопределено);
КонецПроцедуры

&НаКлиенте
Процедура ИменаОбъектовМетаданных(Команда)
	
	ИменаОбъектовМетаданныхНаСервере();
	ОбновитьАдресХранилищаВладельцаСтруктураХраненияБД();
	
КонецПроцедуры

&НаСервере
Процедура ИменаОбъектовМетаданныхНаСервере()
	
	ПросматриваемыйТекст = ПолучитьОбъектОбработки().ПоказатьВТерминахБД(ПросматриваемыйТекст,АдресВХранилищеСтруктураХраненияБазыДанных);
	
КонецПроцедуры


&НаКлиенте
Процедура ВидПросмотраЗапросаТекстаПриИзменении(Элемент)
	
	Если ВидПросмотраЗапросаТекста=0 Тогда
		ИсходныйТекст(Неопределено);
	ИначеЕсли ВидПросмотраЗапросаТекста=1 Тогда
		ИменаОбъектовМетаданных(Неопределено);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте 
Процедура ПоказатьСообщениеПользователю(ТекстСообщения,ИмяПоля=Неопределено)
	
	Сообщение = Новый СообщениеПользователю;
	Сообщение.Текст = ТекстСообщения;
	Если ИмяПоля <> Неопределено Тогда
		Сообщение.Поле = ИмяПоля;
	КонецЕсли;
	
	Сообщение.Сообщить();
	
КонецПроцедуры 

