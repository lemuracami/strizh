#Если Клиент Тогда
 
// Определяет, можно ли изменять фото водителя.
//
Перем ИзменятьФото Экспорт; // <Булево>

Перем обФото;
Перем Фото;
Перем СтрокаИнформации1, СтрокаИнформации2, СтрокаИнформации3, СтрокаИнформации4;

// Подключает интерфейс к форме.
//
// Параметры:
//  Форма     <Форма>
//  Контейнер <ЭлементУправления>: элемент управления формы, на место которого подключается интерфейс.
//  Панель    <Панель>
//
Процедура ПодключитьИнтерфейс(Форма, Контейнер, Панель = Неопределено) Экспорт
	
	Если Водитель = Неопределено Тогда Возврат; КонецЕсли;
	
	ФормаКонтейнер = Форма;
	
	// Создаем дополнительные интерфейсные объекты.
	ИмяИнтерфейса = новаРасширениеФорм.ДобавитьОбъектНаФорму(Форма, ЭтотОбъект);
	
	Отступ = 2;
	
	Фото = новаРасширениеФорм.ДобавитьЭлементНаФорму(Форма, Тип("ПолеКартинки"), Панель);
	Фото.Верх = Контейнер.Верх + Отступ;
	Фото.Лево = Контейнер.Лево + Отступ;
	Фото.Высота = 120;
	Фото.Ширина = 100;
	Фото.Гиперссылка = ИзменятьФото; 
	Фото.РазмерКартинки = РазмерКартинки.АвтоРазмер;
		
	новаРасширениеФорм.УстановитьОбработчикСобытия(Форма, Фото,
		"Нажатие",
		0,
		ИмяИнтерфейса + ".ФотоНажатие(Параметр1);");
		
	ШиринаЗаголовков = 70;
	ЛевоЗаголовков = Контейнер.Лево + Фото.Ширина + 4 * Отступ;
	ЛевоНадписей = ЛевоЗаголовков + ШиринаЗаголовков + Отступ;
	ШиринаИнформации = Контейнер.Ширина - ШиринаЗаголовков - Фото.Ширина - 8;
	
	Верх = Контейнер.Верх + Отступ;
	ВысотаНадписей = 19;
	
	СтрокаИнформации1 = СоздатьСтрокуИнформации(Форма, "ФИО", Верх, ШиринаЗаголовков, ЛевоЗаголовков, ЛевоНадписей, ШиринаИнформации, Отступ, Фото, Панель);
	СтрокаИнформации2 = СоздатьСтрокуИнформации(Форма, "Телефон", СтрокаИнформации1.Верх + СтрокаИнформации1.Высота + Отступ, ШиринаЗаголовков, ЛевоЗаголовков, ЛевоНадписей, ШиринаИнформации, Отступ, Фото, Панель);
	СтрокаИнформации3 = СоздатьСтрокуИнформации(Форма, "Права", СтрокаИнформации2.Верх + СтрокаИнформации2.Высота + Отступ, ШиринаЗаголовков, ЛевоЗаголовков, ЛевоНадписей, ШиринаИнформации, Отступ, Фото, Панель,3);
	СтрокаИнформации4 = СоздатьСтрокуИнформации(Форма, "Паспорт", СтрокаИнформации3.Верх + СтрокаИнформации3.Высота + Отступ, ШиринаЗаголовков, ЛевоЗаголовков, ЛевоНадписей, ШиринаИнформации, Отступ, Фото, Панель,3);
	
	УстановитьВодителя(Водитель);
КонецПроцедуры

// Получает дополнительную информацию для заданного объекта
//
// Параметры:
//  Объект <СправочникСсылка.новаВодители>
//  Вид    <ПеречислениеСсылка.новаВидыДополнительнойИнформации>
//
// Возвращаемое значение:
//  <СправочникСсылка.новаХранилищеДополнительнойИнформации>
//
Функция ПолучитьДополнительнуюИнформацию(Объект,Вид)
	Если НЕ ЗначениеЗаполнено(Объект) ИЛИ НЕ ЗначениеЗаполнено(Вид) Тогда
		Возврат Неопределено;
	КонецЕсли;	
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	новаХранилищеДополнительнойИнформации.Ссылка  КАК Ссылка
	               |ИЗ
	               |	Справочник.новаХранилищеДополнительнойИнформации КАК новаХранилищеДополнительнойИнформации
	               |ГДЕ
	               |	новаХранилищеДополнительнойИнформации.Объект = &Объект
	               |	И новаХранилищеДополнительнойИнформации.ВидИнформации = &Вид";
				   
	Запрос.УстановитьПараметр("Объект",Объект);
	Запрос.УстановитьПараметр("Вид",Вид);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Возврат Выборка.Ссылка;
	Иначе
		Возврат Неопределено;
	КонецЕсли;	
КонецФункции	

Процедура УстановитьВодителя(НовыйВодитель)
	Водитель = НовыйВодитель;
	
	обФото = ПолучитьДополнительнуюИнформацию(Водитель, Перечисления.новаВидыДополнительнойИнформации.Фото);
	Если обФото = Неопределено Тогда
		Фото.Картинка = БиблиотекаКартинок.новаНетФото;
	Иначе
		Фото.Картинка = Новый Картинка(обФото.Хранилище.Получить());
	КонецЕсли;	
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	новаВодители.Ссылка КАК Водитель,
	               |	новаВодители.Наименование КАК ФИО,
	               |	новаВодители.ОсновнойТелефон КАК Телефон,
	               |	новаРейтингПеревозчиковОстатки.НачисленныеБаллыОстаток КАК Баллы,
	               |	новаРейтингПеревозчиковОстатки.ШтрафныеБаллыОстаток КАК Штрафные,
	               |	Права.Наименование КАК Права,
	               |	Паспорт.Наименование КАК Паспорт
	               |ИЗ
	               |	Справочник.новаВодители КАК новаВодители
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.новаРейтингПеревозчиков.Остатки КАК новаРейтингПеревозчиковОстатки
	               |		ПО (новаРейтингПеревозчиковОстатки.Водитель = новаВодители.Ссылка)
	               |		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ 
	               |			новаДокументыВодителей.Наименование КАК Наименование,
	               |			новаДокументыВодителей.Владелец КАК Владелец
	               |		ИЗ
	               |			Справочник.новаДокументыВодителей КАК новаДокументыВодителей
	               |		ГДЕ
	               |			новаДокументыВодителей.ВидДокумента = &ВидДокументаПрава) КАК Права
	               |		ПО (Права.Владелец = новаВодители.Ссылка)
	               |		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ 
	               |			новаДокументыВодителей.Наименование КАК Наименование,
	               |			новаДокументыВодителей.Владелец КАК Владелец
	               |		ИЗ
	               |			Справочник.новаДокументыВодителей КАК новаДокументыВодителей
	               |		ГДЕ
	               |			новаДокументыВодителей.ВидДокумента = &ВидДокументаПаспорт) КАК Паспорт
	               |		ПО (Паспорт.Владелец = новаВодители.Ссылка)
	               |ГДЕ
	               |	новаВодители.Ссылка = &Водитель";
				   
	Запрос.УстановитьПараметр("ВидДокументаПрава", Справочники.новаВидыДокументовВодителей.ВодительскоеУдостоверение);				
	Запрос.УстановитьПараметр("ВидДокументаПаспорт", Справочники.новаВидыДокументовВодителей.Паспорт);				
	Запрос.УстановитьПараметр("Водитель", Водитель);		
	
	Выборка = Запрос.Выполнить().Выбрать();				
	Выборка.Следующий();
	
	ВывестиСтрокуИнформации(СтрокаИнформации1, Выборка.ФИО);
	ВывестиСтрокуИнформации(СтрокаИнформации2, Выборка.Телефон);
	ВывестиСтрокуИнформации(СтрокаИнформации3, Выборка.Права);
	ВывестиСтрокуИнформации(СтрокаИнформации4, Выборка.Паспорт);
КонецПроцедуры

Процедура ВывестиСтрокуИнформации(Строка, Данные)
	Строка.Заголовок = Данные;
КонецПроцедуры	

Функция СоздатьСтрокуИнформации(Форма, Заголовок, Верх, ШиринаЗаголовков, ЛевоЗаголовков, ЛевоНадписей, ШиринаИнформации, Отступ, ОбъектДляПривязки, Панель,КоличествоСтрок=1)
	
	НадписьИнфо = новаРасширениеФорм.ДобавитьЭлементНаФорму(Форма, Тип("Надпись"), Панель);
	НадписьИнфо.Лево = ЛевоЗаголовков;
	НадписьИнфо.Верх = Верх;
	НадписьИнфо.Высота = 19;
	НадписьИнфо.Ширина = ШиринаЗаголовков;
	НадписьИнфо.ПорядокОбхода = 32769;
	НадписьИнфо.Заголовок = Заголовок;
	НадписьИнфо.Шрифт = Новый Шрифт(,,Истина);
    НадписьИнфо.УстановитьПривязку(ГраницаЭлементаУправления.Право,ОбъектДляПривязки,ГраницаЭлементаУправления.Право);
	
	СодержаниеИнфо = новаРасширениеФорм.ДобавитьЭлементНаФорму(Форма, Тип("Надпись"), Панель);
	СодержаниеИнфо.Лево = ЛевоНадписей;
	СодержаниеИнфо.Верх = Верх;
	СодержаниеИнфо.Высота = 19*КоличествоСтрок;
	СодержаниеИнфо.Ширина = ШиринаИнформации;
	СодержаниеИнфо.ПорядокОбхода = 32769;
	СодержаниеИнфо.ЦветТекста = Новый Цвет(0,0,128);
    СодержаниеИнфо.УстановитьПривязку(ГраницаЭлементаУправления.Право,Панель,ГраницаЭлементаУправления.Право);
	
	Возврат СодержаниеИнфо;
КонецФункции

// Обработчик события "Нажатие" картинки, отображающей фото водителя.
//
// Параметры:
//  Элемент <Картинка>
//
Процедура ФотоНажатие(Элемент) Экспорт
	ИмяФайла = "";
	новаОбщиеПроцедуры.ВыбратьФайл(ИмяФайла, "Выберите фотографию", 
	"Все картинки (*.bmp;*.dib;*.rle;*.jpg;*.jpeg;*.tif;*.gif;*.png;*.ico;*.wmf;*.emf)|*.bmp;*.dib;*.rle;*.jpg;*.jpeg;*.tif;*.gif;*.png;*.ico;*.wmf;*.emf|" 
		+ "Все файлы (*.*)|*.*|"
		+ "Формат bmp (*.bmp;*.dib;*.rle)|*.bmp;*.dib;*.rle|"
		+ "Формат JPEG (*.jpg;*.jpeg)|*.jpg;*.jpeg|"
		+ "Формат TIFF (*.tif)|*.tif|"
		+ "Формат GIF (*.gif)|*.gif|"
		+ "Формат PNG (*.png)|*.png|"
		+ "Формат icon (*.ico)|*.ico|"
		+ "Формат метафайл (*.wmf;*.emf)|*.wmf;*.emf|");
		
	Если ПустаяСтрока(ИмяФайла) Тогда Возврат; КонецЕсли;
	
	Если обФото = Неопределено Тогда
		Хранилище = Справочники.новаХранилищеДополнительнойИнформации.СоздатьЭлемент();
	Иначе
		Хранилище = обФото.ПолучитьОбъект();
	КонецЕсли;
	Хранилище.Объект = Водитель.Ссылка;
	Хранилище.ВидИнформации = Перечисления.новаВидыДополнительнойИнформации.Фото;
	Хранилище.Хранилище = Новый ХранилищеЗначения(Новый ДвоичныеДанные(ИмяФайла));
	Хранилище.Записать();
	
	Элемент.Картинка = Новый Картинка(Хранилище.Хранилище.Получить());
КонецПроцедуры	

ЭлементыУправления = Новый Структура;  
ИзменятьФото = Ложь;

#КонецЕсли