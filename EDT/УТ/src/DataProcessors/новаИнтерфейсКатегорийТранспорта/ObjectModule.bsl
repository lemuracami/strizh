#Если Клиент Тогда

Перем обКонтейнер;
Перем Форма;
Перем Панель;

Перем ИмяТабличнойЧасти;
Перем ИмяРеквизитаЗначениеКатегории;
Перем фПрефиксовать;
Перем ИмяИнтерфейса;
Перем соКатегорииНаФорме;
Перем мдКонтейнер;

//Отключение подключенных обработчиков
//
Процедура ВыполнитьОсвобождениеРесурсов() Экспорт
	
	обКонтейнер = Неопределено;
	Форма = Неопределено;
	Панель = Неопределено;
	ИмяТабличнойЧасти = Неопределено;
	ИмяРеквизитаЗначениеКатегории = Неопределено;
	фПрефиксовать = Неопределено;
	ИмяИнтерфейса = Неопределено;
	соКатегорииНаФорме = Неопределено;
	мдКонтейнер = Неопределено;
	
КонецПроцедуры

// Отключает интерфейс от формы.
//
Процедура ОтключитьИнтерфейс() Экспорт
	
	ВыполнитьОсвобождениеРесурсов();
	
КонецПроцедуры

// Подключает интерфейс к форме.
//
// Параметры:
//  ФормаДляПодключения  <Форма>
//  Контейнер            <ЭлементУправления>: элемент управления формы, на место которого подключается интерфейс.
//  ПанельДляПодключения <Панель>
//
Процедура ПодключитьИнтерфейс(ФормаДляПодключения, Контейнер, ПанельДляПодключения) Экспорт
	обКонтейнер = Контейнер;
	Форма = ФормаДляПодключения;
	Панель = ПанельДляПодключения;
	
	фПрефиксовать = Истина;
	мдКонтейнер = обКонтейнер.Метаданные();
	Если новаОбщиеПроцедуры.ОбъектПринадлежитПодсистеме(мдКонтейнер, Метаданные.Подсистемы.новаУправлениеТранспортнойЛогистикой.Подсистемы.новаТранспорт) Тогда
		фПрефиксовать = Ложь;
	КонецЕсли;
	Если новаОбщиеПроцедуры.ОбъектПринадлежитПодсистеме(мдКонтейнер, Метаданные.Подсистемы.новаУправлениеТранспортнойЛогистикой.Подсистемы.новаУправлениеДоставкой) Тогда
		фПрефиксовать = Ложь;
	КонецЕсли;
	Если новаОбщиеПроцедуры.ОбъектПринадлежитПодсистеме(мдКонтейнер, Метаданные.Подсистемы.новаУправлениеТранспортнойЛогистикой.Подсистемы.новаОбщиеДанные) Тогда
		фПрефиксовать = Ложь;
	КонецЕсли;
	
	ИмяТабличнойЧасти = ?(фПрефиксовать, "нова", "") + "КатегорииТранспорта";
	ИмяРеквизитаЗначениеКатегории = ?(фПрефиксовать, "нова", "") + "ЗначениеКатегории";
	
	ИмяИнтерфейса = новаРасширениеФорм.ДобавитьОбъектНаФорму(Форма, ЭтотОбъект);
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	ЗначенияКатегорий.Владелец КАК Категория,
	|	ЗначенияКатегорий.Ссылка КАК Значение
	|ИЗ
	|	Справочник.новаЗначенияКатегорийТранспорта КАК ЗначенияКатегорий
	|ГДЕ
	|	НЕ ЗначенияКатегорий.ПометкаУдаления
	| И НЕ ЗначенияКатегорий.Владелец.ПометкаУдаления
	|
	|УПОРЯДОЧИТЬ ПО ЗначенияКатегорий.Владелец.Флажок ВОЗР, ЗначенияКатегорий.Владелец.Наименование ВОЗР, ЗначенияКатегорий.Наименование ВОЗР
	|
	|ИТОГИ ПО ЗначенияКатегорий.Владелец";
	
	дзКатегории = Запрос.Выполнить().Выгрузить(ОбходРезультатаЗапроса.ПоГруппировкамСИерархией);
	Если дзКатегории.Строки.Количество() = 0 Тогда Возврат; КонецЕсли;
	
	новаРасширениеФорм.УстановитьОбработчикСобытия(Форма, Форма,
		"ПередЗаписью",
		1,
		ИмяИнтерфейса + ".ПередЗаписьюИзФормы(Параметр2);");
	
	новаРасширениеФорм.УстановитьОбработчикСобытия(Форма, Форма,
		"ПриЗакрытии",
		0,
		ИмяИнтерфейса + ".ВыполнитьОсвобождениеРесурсов();");
		
	текСтраница = Панель.ТекущаяСтраница;
	Страница = Панель.Страницы.Добавить("КатегорииТранспорта", "Категории транспорта");
	Панель.ТекущаяСтраница = Страница;
	
	мсФлажки = Новый Массив;
	Для Каждого Строка Из дзКатегории.Строки Цикл
		Если Не Строка.Категория.Флажок Тогда Продолжить; КонецЕсли;
		
		мсФлажки.Добавить(Строка);
	КонецЦикла;
	
	Для ъ = 0 По мсФлажки.Количество() - 1 Цикл
		Значение = мсФлажки[ъ].Строки[0].Значение;
		дзКатегории.Строки.Удалить(мсФлажки[ъ]);
		мсФлажки[ъ] = Значение;
	КонецЦикла;
	
	Если мсФлажки.Количество() > 0 Тогда
		Строка = дзКатегории.Строки.Добавить();
		
		Для Каждого Значение Из мсФлажки Цикл
			СтрокаЗначения = Строка.Строки.Добавить();
			СтрокаЗначения.Значение = Значение;
		КонецЦикла;
	КонецЕсли;
	
	КоличествоРазделителей = дзКатегории.Строки.Количество() - 1;
	
	Отступ = 8;
	ШиринаРазделителя = 6;
	ВысотаНадписи = 0;
	ВысотаПанели = Панель.Высота - 20;
	
	ШиринаРаздела = Цел((Панель.Ширина - Отступ * 2 - КоличествоРазделителей * ШиринаРазделителя) / дзКатегории.Строки.Количество());
	Х = Отступ;
	
	дзКатегории.Колонки.Добавить("Х");
	дзКатегории.Колонки.Добавить("ЭлементСлева");
	дзКатегории.Колонки.Добавить("ГраницаСлева");
	дзКатегории.Колонки.Добавить("ЭлементСправа");
	дзКатегории.Колонки.Добавить("ГраницаСправа");
	
	ЭлементСправа = Панель;
	ГраницаСправа = ГраницаЭлементаУправления.Лево;
	
	Для Каждого Строка Из дзКатегории.Строки Цикл
		Строка.ЭлементСлева = ЭлементСправа;			
		Строка.ГраницаСлева = ГраницаСправа;			
		Строка.Х = Х;
		Х = Х + ШиринаРаздела;
		
		Если дзКатегории.Строки.Индекс(Строка) = дзКатегории.Строки.Количество() - 1 Тогда
			ЭлементСправа = Панель;
			Строка.ГраницаСправа = ГраницаЭлементаУправления.Право;
		Иначе
			ЭлементСправа = новаРасширениеФорм.ДобавитьЭлементНаФорму(Форма, Тип("Разделитель"), Панель);
			ЭлементСправа.Ориентация = Ориентация.Вертикально;
			ЭлементСправа.Лево = Х;
			ЭлементСправа.Ширина = ШиринаРазделителя;
			Х = Х + ШиринаРазделителя;
			ЭлементСправа.Верх = Отступ;
			ЭлементСправа.Высота = ВысотаПанели - Отступ * 2;
			ЭлементСправа.УстановитьПривязку(ГраницаЭлементаУправления.Низ, Панель, ГраницаЭлементаУправления.Низ);
			
			Строка.ГраницаСправа = ГраницаЭлементаУправления.Лево;
			ГраницаСправа = ГраницаЭлементаУправления.Право;
		КонецЕсли;	
		
		Строка.ЭлементСправа = ЭлементСправа;
	КонецЦикла;
	
	соКатегорииНаФорме = Новый Соответствие;
	
	Для Каждого Строка Из дзКатегории.Строки Цикл
		Если ЗначениеЗаполнено(Строка.Категория) Тогда
			Надпись = новаРасширениеФорм.ДобавитьЭлементНаФорму(Форма, Тип("Надпись"), Панель);
			Надпись.Лево = Строка.Х;
			Надпись.Ширина = ШиринаРаздела;
			Надпись.Верх = Отступ;
			Надпись.Высота = 10;
			Надпись.Заголовок = СокрЛП(Строка.Категория) + ":";
			Надпись.УстановитьПривязку(ГраницаЭлементаУправления.Лево, Строка.ЭлементСлева, Строка.ГраницаСлева);
			Надпись.УстановитьПривязку(ГраницаЭлементаУправления.Право, Строка.ЭлементСправа, Строка.ГраницаСправа);
			ВысотаНадписи = Надпись.Высота;
			
			Таблица = новаРасширениеФорм.ДобавитьЭлементНаФорму(Форма, Тип("ТабличноеПоле"), Панель);
			Таблица.Лево = Строка.Х;
			Таблица.Ширина = ШиринаРаздела;
			Таблица.Верх = Надпись.Верх + Надпись.Высота;
			Таблица.Высота = ВысотаПанели - Отступ - Таблица.Верх;
			Таблица.УстановитьПривязку(ГраницаЭлементаУправления.Лево, Строка.ЭлементСлева, Строка.ГраницаСлева);
			Таблица.УстановитьПривязку(ГраницаЭлементаУправления.Право, Строка.ЭлементСправа, Строка.ГраницаСправа);
			Таблица.УстановитьПривязку(ГраницаЭлементаУправления.Низ, Панель, ГраницаЭлементаУправления.Низ);
			Таблица.ТолькоПросмотр = Ложь;
			Таблица.ИзменятьСоставСтрок = Ложь;
			Таблица.АвтоКонтекстноеМеню = Ложь;
			Таблица.Шапка = Ложь;
			Таблица.ИзменяетДанные = Истина;
			Таблица.ВертикальныеЛинии = Ложь;
			Таблица.ГоризонтальныеЛинии = Ложь;
			
			Таблица.Значение = Новый ТаблицаЗначений;
			
			Таблица.Значение.Колонки.Добавить("Выбрано", Новый ОписаниеТипов("Булево"));
			Таблица.Значение.Колонки.Добавить("Значение");
			
			Колонка = Таблица.Колонки.Добавить("Выбрано", "");
			Колонка.ДанныеФлажка = "Выбрано";
			Колонка.Ширина = 3;
			Колонка.ИзменениеРазмера = ИзменениеРазмераКолонки.НеИзменять;
			Колонка.РежимРедактирования = РежимРедактированияКолонки.Непосредственно;
			Колонка.ТолькоПросмотр = Ложь;
			
			Колонка = Таблица.Колонки.Добавить("Значение", "");
			Колонка.Данные = "Значение";
			Колонка.ТолькоПросмотр = Истина;
			
			ТЧ = обКонтейнер[ИмяТабличнойЧасти];
			Для Каждого стрЗначение Из Строка.Строки Цикл
				стрКатегория = Таблица.Значение.Добавить();
				ЗаполнитьЗначенияСвойств(стрКатегория, стрЗначение);
				стрКатегория.Выбрано = ТЧ.Найти(стрЗначение.Значение, ИмяРеквизитаЗначениеКатегории) <> Неопределено;
			КонецЦикла;
			
			соКатегорииНаФорме.Вставить(Строка.Категория, Таблица.Значение);
		Иначе
			У = Отступ + ВысотаНадписи - 1;
			Для Каждого стрЗначение Из Строка.Строки Цикл
				Флажок = новаРасширениеФорм.ДобавитьЭлементНаФорму(Форма, Тип("Флажок"), Панель);
				Флажок.Лево = Строка.Х;
				Флажок.Ширина = ШиринаРаздела;
				Флажок.Верх = У;
				Флажок.Высота = 10;
				Флажок.Заголовок = СокрЛП(стрЗначение.Значение);
				Флажок.УстановитьПривязку(ГраницаЭлементаУправления.Лево, Строка.ЭлементСлева, Строка.ГраницаСлева);
				Флажок.УстановитьПривязку(ГраницаЭлементаУправления.Право, Строка.ЭлементСправа, Строка.ГраницаСправа);
				Флажок.ИзменяетДанные = Истина;
				Флажок.Значение = ТЧ.Найти(стрЗначение.Значение, ИмяРеквизитаЗначениеКатегории) <> Неопределено;
				
				У = У + Флажок.Высота + Отступ;
				
				соКатегорииНаФорме.Вставить(стрЗначение.Значение, Флажок);
			КонецЦикла;
		КонецЕсли;
	КонецЦикла;
	
	Панель.ТекущаяСтраница = текСтраница;
КонецПроцедуры

// Устанавливает признак выбранности у массива категорий.
//
// Параметры:
//  мсЗначения      <Массив(<СправочникСсылка.новаЗначенияКатегорийТранспорта>)>
//  ЗначениеВыбрано <Булево>
//
Процедура УстановитьЗначенияКатегорий(мсЗначения, ЗначениеВыбрано) Экспорт
	Для Каждого Значение Из мсЗначения Цикл
		КонтейнерКатегории = соКатегорииНаФорме[Значение.Владелец]; 
		
		Если ТипЗнч(КонтейнерКатегории) = Тип("ТаблицаЗначений") Тогда
			стрЗначение = КонтейнерКатегории.Найти(Значение, "Значение"); 
			Если стрЗначение <> Неопределено Тогда
				стрЗначение.Выбрано = ЗначениеВыбрано;
			КонецЕсли;
		Иначе
			КонтейнерКатегории.Значение = ЗначениеВыбрано;
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

// Обработчик события "ПередЗаписью" формы, к которой подключен интерфейс.
//
// Параметры:
//  Отказ <Булево>
//
Процедура ПередЗаписьюИзФормы(Отказ) Экспорт
	ТЧ = обКонтейнер[ИмяТабличнойЧасти];
	ТЧ.Очистить();
	
	Для Каждого ЭлКатегория Из соКатегорииНаФорме Цикл
		Если ТипЗнч(ЭлКатегория.Значение) = Тип("ТаблицаЗначений") Тогда
			Для Каждого стрЗначение Из ЭлКатегория.Значение Цикл
				Если Не стрЗначение.Выбрано Тогда Продолжить; КонецЕсли;
				
				стрТЧ = ТЧ.Добавить();
				стрТЧ[ИмяРеквизитаЗначениеКатегории] = стрЗначение.Значение;
			КонецЦикла;
		Иначе
			Если ЭлКатегория.Значение.Значение Тогда
				стрТЧ = ТЧ.Добавить();
				стрТЧ[ИмяРеквизитаЗначениеКатегории] = ЭлКатегория.Ключ;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Если мдКонтейнер.Реквизиты.Найти("КоличествоВыбранныхКатегорий") <> Неопределено Тогда
		соВыбранныеКатегории = Новый Соответствие;
		Для Каждого стрЗначение Из ТЧ Цикл
			соВыбранныеКатегории.Вставить(стрЗначение[ИмяРеквизитаЗначениеКатегории].Владелец, Истина);
		КонецЦикла;
		обКонтейнер.КоличествоВыбранныхКатегорий = соВыбранныеКатегории.Количество();
	КонецЕсли;
КонецПроцедуры

#КонецЕсли