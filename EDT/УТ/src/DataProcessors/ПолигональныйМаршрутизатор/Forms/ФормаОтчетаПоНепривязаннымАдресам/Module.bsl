

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Макет = Обработки.ПолигональныйМаршрутизатор.ПолучитьМакет("МакетОтчетПоНеприязаннымЗаказам");	
	
	ОбластьШапка = Макет.ПолучитьОбласть("ОбластьШапка");
	ОбластьШапка.Параметры.ДатаПланирования = Параметры.ДатаПланирования;
	
	Результат.Вывести(ОбластьШапка);
	
	ОбластьПартнер = Макет.ПолучитьОбласть("ОбластьПартнер");
	ОбластьСтрокаДокумента = Макет.ПолучитьОбласть("ОбластьСтрокаДокумента");
	
	ДеревоПолигонов =  ПолучитьИзВременногоХранилища(Параметры.АдресДереваПолигонов);
	
	МассивДокументов = Новый Массив;
	
	Для Каждого СтрокаДерева Из ДеревоПолигонов.Строки[0].Строки Цикл
		МассивДокументов.Добавить(СтрокаДерева.ЗаказСсылка);
	КонецЦикла;	
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	РеализацияТоваровУслуг.Ссылка КАК Ссылка,
	               |	ВЫБОР
	               |		КОГДА РеализацияТоваровУслуг.ВладелецТовара.Родитель.ОсновнойМагазин = ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
	               |			ТОГДА РеализацияТоваровУслуг.ВладелецТовара
	               |		ИНАЧЕ РеализацияТоваровУслуг.ВладелецТовара.Родитель.ОсновнойМагазин
	               |	КОНЕЦ КАК Партнер,
	               |	РеализацияТоваровУслуг.Номер КАК НомерЗаказаСтриж,
	               |	РеализацияТоваровУслуг.НомерВнешнегоЗаказа КАК НомерЗаказаПартнера,
	               |	РеализацияТоваровУслуг.Телефон КАК Телефон,
	               |	РеализацияТоваровУслуг.АдресДоставки КАК АдресДоставки,
	               |	РеализацияТоваровУслуг.Комментарий КАК Комментарий,
	               |	РеализацияТоваровУслуг.Контрагент.Наименование КАК Клиент
	               |ИЗ
	               |	Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
	               |ГДЕ
	               |	РеализацияТоваровУслуг.Ссылка В(&МассивДокументов)
	               |
	               |ОБЪЕДИНИТЬ ВСЕ
	               |
	               |ВЫБРАТЬ
	               |	ЗаборТовара.Ссылка,
	               |	ВЫБОР
	               |		КОГДА ЗаборТовара.Контрагент.Родитель.ОсновнойМагазин = ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
	               |			ТОГДА ЗаборТовара.Контрагент
	               |		ИНАЧЕ ЗаборТовара.Контрагент.Родитель.ОсновнойМагазин
	               |	КОНЕЦ,
	               |	ЗаборТовара.Номер,
	               |	ЗаборТовара.НомерВнешнегоЗаказа,
	               |	ЗаборТовара.Телефон,
	               |	ЗаборТовара.АдресДоставки,
	               |	ЗаборТовара.Комментарий,
	               |	ЗаборТовара.Контрагент.Наименование
	               |ИЗ
	               |	Документ.ЗаборТовара КАК ЗаборТовара
	               |ГДЕ
	               |	ЗаборТовара.Ссылка В(&МассивДокументов)
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	Партнер
	               |ИТОГИ ПО
	               |	Партнер
	               |АВТОУПОРЯДОЧИВАНИЕ";
	
	Запрос.УстановитьПараметр("МассивДокументов",МассивДокументов);
	
	ВыборкаПоПартнерам = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаПоПартнерам.Следующий() Цикл
		ОбластьПартнер.Параметры.Партнер = ВыборкаПоПартнерам.Партнер;
		Результат.Вывести(ОбластьПартнер);
		Результат.НачатьГруппуСтрок(,Истина);
		ВыборкаПоДокументам = ВыборкаПоПартнерам.Выбрать();
		Пока ВыборкаПоДокументам.Следующий() Цикл
			ОбластьСтрокаДокумента.Параметры.Заполнить(ВыборкаПоДокументам);
			Результат.Вывести(ОбластьСтрокаДокумента);
		КонецЦикла;	
		Результат.ЗакончитьГруппуСтрок();
	КонецЦикла;	
	
	
	
КонецПроцедуры

