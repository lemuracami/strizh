Функция ПолучитьДанныеВыгрузки(ПериодВыгрузки, ДляФормы = Ложь, СхемаКомпоновки = Неопределено) Экспорт

	Если Не ЗначениеЗаполнено(ПериодВыгрузки) Тогда
		ВызватьИсключение "Не задан период выгрузки данных в бухгалтерию"
	КонецЕсли;	
	
	Если СхемаКомпоновки = Неопределено тогда
		СКД = Обработки.ВыгрузкаДанныхВБухгалтерию.ПолучитьМакет("ДанныеПоОтчетКассира");
	Иначе
		СКД = СхемаКомпоновки;
	Конецесли;
	
	
	Компоновщик = Новый КомпоновщикНастроекКомпоновкиДанных;
	URLСКД = ПоместитьВоВременноеХранилище(СКД, Новый УникальныйИдентификатор());
	Компоновщик.Инициализировать(Новый ИсточникДоступныхНастроекКомпоновкиДанных(URLСКД));

	НастройкиКомпоновки = СКД.НастройкиПоУмолчанию;
	
	ПараметрПериодВыгрузки = НастройкиКомпоновки.ПараметрыДанных.Элементы.Найти("ПериодВыгрузки");
	ПараметрПериодВыгрузки.Значение = Новый СтандартныйПериод(НачалоДня(ПериодВыгрузки), КонецДня(ПериодВыгрузки));
	ПараметрПериодВыгрузки.Использование = Истина;
	
	Компоновщик.ЗагрузитьНастройки(НастройкиКомпоновки);
	
	ТаблицаДанных = СкомпоноватьВТаблицуЗначений(СКД, Компоновщик);	
	
	Если Не ДляФормы Тогда
		
		ТаблицаДанных.Свернуть("Партнер, Организация, Спецсчет, ПланВыгрузки","СуммаНаличные, СуммаТерминал");

	КонецЕсли;	
	
	Возврат ТаблицаДанных;
КонецФункции

&НаСервере
Функция ПолучитьДанныеВыгрузкиПоОтчету(ПериодВыгрузки, ДляФормы = Ложь) Экспорт

	Если Не ЗначениеЗаполнено(ПериодВыгрузки) Тогда
		ВызватьИсключение "Не задан период выгрузки данных в бухгалтерию"
	КонецЕсли;	

	СКД = Отчеты.ОтчетКассираНовый.ПолучитьМакет("ОсновнаяСхемаКомпоновкиДанных");
	
	Компоновщик = Новый КомпоновщикНастроекКомпоновкиДанных;
	URLСКД = ПоместитьВоВременноеХранилище(СКД, Новый УникальныйИдентификатор());
	Компоновщик.Инициализировать(Новый ИсточникДоступныхНастроекКомпоновкиДанных(URLСКД));

	НастройкиКомпоновки = СКД.НастройкиПоУмолчанию;
	
	ПараметрПериодВыгрузки = НастройкиКомпоновки.ПараметрыДанных.Элементы.Найти("Период");
	ПараметрПериодВыгрузки.Значение = Новый СтандартныйПериод(НачалоДня(ПериодВыгрузки), КонецДня(ПериодВыгрузки));
	ПараметрПериодВыгрузки.Использование = Истина;
	
	СписокТерминалов = Новый СписокЗначений;
	СписокТерминалов.Добавить(Справочники.РегиональныеТерминалы.МоскваСтриж);
	СписокТерминалов.Добавить(Справочники.РегиональныеТерминалы.СПбСтриж);
	СписокТерминалов.Добавить(Справочники.РегиональныеТерминалы.ПустаяСсылка());
	
	ПараметрТерминал = НастройкиКомпоновки.ПараметрыДанных.Элементы.Найти("ТерминалДоставки");
	ПараметрТерминал.Значение = СписокТерминалов;
	ПараметрТерминал.Использование = Истина;
	
	ПараметрИнтернетМагазин = НастройкиКомпоновки.ПараметрыДанных.Элементы.Найти("ИнтернетМагазин");
	ПараметрИнтернетМагазин.Использование = Ложь;
	
	Компоновщик.ЗагрузитьНастройки(НастройкиКомпоновки);
	
	ТаблицаДанных = СкомпоноватьВТаблицуЗначений(СКД, Компоновщик);	
	
	ТаблицаДанных.Свернуть("Заказ", "Наличные,БезНаличные");
	
	МассивНеНужныхСтрок = Новый Массив;
	
	Для Каждого СтрокаДанных Из ТаблицаДанных Цикл
		
		Если  Не ЗначениеЗаполнено(СтрокаДанных.Заказ) Или (СтрокаДанных.Наличные = 0 И СтрокаДанных.БезНаличные = 0) Тогда
			МассивНеНужныхСтрок.Добавить(СтрокаДанных);
		Конецесли;	
		
	КонецЦикла;	
	
	Для Каждого СтрокаКУдалению Из МассивНеНужныхСтрок Цикл
		ТаблицаДанных.Удалить(СтрокаКУдалению);
	КонецЦикла;		
	
	Запрос = НОвый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ТаблицаДанных.Заказ КАК Заказ,
	               |	ТаблицаДанных.Наличные КАК СуммаНаличные,
	               |	ТаблицаДанных.БезНаличные КАК СуммаТерминал
	               |ПОМЕСТИТЬ ТаблицаЗаказовПред
	               |ИЗ
	               |	&ТаблицаДанных КАК ТаблицаДанных
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ТаблицаЗаказовПред.Заказ КАК Заказ,
	               |	ТаблицаЗаказовПред.СуммаНаличные КАК СуммаНаличные,
	               |	ТаблицаЗаказовПред.СуммаТерминал КАК СуммаТерминал,
	               |	РеализацияТоваровУслуг.Ссылка КАК ЗаказСсылка,
	               |	РеализацияТоваровУслуг.ТерминалДоставки КАК ТерминалДоставки
	               |ПОМЕСТИТЬ ТаблицаЗаказов
	               |ИЗ
	               |	ТаблицаЗаказовПред КАК ТаблицаЗаказовПред
	               |		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
	               |		ПО ТаблицаЗаказовПред.Заказ = РеализацияТоваровУслуг.Ссылка
	               |ГДЕ
	               |	РеализацияТоваровУслуг.Ссылка В(&МассивРеализаций)
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ТаблицаЗаказов.ЗаказСсылка КАК ЗаказСсылка,
	               |	ТаблицаЗаказов.СуммаНаличные КАК СуммаНаличные,
	               |	ТаблицаЗаказов.СуммаТерминал КАК СуммаТерминал,
	               |	ВЫБОР
	               |		КОГДА НЕ ТаблицаЗаказов.ЗаказСсылка.ВладелецТовара.Родитель.ОсновнойМагазин = ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
	               |			ТОГДА ТаблицаЗаказов.ЗаказСсылка.ВладелецТовара.Родитель.ОсновнойМагазин
	               |		ИНАЧЕ ТаблицаЗаказов.ЗаказСсылка.ВладелецТовара
	               |	КОНЕЦ КАК Партнер,
	               |	ВЫБОР
	               |		КОГДА НЕ ТаблицаЗаказов.ЗаказСсылка.ВладелецТовара.Родитель.ОсновнойМагазин = ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
	               |			ТОГДА ТаблицаЗаказов.ЗаказСсылка.ВладелецТовара.Родитель.ОсновнойМагазин.ОсновнойДоговорКонтрагента.Организация
	               |		ИНАЧЕ ТаблицаЗаказов.ЗаказСсылка.ВладелецТовара.ОсновнойДоговорКонтрагента.Организация
	               |	КОНЕЦ КАК Организация,
	               |	ТаблицаЗаказов.ТерминалДоставки КАК ТерминалДоставки
	               |ПОМЕСТИТЬ ДанныеПоПартнерам
	               |ИЗ
	               |	ТаблицаЗаказов КАК ТаблицаЗаказов
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ДанныеПоПартнерам.ЗаказСсылка КАК Заказ,
	               |	ДанныеПоПартнерам.СуммаНаличные КАК СуммаНаличные,
	               |	ДанныеПоПартнерам.СуммаТерминал КАК СуммаТерминал,
	               |	ДанныеПоПартнерам.Партнер КАК Партнер,
	               |	ДанныеПоПартнерам.Организация КАК Организация,
	               |	ВыгрузкаДанныхВБухгалтериюСрезПоследних.Период КАК ДатаВыгрузки,
	               |	ПараметрыКонтрагентовСрезПоследних.Спецсчет КАК Спецсчет,
	               |	ДанныеПоПартнерам.ТерминалДоставки КАК ТерминалДоставки
	               |ПОМЕСТИТЬ ДанныеВыгрузки
	               |ИЗ
	               |	ДанныеПоПартнерам КАК ДанныеПоПартнерам
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ВыгрузкаДанныхВБухгалтерию.СрезПоследних(, ДатаВыгрузки = &ДатаВыгрузки) КАК ВыгрузкаДанныхВБухгалтериюСрезПоследних
	               |		ПО ДанныеПоПартнерам.Партнер = ВыгрузкаДанныхВБухгалтериюСрезПоследних.Партнер
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПараметрыКонтрагентов.СрезПоследних КАК ПараметрыКонтрагентовСрезПоследних
	               |		ПО ДанныеПоПартнерам.Партнер = ПараметрыКонтрагентовСрезПоследних.Контрагент
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ДанныеВыгрузки.Заказ КАК Заказ,
	               |	ДанныеВыгрузки.СуммаНаличные КАК СуммаНаличные,
	               |	ДанныеВыгрузки.СуммаТерминал КАК СуммаТерминал,
	               |	ДанныеВыгрузки.Партнер КАК Партнер,
	               |	ДанныеВыгрузки.Организация КАК Организация,
	               |	ДанныеВыгрузки.ДатаВыгрузки КАК ДатаВыгрузки,
	               |	ДанныеВыгрузки.Спецсчет КАК Спецсчет,
	               |	ПланыВыгрузкиДанныхВБухгалтерию.Ссылка КАК ПланВыгрузки,
	               |	ДанныеВыгрузки.ТерминалДоставки КАК ТерминалДоставки
	               |ИЗ
	               |	ДанныеВыгрузки КАК ДанныеВыгрузки
	               |		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ПланыВыгрузкиДанныхВБухгалтерию КАК ПланыВыгрузкиДанныхВБухгалтерию
	               |		ПО ДанныеВыгрузки.Организация = ПланыВыгрузкиДанныхВБухгалтерию.Организация
	               |			И ДанныеВыгрузки.Спецсчет = ПланыВыгрузкиДанныхВБухгалтерию.Спецсчет
	               |			И (ПланыВыгрузкиДанныхВБухгалтерию.ПланАктивен)";
	
	Запрос.УстановитьПараметр("ТаблицаДанных", 	ТаблицаДанных);
	Запрос.УстановитьПараметр("МассивРеализаций", ТаблицаДанных.ВыгрузитьКолонку("Заказ"));

	Запрос.УстановитьПараметр("ДатаВыгрузки", 	ПериодВыгрузки);

	ТаблицаВыгрузки = Запрос.Выполнить().Выгрузить();
	
	Если Не ДляФормы Тогда
		
		ТаблицаВыгрузки.Свернуть("Партнер, Организация, Спецсчет, ПланВыгрузки, ТерминалДоставки","СуммаНаличные, СуммаТерминал");

	КонецЕсли;	
	
	Возврат ТаблицаВыгрузки;
	
КонецФункции


Функция СкомпоноватьВТаблицуЗначений(СхемаКомпоновкиДанных, КомпоновщикНастроек) Экспорт

    //Запускаем компоновку
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
	МакетКомпоновки = КомпоновщикМакета.Выполнить(СхемаКомпоновкиДанных, КомпоновщикНастроек.Настройки,,,Тип("ГенераторМакетаКомпоновкиДанныхДляКоллекцииЗначений"));

	//Создаем процессор компоновки
	ПроцессорКомпоновки = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновки.Инициализировать(МакетКомпоновки);


	//Выводим в таблицу значений
	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВКоллекциюЗначений;
	ТЗ = ПроцессорВывода.Вывести(ПроцессорКомпоновки, истина);

	Возврат ТЗ;

КонецФункции

