#Если Клиент Тогда

Перем Форма;
Перем Панель;
Перем обКонтейнер;	
Перем ДействияФормыБП;
Перем ИндексКнопки;
	
Перем ИмяИнтерфейса;

//Отключение подключенных обработчиков
//
Процедура ВыполнитьОсвобождениеРесурсов() Экспорт
	
	Форма = Неопределено;
	Панель = Неопределено;
	обКонтейнер = Неопределено;
	ДействияФормыБП = Неопределено;
	ИндексКнопки = Неопределено;
	ИмяИнтерфейса = Неопределено;
	
КонецПроцедуры

// Обработчик события "Нажатие" кнопки просмотра карты бизнес-процесса.
//
Процедура ОткрытьКартуМаршрутаБП() Экспорт
	ФормаМаршрута = ПолучитьОбщуюФорму("новаФормаКартаМаршрутаБизнесПроцесса");
	КартаМаршрута = обКонтейнер.ПолучитьКартуМаршрута();
	ФормаМаршрута.ЭлементыФормы.КартаМаршрутаБП.УстановитьСхему(КартаМаршрута);
	ФормаМаршрута.ВладелецФормы = Форма;
	ФормаМаршрута.Открыть();
КонецПроцедуры

Процедура ПодключитьИнтерфейсИстории()
	
	ИмяИнтерфейса = новаРасширениеФорм.ДобавитьОбъектНаФорму(Форма, ЭтотОбъект);
	
	новаРасширениеФорм.УстановитьОбработчикСобытия(Форма, Форма,
		"ПриЗакрытии",
		0,
		ИмяИнтерфейса + ".ВыполнитьОсвобождениеРесурсов();");
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	Задачи.Автор.Представление КАК Автор,
	|	Задачи.ДатаВыполнения КАК ДатаВыполнения,
	|	Задачи.Наименование КАК ТочкаМаршрута
	|ИЗ
	|	Задача." + обКонтейнер.Метаданные().Задача.Имя + " КАК Задачи
	|ГДЕ
	|	Задачи.БизнесПроцесс = &БизнесПроцесс";
	
	Запрос.УстановитьПараметр("БизнесПроцесс", обКонтейнер.Ссылка);

	Результат = Запрос.Выполнить();
	ВыборкаЗадач = Результат.Выбрать();
	
	Если ВыборкаЗадач.Количество() = 0 Тогда Возврат; КонецЕсли;
 		
	текСтраница = Панель.ТекущаяСтраница;
	Страница = Панель.Страницы.Добавить(, "История");
	Панель.ТекущаяСтраница = Страница;

	Отступ = 6;
	ШиринаРаздела = Панель.Ширина - Отступ * 2.5;
	ВысотаРаздела = Панель.Высота - Отступ * 5;
	ТабличноеПоле = новаРасширениеФорм.ДобавитьЭлементНаФорму(Форма, Тип("ТабличноеПоле"), Панель);
	ТабличноеПоле.Лево = Отступ;
	ТабличноеПоле.Ширина = ШиринаРаздела;
	ТабличноеПоле.Верх = Отступ;
	ТабличноеПоле.Высота = ВысотаРаздела;
	ТабличноеПоле.УстановитьПривязку(ГраницаЭлементаУправления.Право, Панель, ГраницаЭлементаУправления.Право);
	ТабличноеПоле.УстановитьПривязку(ГраницаЭлементаУправления.Низ, Панель, ГраницаЭлементаУправления.Низ);
	ТабличноеПоле.ТолькоПросмотр = Истина;
	ТабличноеПоле.ИзменятьСоставСтрок = Ложь;
	ТабличноеПоле.АвтоКонтекстноеМеню = Ложь;
	ТабличноеПоле.Шапка = Истина;
	ТабличноеПоле.ИзменяетДанные = Истина;
	ТабличноеПоле.ВертикальныеЛинии = Истина;
	ТабличноеПоле.ГоризонтальныеЛинии = Истина;
	
	Колонка = ТабличноеПоле.Колонки.Добавить("ДатаВыполнения", "Дата выполнения");
	Колонка.Данные = "ДатаВыполнения";
	Колонка.Ширина = 16;
	Колонка.ИзменениеРазмера = ИзменениеРазмераКолонки.НеИзменять;
	
	Колонка = ТабличноеПоле.Колонки.Добавить("Автор", "Автор");
	Колонка.Данные = "Автор";
		
	Колонка = ТабличноеПоле.Колонки.Добавить("ТочкаМаршрута", "Точка маршрута");
	Колонка.Данные = "ТочкаМаршрута";
		
	ТабличноеПоле.Значение = Новый ТаблицаЗначений;
	ТабличноеПоле.Значение.Колонки.Добавить("ДатаВыполнения");
	ТабличноеПоле.Значение.Колонки.Добавить("Автор");
	ТабличноеПоле.Значение.Колонки.Добавить("ТочкаМаршрута");
	
	Пока ВыборкаЗадач.Следующий() Цикл
		стрИстория = ТабличноеПоле.Значение.Добавить();
		ЗаполнитьЗначенияСвойств(стрИстория,ВыборкаЗадач);
	КонецЦикла;
	
	Панель.ТекущаяСтраница = текСтраница;
	
КонецПроцедуры

Процедура ПодключитьИнтерфейсМаршрутаБП()
	ДействияФормыБП.Кнопки.Вставить(ИндексКнопки,, ТипКнопкиКоманднойПанели.Разделитель);
	
	Кнопка = ДействияФормыБП.Кнопки.Вставить(ИндексКнопки + 1 ,"КартыМаршрутаБП", ТипКнопкиКоманднойПанели.Действие, "Карта маршрута БП");
	Кнопка.Подсказка = "Карта маршрута бизнес-процесса";
	Кнопка.Пояснение = Кнопка.Подсказка;
	Кнопка.Картинка = БиблиотекаКартинок.ГрафическаяСхема;
	новаРасширениеФорм.УстановитьОбработчикСобытия(Форма, 
		Кнопка,
		"Нажатие",
		0,
		ИмяИнтерфейса + ".ОткрытьКартуМаршрутаБП();");
КонецПроцедуры

// Подключает интерфейс к форме.
//
// Параметры:
//  ФормаБП             <Форма>
//  Контейнер           <БизнесПроцессОбъект>
//  ПанельДляРазмещения <Панель>
//
Процедура ПодключитьИнтерфейс(ФормаБП, Контейнер, ПанельДляРазмещения) Экспорт
	Форма = ФормаБП;
	Панель = ПанельДляРазмещения;
	обКонтейнер = Контейнер;
	
	ДействияФормыБП = Форма.ЭлементыФормы.ДействияФормы;
	ИндексКнопки = Форма.ЭлементыФормы.ДействияФормы.Кнопки.Количество();
	
	ПодключитьИнтерфейсИстории();
	ПодключитьИнтерфейсМаршрутаБП();
КонецПроцедуры
	
#КонецЕсли