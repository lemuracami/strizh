Процедура СформироватьДанные() Экспорт
	#Если Клиент Тогда
		Сообщить("Начало загрузки документов с сервера ТСД (" + Формат(ТекущаяДата(), "ДЛФ=DDT") + ")");
	#КонецеСли
	
	lem.РегламентЗагрузкаДанныхСТСД();
	
	#Если Клиент Тогда
		Сообщить("Загрузка документов с сервера ТСД окончена (" + Формат(ТекущаяДата(), "ДЛФ=DDT") + ")");
		Сообщить("-----------------------------------");
		Сообщить("Формирование маршрутных листов (" + Формат(ТекущаяДата(), "ДЛФ=DDT") + ")");
	#КонецеСли
	
	Зап = Новый Запрос;
	
	Зап.Текст = "ВЫБРАТЬ
	            |	ЗагрузкаСТСДШтрихкоды.Заказ,
	            |	ЗагрузкаСТСДШтрихкоды.КоличествоМест
	            |ПОМЕСТИТЬ ВТПриход
	            |ИЗ
	            |	Документ.ЗагрузкаСТСД.Штрихкоды КАК ЗагрузкаСТСДШтрихкоды
	            |ГДЕ
	            |	ЗагрузкаСТСДШтрихкоды.Ссылка.ТипЗагрузкиТСД В (ЗНАЧЕНИЕ(Перечисление.ТипыЗагрузкиТСД.ЗагрузкаТранспортаWiFiПоСекторам), ЗНАЧЕНИЕ(Перечисление.ТипыЗагрузкиТСД.ПриходWiFiПоСекторам), ЗНАЧЕНИЕ(Перечисление.ТипыЗагрузкиТСД.ПриходWiFi))
	            |;
	            |
	            |////////////////////////////////////////////////////////////////////////////////
	            |ВЫБРАТЬ
	            |	ЗагрузкаСТСДШтрихкоды.Заказ,
	            |	ЗагрузкаСТСДШтрихкоды.Ссылка.Ссылка КАК ДокТСД,
	            |	СУММА(ЗагрузкаСТСДШтрихкоды.КоличествоМест) КАК КоличествоМест,
	            |	новаЗаданияРейсов.Рейс
	            |ПОМЕСТИТЬ ВТЗагруженныеЗаказы
	            |ИЗ
	            |	Документ.ЗагрузкаСТСД.Штрихкоды КАК ЗагрузкаСТСДШтрихкоды
	            |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.новаЗаданияРейсов КАК новаЗаданияРейсов
	            |		ПО (ВЫРАЗИТЬ(ЗагрузкаСТСДШтрихкоды.Заказ КАК Документ.РеализацияТоваровУслуг).Номер = новаЗаданияРейсов.Доставка.Номер)
	            |ГДЕ
	            |	ЗагрузкаСТСДШтрихкоды.Ссылка.Дата >= &ДатаНачалаВыборкиДанных
	            |	И ЗагрузкаСТСДШтрихкоды.Ссылка.ТипЗагрузкиТСД = ЗНАЧЕНИЕ(Перечисление.ТипыЗагрузкиТСД.ЗагрузкаТранспортаWiFiПоСекторам)
	            |	И (ВЫРАЗИТЬ(ЗагрузкаСТСДШтрихкоды.Заказ КАК Документ.РеализацияТоваровУслуг)) <> ЗНАЧЕНИЕ(Документ.РеализацияТоваровУслуг.ПустаяСсылка)
	            |
	            |СГРУППИРОВАТЬ ПО
	            |	ЗагрузкаСТСДШтрихкоды.Заказ,
	            |	ЗагрузкаСТСДШтрихкоды.Ссылка.Ссылка,
	            |	новаЗаданияРейсов.Рейс
	            |;
	            |
	            |////////////////////////////////////////////////////////////////////////////////
	            |ВЫБРАТЬ
	            |	ВТЗагруженныеЗаказы.Заказ,
	            |	ЕСТЬNULL(МаршрутныйЛистСкладаШтрихкоды.Ссылка, ИСТИНА) КАК КОтбору,
	            |	ВТЗагруженныеЗаказы.ДокТСД,
	            |	СУММА(ВТЗагруженныеЗаказы.КоличествоМест) КАК КоличествоМест,
	            |	ВТЗагруженныеЗаказы.Рейс
	            |ПОМЕСТИТЬ ВТЗаказыКФормированию
	            |ИЗ
	            |	ВТЗагруженныеЗаказы КАК ВТЗагруженныеЗаказы
	            |		ЛЕВОЕ СОЕДИНЕНИЕ Документ.МаршрутныйЛистСклада.Штрихкоды КАК МаршрутныйЛистСкладаШтрихкоды
	            |		ПО (ВЫРАЗИТЬ(ВТЗагруженныеЗаказы.Заказ КАК Документ.РеализацияТоваровУслуг).Ссылка = (ВЫРАЗИТЬ(МаршрутныйЛистСкладаШтрихкоды.Заказ.Ссылка КАК Документ.РеализацияТоваровУслуг))
	            |				И МаршрутныйЛистСкладаШтрихкоды.Ссылка.ПометкаУдаления = ЛОЖЬ)
	            |
	            |СГРУППИРОВАТЬ ПО
	            |	ВТЗагруженныеЗаказы.Заказ,
	            |	ЕСТЬNULL(МаршрутныйЛистСкладаШтрихкоды.Ссылка, ИСТИНА),
	            |	ВТЗагруженныеЗаказы.ДокТСД,
	            |	ВТЗагруженныеЗаказы.Рейс
	            |;
	            |
	            |////////////////////////////////////////////////////////////////////////////////
	            |ВЫБРАТЬ
	            |	новаЗаданияРейсов.Рейс,
	            |	РеализацияТоваровУслуг.Ссылка КАК Заказ
	            |ПОМЕСТИТЬ ВТЗаказыПоРейсам
	            |ИЗ
	            |	РегистрСведений.новаЗаданияРейсов КАК новаЗаданияРейсов
	            |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
	            |		ПО новаЗаданияРейсов.Доставка.Номер = РеализацияТоваровУслуг.Номер
	            |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТЗаказыКФормированию КАК ВТЗаказыКФормированию
	            |		ПО новаЗаданияРейсов.Рейс = ВТЗаказыКФормированию.Рейс
	            |ГДЕ
	            |	новаЗаданияРейсов.Доставка.Дата >= &ДатаНачалаВыборкиДанных
	            |	И новаЗаданияРейсов.МаршрутСопровождения = ЛОЖЬ
	            |	И новаЗаданияРейсов.Рейс.ДатаНачала >= &ДатаНачалаВыборкиДанных
	            |;
	            |
	            |////////////////////////////////////////////////////////////////////////////////
	            |ВЫБРАТЬ
	            |	ВТЗаказыПоРейсам.Заказ,
	            |	ВТЗаказыКФормированию.ДокТСД КАК ДокТСД,
	            |	ВТЗаказыПоРейсам.Рейс КАК Рейс,
	            |	ВТЗаказыПоРейсам.Рейс.Транспорт КАК Транспорт,
	            |	ВЫБОР
	            |		КОГДА ЕСТЬNULL(ВТПриход.Заказ, ИСТИНА) = ИСТИНА
	            |			ТОГДА ЛОЖЬ
	            |		ИНАЧЕ ИСТИНА
	            |	КОНЕЦ КАК ЕстьВПриходе,
	            |	ВЫБОР
	            |		КОГДА ЕСТЬNULL(ВТЗаказыКФормированию.Заказ, ИСТИНА) = ИСТИНА
	            |			ТОГДА ЛОЖЬ
	            |		ИНАЧЕ ИСТИНА
	            |	КОНЕЦ КАК Отсканирован,
	            |	ВТПриход.КоличествоМест КАК КоличествоМестВПриходе,
	            |	ЕСТЬNULL(ВТЗаказыКФормированию.КоличествоМест, 0) КАК Поле1
	            |ИЗ
	            |	ВТЗаказыПоРейсам КАК ВТЗаказыПоРейсам
	            |		ЛЕВОЕ СОЕДИНЕНИЕ ВТЗаказыКФормированию КАК ВТЗаказыКФормированию
	            |			ЛЕВОЕ СОЕДИНЕНИЕ ВТПриход КАК ВТПриход
	            |			ПО ((ВЫРАЗИТЬ(ВТЗаказыКФормированию.Заказ КАК Документ.РеализацияТоваровУслуг)) = (ВЫРАЗИТЬ(ВТПриход.Заказ КАК Документ.РеализацияТоваровУслуг)))
	            |		ПО (ВТЗаказыКФормированию.КОтбору = ИСТИНА)
	            |			И (ВТЗаказыПоРейсам.Заказ = (ВЫРАЗИТЬ(ВТЗаказыКФормированию.Заказ КАК Документ.РеализацияТоваровУслуг)))
	            |
	            |СГРУППИРОВАТЬ ПО
	            |	ВТЗаказыКФормированию.ДокТСД,
	            |	ВТЗаказыПоРейсам.Рейс,
	            |	ВТЗаказыПоРейсам.Рейс.Транспорт,
	            |	ВТПриход.КоличествоМест,
	            |	ВЫБОР
	            |		КОГДА ЕСТЬNULL(ВТПриход.Заказ, ИСТИНА) = ИСТИНА
	            |			ТОГДА ЛОЖЬ
	            |		ИНАЧЕ ИСТИНА
	            |	КОНЕЦ,
	            |	ВТЗаказыПоРейсам.Заказ,
	            |	ЕСТЬNULL(ВТЗаказыКФормированию.КоличествоМест, 0),
	            |	ВЫБОР
	            |		КОГДА ЕСТЬNULL(ВТЗаказыКФормированию.Заказ, ИСТИНА) = ИСТИНА
	            |			ТОГДА ЛОЖЬ
	            |		ИНАЧЕ ИСТИНА
	            |	КОНЕЦ
	            |ИТОГИ ПО
	            |	Рейс,
	            |	ДокТСД";
				
	Зап.УстановитьПараметр("ДатаНачалаВыборкиДанных", НачалоДня(ТекущаяДата()));	
	
	Рез = Зап.Выполнить();
	
	ДЗ = Рез.Выгрузить(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Для Каждого СтрокаРейс Из ДЗ.Строки Цикл
		НовДок = Документы.МаршрутныйЛистСклада.СоздатьДокумент();
		НовДок.Рейс = СтрокаРейс.Рейс;
		НовДок.Транспорт = СтрокаРейс.Транспорт;
		ОтсканированХотьОдин = Ложь;
		Для Каждого СтрокаТСД Из СтрокаРейс.Строки Цикл
			НовТСД = НовДок.ЗагрузкиТСД.Добавить();
			НовТСД.ЗагрузкаТСД = СтрокаТСД.ДокТСД;
			Для Каждого СтрокаЗаказ Из СтрокаТСД.Строки Цикл
				Нов = НовДок.Штрихкоды.Добавить();
				ЗаполнитьЗначенияСвойств(Нов, СтрокаЗаказ);
				Если Не ОтсканированХотьОдин Тогда
					ОтсканированХотьОдин = СтрокаЗаказ.Отсканирован;
				КонецеСли;	
			КонецЦикла;	
		КонецЦикла;	
		НовДок.Дата = ТекущаяДата();
		Если ОтсканированХотьОдин Тогда
			НовДок.Записать(РежимЗаписиДокумента.Запись);
			#Если Клиент Тогда
				Сообщить("-------------------" + НовДок.Транспорт.Наименование + "-------------------");
			#КонецеСли
		КонецеСли;	
	КонецЦикла;	
	
	#Если Клиент Тогда
		Сообщить("-----------------------------------");
		Сообщить("Формирование маршрутных листов окончено (" + Формат(ТекущаяДата(), "ДЛФ=DDT") + ")");
	#КонецеСли
КонецПроцедуры	