
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	//Вставить содержимое обработчика
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ПриОткрытииНаСервере();
	
	ПериодУдаления = "12";
	ПолучитьТекстПредупреждения();
	
КонецПроцедуры

&НаСервере
Процедура ПриОткрытииНаСервере()
	
	Состав = Метаданные.ПланыОбмена["ИзмененияДляМП"].Состав;
	
	Для каждого Элемент из Состав Цикл
        
        Стр = СоставПланаОбмена.Добавить();
        Стр.ОбъектМетаданных = Элемент.Метаданные.ПолноеИмя();
				
	КонецЦикла;
	
КонецПроцедуры


&НаСервере
Функция ПолучитьУзлыДляУдаленияМПоДаннымЗаказаВодителя(ДатаПоследнегоДокумента)
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ДанныеЗаказаВодителя.УзелОбмена КАК УзелОбмена,
	               |	МАКСИМУМ(ДанныеЗаказаВодителя.Дата) КАК ДатаПоследнегоДокумента
	               |ПОМЕСТИТЬ УзлыБывшиеВДокументе
	               |ИЗ
	               |	Документ.ДанныеЗаказаВодителя КАК ДанныеЗаказаВодителя
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ДанныеЗаказаВодителя.УзелОбмена
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ДанныеЗаказаВодителя.УзелОбмена КАК УзелОбмена,
	               |	МАКСИМУМ(ДанныеЗаказаВодителя.Дата) КАК ДатаПоследнегоДокумента
	               |ИЗ
	               |	Документ.ДанныеЗаказаВодителя КАК ДанныеЗаказаВодителя
	               |ГДЕ
	               |	НЕ ДанныеЗаказаВодителя.УзелОбмена.ЭтотУзел
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ДанныеЗаказаВодителя.УзелОбмена
	               |
	               |ИМЕЮЩИЕ
	               |	МАКСИМУМ(ДанныеЗаказаВодителя.Дата) < &ДатаПоследнегоДокумента
	               |
	               |ОБЪЕДИНИТЬ ВСЕ
	               |
	               |ВЫБРАТЬ
	               |	ИзмененияДляМП.Ссылка,
	               |	NULL
	               |ИЗ
	               |	ПланОбмена.ИзмененияДляМП КАК ИзмененияДляМП
	               |ГДЕ
	               |	НЕ ИзмененияДляМП.Ссылка В
	               |				(ВЫБРАТЬ
	               |					ВТ.УзелОбмена
	               |				ИЗ
	               |					УзлыБывшиеВДокументе КАК ВТ)
	               |	И НЕ ИзмененияДляМП.ЭтотУзел";
	
	Запрос.УстановитьПараметр("ДатаПоследнегоДокумента", ДатаПоследнегоДокумента);
	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	
	МассивУзлов = Новый Массив;
		
	Пока Выборка.Следующий() Цикл
		МассивУзлов.Добавить(Выборка.УзелОбмена);
	КонецЦикла;
	
	Возврат МассивУзлов;
КонецФункции


&НаСервере
Функция ПолучитьМассивВыбранныхМетаданных()
	МассивВыбранныхМетаданных = Новый Массив;
	Для Каждого Элем из СоставПланаОбмена Цикл
		Если Элем.Выбрано Тогда
			 МассивВыбранныхМетаданных.Добавить(Элем.ОбъектМетаданных);
		КонецЕсли;	 
	КонецЦикла;
	Возврат МассивВыбранныхМетаданных;
КонецФункции

// Якурнов 27.07.2018 13:01:35 экспорная Главная!!!
&НаСервере
Процедура ОчиститьРегистрациюИзмененийПоУзлам(УзлыДляУдаления,МассивПолноеИмяМетаданных) Экспорт
	Для Каждого ПолноеИмяМетаданных Из МассивПолноеИмяМетаданных Цикл
		ПланыОбмена.УдалитьРегистрациюИзменений(УзлыДляУдаления,Метаданные.НайтиПоПолномуИмени(ПолноеИмяМетаданных));
	КонецЦикла;
КонецПроцедуры


&НаКлиенте
Процедура ПолучитьТекстПредупреждения()
	НадписьПредупреждения = "Внимание!!! Будет произведена очистка зарегистрированных изменений для "
	+ Новый ФорматированнаяСтрока("ИзмененияДляМП", Новый Шрифт(,,12,Истина))
	+ " " + Элементы.ПериодУдаления.ТекстРедактирования;
КонецПроцедуры


&НаКлиенте
Процедура ПериодУдаленияПриИзменении(Элемент)
	ПолучитьТекстПредупреждения();
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьОчисткуРегистрацииИзмененияДляМП(Команда)
	
	
	Режим = РежимДиалогаВопрос.ДаНет;
	Ответ = Неопределено;
	
	ПоказатьВопрос(Новый ОписаниеОповещения("ВыполнитьОчисткуРегистрацииИзмененияДляМПЗавершение", ЭтотОбъект), "Осторожно!!! Вы точно хотите очистить записи изменений?", Режим, 0,КодВозвратаДиалога.Нет, "Внимание!!!");
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьОчисткуРегистрацииИзмененияДляМПЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Ответ = РезультатВопроса;
	Если Ответ = КодВозвратаДиалога.Нет Тогда
		Возврат;
	Иначе
		
		ТекДок = Новый ТекстовыйДокумент;
		ВыполнитьОчисткуРегистрацииИзмененияДляМПНаСервере(ТекДок);
		ТекДок.Показать();
		
	КонецЕсли;

КонецПроцедуры


&НаСервере
Процедура ВыполнитьОчисткуРегистрацииИзмененияДляМПНаСервере(ТекДок)
	
	ДатаПосл = ДобавитьМесяц(ТекущаяДата(), -Число(ПериодУдаления));
	МассивУзлов = ПолучитьУзлыДляУдаленияМПоДаннымЗаказаВодителя(ДатаПосл);
	МассивМетаданных = ПолучитьМассивВыбранныхМетаданных();
	
	ОчиститьРегистрациюИзмененийПоУзлам(МассивУзлов,МассивМетаданных);
	
	Для Каждого Элем из МассивУзлов Цикл 
		ТекДок.ДобавитьСтроку(Строка(Элем));
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура КнВыделить(Команда)
	Для Каждого Элем из СоставПланаОбмена Цикл
		Элем.Выбрано = Истина;
	КонецЦикла;	
КонецПроцедуры

&НаКлиенте
Процедура КнСнять(Команда)
	Для Каждого Элем из СоставПланаОбмена Цикл
		Элем.Выбрано = Ложь;
	КонецЦикла;
КонецПроцедуры
