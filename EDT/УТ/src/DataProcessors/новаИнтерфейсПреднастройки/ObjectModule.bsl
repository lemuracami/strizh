// Объект, к которому подключается интерфейс
//
Перем обКонтейнер Экспорт; // <БизнесПроцессОбъект>

// Форма, к которой подключается интерфейс
//
Перем Форма Экспорт; // <Форма>

// Признак преднастройки, используется при формировании интерфейса
//
Перем Преднастройка Экспорт; // <Булево>

// Признак периодичности, используется при формировании интерфейса
//
Перем Периодичность Экспорт; // <Булево>

// Признак группировок, используется при формировании интерфейса
//
Перем Группировки Экспорт; // <Булево>

Перем тзМакеты;
Перем мсПериодичность;
Перем ИмяИнтерфейса;
Перем Индекс;

#Если Клиент Тогда
// Подключает к форме интерфейс.
//
Процедура ПодключитьИнтерфейс() Экспорт
	ИмяИнтерфейса = новаРасширениеФорм.ДобавитьОбъектНаФорму(Форма, ЭтотОбъект);

	Если Преднастройка = Истина Тогда
		ПолучитьСписокПреднастроек();
		ПодключитьИнтерфейсПреднастройки();
	КонецЕсли;
	
	Если Периодичность = Истина Тогда
		мсПериодичность.Добавить("День");
		мсПериодичность.Добавить("Неделя");
		мсПериодичность.Добавить("Месяц");
		ПодключитьИнтерфейсПериодичности();		
	КонецЕсли;
	
	Если Группировки = Истина Тогда
		ПодключитьИнтерфейсГруппировки();	    	
	КонецЕсли; 
КонецПроцедуры

Процедура ПолучитьСписокПреднастроек()
	Для Каждого Макет Из обКонтейнер.Метаданные().Макеты Цикл
		Если Найти(Макет.Имя, "Преднастройка") Тогда
			стрМакет = тзМакеты.Добавить();
			стрМакет.Имя = Макет.Имя;
			стрМакет.Синоним = Макет.Синоним;
		КонецЕсли; 		
	КонецЦикла;
КонецПроцедуры

Процедура ПодключитьИнтерфейсПреднастройки()
	КоманднаяПанель = Форма.ЭлементыФормы.ДействияФормы;
	Если тзМакеты.Количество() > 0 Тогда
		КоманднаяПанель.Кнопки.Вставить(Индекс,, ТипКнопкиКоманднойПанели.Разделитель);
		Индекс = Индекс + 1;
		КнопкаМенюПреднастройки = КоманднаяПанель.Кнопки.Вставить(Индекс,, ТипКнопкиКоманднойПанели.Подменю, "Вид отчета");
		Индекс = Индекс + 1;
		СоздатьКнопкиПреднастройки(КнопкаМенюПреднастройки.Кнопки);
	КонецЕсли;
КонецПроцедуры

Процедура СоздатьКнопкиПреднастройки(Кнопки)
	Для Каждого стрМакет Из тзМакеты Цикл
		Кнопка = Кнопки.Вставить(0, стрМакет.Имя, ТипКнопкиКоманднойПанели.Действие, стрМакет.Синоним);
		новаРасширениеФорм.УстановитьОбработчикСобытия(Форма, Кнопка,
			"Нажатие",
			0,
			ИмяИнтерфейса + ".ИспользоватьПреднастройку(ЭтаФорма, ЭтотОбъект, Параметр1);");
	КонецЦикла;
КонецПроцедуры

// Использует преднастройку для формы
//
// Параметры:
//  Форма  <Форма>
//  Объект <Произвольный>
//  Кнопка <Кнопка>
//
Процедура ИспользоватьПреднастройку(Форма, Объект, Кнопка) Экспорт
	Макет = Объект.ПолучитьМакет(Кнопка.Имя);
	СтрокаXML = Макет.ПолучитьТекст();
	
	ЧтениеXML = Новый ЧтениеXML;
	ЧтениеXML.УстановитьСтроку(СтрокаXML);
	Значение = СериализаторXDTO.ПрочитатьXML(ЧтениеXML);
	
	Форма.ЖурналНастроек.Преднастройка = Кнопка.Текст;
	
	Форма.Сформировать(Значение);
КонецПроцедуры

Процедура ПодключитьИнтерфейсПериодичности()
	КоманднаяПанель = Форма.ЭлементыФормы.ДействияФормы;
	КнопкаМенюПериодичность = КоманднаяПанель.Кнопки.Вставить(Индекс,, ТипКнопкиКоманднойПанели.Подменю, "Периодичность");
	Индекс = Индекс + 1;
	СоздатьКнопкиПериодичности(КнопкаМенюПериодичность.Кнопки);
КонецПроцедуры

Процедура СоздатьКнопкиПериодичности(Кнопки)
	Для каждого элПериодичность Из мсПериодичность Цикл
	    Кнопка = Кнопки.Вставить(0, элПериодичность, ТипКнопкиКоманднойПанели.Действие, элПериодичность);
		новаРасширениеФорм.УстановитьОбработчикСобытия(Форма, Кнопка,
			"Нажатие",
			0,
			ИмяИнтерфейса + ".ИспользоватьПериодичность(ЭтаФорма, Параметр1);");
	КонецЦикла; 		
КонецПроцедуры
 
// Выполняет настройку периода в форме
//
// Параметры:
//  Форма  <Форма>
//  Кнопка <Кнопка>
//
Процедура ИспользоватьПериодичность(Форма, Кнопка) Экспорт
	Если Кнопка.Имя = "День" Тогда
		Форма.Периодичность.Значение = ТипДополненияПериодаКомпоновкиДанных.День;
		Форма.Периодичность.ИмяПоля = "ПериодДень";
		Форма.ЖурналНастроек.Периодичность = "День";
	ИначеЕсли Кнопка.Имя = "Неделя" Тогда
		Форма.Периодичность.Значение = ТипДополненияПериодаКомпоновкиДанных.Неделя;
		Форма.Периодичность.ИмяПоля = "ПериодНеделя";
		Форма.ЖурналНастроек.Периодичность = "Неделя";
	Иначе
		Форма.Периодичность.Значение = ТипДополненияПериодаКомпоновкиДанных.Месяц;	
		Форма.Периодичность.ИмяПоля = "ПериодМесяц";
		Форма.ЖурналНастроек.Периодичность = "Месяц";
	КонецЕсли;
	Форма.Сформировать();
КонецПроцедуры

Процедура ПодключитьИнтерфейсГруппировки()
	КоманднаяПанель = Форма.ЭлементыФормы.ДействияФормы;
	КнопкаМенюГруппировки = КоманднаяПанель.Кнопки.Вставить(Индекс,, ТипКнопкиКоманднойПанели.Подменю, "Группировки");
	Индекс = Индекс + 1;
	СоздатьКнопкиГруппировки(КнопкаМенюГруппировки.Кнопки);
КонецПроцедуры

Процедура СоздатьКнопкиГруппировки(Кнопки)
	Для каждого стрГруппировка Из Форма.тзГруппировки Цикл
	    Кнопка = Кнопки.Вставить(0, стрГруппировка.Поле, ТипКнопкиКоманднойПанели.Действие, стрГруппировка.Представление);
		новаРасширениеФорм.УстановитьОбработчикСобытия(Форма, Кнопка,
			"Нажатие",
			0,
			ИмяИнтерфейса + ".ИспользоватьГруппировку(ЭтаФорма, Параметр1);");
	КонецЦикла; 		
КонецПроцедуры
 
// Применяет группировку 
//
// Параметры:
//  Форма  <Форма>
//  Кнопка <Кнопка>
//
Процедура ИспользоватьГруппировку(Форма, Кнопка) Экспорт
	стрГруппировка = Форма.тзГруппировки.Найти(Истина, "Использовать");
    стрГруппировка.Использовать = Ложь;
	стрГруппировка = Форма.тзГруппировки.Найти(Кнопка.Имя);
	стрГруппировка.Использовать = Истина;
	Форма.Сформировать();
КонецПроцедуры
#КонецЕсли
 
тзМакеты = Новый ТаблицаЗначений;
тзМакеты.Колонки.Добавить("Имя");
тзМакеты.Колонки.Добавить("Синоним");

мсПериодичность = Новый Массив;

Индекс = 1;