// Модуль обмена данными с КИС
Перем СемафорГотовности;
Перем СемафорЗагрузки;
Перем СемафорЗанятости;
Перем СписокШаблонов;
Перем ПутьКАрхиву;

Перем стИмпортированныеДанные;
Перем обПланИмпорта;
Перем СостояниеГотовность;
Перем СостояниеЗагрузка;
Перем СостояниеОжидание;

Перем Обмен;
Перем ИмяАрхива;
Перем ИспользованиеСемафора;
Перем ДанныеЗагружены;

Функция ЗагрузитьДанныеПоОбмену(_Обмен) Экспорт;
	Обмен 			      = _Обмен;
	КаталогЗагрузки		  = _Обмен.КаталогЗагрузки;
	КаталогЗагрузки 	  = ПолучитьПутьКФайлам();
	ИспользованиеСемафора = _Обмен.ИспользованиеСемафораОбмена;
	
	Если НЕ МожноЗагружать()  Тогда
		Возврат ДанныеЗагружены; //Ложь;
	КонецЕсли; 

	ИмяАрхива 		= Формат(ТекущаяДата(), "ДФ=""ггггММддЧЧммсс""");
	ПутьКАрхиву 	= КаталогЗагрузки + "Архивы\" + ИмяАрхива;
	ПутьКАрхиву 	= ПутьКАрхиву + "\";

	Если НайтиФайлы(КаталогЗагрузки + "Архивы\").Количество() = 0 Тогда
		Попытка	
			СоздатьКаталог(КаталогЗагрузки + "Архивы\");
		Исключение
			Возврат Ложь;
		КонецПопытки;
	КонецЕсли;
	
	ЗагрузитьДанныеИзКИС();
	Возврат ДанныеЗагружены;
	
КонецФункции

Процедура ЗагрузитьДанныеИзКИС() Экспорт;

	РегистраторыКонтекст	= Неопределено;
	РегистраторСобытий 		= Неопределено;
	
	РегистраторыКонтекст = Неопределено;
#Если Клиент Тогда	
	новаКонтекст.Свойство("РегистраторСобытий", РегистраторыКонтекст);
#Иначе	
	Если РегистраторыКонтекст = Неопределено Тогда
		РегистраторыКонтекст = Новый Соответствие;
	КонецЕсли;
#КонецЕсли
	
	РегистраторСобытий = РегистраторыКонтекст.Получить(Обмен.Код);
	Если РегистраторСобытий = Неопределено Тогда
		РегистраторСобытий = Обработки.новаРегистраторСобытий.Создать();
		РегистраторСобытий.Заголовок = "Выполнение обмена данными по настройке: <"
										+ Обмен.Наименование+">";
		РегистраторыКонтекст.Вставить(Обмен.Код, РегистраторСобытий);
	КонецЕсли;
	РегистраторСобытий.Очистить();
	
	//Выставляем флаг что мы загружаем
	СменитьСемафор(СемафорГотовности, СемафорЗагрузки);
	//Выполняем импорт данных
	
	//Проверим существование всех файлов
	СуществуютВсеФайлы = Истина;
	Для Каждого Элемент Из Обмен.ИменаФайлов Цикл
		Если Не (НайтиФайлы(КаталогЗагрузки, Элемент.ИмяФайла).Количество() = 1) Тогда
			СуществуютВсеФайлы = Ложь;
			РегистраторСобытий.Добавить(СтатусСообщения.Внимание,,"Не найден файл "+КаталогЗагрузки + Элемент.ИмяФайла);
		КонецЕсли;
	КонецЦикла;
	
	Если СуществуютВсеФайлы Тогда
		Для Каждого ПланИмпорта Из Обмен.ПланыОбмена Цикл
			РегистраторСобытий.Добавить(СтатусСообщения.Информация,,"Начало обмена по плану: ", ПланИмпорта.ПланОбмена.Наименование);
			ПравилоИмпорта = ПланИмпорта.ПланОбмена;
	
			ВыполнитьИмпорт(ПравилоИмпорта, РегистраторСобытий);
	
			РегистраторСобытий.Добавить(СтатусСообщения.Информация,,"Завершен обмен по плану: ", ПланИмпорта.ПланОбмена.Наименование);
	
		КонецЦикла;
		РегистраторСобытий.Добавить(СтатусСообщения.Информация,,"Обмен завершен");
	Иначе
		РегистраторСобытий.Добавить(СтатусСообщения.Важное,,"Обмен остановлен");
	КонецЕсли;	
		
	Если ЗаписыватьЛог Тогда
		РегистраторСобытий.ИмяРезультирующегоФайла = Обмен.КаталогЗагрузки + "\"+"log.mxl";
	Иначе
		РегистраторСобытий.ИмяРезультирующегоФайла = "";
	КонецЕсли;
	Если ВыводитьЛог Тогда
		РегистраторСобытий.ТихийРежим = Ложь;
	Иначе
		РегистраторСобытий.ТихийРежим = Истина;
	КонецЕсли;
	РегистраторСобытий.Вывести();	
	
	//Перемещаем в Архив
	ПереместитьВАрхив();
	//После выполнения меняем флаг сообщая КИС Что данные загружены и ждем следующих
	СменитьСемафор(СемафорЗагрузки, СемафорЗанятости);
	
КонецПРоцедуры

Процедура СменитьСемафор(СемафорС,СемафорНа)
	Если ИспользованиеСемафора Тогда
		ПереместитьФайл(КаталогЗагрузки + СемафорС, КаталогЗагрузки + СемафорНа);
	КонецЕсли;
КонецПроцедуры

Функция МожноЗагружать()
	булМожноЗагружать = Ложь;
	Если ИспользованиеСемафора Тогда
		Если СостояниеСемафора() = СостояниеГотовность Тогда
			булМожноЗагружать = Истина;
		ИначеЕсли СостояниеСемафора() = "" Тогда
			//Если файла семафора нет - создаем
			ФайлСемафора = Новый ЗаписьТекста(КаталогЗагрузки + СемафорГотовности);
			ФайлСемафора.Закрыть();
			булМожноЗагружать = Истина;
		КонецЕсли; 
	Иначе
		булМожноЗагружать = Истина;
	КонецЕсли;
	Возврат булМожноЗагружать;
КонецФункции

Функция ПолучитьПутьКФайлам()
	//Формируем строку пути к файлам
	стрПуть = КаталогЗагрузки;
	стрПуть = СтрЗаменить(стрПуть, "/", "\");
	Если Не (Прав(стрПуть, 1) = "\") Тогда
		стрПуть = стрПуть + "\";
	КонецЕсли;
	Возврат стрПуть;
КонецФункции

Функция СостояниеСемафора()
	Если НайтиФайлы(КаталогЗагрузки, СемафорГотовности).Количество() = 1 Тогда
		Возврат СостояниеГотовность;
	ИначеЕсли  НайтиФайлы(КаталогЗагрузки, СемафорЗагрузки).Количество() = 1 Тогда
		Возврат СостояниеЗагрузка;
	ИначеЕсли  НайтиФайлы(КаталогЗагрузки, СемафорЗанятости).Количество() = 1 Тогда
		Возврат СостояниеОжидание;
	Иначе	
		Возврат "";
	КонецЕсли;
КонецФункции

Процедура ВыполнитьИмпорт(ПравилоИмпорта, РегистраторСобытий = Неопределено)
	//Получаем объект Справочника новаПланыимпортаДанных
	обПланИмпорта = ПравилоИмпорта.ПолучитьОбъект();
	стИмпортированныеДанные = обПланИмпорта.ВыполнитьИмпорт(Обмен);
	Если стИмпортированныеДанные = Неопределено Тогда 
		ДанныеЗагружены = Ложь;
		Возврат; 
	КонецЕсли;
	обПланИмпорта.СохранитьИмпортированныеДанные(стИмпортированныеДанные, РегистраторСобытий);
	ДанныеЗагружены = Истина;
КонецПроцедуры
                           
Процедура ПереместитьВАрхив()
	ЗаписьАрхива = Новый ЗаписьZipФайла(КаталогЗагрузки + "Архивы\"+ИмяАрхива+".zip");
	тзОбмен = Обмен.ИменаФайлов.Выгрузить(); //ИменаФайлов
	Для Каждого Строка Из тзОбмен Цикл
		 Строка.ИмяФайла = Врег(Строка.ИмяФайла);
	 КонецЦикла;
	ТзОбмен.Свернуть("ИмяФайла");
	 
	Если НайтиФайлы(КаталогЗагрузки, "log.mxl").Количество() = 1 Тогда
		ЗаписьАрхива.Добавить(КаталогЗагрузки + "log.mxl");
	КонецЕсли;
	
//	Для Каждого Строка Из Обмен.ИменаФайлов Цикл
	Для Каждого Строка Из тзОбмен Цикл
		Если Строка.ИмяФайла = "" Тогда
			Продолжить;
		КонецЕсли;
		Если НайтиФайлы(КаталогЗагрузки, Строка.ИмяФайла).Количество() = 1 Тогда
			ЗаписьАрхива.Добавить(КаталогЗагрузки + Строка.ИмяФайла);
		КонецЕсли;
	КонецЦикла;
	
	Попытка
		ЗаписьАрхива.Записать();
		//Удаляем все, что записано в архив
		Если НайтиФайлы(КаталогЗагрузки, "log.mxl").Количество() = 1 Тогда
			УдалитьФайлы(КаталогЗагрузки, "log.mxl");
		КонецЕсли;
	
//		Для Каждого Строка Из тзОбмен.ИменаФайлов Цикл
		Для Каждого Строка Из тзОбмен Цикл
			Если Строка.ИмяФайла = "" Тогда
				Продолжить;
			КонецЕсли;
			Если НайтиФайлы(КаталогЗагрузки, Строка.ИмяФайла).Количество() = 1 Тогда
				УдалитьФайлы(КаталогЗагрузки, Строка.ИмяФайла);
			КонецЕсли;
		КонецЦикла;
	Исключение
	КонецПопытки;
		
КонецПроцедуры

// Инициализация
СемафорГотовности 	= "Ready.sem";
СемафорЗагрузки 	= "Loading.sem";
СемафорЗанятости 	= "Waiting.sem";
СостояниеГотовность = "Готовность";
СостояниеЗагрузка	= "Загрузка";
СостояниеОжидание	= "Ожидание";
ДанныеЗагружены = Ложь;