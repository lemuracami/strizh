#Если Клиент Тогда

// Предопределенное свойство. Содержит объект, предоставляющий данные универсальному журналу.
//
Перем КонструкторДанных Экспорт; // <ОбработкаОбъект.новаКонструкторЗапроса>

// Предопределенное свойство. Контейнер параметров источника.
//
Перем ПараметрыЖурнала Экспорт; // <Структура[ПараметрыИсточникаУниверсальногоЖурнала]>

// Предопределенное свойство. Универсальный журнал, к которому подключен источник.
//
Перем Журнал Экспорт; // <ОбработкаОбъект.новаУниверсальныйЖурнал>

Перем СекундВДне;
Перем ЖурналНачалоПериода, ЖурналКонецПериода;

// Предопределенная процедура. Вызывается при выводе строки журнала.
//
// Параметры:
//  ОформлениеСтроки <ОформлениеСтроки>
//  ДанныеСтроки     <СтрокаДереваЗначений>
//
Процедура ПриВыводеСтроки(ОформлениеСтроки, ДанныеСтроки) Экспорт
	Если ДанныеСтроки.Группировка = NULL Тогда
		ОформлениеСтроки.Ячейки.Доступен.ОтображатьФлажок = Ложь;
		
		Если ДанныеСтроки.Доступен = Истина Тогда
			ОформлениеСтроки.Ячейки.Доступен.УстановитьТекст("Работает");
			ОформлениеСтроки.ЦветФона = Новый Цвет(200, 255, 200);
			
		ИначеЕсли ДанныеСтроки.Доступен = Ложь Тогда
			ОформлениеСтроки.Ячейки.Доступен.УстановитьТекст("Не работает");
			ОформлениеСтроки.ЦветФона = Новый Цвет(255, 150, 150);
			
		Иначе
			ОформлениеСтроки.Ячейки.Доступен.УстановитьТекст("Период не введен");
			ОформлениеСтроки.ЦветФона = Новый Цвет(255, 200, 200);
			
		КонецЕсли;
		
		Если ДанныеСтроки.НачалоПериода < ЖурналНачалоПериода Или ДанныеСтроки.НачалоПериода = '00010101' Тогда
			ОформлениеСтроки.Ячейки.НачалоПериода.УстановитьТекст("<<");
			ОформлениеСтроки.Ячейки.НачалоПериода.Шрифт = Новый Шрифт(,, Истина);
			ОформлениеСтроки.Ячейки.НачалоПериода.ЦветТекста = Новый Цвет(0, 0, 255);
		КонецЕсли;
		
		Если ДанныеСтроки.КонецПериода > ЖурналКонецПериода Или ДанныеСтроки.КонецПериода = '30000101' Тогда
			ОформлениеСтроки.Ячейки.КонецПериода.УстановитьТекст(">>");
			ОформлениеСтроки.Ячейки.КонецПериода.Шрифт = Новый Шрифт(,, Истина);
			ОформлениеСтроки.Ячейки.КонецПериода.ЦветТекста = Новый Цвет(0, 0, 255);
		КонецЕсли;
	КонецЕсли;
	
	Если Журнал.ЕстьИерархия Тогда
		Если ДанныеСтроки.Группировка <> NULL Тогда
			Если КонструкторДанных.Группировки[ДанныеСтроки.Группировка] = "Транспорт" Тогда
				ОформлениеСтроки.Шрифт = Новый Шрифт(,, Истина);
				ОформлениеСтроки.ЦветФона = Новый Цвет(216, 203, 166);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

// Предопределенная процедура. Обработчик событий журнала.
//
// Параметры:
//  Событие   <Строка>: имя события.
//  Параметр1 <ПроизвольноеЗначение>
//  Параметр2 <ПроизвольноеЗначение>
//  Параметр3 <ПроизвольноеЗначение>
//
// Возвращаемое значение:
//  <Булево>: Истина, если событие обработано.
//
Функция ОбработатьСобытие(Событие, Параметр1 = Неопределено, Параметр2 = Неопределено, Параметр3 = Неопределено) Экспорт
	Если Событие = "Инициализация" Тогда
		Настроить();
		Возврат Истина;
	
	ИначеЕсли Событие = "Выбор" Тогда
		Журнал.ТекущиеДанные = Параметр1;
		ИзменитьПериодДоступности();
		
	ИначеЕсли Событие = "ИзменениеПараметров" Тогда
		СформироватьОтбор();
		
	ИначеЕсли Событие = "ИспользованиеИнструмента" Тогда
		Если Параметр1 = "НовыйПериодДоступности" Тогда
			ИзменитьПериодДоступности();
		КонецЕсли;
	КонецЕсли;	
КонецФункции

Процедура ИзменитьПериодДоступности()
	обПланирование = Документы.новаПланированиеРаботыТранспорта.СоздатьДокумент();
	
	стрДоступность = обПланирование.ДоступностьТранспорта.Добавить();
	Если Журнал.ТекущиеДанные = Неопределено Тогда
		стрДоступность.Доступен = Истина;
	Иначе
		стрДоступность.Доступен = Журнал.ТекущиеДанные.Доступен;
		стрДоступность.Транспорт = Журнал.ТекущиеДанные.Транспорт;
		стрДоступность.Гараж = Журнал.ТекущиеДанные.Гараж;
		стрДоступность.НачалоПериода = Журнал.ТекущиеДанные.Транспорт;
		стрДоступность.КонецПериода = Журнал.ТекущиеДанные.Транспорт;
		стрДоступность.НачалоПериода = Журнал.ТекущиеДанные.НачалоПериода;
		стрДоступность.КонецПериода = ?(Журнал.ТекущиеДанные.КонецПериода = '30000101', '00010101', Журнал.ТекущиеДанные.КонецПериода);
	КонецЕсли;
	
	Форма = обПланирование.ПолучитьФорму("ФормаБыстрогоВвода");
	Форма.Открыть();
КонецПроцедуры

Процедура СформироватьОтбор()
	ЖурналНачалоПериода = ПараметрыЖурнала.ИнтерфейсВводаПериода.НачалоПериода;
	ЖурналКонецПериода = ПараметрыЖурнала.ИнтерфейсВводаПериода.КонецПериода;
	ЖурналКонецПериода = ?(ЗначениеЗаполнено(ЖурналКонецПериода), ЖурналКонецПериода, '30000101');

	КонструкторДанных.ДобавитьПараметр("ЖурналНачалоПериода", ЖурналНачалоПериода);
	КонструкторДанных.ДобавитьПараметр("ЖурналКонецПериода", ЖурналКонецПериода);
	
	// Михушкин --->> 
	Если РольДоступна("РегиональныйЛогист") Тогда
		ТерминалДоставки = ПараметрыСеанса.ТерминалДоставки;
		//ТерминалДоставки = Справочники.РегиональныеТерминалы.СПбСтриж;
		КонструкторДанных.ДобавитьПараметр("ТерминалДоставки", ТерминалДоставки);	
	КонецЕсли;	
	// <<--- Михушкин 	
	
	Если ЗначениеЗаполнено(ПараметрыЖурнала.ИнтерфейсВводаПериода.НачалоПериода) И ЗначениеЗаполнено(ПараметрыЖурнала.ИнтерфейсВводаПериода.КонецПериода) Тогда
		КонструкторДанных.ПостоянныйОтбор = "ПериодыДоступности.НачалоПериода <= &ЖурналКонецПериода И ПериодыДоступности.КонецПериода >= &ЖурналНачалоПериода";
		
	ИначеЕсли ЗначениеЗаполнено(ПараметрыЖурнала.ИнтерфейсВводаПериода.НачалоПериода) И Не ЗначениеЗаполнено(ПараметрыЖурнала.ИнтерфейсВводаПериода.КонецПериода) Тогда
		КонструкторДанных.ПостоянныйОтбор = "ПериодыДоступности.КонецПериода >= &ЖурналНачалоПериода";
		
	ИначеЕсли ЗначениеЗаполнено(ПараметрыЖурнала.ИнтерфейсВводаПериода.КонецПериода) И Не ЗначениеЗаполнено(ПараметрыЖурнала.ИнтерфейсВводаПериода.НачалоПериода) Тогда
		КонструкторДанных.ПостоянныйОтбор = "ПериодыДоступности.НачалоПериода <= &ЖурналКонецПериода";
		
	Иначе
		КонструкторДанных.ПостоянныйОтбор = "ИСТИНА";
	КонецЕсли;
	
	// Михушкин --->> 
	Если РольДоступна("РегиональныйЛогист") Тогда
		КонструкторДанных.ПостоянныйОтбор = КонструкторДанных.ПостоянныйОтбор + " И (ПериодыДоступности.Транспорт.ТерминалДоставки = &ТерминалДоставки ИЛИ ПериодыДоступности.Транспорт.Родитель.ТерминалДоставки = &ТерминалДоставки)";
	КонецЕсли;
	// <<--- Михушкин
	
КонецПроцедуры

Процедура Настроить()
	ПериодПоУмолчанию = Новый Структура("НачалоПериода, КонецПериода", НачалоГода(ТекущаяДата()), КонецГода(ТекущаяДата()));
	мсИнструменты = Новый Массив;
	мсИнструменты.Добавить(Новый Структура("Имя, Картинка, Текст", "НовыйПериодДоступности", БиблиотекаКартинок.ДобавитьЭлементСписка, "Ввести период доступности"));
	ПараметрыЖурнала = Новый Структура("ИнтерфейсВводаПериода, Инструменты, ОбновлятьПослеПеретаскивания", ПериодПоУмолчанию, мсИнструменты, Истина);

	КонструкторДанных = новаЖурналы.НовыйКонструкторЗапроса();
	КонструкторДанных.ПостОбработчикДанных = ЭтотОбъект;
	
	СформироватьОтбор();
	
	КонструкторДанных.ТекстЗапроса = "
	|РегистрСведений.новаДоступностьТранспорта КАК ПериодыДоступности
	|{СОЕДИНЕНИЯ}
	|";

	КонструкторДанных.ПостояннаяВыборка = ",
	|	ПериодыДоступности.Транспорт КАК Транспорт,
	|	Представление(ПериодыДоступности.Транспорт) КАК Транспорт_Пред,
	|	ПериодыДоступности.НачалоПериода КАК НачалоПериода,
	|	ПериодыДоступности.КонецПериода КАК КонецПериода,
	|	ПериодыДоступности.Гараж КАК Гараж,
	|	Представление(ПериодыДоступности.Гараж) КАК Гараж_Пред,
	|	ПериодыДоступности.Документ.Комментарий КАК Комментарий,
	|	ПериодыДоступности.Доступен КАК Доступен";
	
	КонструкторДанных.ДобавитьПоле("Транспорт");
	КонструкторДанных.ДобавитьОтборПоля(, "ПериодыДоступности.Транспорт");
	КонструкторДанных.ДобавитьСортировкуПоля("ПериодыДоступности.Транспорт.Наименование");
	
	КонструкторДанных.ДобавитьПоле("Доступен");
	КонструкторДанных.ИсключитьПолеИзОтбораИСортировки();
	
	КонструкторДанных.ДобавитьПоле("НачалоПериода");
	КонструкторДанных.ДобавитьПоле("КонецПериода");
	
	КонструкторДанных.ДобавитьПоле("Комментарий");
	КонструкторДанных.ДобавитьОтборПоля(, "ПериодыДоступности.Документ.Комментарий");
	
	КонструкторДанных.ДобавитьПоле("Гараж");
	КонструкторДанных.ДобавитьОтборПоля(, "ПериодыДоступности.Гараж");
	КонструкторДанных.ДобавитьСортировкуПоля("ПериодыДоступности.Гараж.Наименование");
	
	КонструкторДанных.ДобавитьДоступнуюГруппировку("Транспорт");
	
	КонструкторДанных.Ключи.Вставить(NULL, Новый Структура("Транспорт, НачалоПериода"));

	КонструкторДанных.СформироватьКолонки();
КонецПроцедуры

Процедура ОбработатьПериоды(Периоды)
	Если Периоды.Количество() = 0 Тогда Возврат; КонецЕсли;
	
	Периоды.Сортировать("НачалоПериода ВОЗР");
	
	мсПериоды = Новый Массив;
	Для Каждого Период Из Периоды Цикл мсПериоды.Добавить(Период); КонецЦикла;
	
	ПоследнийПериод = ЖурналНачалоПериода;
	ПоследнийПериодДата = '00010101';
	Для Каждого Период Из мсПериоды Цикл
		Если Период.НачалоПериода > ПоследнийПериод Тогда
			НовыйПериод = Периоды.Добавить();
			ЗаполнитьЗначенияСвойств(НовыйПериод, Период);
			НовыйПериод.Гараж = Неопределено;
			НовыйПериод.Гараж_Пред = "";
			НовыйПериод.Комментарий = "";
			НовыйПериод.Доступен = NULL;
			НовыйПериод.НачалоПериода = ПоследнийПериодДата;
			НовыйПериод.КонецПериода = Период.НачалоПериода - СекундВДне;
		КонецЕсли;
		ПоследнийПериод = Период.КонецПериода + СекундВДне;
		ПоследнийПериодДата = ПоследнийПериод;
	КонецЦикла;
	
	Если ПоследнийПериод < ЖурналКонецПериода Тогда
		НовыйПериод = Периоды.Добавить();
		ЗаполнитьЗначенияСвойств(НовыйПериод, Период);
		НовыйПериод.Гараж = Неопределено;
		НовыйПериод.Гараж_Пред = "";
		НовыйПериод.Комментарий = "";
		НовыйПериод.Доступен = NULL;
		НовыйПериод.НачалоПериода = ПоследнийПериодДата;
		НовыйПериод.КонецПериода = '30000101';
	КонецЕсли;
	
	Периоды.Сортировать("НачалоПериода ВОЗР");
КонецПроцедуры

Процедура ОбработатьГруппировку(Строки)
	Для Каждого Строка Из Строки Цикл
		Если Строка.Группировка = 0 Тогда
			ОбработатьПериоды(Строка.Строки);
		Иначе
			ОбработатьГруппировку(Строка.Строки);
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

// Предопределенная процедура. Вызывается источником данных для обработки результата запроса.
//
// Параметры:
//  дзДанные <ДеревоЗначений>
//
Процедура ПостОбработкаДанных(дзДанные) Экспорт
	Если дзДанные.Строки.Количество() > 0 Тогда
		ОбработатьГруппировку(дзДанные.Строки[0].Строки);
	КонецЕсли;
КонецПроцедуры

СекундВДне = 60 * 60 * 24;

#КонецЕсли