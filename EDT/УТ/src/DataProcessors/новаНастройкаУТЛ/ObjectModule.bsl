#Если Клиент Тогда

// Дерево страниц настройки, создается при создании обработки.
//
Перем ДеревоСтраниц Экспорт; // <ДеревоЗначений[ДеревоСтраницНастроек]>

Перем соИнтерфейсыСтраниц;

// Подключает к форме интерфейс редактирования страницы настройки.
//
// Параметры:
//  Страница  <СтрокаДереваЗначений[СтрокаДереваСтраницНастроек]>
//  Контейнер <ЭлементУправления>: элемент управления, на месте которого будет размещен интерфейс.
//  Форма     <Форма>
//  Панель    <Панель>
//
Процедура ПодключитьИнтерфейс(Страница, Контейнер, Форма, Панель) Экспорт
	Страница.Форма = Форма;
	Страница.Панель = Панель;
	Страница.Контейнер = Контейнер;
	
	Страница.Контейнер.Видимость = Ложь;
	
	Интерфейс = ПолучитьФорму(Страница.Имя, ЭтотОбъект, Новый УникальныйИдентификатор());
	соИнтерфейсыСтраниц.Вставить(Страница.Имя, Интерфейс);
	
	Интерфейс.ПодключитьИнтерфейс(Страница);
	
	Если Страница.ИмеетСтандартныеКнопки Тогда
		Интерфейс.ОтменитьИзменениеНастройки();
	КонецЕсли;
КонецПроцедуры

// Отменяет изменения настроек, сделанные на странице.
//
// Параметры:
//  Страница <СтрокаДереваЗначений[СтрокаДереваСтраницНастроек]>
//
Процедура ОтменитьИзменениеНастройки(Страница) Экспорт
	соИнтерфейсыСтраниц[Страница.Имя].ОтменитьИзменениеНастройки();
КонецПроцедуры

// Записывает в ИБ настройки, сделанные на странице.
//
// Параметры:
//  Страница <СтрокаДереваЗначений[СтрокаДереваСтраницНастроек]>
//  Отказ    <Булево>
//
Процедура ПрименитьНастройку(Страница, Отказ) Экспорт
	соИнтерфейсыСтраниц[Страница.Имя].ПрименитьНастройку(Отказ);
КонецПроцедуры

// Устанавливает настройки по умолчанию для страницы.
//
// Параметры:
//  Страница <СтрокаДереваЗначений[СтрокаДереваСтраницНастроек]>
//
Процедура УстановитьНастройкуПоУмолчанию(Страница) Экспорт
	соИнтерфейсыСтраниц[Страница.Имя].УстановитьНастройкуПоУмолчанию();
КонецПроцедуры

// Открывает окно справки по странице.
//
// Параметры:
//  Страница <СтрокаДереваЗначений[СтрокаДереваСтраницНастроек]>
//
Процедура ПоказатьСправку(Страница) Экспорт
	ОткрытьСправку(Метаданные().Формы[Страница.Имя]);	
КонецПроцедуры

Функция ЕстьАдминистраторУТЛ()
	
	Результат = Ложь;
	
	Если ПравоДоступа("Администрирование", Метаданные) Тогда
		
		мсПользователи = ПользователиИнформационнойБазы.ПолучитьПользователей();
		Для Каждого Пользователь Из мсПользователи Цикл
			
			Если Пользователь.Роли.Содержит(Метаданные.Роли.новаАдминистраторУТЛ) Тогда
				
				Результат = Истина;
				Прервать;
			КонецЕсли;	
		КонецЦикла;
		
	Иначе
		
		Результат = Истина;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

ДеревоСтраниц = Новый ДеревоЗначений;
ДеревоСтраниц.Колонки.Добавить("Представление");
ДеревоСтраниц.Колонки.Добавить("Имя");
ДеревоСтраниц.Колонки.Добавить("ИмеетИнтерфейс", Новый ОписаниеТипов("Булево"));
ДеревоСтраниц.Колонки.Добавить("ИмеетСтандартныеКнопки", Новый ОписаниеТипов("Булево"));
ДеревоСтраниц.Колонки.Добавить("НеОтображатьВНастройке", Новый ОписаниеТипов("Булево"));
ДеревоСтраниц.Колонки.Добавить("ОтображатьВПомощникеНачалаРаботы", Новый ОписаниеТипов("Булево"));
ДеревоСтраниц.Колонки.Добавить("Форма");
ДеревоСтраниц.Колонки.Добавить("Панель");
ДеревоСтраниц.Колонки.Добавить("Контейнер");
ДеревоСтраниц.Колонки.Добавить("ОбъектСправки");

соИнтерфейсыСтраниц = Новый Соответствие;

Если Не ЕстьАдминистраторУТЛ() Тогда
	стрНастройка = ДеревоСтраниц.Строки.Добавить();
	стрНастройка.Представление = "Создание пользователя";
	стрНастройка.Имя = "СозданиеПользователей";
	стрНастройка.ИмеетИнтерфейс = Истина;
	стрНастройка.ИмеетСтандартныеКнопки = Истина;
	стрНастройка.НеОтображатьВНастройке = Истина;
	стрНастройка.ОтображатьВПомощникеНачалаРаботы = Истина;
КонецЕсли;

//стрНастройка = ДеревоСтраниц.Строки.Добавить();
//стрНастройка.Представление = "Техподдержка";
//стрНастройка.Имя = "Техподдержка";
//стрНастройка.ИмеетИнтерфейс = Истина;
//стрНастройка.ИмеетСтандартныеКнопки = Истина;
//стрНастройка.НеОтображатьВНастройке = Ложь;
//стрНастройка.ОтображатьВПомощникеНачалаРаботы = Истина;

стрНастройка = ДеревоСтраниц.Строки.Добавить();
стрНастройка.Представление = "Корпоративный сервер логистики";
стрНастройка.Имя = "КорпоративныйСерверЛогистики";
стрНастройка.ИмеетИнтерфейс = Истина;
стрНастройка.ИмеетСтандартныеКнопки = Истина;
стрНастройка.НеОтображатьВНастройке = Ложь;
стрНастройка.ОтображатьВПомощникеНачалаРаботы = Истина;

стрНастройка = ДеревоСтраниц.Строки.Добавить();
стрНастройка.Представление = "Доходы и расходы";
стрНастройка.Имя = "ДоходыРасходы";
стрНастройка.ИмеетИнтерфейс = Истина;
стрНастройка.ИмеетСтандартныеКнопки = Истина;
стрНастройка.НеОтображатьВНастройке = Ложь;
стрНастройка.ОтображатьВПомощникеНачалаРаботы = Истина;

Интерфейсы = ДеревоСтраниц.Строки.НайтиСтроки(Новый Структура("ИмеетИнтерфейс", Истина), Истина);
Для Каждого стрНастройка Из Интерфейсы Цикл
	стрНастройка.ОбъектСправки = Метаданные().Формы[стрНастройка.Имя];
КонецЦикла;

#КонецЕсли