

Процедура ЗаполнитьДоговоры(Док)
	
	Зап = Новый Запрос;
	Зап.Текст = "ВЫБРАТЬ
	            |	АктивныеДоговорыСубагентовСрезПоследних.Договор
	            |ИЗ
	            |	РегистрСведений.АктивныеДоговорыСубагентов.СрезПоследних(&ДатаЗапроса, ) КАК АктивныеДоговорыСубагентовСрезПоследних
	            |ГДЕ
	            |	АктивныеДоговорыСубагентовСрезПоследних.ДоговорАктивен = ИСТИНА";
				
				
	Если Не ЗначениеЗаполнено(Док.ДатаЗакрытия) Тогда
		ДЗ = Док.Дата;
	Иначе
		ДЗ = Док.ДатаЗакрытия;
	КонецЕсли;	
	
	Зап.УстановитьПараметр("ДатаЗапроса", КонецДня(ДЗ));			
	Рез = Зап.Выполнить().Выгрузить();
	
	Док.ДоговорыСубагентов.Очистить();
	
	Для Каждого Тек Из Рез Цикл
		Нов = Док.ДоговорыСубагентов.Добавить();
		Нов.Договор = Тек.Договор;
	КонецЦикла;	
	
КонецПроцедуры	


Процедура ОбработатьДокументыЗакрытиеЗаказа(ДатаНачала, ДатаОкончания) Экспорт
	
	Если НЕ значениезаполнено(ДатаНачала) ИЛИ Не Значениезаполнено(ДатаОкончания) тогда
		
		Сообщить("Необходимо установить корректный период!");		
		Возврат;
		
	КонецЕсли;	
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ 
		|	ЗакрытиеЗаказов.Ссылка КАК Ссылка
		|ИЗ
		|	Документ.ЗакрытиеЗаказов КАК ЗакрытиеЗаказов
		|ГДЕ
		|	ЗакрытиеЗаказов.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
		|	И ЗакрытиеЗаказов.Проведен";
	
	Запрос.УстановитьПараметр("ДатаНачала", ДатаНачала);
	Запрос.УстановитьПараметр("ДатаОкончания", ДатаОкончания);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
		
	ВсегоДокументов = ВыборкаДетальныеЗаписи.Количество();
	СчОбработанных = 0;
	СИзмененнымСоставомДвижений = 0;
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		
		ДокОбъект  = ВыборкаДетальныеЗаписи.Ссылка.Получитьобъект();
		
		БылоДвижений = ДокОбъект.Движения.УчетСубагентов.Количество();
		
		ЗаполнитьДоговоры(ДокОбъект);
		
		ДокОбъект.ОбменДанными.Загрузка = Истина;
		ДокОбъект.Записать();
		
		ДокОбъект.ОтразитьДвиженияПоСубагентам();		 
		ДокОбъект.Движения.УчетСубагентов.Записать();
		
		СталоДвижений  = ДокОбъект.Движения.УчетСубагентов.Количество();
		
		Если БылоДвижений <> СталоДвижений Тогда
			СИзмененнымСоставомДвижений = СИзмененнымСоставомДвижений + 1;
		КонецЕсли;
	
		СчОбработанных = СчОбработанных + 1;
		
		#Если Клиент Тогда
			Состояние("Обработано " + Строка(СчОбработанных) + " (Изменился состав " + Строка(СИзмененнымСоставомДвижений) +  ")" + " из " + Строка(ВсегоДокументов));
		#КонецЕсли
		
	КонецЦикла;
	
	Сообщить("Обработано " + Строка(СчОбработанных) + " (Изменился состав " + Строка(СИзмененнымСоставомДвижений) +  ")" + " из " + Строка(ВсегоДокументов));
	
КонецПроцедуры
