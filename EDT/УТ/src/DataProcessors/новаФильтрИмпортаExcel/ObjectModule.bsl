#Если Клиент Тогда

// Маска доступных файлов, выбираемых в диалоге
//
Перем МаскаФайла Экспорт; // <Строка>

// Задача импорта данных
//
Перем Задание Экспорт; // <СправочникСсылка.новаЗадачаИмпортаДанных>

// Выполняет настройку фильтра импорта по образцу из файла
//
Процедура НастроитьПоОбразцу() Экспорт
	Форма = ПолучитьФорму("ФормаМастерНастройки");
	Форма.ОбразецФайлаЗагрузки = Задание.Образец;
	Форма.Открыть();
КонецПроцедуры

// Получает данные для фильтра импорта из файла
//
// Параметры:
//  ИмяФайла       <Строка>
//  тзСоставДанных <ТаблицаЗначений>
//
// Возвращаемое значение:
//  <ТаблицаЗначений>
//
Функция ПолучитьДанные(ИмяФайла, тзСоставДанных) Экспорт
	тзДанные = Новый ТаблицаЗначений;
	тзДанные.Колонки.Добавить("ИдСтроки");
	Для Каждого стрСоставДанных Из тзСоставДанных Цикл
		тзДанные.Колонки.Добавить(стрСоставДанных.Ид,, стрСоставДанных.Представление);
	КонецЦикла;
	
	Эксель = Новый COMОбъект("Excel.Application");
	Попытка
		Эксель.Workbooks.Open(ИмяФайла);
		
		Лист = Эксель.Workbooks.Application.ActiveWorkbook.ActiveSheet;
		
		мсПоля = Новый Массив;
		мсКолонки = Новый Массив;
		
		тзЯчейки = Новый ТаблицаЗначений;
		тзЯчейки.Колонки.Добавить("Поле");
		тзЯчейки.Колонки.Добавить("Строка");
		тзЯчейки.Колонки.Добавить("Колонка");
		
		СтрокаДанных = Неопределено;
		Для Каждого стрСоставДанных Из тзСоставДанных Цикл
			Если Лев(стрСоставДанных.Ид, 5) = "CELL_" Тогда
				стрИмяЯчейки = СтрЗаменить(Прав(стрСоставДанных.Ид, СтрДлина(стрСоставДанных.Ид) - 5), "R", "");
				стрИмяЯчейки = СтрЗаменить(стрИмяЯчейки, "C", Символы.ПС);
				
				стрЯчейка = тзЯчейки.Добавить();
				стрЯчейка.Поле = стрСоставДанных.Ид;
				стрЯчейка.Строка = Число(СтрПолучитьСтроку(стрИмяЯчейки, 1));
				стрЯчейка.Колонка = Число(СтрПолучитьСтроку(стрИмяЯчейки, 2));
			Иначе
				СтрокаДанных = стрСоставДанных;
				мсПоля.Добавить(стрСоставДанных.Ид);
				мсКолонки.Добавить(Лист.Range(стрСоставДанных.Ид).Columns.Column);
			КонецЕсли;
		КонецЦикла;
		ВсегоКолонок = мсПоля.Количество() - 1;
		
		ПерваяСтрока = Лист.Range(СтрокаДанных.Ид).Rows.Row;
		ВсегоСтрок = Лист.Cells.SpecialCells(11).Row;
		Для Строка = ПерваяСтрока По ВсегоСтрок Цикл
			Состояние("Чтение " + ИмяФайла + " " + Строка(ВсегоСтрок - Строка));
			
			//Если СокрЛП(Лист.Cells(Строка, мсКолонки[0]).Value) = "" Тогда Прервать; КонецЕсли;
			
			стрДанные = тзДанные.Добавить();
			стрДанные.ИдСтроки = Строка;
			Для ъ = 0 По ВсегоКолонок Цикл
				стрДанные[мсПоля[ъ]] = СокрЛП(Лист.Cells(Строка, мсКолонки[ъ]).Value);
			КонецЦикла;
			
			Для Каждого стрЯчейка Из тзЯчейки Цикл
				стрДанные[стрЯчейка.Поле] = СокрЛП(Лист.Cells(стрЯчейка.Строка, стрЯчейка.Колонка).Value);
			КонецЦикла;
		КонецЦикла;
		
		Эксель.Quit();
	Исключение
		Сообщить(ОписаниеОшибки());
		Эксель.Quit();
	КонецПопытки;
	
	Возврат тзДанные;
КонецФункции


МаскаФайла = "Файлы Excel|*.xls;*.xlsx";

#КонецЕсли
