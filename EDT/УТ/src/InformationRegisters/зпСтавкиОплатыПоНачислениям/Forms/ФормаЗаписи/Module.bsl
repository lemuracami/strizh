
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	КопированиеЗаписи = Не Параметры.ЗначениеКопирования.Пустой();
	
	ТарифРасчетаЗП = Запись.ТарифРасчетаЗП;
	
	СхемаБрейковПробегаПоРейсу = ТарифРасчетаЗП.СхемаБрейковПробегаПоРейсу;
	СхемаБрейковЧасовПереработки = ТарифРасчетаЗП.СхемаБрейковЧасовПереработки;
	
	СхемаРасчетаЗП = Запись.СхемаРасчетаЗП;
	Если Не СхемаРасчетаЗП.ВидПолигоновПарковкиСтандарт.Пустая() Тогда
		Элементы.ВидПолигоновПарковки.СписокВыбора.Добавить(СхемаРасчетаЗП.ВидПолигоновПарковкиСтандарт);
	КонецЕсли;
	Если Не СхемаРасчетаЗП.ВидПолигоновПарковкиПовышеннаяОплата.Пустая() Тогда
		Элементы.ВидПолигоновПарковки.СписокВыбора.Добавить(СхемаРасчетаЗП.ВидПолигоновПарковкиПовышеннаяОплата);
	КонецЕсли;
	Элементы.ВидПолигоновПарковки.СписокВыбора.СортироватьПоПредставлению();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВложенныйЗапрос.Ссылка КАК Ссылка,
	|	ВложенныйЗапрос.Наименование КАК Наименование
	|ИЗ
	|	(ВЫБРАТЬ
	|		зпНачисления.Ссылка КАК Ссылка,
	|		зпНачисления.Наименование КАК Наименование
	|	ИЗ
	|		Справочник.зпНачисления КАК зпНачисления
	|	ГДЕ
	|		зпНачисления.СпособРасчета В (ЗНАЧЕНИЕ(Перечисление.зпСпособыРасчетаНачислений.СтавкаЗаСмену), ЗНАЧЕНИЕ(Перечисление.зпСпособыРасчетаНачислений.УчетЧасов), ЗНАЧЕНИЕ(Перечисление.зпСпособыРасчетаНачислений.ПоЗаказу), ЗНАЧЕНИЕ(Перечисление.зпСпособыРасчетаНачислений.ЗаКилометрПробега), ЗНАЧЕНИЕ(Перечисление.зпСпособыРасчетаНачислений.СтавкаЗаЧас), ЗНАЧЕНИЕ(Перечисление.зпСпособыРасчетаНачислений.СтавкаЗаЧасПереработки), ЗНАЧЕНИЕ(Перечисление.зпСпособыРасчетаНачислений.ФиксированнаяСтавка), ЗНАЧЕНИЕ(Перечисление.зпСпособыРасчетаНачислений.ПримеркаЗаказаПоТарифуПартнера))
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		зпНачисления.Ссылка,
	|		зпНачисления.Наименование
	|	ИЗ
	|		Справочник.зпНачисления КАК зпНачисления
	|	ГДЕ
	|		зпНачисления.Ссылка В (ЗНАЧЕНИЕ(Справочник.зпНачисления.ОплатаПодъемаНаЭтажПриДоставкеЗаказа), ЗНАЧЕНИЕ(Справочник.зпНачисления.ДоплатаЗаПарковку))) КАК ВложенныйЗапрос
	|
	|УПОРЯДОЧИТЬ ПО
	|	Наименование";
	
	Элементы.Начисление.СписокВыбора.ЗагрузитьЗначения(Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка"));

	УправлениеДоступностьюЭлементов();
	
КонецПроцедуры

&НаКлиенте
Процедура НачислениеПриИзменении(Элемент)
	
	УправлениеДоступностьюЭлементов();
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьПараметрыНачисления(Начисление)
	
	ПараметрыНачисления = Новый Структура();
	ПараметрыНачисления.Вставить("УчетЧасовПереработки", 	Начисление.УчетЧасовПереработки);
	ПараметрыНачисления.Вставить("СпособРасчетаПоЗаказу", 	Начисление.СпособРасчета = Перечисления.зпСпособыРасчетаНачислений.ПоЗаказу);
	ПараметрыНачисления.Вставить("СпособРасчетаЗаСмену", 	Начисление.СпособРасчета = Перечисления.зпСпособыРасчетаНачислений.СтавкаЗаСмену);
	Возврат ПараметрыНачисления; 
	
КонецФункции

//Геннадий #4455 29.10.2021 ->
&НаСервере
Процедура УправлениеДоступностьюЭлементов()
	
	Начисление = Запись.Начисление;
	
	Если Начисление = Справочники.зпНачисления.ОплатаПодъемаНаЭтажПриДоставкеЗаказа Тогда
		Элементы.ПроцентОплатыПодъема.Доступность = Истина;
	Иначе
		Элементы.ПроцентОплатыПодъема.Доступность = Ложь;
		Запись.ПроцентОплатыПодъема = 0;
	КонецЕсли;
	
	Если Начисление = Справочники.зпНачисления.ДоплатаЗаПарковку Тогда
		Элементы.ВидПолигоновПарковки.Доступность = Истина;
		Элементы.СтавкаЗаПарковку.Доступность = Истина;
	Иначе
		Элементы.ВидПолигоновПарковки.Доступность = Ложь;
		Элементы.СтавкаЗаПарковку.Доступность = Ложь;
		Запись.ВидПолигоновПарковки = Неопределено;
		Запись.СтавкаЗаПарковку = 0;
	КонецЕсли;
	
	СпособРасчета = Начисление.СпособРасчета;
	
	Если СпособРасчета = Перечисления.зпСпособыРасчетаНачислений.ПоЗаказу Тогда
		//СпособРасчетаПоЗаказу
		Элементы.СтавкаЗаЗаказ.Доступность = Истина;
	Иначе
		Элементы.СтавкаЗаЗаказ.Доступность = Ложь;
		Запись.СтавкаЗаЗаказ = 0;
	КонецЕсли;
	
	Если СпособРасчета = Перечисления.зпСпособыРасчетаНачислений.СтавкаЗаСмену Или СпособРасчета = Перечисления.зпСпособыРасчетаНачислений.УчетЧасов Или Начисление.УчетЧасовПереработки Тогда
		//СпособРасчетаЗаСмену
		Элементы.СтавкаЗаСмену.Доступность = Истина;
	Иначе
		Элементы.СтавкаЗаСмену.Доступность = Ложь;
		Запись.СтавкаЗаСмену = 0;
	КонецЕсли;
	
	Если СпособРасчета = Перечисления.зпСпособыРасчетаНачислений.ЗаКилометрПробега Тогда
		Элементы.СтавкаЗаКилометрПробега.Доступность = Истина;
		Элементы.БрейкМинимальногоПробегаПоРейсу.Доступность = Истина;
	Иначе
		Элементы.СтавкаЗаКилометрПробега.Доступность = Ложь;
		Запись.СтавкаЗаКилометрПробега = 0;
		Элементы.БрейкМинимальногоПробегаПоРейсу.Доступность = Ложь;
		Запись.БрейкМинимальногоПробегаПоРейсу = Неопределено;
	КонецЕсли;
	
	Если СпособРасчета = Перечисления.зпСпособыРасчетаНачислений.СтавкаЗаЧас Или СпособРасчета = Перечисления.зпСпособыРасчетаНачислений.СтавкаЗаЧасПереработки Тогда
		Элементы.СтавкаЗаЧас.Доступность = Истина;
		Элементы.ДобавляемоеКоличествоЧасов.Доступность = Истина;
	Иначе
		Элементы.СтавкаЗаЧас.Доступность = Ложь;
		Запись.СтавкаЗаЧас = 0;
		Элементы.ДобавляемоеКоличествоЧасов.Доступность = Ложь;
		Запись.ДобавляемоеКоличествоЧасов = 0;
	КонецЕсли;
	
	Если СпособРасчета = Перечисления.зпСпособыРасчетаНачислений.СтавкаЗаЧас Тогда
		Элементы.ВремяНачалаРейсаРасчетное.Доступность = Истина;
	Иначе
		Элементы.ВремяНачалаРейсаРасчетное.Доступность = Ложь;
		Запись.ВремяНачалаРейсаРасчетное = Неопределено;
	КонецЕсли;
	
	Если СпособРасчета = Перечисления.зпСпособыРасчетаНачислений.СтавкаЗаЧасПереработки Тогда
		Элементы.БрейкЧасовПереработки.Доступность = Истина;
	Иначе
		Элементы.БрейкЧасовПереработки.Доступность = Ложь;
		Запись.БрейкЧасовПереработки = Неопределено;
	КонецЕсли;
	
	Если СпособРасчета = Перечисления.зпСпособыРасчетаНачислений.ФиксированнаяСтавка Тогда
		Элементы.ФиксированнаяСтавка.Доступность = Истина;
	Иначе
		Элементы.ФиксированнаяСтавка.Доступность = Ложь;
		Запись.ФиксированнаяСтавка = 0;
	КонецЕсли;
	
КонецПроцедуры	
//Геннадий 29.10.2021 <-

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	Если Запись.Должность.Пустая() Или КопированиеЗаписи Тогда
		Запись.Должность = Запись.Роль;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	Если ТекущийОбъект.Начисление = Справочники.зпНачисления.ДоплатаЗаПарковку Тогда
		Если ТекущийОбъект.ВидПолигоновПарковки.Пустая() Тогда
			Отказ = Истина;
			Сообщить("Не указан Вид полигонов парковки");
		КонецЕсли;
		Если Не ТекущийОбъект.СтавкаЗаПарковку Тогда
			Отказ = Истина;
			Сообщить("Не указана Ставка за парковку");
		КонецЕсли;
	КонецЕсли;
	СпособРасчета = ТекущийОбъект.Начисление.СпособРасчета;
	Если СпособРасчета = Перечисления.зпСпособыРасчетаНачислений.ЗаКилометрПробега Тогда
		Если ТекущийОбъект.БрейкМинимальногоПробегаПоРейсу.Пустая() Тогда
			Отказ = Истина;
			Сообщить("Не указан Брейк минимального пробега по рейсу");
		Конецесли;
	КонецЕсли;
	
КонецПроцедуры


