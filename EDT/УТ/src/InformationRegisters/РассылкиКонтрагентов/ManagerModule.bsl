#Область ПрограммныйИнтерфейс

Функция СписокПодчиненныхКонтрагентов(Родитель) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|   Контрагенты.Ссылка КАК Контрагент
	|ИЗ
	|   Справочник.Контрагенты КАК Контрагенты
	|ГДЕ
	|   НЕ Контрагенты.ЭтоГруппа
	|   И Контрагенты.Родитель.ОсновнойМагазин = &Родитель";
	
	Запрос.УстановитьПараметр("Родитель", Родитель);
	
	Возврат Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Контрагент");       
	
КонецФункции	

Функция СписокПодчиненныхРассылокПоРодителю(Родитель, ТипРассылки, Почта) Экспорт
		
	СписокПодчиненных = СписокПодчиненныхКонтрагентов(Родитель);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	РассылкиКонтрагентов.Контрагент КАК Контрагент,
	|	РассылкиКонтрагентов.ТипРассылки КАК ТипРассылки,
	|	РассылкиКонтрагентов.Почта КАК Почта,
	|	РассылкиКонтрагентов.Активна КАК Активна
	|ИЗ
	|	РегистрСведений.РассылкиКонтрагентов КАК РассылкиКонтрагентов
	|ГДЕ
	|	РассылкиКонтрагентов.Контрагент В(&СписокКонтрагентов)
	|	И РассылкиКонтрагентов.ТипРассылки = &ТипРассылки
	|	И РассылкиКонтрагентов.Почта = &Почта";
	
	Запрос.УстановитьПараметр("Почта", Почта);
	Запрос.УстановитьПараметр("СписокКонтрагентов", СписокПодчиненных);
	Запрос.УстановитьПараметр("ТипРассылки", ТипРассылки);
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции	

Процедура УдалитьАдресаРассылокПодчиненныхКонтрагентов(Родитель, ТипРассылки, Почта) Экспорт
	
	АресаРассылкиКУдалению = СписокПодчиненныхРассылокПоРодителю(Родитель, ТипРассылки, Почта);
	
	Для Каждого АдресРассылки Из АресаРассылкиКУдалению Цикл
		ЗаписьПодчиненного = регистрыСведений.РассылкиКонтрагентов.СоздатьМенеджерЗаписи();
		
		ЗаписьПодчиненного.Контрагент = АдресРассылки.Контрагент;
		ЗаписьПодчиненного.ТипРассылки = АдресРассылки.ТипРассылки;
		ЗаписьПодчиненного.Почта = АдресРассылки.Почта;
		ЗаписьПодчиненного.Активна = АдресРассылки.Активна;
		ЗаписьПодчиненного.Удалить();
		
	КонецЦикла;	
	
Конецпроцедуры	

#КонецОбласти