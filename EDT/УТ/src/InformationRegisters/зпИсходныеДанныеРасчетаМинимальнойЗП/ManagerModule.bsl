
Функция ПолучитьТаблицуДанных(ДатаФормирования, Регион, СхемаРасчетаЗП, ТарифРасчетаЗП, ДатаРасчета, Рейс, Водитель, Экспедитор) Экспорт
	
	ФизЛица = Новый Массив;
	ФизЛица.Добавить(Водитель);
	
	Если ЗначениеЗаполнено(Экспедитор) Тогда
		ФизЛица.Добавить(Экспедитор);
		СоставЭкипажа = Перечисления.зпСоставЭкипажа.ВодительСЭкспедитором;
	Иначе
		СоставЭкипажа = Перечисления.зпСоставЭкипажа.ВодительБезЭкспедитора;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ДатаФормирования", ДатаФормирования);
	Запрос.УстановитьПараметр("Регион", Регион);
	Запрос.УстановитьПараметр("СхемаРасчетаЗП", СхемаРасчетаЗП);
	Запрос.УстановитьПараметр("ТарифРасчетаЗП", ТарифРасчетаЗП);
	Запрос.УстановитьПараметр("ДатаРасчета", ДатаРасчета);
	Запрос.УстановитьПараметр("КонецРейса", КонецДня(ДатаРасчета));
	Запрос.УстановитьПараметр("Рейс", Рейс);
	Запрос.УстановитьПараметр("Водитель", Водитель);
	Запрос.УстановитьПараметр("Экспедитор", Экспедитор);
	Запрос.УстановитьПараметр("ФизЛица", ФизЛица);
	Запрос.УстановитьПараметр("СоставЭкипажа", СоставЭкипажа);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВложенныйЗапрос.ФизЛицо КАК ФизЛицо,
	|	ВложенныйЗапрос.ОсновноеНачисление КАК ОсновноеНачисление
	|ПОМЕСТИТЬ ВТ_ОсновныеНачисления
	|ИЗ
	|	(ВЫБРАТЬ ПЕРВЫЕ 1
	|		РаботникиОрганизации.ФизЛицо КАК ФизЛицо,
	|		РаботникиОрганизации.ОсновноеНачисление КАК ОсновноеНачисление
	|	ИЗ
	|		РегистрСведений.РаботникиОрганизации КАК РаботникиОрганизации
	|	ГДЕ
	|		РаботникиОрганизации.Период <= &КонецРейса
	|		И РаботникиОрганизации.ФизЛицо = &Водитель
	|		И РаботникиОрганизации.Состояние = ЗНАЧЕНИЕ(Перечисление.СостояниеРаботникаОрганизации.ПринятНаРаботуВОрганизацию)
	|	
	|	УПОРЯДОЧИТЬ ПО
	|		РаботникиОрганизации.Период УБЫВ) КАК ВложенныйЗапрос
	|ГДЕ
	|	ВложенныйЗапрос.ОсновноеНачисление <> ЗНАЧЕНИЕ(Справочник.зпНачисления.ПустаяСсылка)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВложенныйЗапрос.ФизЛицо,
	|	ВложенныйЗапрос.ОсновноеНачисление
	|ИЗ
	|	(ВЫБРАТЬ ПЕРВЫЕ 1
	|		РаботникиОрганизации.ФизЛицо КАК ФизЛицо,
	|		РаботникиОрганизации.ОсновноеНачисление КАК ОсновноеНачисление
	|	ИЗ
	|		РегистрСведений.РаботникиОрганизации КАК РаботникиОрганизации
	|	ГДЕ
	|		РаботникиОрганизации.Период <= &КонецРейса
	|		И РаботникиОрганизации.ФизЛицо = &Экспедитор
	|		И РаботникиОрганизации.Состояние = ЗНАЧЕНИЕ(Перечисление.СостояниеРаботникаОрганизации.ПринятНаРаботуВОрганизацию)
	|	
	|	УПОРЯДОЧИТЬ ПО
	|		РаботникиОрганизации.Период УБЫВ) КАК ВложенныйЗапрос
	|ГДЕ
	|	ВложенныйЗапрос.ОсновноеНачисление <> ЗНАЧЕНИЕ(Справочник.зпНачисления.ПустаяСсылка)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	зпИсходныеДанныеРасчетаЗППоЗаказамСрезПоследних.ФизЛицо КАК ФизЛицо,
	|	зпИсходныеДанныеРасчетаЗППоЗаказамСрезПоследних.ВидЗанятости КАК ВидЗанятости,
	|	зпИсходныеДанныеРасчетаЗППоЗаказамСрезПоследних.Роль КАК Роль,
	|	СУММА(зпИсходныеДанныеРасчетаЗППоЗаказамСрезПоследних.СуммаРасчетаЗП) КАК СуммаРасчетаЗП
	|ПОМЕСТИТЬ ВТ_ИсходныеДанныеРасчетаЗППоЗаказам
	|ИЗ
	|	РегистрСведений.зпИсходныеДанныеРасчетаЗППоЗаказам.СрезПоследних(
	|			&ДатаФормирования,
	|			Регион = &Регион
	|				И СхемаРасчетаЗП = &СхемаРасчетаЗП
	|				И ДатаРасчета = &ДатаРасчета
	|				И Рейс = &Рейс
	|				И ФизЛицо В (&ФизЛица)
	|				И СоставЭкипажа = &СоставЭкипажа) КАК зпИсходныеДанныеРасчетаЗППоЗаказамСрезПоследних
	|
	|СГРУППИРОВАТЬ ПО
	|	зпИсходныеДанныеРасчетаЗППоЗаказамСрезПоследних.ФизЛицо,
	|	зпИсходныеДанныеРасчетаЗППоЗаказамСрезПоследних.Роль,
	|	зпИсходныеДанныеРасчетаЗППоЗаказамСрезПоследних.ВидЗанятости
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	зпИсходныеДанныеРасчетаЗППоРейсамСрезПоследних.ФизЛицо КАК ФизЛицо,
	|	зпИсходныеДанныеРасчетаЗППоРейсамСрезПоследних.ВыполненоЗаказовПроцент КАК ВыполненоЗаказовПроцент,
	|	зпИсходныеДанныеРасчетаЗППоРейсамСрезПоследних.ПробегУчетный КАК ПланируемыйПробег,
	|	ВЫБОР
	|		КОГДА зпИсходныеДанныеРасчетаЗППоРейсамСрезПоследних.ЭтоВыходнойДень
	|				ИЛИ зпИсходныеДанныеРасчетаЗППоРейсамСрезПоследних.ЭтоПраздничныйДень
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыДнейПроизводственногоКалендаря.Выходной)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВидыДнейПроизводственногоКалендаря.Рабочий)
	|	КОНЕЦ КАК ВидДня
	|ПОМЕСТИТЬ ВТ_ИсходныеДанныеРасчетаЗППоРейсам
	|ИЗ
	|	РегистрСведений.зпИсходныеДанныеРасчетаЗППоРейсам.СрезПоследних(
	|			&ДатаФормирования,
	|			Регион = &Регион
	|				И СхемаРасчетаЗП = &СхемаРасчетаЗП
	|				И ДатаРасчета = &ДатаРасчета
	|				И Рейс = &Рейс
	|				И ФизЛицо В (&ФизЛица)
	|				И СоставЭкипажа = &СоставЭкипажа) КАК зпИсходныеДанныеРасчетаЗППоРейсамСрезПоследних
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	зпИсходныеДанныеРасчетаЗППоЗаказамДополнительныеНачисленияСрезПоследних.ФизЛицо КАК ФизЛицо,
	|	зпИсходныеДанныеРасчетаЗППоЗаказамДополнительныеНачисленияСрезПоследних.Начисление КАК Начисление,
	|	СУММА(зпИсходныеДанныеРасчетаЗППоЗаказамДополнительныеНачисленияСрезПоследних.Сумма) КАК Сумма
	|ПОМЕСТИТЬ ВТ_ИсходныеДанныеРасчетаЗППоЗаказамДополнительныеНачисления
	|ИЗ
	|	РегистрСведений.зпИсходныеДанныеРасчетаЗППоЗаказамДополнительныеНачисления.СрезПоследних(
	|			&ДатаФормирования,
	|			Регион = &Регион
	|				И СхемаРасчетаЗП = &СхемаРасчетаЗП
	|				И ТарифРасчетаЗП = &ТарифРасчетаЗП
	|				И ДатаРасчета = &ДатаРасчета
	|				И Рейс = &Рейс
	|				И ФизЛицо В (&ФизЛица)
	|				И СоставЭкипажа = &СоставЭкипажа) КАК зпИсходныеДанныеРасчетаЗППоЗаказамДополнительныеНачисленияСрезПоследних
	|
	|СГРУППИРОВАТЬ ПО
	|	зпИсходныеДанныеРасчетаЗППоЗаказамДополнительныеНачисленияСрезПоследних.ФизЛицо,
	|	зпИсходныеДанныеРасчетаЗППоЗаказамДополнительныеНачисленияСрезПоследних.Начисление
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВТ_ОсновныеНачисления.ФизЛицо КАК ФизЛицо,
	|	ВТ_ОсновныеНачисления.ОсновноеНачисление КАК ОсновноеНачисление,
	|	ВТ_ИсходныеДанныеРасчетаЗППоЗаказам.Роль КАК Роль,
	|	ВТ_ИсходныеДанныеРасчетаЗППоРейсам.ВидДня КАК ВидДня,
	|	ЕСТЬNULL(ВЗ_БрейкМинимальногоПроцентаВыполнения.Ссылка, ЗНАЧЕНИЕ(Справочник.зпБрейки.ПустаяСсылка)) КАК БрейкМинимальногоПроцентаВыполнения,
	|	ЕСТЬNULL(ВЗ_БрейкМинимальногоПробегаПоРейсу.Ссылка, ЗНАЧЕНИЕ(Справочник.зпБрейки.ПустаяСсылка)) КАК БрейкМинимальногоПробегаПоРейсу
	|ПОМЕСТИТЬ ВТ_ИзмеренияСтавок
	|ИЗ
	|	ВТ_ОсновныеНачисления КАК ВТ_ОсновныеНачисления
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ИсходныеДанныеРасчетаЗППоЗаказам КАК ВТ_ИсходныеДанныеРасчетаЗППоЗаказам
	|		ПО ВТ_ОсновныеНачисления.ФизЛицо = ВТ_ИсходныеДанныеРасчетаЗППоЗаказам.ФизЛицо
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ИсходныеДанныеРасчетаЗППоРейсам КАК ВТ_ИсходныеДанныеРасчетаЗППоРейсам
	|		ПО ВТ_ОсновныеНачисления.ФизЛицо = ВТ_ИсходныеДанныеРасчетаЗППоРейсам.ФизЛицо
	|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|			ВТ_ИсходныеДанныеРасчетаЗППоРейсам.ФизЛицо КАК ФизЛицо,
	|			зпБрейки.Ссылка КАК Ссылка
	|		ИЗ
	|			Справочник.зпБрейки КАК зпБрейки
	|				ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.зпТарифыРасчетаЗП КАК зпТарифыРасчетаЗП
	|				ПО (зпТарифыРасчетаЗП.Ссылка = &ТарифРасчетаЗП)
	|					И зпБрейки.Владелец = зпТарифыРасчетаЗП.СхемаБрейковПроцентаВыполненияЗаказов
	|					И (НЕ зпБрейки.ПометкаУдаления)
	|				ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.зпБрейки.ГраницыБрейка КАК зпБрейкиГраницыБрейка
	|					ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ИсходныеДанныеРасчетаЗППоРейсам КАК ВТ_ИсходныеДанныеРасчетаЗППоРейсам
	|					ПО (ВТ_ИсходныеДанныеРасчетаЗППоРейсам.ВыполненоЗаказовПроцент МЕЖДУ зпБрейкиГраницыБрейка.ЗначениеОт И зпБрейкиГраницыБрейка.ЗначениеДо)
	|				ПО зпБрейки.Ссылка = зпБрейкиГраницыБрейка.Ссылка) КАК ВЗ_БрейкМинимальногоПроцентаВыполнения
	|		ПО ВТ_ОсновныеНачисления.ФизЛицо = ВЗ_БрейкМинимальногоПроцентаВыполнения.ФизЛицо
	|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|			ВТ_ИсходныеДанныеРасчетаЗППоРейсам.ФизЛицо КАК ФизЛицо,
	|			зпБрейки.Ссылка КАК Ссылка
	|		ИЗ
	|			Справочник.зпБрейки КАК зпБрейки
	|				ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.зпТарифыРасчетаЗП КАК зпТарифыРасчетаЗП
	|				ПО (зпТарифыРасчетаЗП.Ссылка = &ТарифРасчетаЗП)
	|					И зпБрейки.Владелец = зпТарифыРасчетаЗП.СхемаБрейковПробегаПоРейсу
	|					И (НЕ зпБрейки.ПометкаУдаления)
	|				ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.зпБрейки.ГраницыБрейка КАК зпБрейкиГраницыБрейка
	|					ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ИсходныеДанныеРасчетаЗППоРейсам КАК ВТ_ИсходныеДанныеРасчетаЗППоРейсам
	|					ПО (ВТ_ИсходныеДанныеРасчетаЗППоРейсам.ПланируемыйПробег МЕЖДУ зпБрейкиГраницыБрейка.ЗначениеОт И зпБрейкиГраницыБрейка.ЗначениеДо)
	|				ПО зпБрейки.Ссылка = зпБрейкиГраницыБрейка.Ссылка) КАК ВЗ_БрейкМинимальногоПробегаПоРейсу
	|		ПО ВТ_ОсновныеНачисления.ФизЛицо = ВЗ_БрейкМинимальногоПробегаПоРейсу.ФизЛицо
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВТ_ИзмеренияСтавок.ФизЛицо КАК ФизЛицо,
	|	зпМинимальныеСтавки.БрейкМинимальногоПроцентаВыполнения КАК БрейкМинимальногоПроцентаВыполнения,
	|	зпМинимальныеСтавки.БрейкМинимальногоПробегаПоРейсу КАК БрейкМинимальногоПробегаПоРейсу,
	|	зпМинимальныеСтавки.МинимальнаяСтавка КАК МинимальнаяСтавка
	|ПОМЕСТИТЬ ВТ_Брейки
	|ИЗ
	|	РегистрСведений.зпМинимальныеСтавки КАК зпМинимальныеСтавки
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ИзмеренияСтавок КАК ВТ_ИзмеренияСтавок
	|		ПО (зпМинимальныеСтавки.СхемаРасчетаЗП = &СхемаРасчетаЗП)
	|			И (зпМинимальныеСтавки.ТарифРасчетаЗП = &ТарифРасчетаЗП)
	|			И (зпМинимальныеСтавки.ВидМинимальнойСтавки = ЗНАЧЕНИЕ(Перечисление.зпВидыМинимальныхСтавок.ЗаДень))
	|			И зпМинимальныеСтавки.ОсновноеНачисление = ВТ_ИзмеренияСтавок.ОсновноеНачисление
	|			И зпМинимальныеСтавки.Роль = ВТ_ИзмеренияСтавок.Роль
	|			И (зпМинимальныеСтавки.СоставЭкипажа = &СоставЭкипажа)
	|			И (зпМинимальныеСтавки.ВидДня В (ВТ_ИзмеренияСтавок.ВидДня, ЗНАЧЕНИЕ(Перечисление.ВидыДнейПроизводственногоКалендаря.ПустаяСсылка)))
	|			И (зпМинимальныеСтавки.БрейкМинимальногоПроцентаВыполнения В (ВТ_ИзмеренияСтавок.БрейкМинимальногоПроцентаВыполнения, ЗНАЧЕНИЕ(Справочник.зпБрейки.ПустаяСсылка)))
	|			И (зпМинимальныеСтавки.БрейкМинимальногоПробегаПоРейсу В (ВТ_ИзмеренияСтавок.БрейкМинимальногоПробегаПоРейсу, ЗНАЧЕНИЕ(Справочник.зпБрейки.ПустаяСсылка)))
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|			зпМинимальныеСтавки.ОсновноеНачисление КАК ОсновноеНачисление,
	|			зпМинимальныеСтавки.Роль КАК Роль,
	|			МАКСИМУМ(зпМинимальныеСтавки.МинимальнаяСтавка) КАК МинимальнаяСтавка
	|		ИЗ
	|			РегистрСведений.зпМинимальныеСтавки КАК зпМинимальныеСтавки
	|				ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ИзмеренияСтавок КАК ВТ_ИзмеренияСтавок
	|				ПО (зпМинимальныеСтавки.СхемаРасчетаЗП = &СхемаРасчетаЗП)
	|					И (зпМинимальныеСтавки.ТарифРасчетаЗП = &ТарифРасчетаЗП)
	|					И (зпМинимальныеСтавки.ВидМинимальнойСтавки = ЗНАЧЕНИЕ(Перечисление.зпВидыМинимальныхСтавок.ЗаДень))
	|					И зпМинимальныеСтавки.ОсновноеНачисление = ВТ_ИзмеренияСтавок.ОсновноеНачисление
	|					И зпМинимальныеСтавки.Роль = ВТ_ИзмеренияСтавок.Роль
	|					И (зпМинимальныеСтавки.СоставЭкипажа = &СоставЭкипажа)
	|					И (зпМинимальныеСтавки.ВидДня В (ВТ_ИзмеренияСтавок.ВидДня, ЗНАЧЕНИЕ(Перечисление.ВидыДнейПроизводственногоКалендаря.ПустаяСсылка)))
	|					И (зпМинимальныеСтавки.БрейкМинимальногоПроцентаВыполнения В (ВТ_ИзмеренияСтавок.БрейкМинимальногоПроцентаВыполнения, ЗНАЧЕНИЕ(Справочник.зпБрейки.ПустаяСсылка)))
	|					И (зпМинимальныеСтавки.БрейкМинимальногоПробегаПоРейсу В (ВТ_ИзмеренияСтавок.БрейкМинимальногоПробегаПоРейсу, ЗНАЧЕНИЕ(Справочник.зпБрейки.ПустаяСсылка)))
	|		
	|		СГРУППИРОВАТЬ ПО
	|			зпМинимальныеСтавки.ОсновноеНачисление,
	|			зпМинимальныеСтавки.Роль) КАК ВЗ_МаксимальныеСтавки
	|		ПО зпМинимальныеСтавки.ОсновноеНачисление = ВЗ_МаксимальныеСтавки.ОсновноеНачисление
	|			И зпМинимальныеСтавки.Роль = ВЗ_МаксимальныеСтавки.Роль
	|			И зпМинимальныеСтавки.МинимальнаяСтавка = ВЗ_МаксимальныеСтавки.МинимальнаяСтавка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	&ДатаФормирования КАК Период,
	|	&Регион КАК Регион,
	|	&СхемаРасчетаЗП КАК СхемаРасчетаЗП,
	|	&ТарифРасчетаЗП КАК ТарифРасчетаЗП,
	|	&ДатаРасчета КАК ДатаРасчета,
	|	ВТ_ОсновныеНачисления.ОсновноеНачисление КАК ОсновноеНачисление,
	|	ВТ_ИсходныеДанныеРасчетаЗППоЗаказам.ВидЗанятости КАК ВидЗанятости,
	|	&Рейс КАК Рейс,
	|	&СоставЭкипажа КАК СоставЭкипажа,
	|	ВТ_ОсновныеНачисления.ФизЛицо КАК ФизЛицо,
	|	ВТ_ИсходныеДанныеРасчетаЗППоЗаказам.Роль КАК Роль,
	|	ВТ_ИсходныеДанныеРасчетаЗППоЗаказам.СуммаРасчетаЗП КАК СуммаЗаказы,
	|	ВТ_ИсходныеДанныеРасчетаЗППоЗаказамДополнительныеНачисления_Подъем.Сумма КАК СуммаПодъем,
	|	ВТ_ИсходныеДанныеРасчетаЗППоЗаказамДополнительныеНачисления_Примерка.Сумма КАК СуммаПримерка,
	|	ВТ_ИсходныеДанныеРасчетаЗППоЗаказам.СуммаРасчетаЗП + ЕСТЬNULL(ВТ_ИсходныеДанныеРасчетаЗППоЗаказамДополнительныеНачисления_Подъем.Сумма, 0) + ЕСТЬNULL(ВТ_ИсходныеДанныеРасчетаЗППоЗаказамДополнительныеНачисления_Примерка.Сумма, 0) КАК СуммаИтого,
	|	ВТ_ИсходныеДанныеРасчетаЗППоРейсам.ВыполненоЗаказовПроцент КАК ПроцентВыполненияЗаказов,
	|	ВТ_ИсходныеДанныеРасчетаЗППоРейсам.ВидДня КАК ВидДня,
	|	ВТ_ИсходныеДанныеРасчетаЗППоРейсам.ПланируемыйПробег КАК Пробег,
	|	ВТ_Брейки.БрейкМинимальногоПроцентаВыполнения КАК БрейкМинимальногоПроцентаВыполнения,
	|	ВТ_Брейки.БрейкМинимальногоПробегаПоРейсу КАК БрейкМинимальногоПробегаПоРейсу,
	|	ВТ_ИсходныеДанныеРасчетаЗППоЗаказам.СуммаРасчетаЗП + ЕСТЬNULL(ВТ_ИсходныеДанныеРасчетаЗППоЗаказамДополнительныеНачисления_Подъем.Сумма, 0) + ЕСТЬNULL(ВТ_ИсходныеДанныеРасчетаЗППоЗаказамДополнительныеНачисления_Примерка.Сумма, 0) < ЕСТЬNULL(ВТ_Брейки.МинимальнаяСтавка, 0)
	|		И ВЗ_ОтменаНачисленияМинимальнойСтавки.ФизЛицо ЕСТЬ NULL КАК ПрименяетсяМинимальнаяСтавка,
	|	ВТ_Брейки.МинимальнаяСтавка КАК СуммаМинимальнойСтавки,
	|	НЕ ВЗ_ОтменаНачисленияМинимальнойСтавки.ФизЛицо ЕСТЬ NULL КАК ОтменаНачисленияМинимальнойСтавки
	|ИЗ
	|	ВТ_ОсновныеНачисления КАК ВТ_ОсновныеНачисления
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ИсходныеДанныеРасчетаЗППоЗаказам КАК ВТ_ИсходныеДанныеРасчетаЗППоЗаказам
	|		ПО ВТ_ОсновныеНачисления.ФизЛицо = ВТ_ИсходныеДанныеРасчетаЗППоЗаказам.ФизЛицо
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ИсходныеДанныеРасчетаЗППоРейсам КАК ВТ_ИсходныеДанныеРасчетаЗППоРейсам
	|		ПО ВТ_ОсновныеНачисления.ФизЛицо = ВТ_ИсходныеДанныеРасчетаЗППоРейсам.ФизЛицо
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ИсходныеДанныеРасчетаЗППоЗаказамДополнительныеНачисления КАК ВТ_ИсходныеДанныеРасчетаЗППоЗаказамДополнительныеНачисления_Подъем
	|		ПО ВТ_ОсновныеНачисления.ФизЛицо = ВТ_ИсходныеДанныеРасчетаЗППоЗаказамДополнительныеНачисления_Подъем.ФизЛицо
	|			И (ВТ_ИсходныеДанныеРасчетаЗППоЗаказамДополнительныеНачисления_Подъем.Начисление = ЗНАЧЕНИЕ(Справочник.зпНачисления.ОплатаПодъемаНаЭтажПриДоставкеЗаказа))
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ИсходныеДанныеРасчетаЗППоЗаказамДополнительныеНачисления КАК ВТ_ИсходныеДанныеРасчетаЗППоЗаказамДополнительныеНачисления_Примерка
	|		ПО ВТ_ОсновныеНачисления.ФизЛицо = ВТ_ИсходныеДанныеРасчетаЗППоЗаказамДополнительныеНачисления_Примерка.ФизЛицо
	|			И (ВТ_ИсходныеДанныеРасчетаЗППоЗаказамДополнительныеНачисления_Примерка.Начисление = ЗНАЧЕНИЕ(Справочник.зпНачисления.Примерка))
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Брейки КАК ВТ_Брейки
	|		ПО ВТ_ОсновныеНачисления.ФизЛицо = ВТ_Брейки.ФизЛицо
	|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ РАЗЛИЧНЫЕ
	|			зпИсходныеДанныеРасчетаМинимальнойЗПСрезПоследних.ФизЛицо КАК ФизЛицо
	|		ИЗ
	|			РегистрСведений.зпИсходныеДанныеРасчетаМинимальнойЗП.СрезПоследних(&ДатаФормирования, Рейс = &Рейс) КАК зпИсходныеДанныеРасчетаМинимальнойЗПСрезПоследних
	|		ГДЕ
	|			зпИсходныеДанныеРасчетаМинимальнойЗПСрезПоследних.ОтменаНачисленияМинимальнойСтавки) КАК ВЗ_ОтменаНачисленияМинимальнойСтавки
	|		ПО ВТ_ОсновныеНачисления.ФизЛицо = ВЗ_ОтменаНачисленияМинимальнойСтавки.ФизЛицо";
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

//Неопределено - все разрешено
//Истина - все запрещено, любая модификация движений
//Ложь - запрещено формирование минималки
Функция ПолнаяБлокировкаФормирования(Регион, ДатаРасчета, ДатаДвижений) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Регион", Регион);
	Запрос.УстановитьПараметр("НачалоПериода", НачалоМесяца(ДатаРасчета));
	Запрос.УстановитьПараметр("ОкончаниеПериода", КонецМесяца(ДатаРасчета));
	Запрос.УстановитьПараметр("ДатаДвижений", ДатаДвижений);
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	зпФиксацияМесяца.Дата >= &ДатаДвижений КАК ПолнаяБлокировка
	|ИЗ
	|	Документ.зпФиксацияМесяца КАК зпФиксацияМесяца
	|ГДЕ
	|	зпФиксацияМесяца.Регион = &Регион
	|	И зпФиксацияМесяца.Проведен
	|	И зпФиксацияМесяца.ПериодРасчета МЕЖДУ &НачалоПериода И &ОкончаниеПериода";
	
	Результат = Запрос.Выполнить();
	Если Результат.Пустой() Тогда
		Возврат Неопределено;
	Иначе
		Выборка = Результат.Выбрать();
		Выборка.Следующий();
		Возврат Выборка.ПолнаяБлокировка;
	КонецЕсли;
	
КонецФункции

//Асеев 09.02.2024 (Задача № 5227)>>>
//возвращает ДатуРасчета записей, которые нельзя удалять
Функция ПолучитьДатуРасчетаЗапрещенныхКУдалениюЗаписей(Регистратор) Экспорт
	
	//запрещено удаление минималки, которая вошла в фиксацию месяца
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Регистратор", Регистратор);
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	зпИсходныеДанныеРасчетаМинимальнойЗП.ДатаРасчета КАК ДатаРасчета
	|ИЗ
	|	РегистрСведений.зпИсходныеДанныеРасчетаМинимальнойЗП КАК зпИсходныеДанныеРасчетаМинимальнойЗП
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.зпФиксацияМесяца КАК зпФиксацияМесяца
	|		ПО (зпИсходныеДанныеРасчетаМинимальнойЗП.Регистратор = &Регистратор)
	|			И зпИсходныеДанныеРасчетаМинимальнойЗП.Период <= зпФиксацияМесяца.Дата
	|			И зпИсходныеДанныеРасчетаМинимальнойЗП.Регион = зпФиксацияМесяца.Регион
	|			И (зпФиксацияМесяца.Проведен)
	|			И (зпФиксацияМесяца.ПериодРасчета МЕЖДУ НАЧАЛОПЕРИОДА(зпИсходныеДанныеРасчетаМинимальнойЗП.ДатаРасчета, МЕСЯЦ) И КОНЕЦПЕРИОДА(зпИсходныеДанныеРасчетаМинимальнойЗП.ДатаРасчета, МЕСЯЦ))";
	
	Результат = Запрос.Выполнить();
	Если Результат.Пустой() Тогда
		Возврат Неопределено;
	Иначе
		Выборка = Результат.Выбрать();
		Выборка.Следующий();
		Возврат Выборка.ДатаРасчета;
	КонецЕсли;
	
КонецФункции
//Асеев 09.02.2024 (Задача № 5227)<<<
