
&НаСервере
Процедура ПриЗаписиНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	//Асеев 28.02.2022 (Задача № 4749)>>>
	//учитывая что двух абсолютно одинаковых по измерениям записей быть не может
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ТарифПримерки", Запись.ТарифПримерки);
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ПоказателиТарифаПримерки1.КоличествоSKUОт КАК КоличествоSKUОт,
	|	ПоказателиТарифаПримерки1.КоличествоSKUДо КАК КоличествоSKUДо
	|ИЗ
	|	РегистрСведений.ПоказателиТарифаПримерки КАК ПоказателиТарифаПримерки1
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ПоказателиТарифаПримерки КАК ПоказателиТарифаПримерки2
	|		ПО (ПоказателиТарифаПримерки1.ТарифПримерки = &ТарифПримерки)
	|			И (ПоказателиТарифаПримерки2.ТарифПримерки = &ТарифПримерки)
	|			И ПоказателиТарифаПримерки1.СоставЭкипажа = ПоказателиТарифаПримерки2.СоставЭкипажа
	|			И ПоказателиТарифаПримерки1.Роль = ПоказателиТарифаПримерки2.Роль
	|			И (ПоказателиТарифаПримерки1.КоличествоSKUОт <= ПоказателиТарифаПримерки2.КоличествоSKUОт
	|					И ПоказателиТарифаПримерки1.КоличествоSKUДо >= ПоказателиТарифаПримерки2.КоличествоSKUОт
	|				ИЛИ ПоказателиТарифаПримерки1.КоличествоSKUОт >= ПоказателиТарифаПримерки2.КоличествоSKUОт
	|					И ПоказателиТарифаПримерки1.КоличествоSKUОт <= ПоказателиТарифаПримерки2.КоличествоSKUДо)
	|			И (ПоказателиТарифаПримерки1.КоличествоSKUОт <> ПоказателиТарифаПримерки2.КоличествоSKUОт
	|				ИЛИ ПоказателиТарифаПримерки1.КоличествоSKUДо <> ПоказателиТарифаПримерки2.КоличествоSKUДо)
	|
	|УПОРЯДОЧИТЬ ПО
	|	КоличествоSKUОт";
	
	Результат = Запрос.Выполнить();
	Если Результат.Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	Отказ = Истина;
	
	МассивСтрок = Новый Массив;
	МассивСтрок.Добавить("Пересечение диапазонов SKU:");
	
	Выборка = Результат.Выбрать();
	Пока Выборка.Следующий() Цикл
		МассивСтрок.Добавить("Количество SKUОт = " + Выборка.КоличествоSKUОт + ", Количество SKUДо = " + Выборка.КоличествоSKUДо);
	КонецЦикла;
	
	Сообщить(СтрСоединить(МассивСтрок, Символы.ПС));
	//Асеев 28.02.2022 (Задача № 4749)<<<
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	//Асеев 28.02.2022 (Задача № 4749)>>>
	ТарифПримерки = Неопределено;
	Параметры.Свойство("ТарифПримерки", ТарифПримерки);
	Если ТарифПримерки <> Неопределено Тогда
		Запись.ТарифПримерки = ТарифПримерки;
	КонецЕсли;
	//Асеев 28.02.2022 (Задача № 4749)<<<
	
КонецПроцедуры
