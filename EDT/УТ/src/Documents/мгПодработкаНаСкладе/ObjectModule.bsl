
Процедура ОбработкаПроведения(Отказ, Режим)
	
	// регистр мгЗарплатаНачисления Приход
	Движения.мгЗарплатаНачисления.Записать();
	Движения.мгЗарплатаНачисления.Записывать = Истина;
	Движения.мгЗарплатаНачисления.Очистить();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	мгПодработкаНаСкладеПодработкаНаСкладе.Организация КАК Организация,
		|	мгПодработкаНаСкладеПодработкаНаСкладе.ФИОСотрудника КАК ФИОСотрудника,
		|	мгПодработкаНаСкладеПодработкаНаСкладе.ДатаПодработки КАК ДатаПодработки,
		|	мгПодработкаНаСкладеПодработкаНаСкладе.НомерСтроки КАК НомерСтроки
		|ПОМЕСТИТЬ вт_мгПодработкаНаСкладе
		|ИЗ
		|	Документ.мгПодработкаНаСкладе.ПодработкаНаСкладе КАК мгПодработкаНаСкладеПодработкаНаСкладе
		|ГДЕ
		|	мгПодработкаНаСкладеПодработкаНаСкладе.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	вт_мгПодработкаНаСкладе.Организация КАК Организация,
		|	вт_мгПодработкаНаСкладе.ФИОСотрудника КАК ФИОСотрудника,
		|	вт_мгПодработкаНаСкладе.ДатаПодработки КАК ДатаПодработки,
		|	вт_мгПодработкаНаСкладе.НомерСтроки КАК НомерСтроки,
		|	мгЗарплатаНачисления.Регистратор КАК Регистратор
		|ИЗ
		|	вт_мгПодработкаНаСкладе КАК вт_мгПодработкаНаСкладе
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.мгЗарплатаНачисления КАК мгЗарплатаНачисления
		|		ПО вт_мгПодработкаНаСкладе.Организация = мгЗарплатаНачисления.Организация
		|			И вт_мгПодработкаНаСкладе.ФИОСотрудника = мгЗарплатаНачисления.Сотрудник
		|			И вт_мгПодработкаНаСкладе.ДатаПодработки = мгЗарплатаНачисления.Период
		|			И (ЗНАЧЕНИЕ(Справочник.мгНачисленияУдержания.ПодработкаНаСкладе) = мгЗарплатаНачисления.ВидНачисления)";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	РезультатЗапроса 		= Запрос.Выполнить();	
	ВыборкаДетальныеЗаписи 	= РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		
		Если ЗначениеЗаполнено(ВыборкаДетальныеЗаписи.Регистратор) Тогда
			
			Отказ		= Истина;
			Сообщение 	= Новый СообщениеПользователю();
		    Сообщение.Текст = "Номер строки " + ВыборкаДетальныеЗаписи.НомерСтроки +  ". Сотрудник " + """" + ВыборкаДетальныеЗаписи.ФИОСотрудника + """" + " уже зарегистрирован на " + Формат(ВыборкаДетальныеЗаписи.ДатаПодработки,"ДФ=dd.MM.yyyy") + " документом:" + ВыборкаДетальныеЗаписи.Регистратор ;
		    Сообщение.Поле = "ПодработкаНаСкладе["+(ВыборкаДетальныеЗаписи.НомерСтроки-1)+"].ДатаПодработки";
		    Сообщение.УстановитьДанные(ЭтотОбъект);
		    Сообщение.Сообщить();
	
		КонецЕсли;
		
		Если Отказ Тогда
			Возврат;	
		КонецЕсли; 
		
		Движение 				= Движения.мгЗарплатаНачисления.Добавить();
		Движение.ВидДвижения 	= ВидДвиженияНакопления.Приход;
		
		Движение.Период 		= НачалоДня(ВыборкаДетальныеЗаписи.ДатаПодработки);
		Движение.Организация 	= ВыборкаДетальныеЗаписи.Организация;
		Движение.Сотрудник 		= ВыборкаДетальныеЗаписи.ФИОСотрудника;
		Движение.Терминал 		= Терминал;
		Движение.ВидНачисления 	= ПредопределенноеЗначение("Справочник.мгНачисленияУдержания.ПодработкаНаСкладе");
		Движение.Тариф 			= Тариф;
		
	КонецЦикла;

	
	
КонецПроцедуры
