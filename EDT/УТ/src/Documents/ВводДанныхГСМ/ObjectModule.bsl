
Процедура ОбработкаПроведения(Отказ, Режим)
	
	ЦенаГСМ = 0;
	
	// регистр РасходГСМ 
	Движения.РасходГСМ.Записывать = Истина;
	Движения.РасходГСМ.Очистить();
	Для Каждого ТекСтрокаТранспорт Из Транспорт Цикл
		
		Движение = Движения.РасходГСМ.Добавить();
		Движение.Период = ДатаОтчета;
		Движение.Рейс = ТекСтрокаТранспорт.Рейс;
		Движение.Транспорт = ТекСтрокаТранспорт.Транспорт;
		Движение.ТерминалДоставки = ТерминалДоставки;
		Движение.Водитель = ТекСтрокаТранспорт.Водитель;
		Движение.РасходГСМ = ТекСтрокаТранспорт.РасходГСМ;
		Движение.ПоказанияОдометраНаНачалоДня = ТекСтрокаТранспорт.ПоказанияОдометраНаНачалоДня;
		Движение.ПоказанияОдометраНаКонецДня = ТекСтрокаТранспорт.ПоказанияОдометраНаКонецДня;
		
		ТипГСМ = ТекСтрокаТранспорт.Транспорт.Марка.ТипГСМ;
		МасСтоимостьГСМ = ЭтотОбъект.СтоимостьГСМ.НайтиСтроки(Новый Структура("ТипГСМ", ТипГСМ));
		Если МасСтоимостьГСМ.Количество() Тогда	
			ЦенаГСМ = МасСтоимостьГСМ[0].Стоимость;		
		КонецЕсли;		
		Движение.СуммаЗатрат = ТекСтрокаТранспорт.РасходГСМ * ЦенаГСМ;
		
	КонецЦикла;
	
	
	
	// РН СтоимостьГСМ
	Движения.СтоимостьГСМ.Записывать = Истина;
	Движения.СтоимостьГСМ.Очистить();
	Для каждого Стр Из СтоимостьГСМ Цикл		
		Движение = Движения.СтоимостьГСМ.Добавить();
		Движение.Период = ДатаОтчета;
		Движение.ТерминалДоставки = ТерминалДоставки;
		Движение.ТипГСМ = Стр.ТипГСМ;
		Движение.Стоимость = Стр.Стоимость;			
	КонецЦикла;
	
	
	
	// РН ВводДанныхОРасходахКомпании	
	Движения.ВводДанныхОРасходахКомпании.Записывать = Истина;
	Движения.ВводДанныхОРасходахКомпании.Очистить();
	
	Запрос = Новый Запрос("ВЫБРАТЬ
	                      |	ВводДанныхГСМТранспорт.Транспорт,
	                      |	ВводДанныхГСМТранспорт.Транспорт.Марка.ТипГСМ,
	                      |	СУММА(ВводДанныхГСМТранспорт.РасходГСМ) КАК РасходГСМ
	                      |ПОМЕСТИТЬ ВТ_ТЧ
	                      |ИЗ
	                      |	Документ.ВводДанныхГСМ.Транспорт КАК ВводДанныхГСМТранспорт
	                      |ГДЕ
	                      |	ВводДанныхГСМТранспорт.Ссылка = &Ссылка
	                      |
	                      |СГРУППИРОВАТЬ ПО
	                      |	ВводДанныхГСМТранспорт.Транспорт,
	                      |	ВводДанныхГСМТранспорт.Транспорт.Марка.ТипГСМ
	                      |;
	                      |
	                      |////////////////////////////////////////////////////////////////////////////////
	                      |ВЫБРАТЬ
	                      |	ВТ_ТЧ.Транспорт,
	                      |	ВТ_ТЧ.РасходГСМ * ВводДанныхГСМСтоимостьГСМ.Стоимость КАК СуммаРасход
	                      |ИЗ
	                      |	ВТ_ТЧ КАК ВТ_ТЧ
	                      |		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ВводДанныхГСМ.СтоимостьГСМ КАК ВводДанныхГСМСтоимостьГСМ
	                      |		ПО ВТ_ТЧ.ТранспортМаркаТипГСМ = ВводДанныхГСМСтоимостьГСМ.ТипГСМ
	                      |ГДЕ
	                      |	ВводДанныхГСМСтоимостьГСМ.Ссылка = &Ссылка");
	Запрос.УстановитьПараметр("Ссылка", ЭтотОбъект.Ссылка);
	
	Рез = Запрос.Выполнить().Выбрать();					  
	СуммаРасход = 0;
	
	Пока Рез.Следующий() Цикл		
		СуммаРасход = СуммаРасход + Рез.СуммаРасход;		
	КонецЦикла;
	
	
	Если НЕ Отказ Тогда		
		Движение = Движения.ВводДанныхОРасходахКомпании.Добавить();
		Движение.Период = ДатаОтчета;
		Движение.СтатьяЗатрат = Справочники.СтатьиЗатратКомпании.ГСМ;
		Движение.ТипЗатрат    = Движение.СтатьяЗатрат.ТипЗатрат;
		Движение.ВидЗатрат = Перечисления.ВидыЗатратКомпании.РаспределяемыеРасходы;
		Движение.Сумма = СуммаРасход;	
	КонецЕсли;	
	
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если РежимЗаписи = РежимЗаписиДокумента.Проведение Тогда	
	
		Для каждого Стр Из Транспорт Цикл
			//Если Стр.РасходГСМ = 0 ИЛИ Стр.ПоказанияОдометраНаКонецДня = 0 Тогда
			Если Стр.ПоказанияОдометраНаКонецДня = 0 Тогда
				Сообщить("Есть строки с пустыми данными, документ не проведен");
				Отказ = Истина;
				Возврат;
			КонецЕсли;
		КонецЦикла;	
		
		
		// проверка на заполненность Типа ГСМ
		Запрос = Новый Запрос("ВЫБРАТЬ
		                      |	ВводДанныхГСМТранспорт.Транспорт.Марка КАК Марка,
		                      |	ВЫБОР
		                      |		КОГДА ЕСТЬNULL(ВводДанныхГСМТранспорт.Транспорт.Марка.ТипГСМ, ЗНАЧЕНИЕ(Справочник.ТипыГСМ.ПустаяСсылка)) = ЗНАЧЕНИЕ(Справочник.ТипыГСМ.ПустаяСсылка)
		                      |			ТОГДА ИСТИНА
		                      |		ИНАЧЕ ЛОЖЬ
		                      |	КОНЕЦ КАК НеЗаполненТипГСМ
		                      |ИЗ
		                      |	Документ.ВводДанныхГСМ.Транспорт КАК ВводДанныхГСМТранспорт
		                      |ГДЕ
		                      |	ВводДанныхГСМТранспорт.Ссылка.Ссылка = &Ссылка");
		Запрос.УстановитьПараметр("Ссылка", ЭтотОбъект.Ссылка);		
		
		Рез = Запрос.Выполнить().Выбрать();
		
		Пока Рез.Следующий() Цикл		
			Если Рез.НеЗаполненТипГСМ Тогда		
				Сообщить("Для марки транспорта " + Рез.Марка + " не указан тип ГСМ");
				Отказ = Истина;			
			КонецЕсли;
		КонецЦикла;	
		
	КонецЕсли;					  
	
КонецПроцедуры
