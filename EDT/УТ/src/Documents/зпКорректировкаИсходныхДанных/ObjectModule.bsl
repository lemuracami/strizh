
Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	ТипДанных = ТипЗнч(ДанныеЗаполнения);
	Если ТипДанных = Тип("Структура") Тогда
		
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, ДанныеЗаполнения);
		
		Если СпособВнесенияДокумента = Перечисления.зпСпособыВнесенияДокументаКорректировкиЗП.ОтчетФорма3 Тогда
			
			Запрос = Новый Запрос;
			Запрос.УстановитьПараметр("Рейс", ДанныеЗаполнения.Рейс);
			Запрос.УстановитьПараметр("Заказ", ДанныеЗаполнения.Заказ);
			Запрос.УстановитьПараметр("ФизЛицо", ДанныеЗаполнения.ФизЛицо);
			Запрос.Текст =
			"ВЫБРАТЬ
			|	*
			|ИЗ
			|	РегистрСведений.зпИсходныеДанныеРасчетаЗППоЗаказам.СрезПоследних(
			|			,
			|			Рейс = &Рейс
			|				И ФизЛицо = &ФизЛицо
			|				И Заказ = &Заказ) КАК зпИсходныеДанныеРасчетаЗППоЗаказамСрезПоследних";
			
			Выборка = Запрос.Выполнить().Выбрать();
			Если Выборка.Следующий() Тогда
				ЗаполнитьЗначенияСвойств(ЭтотОбъект, Выборка);
				КорректируемыйДокумент = Выборка.Регистратор;
				ОсновноеНачисление = Выборка.Начисление;
			КонецЕсли;
			
		ИначеЕсли СпособВнесенияДокумента = Перечисления.зпСпособыВнесенияДокументаКорректировкиЗП.ОтчетФорма2_ВремяУчетное Тогда
			
			Запрос = Новый Запрос;
			Запрос.УстановитьПараметр("Рейс", ДанныеЗаполнения.Рейс);
			Запрос.УстановитьПараметр("ФизЛицо", ДанныеЗаполнения.ФизЛицо);
			Запрос.Текст =
			"ВЫБРАТЬ
			|	*
			|ИЗ
			|	РегистрСведений.зпИсходныеДанныеРасчетаЗПЧасы.СрезПоследних(
			|			,
			|			Рейс = &Рейс
			|				И ФизЛицо = &ФизЛицо) КАК зпИсходныеДанныеРасчетаЗПЧасыСрезПоследних";
			
			Выборка = Запрос.Выполнить().Выбрать();
			Если Выборка.Следующий() Тогда
				ЗаполнитьЗначенияСвойств(ЭтотОбъект, Выборка);
				КорректируемыйДокумент = Выборка.Регистратор;
				
				Выборка.Сбросить();
				Пока Выборка.Следующий() Цикл
					ЗаполнитьЗначенияСвойств(НовыеРасчетныеДанные_зпИсходныеДанныеРасчетаЗПЧасы.Добавить(), Выборка);
				КонецЦикла;
				
			КонецЕсли;
			
		//Асеев 23.10.2023 (Задача № 5151)>>>
		ИначеЕсли СпособВнесенияДокумента = Перечисления.зпСпособыВнесенияДокументаКорректировкиЗП.ОтчетФорма2_УчетныйПробег Тогда
			
			Запрос = Новый Запрос;
			Запрос.УстановитьПараметр("Рейс", ДанныеЗаполнения.Рейс);
			Запрос.УстановитьПараметр("ФизЛицо", ДанныеЗаполнения.ФизЛицо);
			Запрос.Текст =
			"ВЫБРАТЬ
			|	зпИсходныеДанныеРасчетаЗППоРейсамСрезПоследних.Регион КАК Регион,
			|	зпИсходныеДанныеРасчетаЗППоРейсамСрезПоследних.СхемаРасчетаЗП КАК СхемаРасчетаЗП,
			|	зпИсходныеДанныеРасчетаЗППоРейсамСрезПоследних.Регистратор КАК КорректируемыйДокумент,
			|	зпИсходныеДанныеРасчетаЗППоРейсамСрезПоследних.ДатаРасчета КАК ДатаРасчета,
			|	зпИсходныеДанныеРасчетаЗППоРейсамСрезПоследних.ВидЗанятости КАК ВидЗанятости,
			|	зпИсходныеДанныеРасчетаЗППоРейсамСрезПоследних.СоставЭкипажа КАК СоставЭкипажа,
			|	зпИсходныеДанныеРасчетаЗППоРейсамСрезПоследних.Роль КАК Роль,
			|	зпИсходныеДанныеРасчетаЗППоРейсамСрезПоследних.ПробегУчетный КАК ПробегУчетный,
			|	зпИсходныеДанныеРасчетаЗППоРейсамСрезПоследних.ПробегЗаМКАД КАК ПробегЗаМКАД,
			|	ВЫБОР
			|		КОГДА зпИсходныеДанныеРасчетаЗППоРейсамСрезПоследних.Роль = ЗНАЧЕНИЕ(Справочник.РолиСотрудников.Экспедитор)
			|			ТОГДА новаОтчетВодителя.ОсновноеНачислениеЭкспедитор
			|		ИНАЧЕ новаОтчетВодителя.ОсновноеНачислениеВодитель
			|	КОНЕЦ КАК ОсновноеНачисление,
			|	новаОтчетВодителя.ТарифРасчетаЗП КАК ТарифРасчетаЗП
			|ИЗ
			|	РегистрСведений.зпИсходныеДанныеРасчетаЗППоРейсам.СрезПоследних(
			|			,
			|			Рейс = &Рейс
			|				И ФизЛицо = &ФизЛицо) КАК зпИсходныеДанныеРасчетаЗППоРейсамСрезПоследних
			|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.новаОтчетВодителя КАК новаОтчетВодителя
			|		ПО зпИсходныеДанныеРасчетаЗППоРейсамСрезПоследних.Регистратор = новаОтчетВодителя.Ссылка
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	зпИсходныеДанныеРасчетаЗППоРейсамДополнительныеНачисленияСрезПоследних.Начисление КАК Начисление,
			|	зпИсходныеДанныеРасчетаЗППоРейсамДополнительныеНачисленияСрезПоследних.Сумма КАК Сумма,
			|	зпИсходныеДанныеРасчетаЗППоРейсамДополнительныеНачисленияСрезПоследних.ВидПолигонаПарковки КАК ВидПолигонаПарковки,
			|	зпИсходныеДанныеРасчетаЗППоРейсамДополнительныеНачисленияСрезПоследних.ОбщийКилометражПоРейсу КАК ОбщийКилометражПоРейсу,
			|	зпИсходныеДанныеРасчетаЗППоРейсамДополнительныеНачисленияСрезПоследних.БрейкПробегаПоРейсу КАК БрейкПробегаПоРейсу,
			|	зпИсходныеДанныеРасчетаЗППоРейсамДополнительныеНачисленияСрезПоследних.СтавкаЗаКилометрПробега КАК СтавкаЗаКилометрПробега,
			|	зпИсходныеДанныеРасчетаЗППоРейсамДополнительныеНачисленияСрезПоследних.ТарифицируемыйКилометражПоРейсу КАК ТарифицируемыйКилометражПоРейсу
			|ИЗ
			|	РегистрСведений.зпИсходныеДанныеРасчетаЗППоРейсамДополнительныеНачисления.СрезПоследних(
			|			,
			|			Начисление = ЗНАЧЕНИЕ(Справочник.зпНачисления.ДоплатаЗаПробег)
			|				И Рейс = &Рейс
			|				И ФизЛицо = &ФизЛицо) КАК зпИсходныеДанныеРасчетаЗППоРейсамДополнительныеНачисленияСрезПоследних";
			
			РезультатПакета = Запрос.ВыполнитьПакет();
			
			Выборка = РезультатПакета[0].Выбрать();
			Если Выборка.Следующий() Тогда
				ЗаполнитьЗначенияСвойств(ЭтотОбъект, Выборка);
			КонецЕсли;
			Выборка = РезультатПакета[1].Выбрать();
			Если Выборка.Следующий() Тогда
				ЗаполнитьЗначенияСвойств(НовыеРасчетныеДанные_зпИсходныеДанныеРасчетаЗППоРейсамДополнительныеНачисления.Добавить(), Выборка);
			КонецЕсли;
			
			//Асеев 23.10.2023 (Задача № 5151)<<<
		//Асеев 25.10.2023 (Задача № 5153)>>>
		ИначеЕсли СпособВнесенияДокумента = Перечисления.зпСпособыВнесенияДокументаКорректировкиЗП.ОтчетФорма2_ПробегЗаМКАД Тогда
			
			Запрос = Новый Запрос;
			Запрос.УстановитьПараметр("Рейс", ДанныеЗаполнения.Рейс);
			Запрос.УстановитьПараметр("ФизЛицо", ДанныеЗаполнения.ФизЛицо);
			Запрос.Текст =
			"ВЫБРАТЬ
			|	зпИсходныеДанныеРасчетаЗППоРейсамСрезПоследних.Регион КАК Регион,
			|	зпИсходныеДанныеРасчетаЗППоРейсамСрезПоследних.СхемаРасчетаЗП КАК СхемаРасчетаЗП,
			|	зпИсходныеДанныеРасчетаЗППоРейсамСрезПоследних.Регистратор КАК КорректируемыйДокумент,
			|	зпИсходныеДанныеРасчетаЗППоРейсамСрезПоследних.ДатаРасчета КАК ДатаРасчета,
			|	зпИсходныеДанныеРасчетаЗППоРейсамСрезПоследних.ВидЗанятости КАК ВидЗанятости,
			|	зпИсходныеДанныеРасчетаЗППоРейсамСрезПоследних.СоставЭкипажа КАК СоставЭкипажа,
			|	зпИсходныеДанныеРасчетаЗППоРейсамСрезПоследних.Роль КАК Роль,
			|	зпИсходныеДанныеРасчетаЗППоРейсамСрезПоследних.ПробегУчетный КАК ПробегУчетный,
			|	зпИсходныеДанныеРасчетаЗППоРейсамСрезПоследних.ПробегЗаМКАД КАК ПробегЗаМКАД,
			|	ВЫБОР
			|		КОГДА зпИсходныеДанныеРасчетаЗППоРейсамСрезПоследних.Роль = ЗНАЧЕНИЕ(Справочник.РолиСотрудников.Экспедитор)
			|			ТОГДА новаОтчетВодителя.ОсновноеНачислениеЭкспедитор
			|		ИНАЧЕ новаОтчетВодителя.ОсновноеНачислениеВодитель
			|	КОНЕЦ КАК ОсновноеНачисление,
			|	новаОтчетВодителя.ТарифРасчетаЗП КАК ТарифРасчетаЗП
			|ИЗ
			|	РегистрСведений.зпИсходныеДанныеРасчетаЗППоРейсам.СрезПоследних(
			|			,
			|			Рейс = &Рейс
			|				И ФизЛицо = &ФизЛицо) КАК зпИсходныеДанныеРасчетаЗППоРейсамСрезПоследних
			|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.новаОтчетВодителя КАК новаОтчетВодителя
			|		ПО зпИсходныеДанныеРасчетаЗППоРейсамСрезПоследних.Регистратор = новаОтчетВодителя.Ссылка
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	зпИсходныеДанныеРасчетаЗППоРейсамДополнительныеНачисленияСрезПоследних.Начисление КАК Начисление,
			|	зпИсходныеДанныеРасчетаЗППоРейсамДополнительныеНачисленияСрезПоследних.Сумма КАК Сумма,
			|	зпИсходныеДанныеРасчетаЗППоРейсамДополнительныеНачисленияСрезПоследних.ВидПолигонаПарковки КАК ВидПолигонаПарковки,
			|	зпИсходныеДанныеРасчетаЗППоРейсамДополнительныеНачисленияСрезПоследних.ОбщийКилометражПоРейсу КАК ОбщийКилометражПоРейсу,
			|	зпИсходныеДанныеРасчетаЗППоРейсамДополнительныеНачисленияСрезПоследних.БрейкПробегаПоРейсу КАК БрейкПробегаПоРейсу,
			|	зпИсходныеДанныеРасчетаЗППоРейсамДополнительныеНачисленияСрезПоследних.СтавкаЗаКилометрПробега КАК СтавкаЗаКилометрПробега,
			|	зпИсходныеДанныеРасчетаЗППоРейсамДополнительныеНачисленияСрезПоследних.ТарифицируемыйКилометражПоРейсу КАК ТарифицируемыйКилометражПоРейсу
			|ИЗ
			|	РегистрСведений.зпИсходныеДанныеРасчетаЗППоРейсамДополнительныеНачисления.СрезПоследних(
			|			,
			|			Начисление = ЗНАЧЕНИЕ(Справочник.зпНачисления.ДоплатаЗаПробегЗаМКАД)
			|				И Рейс = &Рейс
			|				И ФизЛицо = &ФизЛицо) КАК зпИсходныеДанныеРасчетаЗППоРейсамДополнительныеНачисленияСрезПоследних";
			
			РезультатПакета = Запрос.ВыполнитьПакет();
			
			Выборка = РезультатПакета[0].Выбрать();
			Если Выборка.Следующий() Тогда
				ЗаполнитьЗначенияСвойств(ЭтотОбъект, Выборка);
			КонецЕсли;
			Выборка = РезультатПакета[1].Выбрать();
			Если Выборка.Следующий() Тогда
				ЗаполнитьЗначенияСвойств(НовыеРасчетныеДанные_зпИсходныеДанныеРасчетаЗППоРейсамДополнительныеНачисления.Добавить(), Выборка);
			КонецЕсли;
			
			//Асеев 25.10.2023 (Задача № 5153)<<<
		//Асеев 27.05.2024 (Задача № 5275)>>>
		ИначеЕсли СпособВнесенияДокумента = Перечисления.зпСпособыВнесенияДокументаКорректировкиЗП.ОтчетФорма2_ОтменаНачисленияМинимальнойСтавки Тогда
			Запрос = Новый Запрос;
			Запрос.УстановитьПараметр("Рейс", ДанныеЗаполнения.Рейс);
			Запрос.УстановитьПараметр("ФизЛицо", ДанныеЗаполнения.ФизЛицо);
			Запрос.Текст =
			"ВЫБРАТЬ
			|	зпИсходныеДанныеРасчетаЗППоРейсамСрезПоследних.Регион КАК Регион,
			|	зпИсходныеДанныеРасчетаЗППоРейсамСрезПоследних.СхемаРасчетаЗП КАК СхемаРасчетаЗП,
			|	зпИсходныеДанныеРасчетаЗППоРейсамСрезПоследних.Регистратор КАК КорректируемыйДокумент,
			|	зпИсходныеДанныеРасчетаЗППоРейсамСрезПоследних.ДатаРасчета КАК ДатаРасчета,
			|	зпИсходныеДанныеРасчетаЗППоРейсамСрезПоследних.ВидЗанятости КАК ВидЗанятости,
			|	зпИсходныеДанныеРасчетаЗППоРейсамСрезПоследних.СоставЭкипажа КАК СоставЭкипажа,
			|	зпИсходныеДанныеРасчетаЗППоРейсамСрезПоследних.Роль КАК Роль,
			|	зпИсходныеДанныеРасчетаЗППоРейсамСрезПоследних.ПробегУчетный КАК ПробегУчетный,
			|	зпИсходныеДанныеРасчетаЗППоРейсамСрезПоследних.ПробегЗаМКАД КАК ПробегЗаМКАД,
			|	ВЫБОР
			|		КОГДА зпИсходныеДанныеРасчетаЗППоРейсамСрезПоследних.Роль = ЗНАЧЕНИЕ(Справочник.РолиСотрудников.Экспедитор)
			|			ТОГДА новаОтчетВодителя.ОсновноеНачислениеЭкспедитор
			|		ИНАЧЕ новаОтчетВодителя.ОсновноеНачислениеВодитель
			|	КОНЕЦ КАК ОсновноеНачисление,
			|	новаОтчетВодителя.ТарифРасчетаЗП КАК ТарифРасчетаЗП
			|ИЗ
			|	РегистрСведений.зпИсходныеДанныеРасчетаЗППоРейсам.СрезПоследних(
			|			,
			|			Рейс = &Рейс
			|				И ФизЛицо = &ФизЛицо) КАК зпИсходныеДанныеРасчетаЗППоРейсамСрезПоследних
			|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.новаОтчетВодителя КАК новаОтчетВодителя
			|		ПО зпИсходныеДанныеРасчетаЗППоРейсамСрезПоследних.Регистратор = новаОтчетВодителя.Ссылка";
			
			Выборка = Запрос.Выполнить().Выбрать();
			Если Выборка.Следующий() Тогда
				ЗаполнитьЗначенияСвойств(ЭтотОбъект, Выборка);
			КонецЕсли;
			//Асеев 27.05.2024 (Задача № 5275)<<<
		КонецЕсли;
		
	КонецЕсли;
	
	Пользователь = ПараметрыСеанса.ТекущийПользователь;
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	Если ФормированиеДвижений_зпИсходныеДанныеРасчетаЗППоЗаказам Тогда
		//Движения.зпИсходныеДанныеРасчетаЗППоЗаказам.Записывать = Истина;
		Движения.зпИсходныеДанныеРасчетаЗППоЗаказам.Очистить();
		
		Движение = Движения.зпИсходныеДанныеРасчетаЗППоЗаказам.Добавить();
		Движение.Период = Дата;
		Движение.Регион = Регион;
		Движение.СхемаРасчетаЗП = СхемаРасчетаЗП;
		Движение.ДатаРасчета = ДатаРасчета;
		Движение.ВидЗанятости = ВидЗанятости;
		Движение.Рейс = Рейс;
		Движение.ФизЛицо = ФизЛицо;
		Движение.СоставЭкипажа = СоставЭкипажа;
		Движение.Роль = Роль;
		Движение.ТипЗаказа = ТипЗаказа;
		Движение.Заказ = Заказ;
		Движение.КатегорияЗаказа = КатегорияЗаказа;
		Движение.ЗонаДоставки = ЗонаДоставки;
		Движение.УровеньОбслуживанияЗаказа = УровеньОбслуживанияЗаказа;
		Движение.ПолигонМаршрутизацииЗаказа = ПолигонМаршрутизацииЗаказа;
		Движение.ПолигонМаршрутизацииСоСложнойПарковкой = ПолигонМаршрутизацииСоСложнойПарковкой;
		Движение.СуммаРасчетаЗП = СуммаРасчетаЗП;
		Движение.ПричинаНевыполненияДоставки = ПричинаНевыполненияДоставки;
		Движение.Начисление = ОсновноеНачисление;
		Движение.ЗначениеПоказателя = ЗначениеПоказателя;
		Движение.Широта = Широта;
		Движение.Долгота = Долгота;
		Движение.ТарифРасчетаЗП = ТарифРасчетаЗП;
		Движение.ВесЗаказа = ВесЗаказа;
		
		Движения.зпИсходныеДанныеРасчетаЗППоЗаказам.Записать();
	КонецЕсли;
	
	//Асеев 17.10.2023 (Задача № 5147)>>>
	Если ФормированиеДвижений_зпИсходныеДанныеРасчетаЗПЧасы Тогда
		Движения.зпИсходныеДанныеРасчетаЗПЧасы.Записывать = Истина;
		Движения.зпИсходныеДанныеРасчетаЗПЧасы.Очистить();
		
		Для Каждого СтрокаРасчета Из НовыеРасчетныеДанные_зпИсходныеДанныеРасчетаЗПЧасы Цикл
			Движение = Движения.зпИсходныеДанныеРасчетаЗПЧасы.Добавить();
			Движение.Период = Дата;
			Движение.Регион = Регион;
			Движение.СхемаРасчетаЗП = СхемаРасчетаЗП;
			Движение.ТарифРасчетаЗП = ТарифРасчетаЗП;
			Движение.ОсновноеНачисление = ОсновноеНачисление;
			Движение.ВидЗанятости = ВидЗанятости;
			Движение.ДатаРасчета = ДатаРасчета;
			Движение.Рейс = Рейс;
			Движение.ФизЛицо = ФизЛицо;
			Движение.СоставЭкипажа = СоставЭкипажа;
			Движение.Роль = Роль;
			ЗаполнитьЗначенияСвойств(Движение, СтрокаРасчета);
		КонецЦикла;
	КонецЕсли;
	//Асеев 17.10.2023 (Задача № 5147)<<<
	
	//Асеев 23.10.2023 (Задача № 5151)>>>
	Если ФормированиеДвижений_зпИсходныеДанныеРасчетаЗППоРейсам Тогда
		Движения.зпИсходныеДанныеРасчетаЗППоРейсам.Записывать = Истина;
		Движения.зпИсходныеДанныеРасчетаЗППоРейсам.Очистить();
		
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("Регистратор", КорректируемыйДокумент);
		Запрос.УстановитьПараметр("ФизЛицо", ФизЛицо);
		Запрос.Текст =
		"ВЫБРАТЬ
		|	*
		|ИЗ
		|	РегистрСведений.зпИсходныеДанныеРасчетаЗППоРейсам КАК зпИсходныеДанныеРасчетаЗППоРейсам
		|ГДЕ
		|	зпИсходныеДанныеРасчетаЗППоРейсам.Регистратор = &Регистратор
		|	И зпИсходныеДанныеРасчетаЗППоРейсам.ФизЛицо = &ФизЛицо";
		
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			Движение = Движения.зпИсходныеДанныеРасчетаЗППоРейсам.Добавить();
			ЗаполнитьЗначенияСвойств(Движение, Выборка);
			Движение.Период = Дата;
			Движение.ПробегУчетный = ПробегУчетный;
			Движение.ПробегЗаМКАД = ПробегЗаМКАД;
		КонецЕсли;
	КонецЕсли;
	
	Если ФормированиеДвижений_зпИсходныеДанныеРасчетаЗППоРейсамДополнительныеНачисления Тогда
		Движения.зпИсходныеДанныеРасчетаЗППоРейсамДополнительныеНачисления.Записывать = Истина;
		Движения.зпИсходныеДанныеРасчетаЗППоРейсамДополнительныеНачисления.Очистить();
		
		Для Каждого СтрокаРасчета Из НовыеРасчетныеДанные_зпИсходныеДанныеРасчетаЗППоРейсамДополнительныеНачисления Цикл
			Движение = Движения.зпИсходныеДанныеРасчетаЗППоРейсамДополнительныеНачисления.Добавить();
			ЗаполнитьЗначенияСвойств(Движение, ЭтотОбъект);
			ЗаполнитьЗначенияСвойств(Движение, СтрокаРасчета);
			Движение.Период = Дата;
		КонецЦикла;
	КонецЕсли;
	//Асеев 23.10.2023 (Задача № 5151)<<<
	
	
	Движения.эк_ЗатратыЗаработнаяПлата.Очистить();
	//Асеев 27.05.2024 (Задача № 5275)>>>
	Движения.эк_ЗатратыЗаработнаяПлата.Записать();//не учитывать собственные движения при перепроведении
	//Асеев 27.05.2024 (Задача № 5275)<<<
	Движения.эк_ЗатратыЗаработнаяПлата.Записывать = Истина;
	
	РегистрыСведений.эк_ЗатратыЗаработнаяПлата.ЗаполнитьДвижения(Движения.эк_ЗатратыЗаработнаяПлата, Движения.зпИсходныеДанныеРасчетаЗППоЗаказам);
	
	РегистрыСведений.эк_ЗатратыЗаработнаяПлата.РаспределитьЧасы(Движения.эк_ЗатратыЗаработнаяПлата, Движения.зпИсходныеДанныеРасчетаЗПЧасы);
	
	//зпИсходныеДанныеРасчетаЗППоЗаказам - не передаем, т.к. нужна инфа по всем заказам
	РегистрыСведений.эк_ЗатратыЗаработнаяПлата.РаспределитьДопНачисленияРейсов(Движения.эк_ЗатратыЗаработнаяПлата, Движения.зпИсходныеДанныеРасчетаЗППоРейсамДополнительныеНачисления);
	
	//Асеев 27.05.2024 (Задача № 5275)>>>
	Если ФормированиеДвижений_зпИсходныеДанныеРасчетаМинимальнойЗП Тогда
		Движения.зпИсходныеДанныеРасчетаМинимальнойЗП.Очистить();
		Движения.зпИсходныеДанныеРасчетаМинимальнойЗП.Записать();//не учитывать собственные движения при перепроведении
		Движения.зпИсходныеДанныеРасчетаМинимальнойЗП.Записывать = Истина;
		Если ОтменаНачисленияМинимальнойСтавки Тогда
			Запрос = Новый Запрос;
			Запрос.УстановитьПараметр("Период", Дата);
			Запрос.УстановитьПараметр("Рейс", Рейс);
			Запрос.УстановитьПараметр("ФизЛицо", ФизЛицо);
			Запрос.Текст =
			"ВЫБРАТЬ
			|	*
			|ИЗ
			|	РегистрСведений.зпИсходныеДанныеРасчетаМинимальнойЗП.СрезПоследних(
			|			&Период,
			|			Рейс = &Рейс
			|				И ФизЛицо = &ФизЛицо) КАК зпИсходныеДанныеРасчетаМинимальнойЗПСрезПоследних
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	*
			|ИЗ
			|	РегистрСведений.эк_ЗатратыЗаработнаяПлата.СрезПоследних(
			|			&Период,
			|			Рейс = &Рейс
			|				И Начисление = ЗНАЧЕНИЕ(Справочник.зпНачисления.ДоплатаДоМинимальногоРазмераОплаты)) КАК эк_ЗатратыЗаработнаяПлатаСрезПоследних
			|ГДЕ
			|	эк_ЗатратыЗаработнаяПлатаСрезПоследних.ФизЛицо = &ФизЛицо";
			
			РезультатПакета = Запрос.ВыполнитьПакет();
			
			Выборка = РезультатПакета[0].Выбрать();
			Пока Выборка.Следующий() Цикл
				Движение = Движения.зпИсходныеДанныеРасчетаМинимальнойЗП.Добавить();
				ЗаполнитьЗначенияСвойств(Движение, Выборка);
				Движение.Период = Дата;
				Движение.ОтменаНачисленияМинимальнойСтавки = Истина;
				Движение.ПрименяетсяМинимальнаяСтавка = Ложь;
			КонецЦикла;
			
			Выборка = РезультатПакета[1].Выбрать();
			Пока Выборка.Следующий() Цикл
				Движение = Движения.эк_ЗатратыЗаработнаяПлата.Добавить();
				ЗаполнитьЗначенияСвойств(Движение, Выборка);
				Движение.Период = Дата;
				Движение.СуммаЗП = 0;
			КонецЦикла;
			
		КонецЕсли;
	КонецЕсли;
	//Асеев 27.05.2024 (Задача № 5275)<<<
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	УжеЗаписан = Не ЭтоНовый();
	
	Если УжеЗаписан Тогда
		Если baoВызовСервера.РейсЗакрыт(Ссылка) Тогда
			Отказ = Истина;
			Сообщить("Документ закрыт для редактирования");
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	РежимФорма3 = СпособВнесенияДокумента = Перечисления.зпСпособыВнесенияДокументаКорректировкиЗП.ОтчетФорма3;
	РежимФорма2_ВремяУчетное = СпособВнесенияДокумента = Перечисления.зпСпособыВнесенияДокументаКорректировкиЗП.ОтчетФорма2_ВремяУчетное;
	РежимФорма2_УчетныйПробег = СпособВнесенияДокумента = Перечисления.зпСпособыВнесенияДокументаКорректировкиЗП.ОтчетФорма2_УчетныйПробег Или СпособВнесенияДокумента = Перечисления.зпСпособыВнесенияДокументаКорректировкиЗП.ОтчетФорма2_ПробегЗаМКАД;
	//Асеев 27.05.2024 (Задача № 5275)>>>
	РежимФорма2_ОтменаНачисленияМинимальнойСтавки = СпособВнесенияДокумента = Перечисления.зпСпособыВнесенияДокументаКорректировкиЗП.ОтчетФорма2_ОтменаНачисленияМинимальнойСтавки;
	//Асеев 27.05.2024 (Задача № 5275)<<<

	ФормированиеДвижений_зпИсходныеДанныеРасчетаЗППоЗаказам = ФормированиеДвижений_зпИсходныеДанныеРасчетаЗППоЗаказам И РежимФорма3;
	ФормированиеДвижений_зпИсходныеДанныеРасчетаЗПЧасы = ФормированиеДвижений_зпИсходныеДанныеРасчетаЗПЧасы И РежимФорма2_ВремяУчетное;
	ФормированиеДвижений_зпИсходныеДанныеРасчетаЗППоРейсам = ФормированиеДвижений_зпИсходныеДанныеРасчетаЗППоРейсам И РежимФорма2_УчетныйПробег;
	ФормированиеДвижений_зпИсходныеДанныеРасчетаЗППоРейсамДополнительныеНачисления = ФормированиеДвижений_зпИсходныеДанныеРасчетаЗППоРейсамДополнительныеНачисления И РежимФорма2_УчетныйПробег;
	//Асеев 27.05.2024 (Задача № 5275)>>>
	ФормированиеДвижений_зпИсходныеДанныеРасчетаМинимальнойЗП = ФормированиеДвижений_зпИсходныеДанныеРасчетаМинимальнойЗП И РежимФорма2_ОтменаНачисленияМинимальнойСтавки;
	
	Если РежимЗаписи = РежимЗаписиДокумента.Проведение Или РежимЗаписи = РежимЗаписиДокумента.ОтменаПроведения Тогда
		Если ФормированиеДвижений_зпИсходныеДанныеРасчетаМинимальнойЗП Тогда
			//проверка на наличие поздних движений по минималке
			Запрос = Новый Запрос;
			Запрос.УстановитьПараметр("Дата", Дата);
			Если УжеЗаписан Тогда
				Запрос.УстановитьПараметр("Ссылка", Ссылка);
				Запрос.Текст =
				"ВЫБРАТЬ ПЕРВЫЕ 1
				|	ИСТИНА КАК Поле1
				|ИЗ
				|	РегистрСведений.зпИсходныеДанныеРасчетаМинимальнойЗП КАК зпИсходныеДанныеРасчетаМинимальнойЗП
				|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.зпКорректировкаИсходныхДанных КАК зпКорректировкаИсходныхДанных
				|		ПО (зпКорректировкаИсходныхДанных.Ссылка = &Ссылка)
				|			И (зпКорректировкаИсходныхДанных.Проведен)
				|			И зпИсходныеДанныеРасчетаМинимальнойЗП.Период > зпКорректировкаИсходныхДанных.Дата
				|			И зпИсходныеДанныеРасчетаМинимальнойЗП.Рейс = зпКорректировкаИсходныхДанных.Рейс
				|			И зпИсходныеДанныеРасчетаМинимальнойЗП.ФизЛицо = зпКорректировкаИсходныхДанных.ФизЛицо
				|
				|ОБЪЕДИНИТЬ ВСЕ
				|
				|ВЫБРАТЬ ПЕРВЫЕ 1
				|	ИСТИНА
				|ИЗ
				|	РегистрСведений.зпИсходныеДанныеРасчетаМинимальнойЗП КАК зпИсходныеДанныеРасчетаМинимальнойЗП
				|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.зпКорректировкаИсходныхДанных КАК зпКорректировкаИсходныхДанных
				|		ПО (зпКорректировкаИсходныхДанных.Ссылка = &Ссылка)
				|			И (зпИсходныеДанныеРасчетаМинимальнойЗП.Период > &Дата)
				|			И зпИсходныеДанныеРасчетаМинимальнойЗП.Рейс = зпКорректировкаИсходныхДанных.Рейс
				|			И зпИсходныеДанныеРасчетаМинимальнойЗП.ФизЛицо = зпКорректировкаИсходныхДанных.ФизЛицо";
			Иначе
				Запрос.УстановитьПараметр("Рейс", Рейс);
				Запрос.УстановитьПараметр("ФизЛицо", ФизЛицо);
				Запрос.Текст =
				"ВЫБРАТЬ ПЕРВЫЕ 1
				|	ИСТИНА КАК Поле1
				|ИЗ
				|	РегистрСведений.зпИсходныеДанныеРасчетаМинимальнойЗП КАК зпИсходныеДанныеРасчетаМинимальнойЗП
				|ГДЕ
				|	зпИсходныеДанныеРасчетаМинимальнойЗП.Период > &Дата
				|	И зпИсходныеДанныеРасчетаМинимальнойЗП.Рейс = &Рейс
				|	И зпИсходныеДанныеРасчетаМинимальнойЗП.ФизЛицо = &ФизЛицо";
			КонецЕсли;
			Если Не Запрос.Выполнить().Пустой() Тогда
				Отказ = Истина;
				Сообщить("Минимальные ставки уже изменены более поздними документами");
				Возврат;
			КонецЕсли;
			//проверка на наличие закрытых периодов
			Запрос.УстановитьПараметр("Регион", Регион);
			Запрос.УстановитьПараметр("НачалоПериода", НачалоМесяца(ДатаРасчета));
			Запрос.УстановитьПараметр("ОкончаниеПериода", КонецМесяца(ДатаРасчета));
			Запрос.Текст =
			"ВЫБРАТЬ ПЕРВЫЕ 1
			|	ИСТИНА КАК ЭтоРасчетныйПериод,
			|	зпЗакрытыеРасчетныеПериоды.РасчетныйПериод КАК РасчетныйПериод
			|ИЗ
			|	РегистрСведений.зпЗакрытыеРасчетныеПериоды КАК зпЗакрытыеРасчетныеПериоды
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ (ВЫБРАТЬ ПЕРВЫЕ 1
			|			зпЗакрытыеРасчетныеПериоды.Период КАК Период
			|		ИЗ
			|			РегистрСведений.зпЗакрытыеРасчетныеПериоды КАК зпЗакрытыеРасчетныеПериоды
			|		ГДЕ
			|			зпЗакрытыеРасчетныеПериоды.Регион = &Регион
			|			И зпЗакрытыеРасчетныеПериоды.РасчетныйПериод МЕЖДУ &НачалоПериода И &ОкончаниеПериода
			|		
			|		УПОРЯДОЧИТЬ ПО
			|			Период УБЫВ) КАК ВЗ_МаксПериоды
			|		ПО зпЗакрытыеРасчетныеПериоды.Период = ВЗ_МаксПериоды.Период
			|			И (зпЗакрытыеРасчетныеПериоды.Регион = &Регион)
			|			И (зпЗакрытыеРасчетныеПериоды.РасчетныйПериод МЕЖДУ &НачалоПериода И &ОкончаниеПериода)
			|			И (зпЗакрытыеРасчетныеПериоды.ПериодЗакрыт)
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ ПЕРВЫЕ 1
			|	ЛОЖЬ,
			|	NULL
			|ИЗ
			|	Документ.зпФиксацияМесяца КАК зпФиксацияМесяца
			|ГДЕ
			|	зпФиксацияМесяца.Регион = &Регион
			|	И зпФиксацияМесяца.Проведен
			|	И зпФиксацияМесяца.ПериодРасчета МЕЖДУ &НачалоПериода И &ОкончаниеПериода";
			Результат = Запрос.Выполнить();
			Если Не Результат.Пустой() Тогда
				Выборка = Результат.Выбрать();
				Выборка.Следующий();
				
				Отказ = Истина;
				Если Выборка.ЭтоРасчетныйПериод Тогда
					Сообщить("Расчетный период з/п закрыт " + Формат(Выборка.РасчетныйПериод, "ДФ='MMMM yyyy'") + " (контроль документа)");
				Иначе
					Сообщить("Произведена фиксация месяца по минимальной з/п " + Формат(ДатаРасчета, "ДФ='MMMM yyyy'") + " (контроль документа)");
				КонецЕсли;
				Возврат;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	//Асеев 27.05.2024 (Задача № 5275)<<<
	
КонецПроцедуры
