
Процедура ОбработкаПроведения(Отказ, Режим)
	//{{__КОНСТРУКТОР_ДВИЖЕНИЙ_РЕГИСТРОВ
	// Данный фрагмент построен конструктором.
	// При повторном использовании конструктора, внесенные вручную изменения будут утеряны!!!

	// регистр СтатусыЗапросовНаПередачуЗаказовЭкипажам
	Движения.СтатусыЗапросовНаПередачуЗаказовЭкипажам.Записывать = Истина;
	Движения.СтатусыЗапросовНаПередачуЗаказовЭкипажам.Очистить();
	Движение = Движения.СтатусыЗапросовНаПередачуЗаказовЭкипажам.Добавить();
	Движение.Период = Дата;
	Движение.ЗапросНаПередачуЗаказовЭкипажу = Запрос;
	Движение.СтатусЗапроса = ОбщийСтатусЗапроса;
	
	СтатусПодтвержден = Перечисления.СтатусыЗапросовНаПередачуЗаказовЭкипажам.ЗапросПодтвержден;
	
	Если ОбщийСтатусЗапроса = СтатусПодтвержден Тогда
		Отбор = Новый Структура("СтатусЗапроса", СтатусПодтвержден);
		ЗаказыКПеремещению = Заказы.НайтиСтроки(Отбор);
		ЗапросРейсов = Новый Запрос;
		ЗапросРейсов.Текст = "ВЫБРАТЬ
		|	ЗапросНаПередачуЗаказовЭкипажу.РейсИсточник КАК РейсИсточник,
		|	ЗапросНаПередачуЗаказовЭкипажу.РейсПриемник КАК РейсПриемник
		|ИЗ
		|	Документ.ПодтверждениеПередачиЗаказовЭкипажу КАК ПодтверждениеПередачиЗаказовЭкипажу
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЗапросНаПередачуЗаказовЭкипажу КАК ЗапросНаПередачуЗаказовЭкипажу
		|		ПО ПодтверждениеПередачиЗаказовЭкипажу.Запрос = ЗапросНаПередачуЗаказовЭкипажу.Ссылка
		|ГДЕ
		|	ПодтверждениеПередачиЗаказовЭкипажу.Ссылка = &Ссылка";
		ЗапросРейсов.УстановитьПараметр("Ссылка",Ссылка);
		Выборка = ЗапросРейсов.Выполнить().Выбрать();
		Выборка.Следующий();
		РейсИсточник = Выборка.РейсИсточник.ПолучитьОбъект();
		РейсПриемник = Выборка.РейсПриемник.ПолучитьОбъект();
		Для Каждого ЗаказКПеремещению Из ЗаказыКПеремещению Цикл
			СтрокаЗаказаВИсточнике = РейсИсточник.Заказы.Найти(ЗаказКПеремещению.Заказ);
			Если СтрокаЗаказаВИсточнике = Неопределено Тогда 
				ВызватьИсключение "Заказ " + ЗаказКПеремещению.Заказ.Номер + " отсутствует в рейсе-источнике!";
			КонецЕсли;
			Если СтрокаЗаказаВИсточнике.УдаленИзРейса Тогда 
				ВызватьИсключение "Заказ " + ЗаказКПеремещению.Заказ.Номер + " удален из рейса-источника!";
			КонецЕсли;
			СтрокаВРейсеПриемнике = РейсПриемник.Заказы.Найти(ЗаказКПеремещению.Заказ);
			Если СтрокаВРейсеПриемнике = Неопределено Тогда 
				СтрокаВРейсеПриемнике = РейсПриемник.Заказы.Добавить();
			КонецЕсли;	
			ЗаполнитьЗначенияСвойств(СтрокаВРейсеПриемнике, СтрокаЗаказаВИсточнике);
			СтрокаЗаказаВИсточнике.УдаленИзРейса = Истина;
		КонецЦикла;
		
		Попытка
			РейсИсточник.Записать(РежимЗаписиДокумента.Проведение);
		Исключение
			ВызватьИсключение "Не удалось провести рейс-источник! " + ОписаниеОшибки();
		КонецПопытки;	
		
		Попытка
			РейсПриемник.Записать(РежимЗаписиДокумента.Проведение);
		Исключение
			ВызватьИсключение "Не удалось провести рейс-приемник! " + ОписаниеОшибки();
		КонецПопытки;	
	КонецЕсли;	
КонецПроцедуры
