
Процедура ОбработкаПроведения(Отказ, Режим)

	//Асеев 25.08.2022 (Задача № 4881)>>>
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Рейс", Рейс);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Рейс.ТерминалДоставки КАК ТерминалДоставки
	|ИЗ
	|	Документ.Рейс КАК Рейс
	|ГДЕ
	|	Рейс.Ссылка = &Рейс";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Выборка.Следующий();
	
	ТерминалДоставки = Выборка.ТерминалДоставки;
	//Асеев 25.08.2022 (Задача № 4881)<<<
	
	// регистр СостояниеДСПриУдаленномЗакрытии
	Движения.СостояниеДСПриУдаленномЗакрытии.Записывать = Истина;
	Движения.СостояниеДСПриУдаленномЗакрытии.Очистить();
	Для Каждого ТекСтрокаЗаказы Из Заказы Цикл
		//Асеев 25.08.2022 (Задача № 4881)>>>
		//Если Не ТекСтрокаЗаказы.СуммаПринята Тогда
		Если Не ТекСтрокаЗаказы.СуммаПринята Или ТекСтрокаЗаказы.СуммаНеУчитывается Тогда
		//Асеев 25.08.2022 (Задача № 4881)<<<
			Продолжить;
		КонецеСли;	
		Движение = Движения.СостояниеДСПриУдаленномЗакрытии.Добавить();
		Движение.Период = Дата;
		Движение.Заказ = ТекСтрокаЗаказы.Заказ;
		Движение.Рейс = Рейс;
		Движение.ДСПринятыКассиром = Истина;
		Движение.СуммаДС = ТекСтрокаЗаказы.ПринятаяСумма;
		Движение.Транспорт = Транспорт;
		Движение.Водитель = Водитель;
		Движение.ТипОплаты = ТекСтрокаЗаказы.ТипОплаты;
	КонецЦикла;

	//Асеев 25.08.2022 (Задача № 4881)>>>
	Движения.СтатусыДвиженияПриложенныхДокументов.Очистить();
	//Движения.СтатусыДвиженияПриложенныхДокументов.Записывать = Истина;
	Для Каждого СтрокаДоставки Из Заказы Цикл
		Если СтрокаДоставки.ТребуетсяПодписаниеИВозвратСопрДокументов Или СтрокаДоставки.ТребуетсяПодписаниеИВозвратБНДокументов Тогда
			Если СтрокаДоставки.СопрДокументПоступил И СтрокаДоставки.ТребуетсяПереподписаниеСопрДокумента Тогда
				Движение = Движения.СтатусыДвиженияПриложенныхДокументов.Добавить();
				Движение.Период = Дата - 1;
				Движение.Регион = ТерминалДоставки;
				Движение.Заказ = СтрокаДоставки.Заказ;
				Движение.ВидПриложенногоДокумента = Справочники.ВидыПриложенныхДокументов.СопроводительныеДокументы;
				Движение.Статус = Справочники.СтатусыДвиженияПриложенныхДокументов.ДокументыВозвращеныВТК;
				
				Движение = Движения.СтатусыДвиженияПриложенныхДокументов.Добавить();
				Движение.Период = Дата;
				Движение.Регион = ТерминалДоставки;
				Движение.Заказ = СтрокаДоставки.Заказ;
				Движение.ВидПриложенногоДокумента = Справочники.ВидыПриложенныхДокументов.СопроводительныеДокументы;
				Движение.Статус = Справочники.СтатусыДвиженияПриложенныхДокументов.ДокументыТребуютПереподписания;
			ИначеЕсли СтрокаДоставки.СопрДокументПоступил Тогда
				Движение = Движения.СтатусыДвиженияПриложенныхДокументов.Добавить();
				Движение.Период = Дата;
				Движение.Регион = ТерминалДоставки;
				Движение.Заказ = СтрокаДоставки.Заказ;
				Движение.ВидПриложенногоДокумента = Справочники.ВидыПриложенныхДокументов.СопроводительныеДокументы;
				Движение.Статус = Справочники.СтатусыДвиженияПриложенныхДокументов.ДокументыВозвращеныВТК;
			Иначе
				Движение = Движения.СтатусыДвиженияПриложенныхДокументов.Добавить();
				Движение.Период = Дата;
				Движение.Регион = ТерминалДоставки;
				Движение.Заказ = СтрокаДоставки.Заказ;
				Движение.ВидПриложенногоДокумента = Справочники.ВидыПриложенныхДокументов.СопроводительныеДокументы;
				Движение.Статус = Справочники.СтатусыДвиженияПриложенныхДокументов.ДокументыНеВозвращеныВТК;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	Движения.СтатусыДвиженияПриложенныхДокументов.Записать();
	Если ТерминалДоставки = Справочники.РегиональныеТерминалы.МоскваСтриж Тогда
		РассылкаОНесданныхДокументах(ТерминалДоставки);
	КонецЕсли;
	//Асеев 25.08.2022 (Задача № 4881)<<<
	
КонецПроцедуры

//Асеев 25.08.2021 (Задача № 4649)>>>
Процедура РассылкаОНесданныхДокументах(ТерминалДоставки)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Период", Дата);
	Запрос.УстановитьПараметр("Доставки", Заказы.ВыгрузитьКолонку("Заказ"));
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	СтатусыДвиженияПриложенныхДокументовСрезПоследних.Заказ.Номер КАК НомерЗаказа,
	|	СтатусыДвиженияПриложенныхДокументовСрезПоследних.Заказ.ВладелецТовара.Наименование КАК Партнер,
	|	СтатусыДвиженияПриложенныхДокументовСрезПоследних.Регистратор ССЫЛКА Документ.ПриемДСОтЭкипажейУдаленногоЗакрытия КАК ЭтоКасса,
	|	ПриемДСОтЭкипажейУдаленногоЗакрытияЗаказы.ТипОплаты.Наименование КАК ТипОплаты,
	|	ВЫБОР
	|		КОГДА ПриемДСОтЭкипажейУдаленногоЗакрытияЗаказы.РезультатДоставки В (ЗНАЧЕНИЕ(Справочник.новаРезультатМестнойДоставки.Выполнена), ЗНАЧЕНИЕ(Справочник.новаРезультатМестнойДоставки.ВыполненаЧастично))
	|			ТОГДА ПриемДСОтЭкипажейУдаленногоЗакрытияЗаказы.РезультатДоставки.Наименование
	|		ИНАЧЕ ПриемДСОтЭкипажейУдаленногоЗакрытияЗаказы.ПричинаНеВыполнения.Наименование
	|	КОНЕЦ КАК Статус
	|ИЗ
	|	РегистрСведений.СтатусыДвиженияПриложенныхДокументов.СрезПоследних(&Период, Заказ В (&Доставки)) КАК СтатусыДвиженияПриложенныхДокументовСрезПоследних
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПриемДСОтЭкипажейУдаленногоЗакрытия.Заказы КАК ПриемДСОтЭкипажейУдаленногоЗакрытияЗаказы
	|		ПО (ПриемДСОтЭкипажейУдаленногоЗакрытияЗаказы.Ссылка = &Ссылка)
	|			И СтатусыДвиженияПриложенныхДокументовСрезПоследних.Заказ = ПриемДСОтЭкипажейУдаленногоЗакрытияЗаказы.Заказ
	|ГДЕ
	|	СтатусыДвиженияПриложенныхДокументовСрезПоследних.Статус = ЗНАЧЕНИЕ(Справочник.СтатусыДвиженияПриложенныхДокументов.ДокументыНеВозвращеныВТК)
	|
	|УПОРЯДОЧИТЬ ПО
	|	ЭтоКасса,
	|	Партнер,
	|	НомерЗаказа
	|ИТОГИ ПО
	|	ЭтоКасса
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПриемДСОтЭкипажейУдаленногоЗакрытия.Транспорт.Наименование КАК Автомобиль,
	|	ПриемДСОтЭкипажейУдаленногоЗакрытия.Водитель.Наименование КАК Водитель,
	|	ПриемДСОтЭкипажейУдаленногоЗакрытия.Экспедитор.Наименование КАК Экспедитор
	|ИЗ
	|	Документ.ПриемДСОтЭкипажейУдаленногоЗакрытия КАК ПриемДСОтЭкипажейУдаленногоЗакрытия
	|ГДЕ
	|	ПриемДСОтЭкипажейУдаленногоЗакрытия.Ссылка = &Ссылка";
	
	
	РезультатПакета = Запрос.ВыполнитьПакет();
	
	Результат = РезультатПакета[0];
	Если Результат.Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	ВыборкаЗаголовок = РезультатПакета[1].Выбрать();
	ВыборкаЗаголовок.Следующий();
	
	ДатаРассылки = ТекущаяДата();
	
	Макет = Документы.ПриходДСПоОтчетуВодителя.ПолучитьМакет("РассылкаОНесданныхДокументах");
	
	ТабДок = Новый ТабличныйДокумент;
	
	Область = Макет.ПолучитьОбласть("Заголовок");
	ПараметрыОбласти = Область.Параметры;
	ПараметрыОбласти.Заполнить(ВыборкаЗаголовок);
	ПараметрыОбласти.ДатаРассылки = ДатаРассылки;
	ТабДок.Вывести(Область);
	
	ОбластьШапка = Макет.ПолучитьОбласть("Шапка");
	ОбластьСтрока = Макет.ПолучитьОбласть("Строка");
	
	ВыборкаЭтоКасса = Результат.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаЭтоКасса.Следующий() Цикл
		
		ПараметрыОбласти = ОбластьШапка.Параметры;
		Если ВыборкаЭтоКасса.ЭтоКасса Тогда
			ПараметрыОбласти.Операции = "сдаче в кассу";
			ПараметрыОбласти.Документы = "безналичные/сопроводительные";
		Иначе
			ПараметрыОбласти.Операции = "переносе";
			ПараметрыОбласти.Документы = "безналичные";
		КонецЕсли;
		ТабДок.Вывести(ОбластьШапка);
		
		ПараметрыОбласти = ОбластьСтрока.Параметры;
		
		Выборка = ВыборкаЭтоКасса.Выбрать();
		Пока Выборка.Следующий() Цикл
			ПараметрыОбласти.Заполнить(Выборка);
			ПараметрыОбласти.НомерЗаказа = СокрЛП(ПараметрыОбласти.НомерЗаказа);
			ТабДок.Вывести(ОбластьСтрока);
		КонецЦикла;
		
	КонецЦикла;
	
	Область = Макет.ПолучитьОбласть("Подвал");
	ПараметрыОбласти = Область.Параметры;
	ПараметрыОбласти.ПриходДС = ЭтотОбъект;
	ТабДок.Вывести(Область);
	
	//#Если Клиент Тогда
	//	ТабДок.Показать();
	//#КонецЕсли
	
	//ТекстПисьма = "Автомобиль: " + ВыборкаЗаголовок.Автомобиль + "<br>"
	//	+ "Водитель: " + ВыборкаЗаголовок.Водитель + "<br>"
	//	+ "Экспедитор: " + ВыборкаЗаголовок.Экспедитор + "<br>"
	//	+ "<br>"
	//	+ "При сдаче в кассу, не сданы безналичные/сопроводительные документы<br>" + ЭтотОбъект; 
		
	//Сообщить(ТекстПисьма);
		
	ИмяФайла = КаталогВременныхФайлов() + Номер + ".html";
	
	ТабДок.Записать(ИмяФайла, ТипФайлаТабличногоДокумента.HTML);
	
	ТекстПисьма = Новый ТекстовыйДокумент;
	ТекстПисьма.Прочитать(ИмяФайла);
	ТекстПисьма = ТекстПисьма.ПолучитьТекст();
	
	МассивПолучателей = Новый Массив;
	Если ПараметрыСеанса.ЭтоТестоваяСреда Тогда
		МассивПолучателей.Добавить("m.aseev@strizh-logistic.ru");
	Иначе
		Если ТерминалДоставки = Справочники.РегиональныеТерминалы.МоскваСтриж Тогда
			МассивПолучателей.Добавить("kassir@strizh-logistic.ru");
		ИначеЕсли ТерминалДоставки = Справочники.РегиональныеТерминалы.СПбСтриж Тогда
			МассивПолучателей.Добавить("kassir_spb@strizh-logistic.ru");
		КонецЕсли;
		МассивПолучателей.Добавить("logist@strizh-logistic.ru");
		МассивПолучателей.Добавить("stanislav.tumakov@strizh-logistic.ru");
		МассивПолучателей.Добавить("y.semichastnov@strizh-logistic.ru");
		МассивПолучателей.Добавить("inna.illarionova@strizh-logistic.ru");
		МассивПолучателей.Добавить("dmitry.sherbinkin@strizh-logistic.ru");
	КонецЕсли;
	
	ТекстОшибки = "";
	ОбработкаUnion = Обработки.СТРИЖ_ИнтеграцияUniOne.Создать();
	ОбработкаUnion.ОтправитьEMAIL_HTTP(МассивПолучателей, "Несданные документы " + Формат(ДатаРассылки, "ДФ=dd.MM.yyyy"), ТекстПисьма,, "Сервер 1С",, ТипТекстаПочтовогоСообщения.HTML,, ТекстОшибки,,,, Ложь);
	
	Попытка
		УдалитьФайлы(ИмяФайла);
	Исключение
	КонецПопытки;
	
КонецПроцедуры
//Асеев 25.08.2021 (Задача № 4649)<<<


