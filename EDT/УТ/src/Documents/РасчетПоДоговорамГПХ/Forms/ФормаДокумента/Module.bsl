
&НаСервере
Процедура ЗаполнитьНаСервере()
	Для каждого Сотрудник Из Объект.ДоговорыГПХ Цикл
		Количество = ПолучитьКоличествоЗаказов(Сотрудник.Сотрудник);	
		Сотрудник.КоличествоЗаказов = Количество;
		Сотрудник.Сумма = Количество * Объект.Ставка;
		Сотрудник.СуммаНДФЛ = Сотрудник.Сумма * 0.13;
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура Заполнить(Команда)
	ЗаполнитьНаСервере();
КонецПроцедуры

&НаСервере
Функция ПолучитьКоличествоЗаказов(Сотрудник)
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	СводПоЗарплатеОбороты.ПоказательКоличестваОборот КАК Доставка,
		|	СводПоЗарплатеОбороты.ФизЛицо КАК ФизЛицо
		|ИЗ
		|	РегистрНакопления.СводПоЗарплате.Обороты(&ДатаНач, &ДатаКон, , ) КАК СводПоЗарплатеОбороты
		|ГДЕ
		|	СводПоЗарплатеОбороты.ФизЛицо = &ФизЛицо";
	
	Запрос.УстановитьПараметр("ДатаКон", КонецДня(Объект.ОкончаниеПериода));
	Запрос.УстановитьПараметр("ДатаНач", НачалоДня(Объект.НачалоПериода));
	Запрос.УстановитьПараметр("ФизЛицо", Сотрудник);
	
	РезультатЗапроса = Запрос.Выполнить().Выбрать();
	Пока РезультатЗапроса.Следующий() Цикл
		Возврат РезультатЗапроса.Доставка;
	КонецЦикла;
	Сообщить("Для " + Сотрудник + " нет записей в отчетный период");
	Возврат 0;
КонецФункции

&НаСервере
Функция СформироватьБланки()
	Отбор = Новый Структура;
	Отбор.Вставить("Печать", Истина);
	ВрТаб = Объект.ДоговорыГПХ.Выгрузить(Отбор);
	Если ВрТаб.Количество() = 0 Тогда
		ВрТаб = Объект.ДоговорыГПХ;
	КонецЕсли;
	
	ТабДок = Новый ТабличныйДокумент;
	Макет = Документы.РасчетПоДоговорамГПХ.ПолучитьМакет("Макет");
	Область = Макет.ПолучитьОбласть("Шапка");
	Для Каждого ТекСтр Из ВрТаб Цикл
		Область.Параметры.Дата = ФорматДаты(Объект.ОкончаниеПериода);
		Область.Параметры.Компания = ТекСтр.Контрагент;
		Область.Параметры.НомерДог = ТекСтр.Договор.Номер;
		Область.Параметры.ДатаДог = ФорматДаты(ТекСтр.Договор.Дата);
		Область.Параметры.ГенДир = НайтиГенДир(ТекСтр.Контрагент);
		Область.Параметры.ФИО = ТекСтр.Сотрудник;
		Область.Параметры.ДатаНачало = ФорматДаты(Объект.НачалоПериода);
		Область.Параметры.ДатаКонец = ФорматДаты(Объект.ОкончаниеПериода);
		Область.Параметры.Сумма = ТекСтр.Сумма;
		Область.Параметры.СуммаПрописью = ЧислоПрописью(ТекСтр.Сумма, "Л=ru_RU;ДП=Истина;НП=Истина", "рубль, рубля, рублей, м, копейка, копейки, копеек, ж");
		Проп = ЧислоПрописью(Окр(ТекСтр.СуммаНДФЛ,0), "Л=ru_RU;НП=Истина", "рубль, рубля, рублей, , , , , 0");
		Область.Параметры.НДФЛПрописью = Лев(Проп,СтрДлина(Проп)- 4);
		ТабДок.Вывести(Область);
		
		ТабДок.ВывестиГоризонтальныйРазделительСтраниц();

	КонецЦикла;
	
	Возврат ТабДок;	
КонецФункции

&НаСервере
Функция ФорматДаты(Дата)
	Стр = СтрЗаменить(Формат(Дата, "ДФ=dd_.MMMM.yyyy"),"."," ");
	Если Стр <> "" Тогда
		Стр = СтрЗаменить(Стр,"_","»");
		Стр = "«" + Стр;
	КонецЕсли;	
	Возврат Стр;	
КонецФункции

&НаСервере
Функция НайтиГенДир(Владелец)
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	КонтактныеЛицаКонтрагентов.КонтактноеЛицо КАК КонтактноеЛицо
		|ИЗ
		|	Справочник.КонтактныеЛицаКонтрагентов КАК КонтактныеЛицаКонтрагентов
		|ГДЕ
		|	КонтактныеЛицаКонтрагентов.Владелец = &Владелец
		|	И КонтактныеЛицаКонтрагентов.РольКонтактногоЛица = &РольКонтактногоЛица
		|	И КонтактныеЛицаКонтрагентов.ПометкаУдаления = ЛОЖЬ";
	
	Запрос.УстановитьПараметр("Владелец", Владелец);
	Запрос.УстановитьПараметр("РольКонтактногоЛица", Справочники.РолиКонтактныхЛиц.Гендиректор);
	
	РезультатЗапроса = Запрос.Выполнить();	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		Возврат ВыборкаДетальныеЗаписи.КонтактноеЛицо;
	КонецЦикла;	
	Возврат "";	
КонецФункции

&НаКлиенте
Процедура Печать(Команда)
	ТабДок = СформироватьБланки();
	ТабДок.АвтоМасштаб = Истина;	
	ТабДок.ОриентацияСтраницы = ОриентацияСтраницы.Портрет;
	ТабДок.ОтображатьСетку = Ложь;
	ТабДок.Защита = Ложь;
	ТабДок.ТолькоПросмотр = Ложь;
	ТабДок.ОтображатьЗаголовки = Ложь;
	ТабДок.Показать();
КонецПроцедуры

&НаКлиенте
Процедура УбратьГалочки(Команда)
	Для каждого Стр из Объект.ДоговорыГПХ Цикл
		Стр.Печать = Ложь;
	КонецЦикла;
КонецПроцедуры
