
//Асеев 20.05.2022 (Задача № 4809)>>>
Процедура ПечатьВнесистемнойВозвратнойНакладной(ТабДок, Ссылка) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВозвратТоваровПоставщику.Номер КАК Номер,
	|	ВозвратТоваровПоставщику.Дата КАК Дата,
	|	НаборнаяВедомостьДляВозврата.Номер КАК СделкаНомер,
	|	ВозвратТоваровПоставщику.СкладМагазина КАК СкладМагазина,
	|	ВозвратТоваровПоставщику.СкладМагазина.Наименование КАК СкладМагазинаНаименование,
	|	ВозвратТоваровПоставщику.СкладМагазина.Адрес КАК СкладМагазинаАдрес,
	|	ВозвратТоваровПоставщику.Контрагент КАК Контрагент,
	|	ВозвратТоваровПоставщику.Контрагент.НаименованиеПолное КАК КонтрагентНаименованиеПолное,
	|	ВозвратТоваровПоставщику.Контрагент.ИНН КАК КонтрагентИНН,
	|	ВозвратТоваровПоставщику.Контрагент.КПП КАК КонтрагентКПП,
	|	ВозвратТоваровПоставщику.ДоговорКонтрагента.Организация.Наименование КАК ДоговорКонтрагентаОрганизацияНаименование
	|ИЗ
	|	Документ.ВозвратТоваровПоставщику КАК ВозвратТоваровПоставщику
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.НаборнаяВедомостьДляВозврата КАК НаборнаяВедомостьДляВозврата
	|		ПО ВозвратТоваровПоставщику.Сделка = НаборнаяВедомостьДляВозврата.Ссылка
	|ГДЕ
	|	ВозвратТоваровПоставщику.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВозвратТоваровПоставщикуВнесистемныйВозврат.НомерСтроки КАК НПП,
	|	ВозвратТоваровПоставщикуВнесистемныйВозврат.Идентификатор КАК Артикул,
	|	ВозвратТоваровПоставщикуВнесистемныйВозврат.Наименование КАК Номенклатура,
	|	ВозвратТоваровПоставщикуВнесистемныйВозврат.Количество КАК Количество
	|ИЗ
	|	Документ.ВозвратТоваровПоставщику.ВнесистемныйВозврат КАК ВозвратТоваровПоставщикуВнесистемныйВозврат
	|ГДЕ
	|	ВозвратТоваровПоставщикуВнесистемныйВозврат.Ссылка = &Ссылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	ВозвратТоваровПоставщикуВнесистемныйВозврат.НомерСтроки
	|ИТОГИ
	|	СУММА(Количество)
	|ПО
	|	ОБЩИЕ";
	
	РезультатПакета = Запрос.ВыполнитьПакет();
	
	ВыборкаШапка = РезультатПакета[0].Выбрать();
	ВыборкаШапка.Следующий();
	
	ВыборкаТовары = РезультатПакета[1].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Если ВыборкаТовары.Следующий() Тогда
		КоличествоОбщее = ВыборкаТовары.Количество;
		ВыборкаТовары = ВыборкаТовары.Выбрать();
	КонецЕсли;
	
	
	Макет = ПолучитьМакет("ВозвратнаяНакладнаяВнесистемная");
	
	// шапка
	ЮрАдрес    = УправлениеКонтактнойИнформацией.ПолучитьАдресИзКонтактнойИнформации(ВыборкаШапка.Контрагент, "Юридический");	
	ТекстГрузополучатель = "" + ВыборкаШапка.КонтрагентНаименованиеПолное + ", ИНН" + ВыборкаШапка.КонтрагентИНН  + ", " + ВыборкаШапка.КонтрагентКПП + "," + ЮрАдрес;
	
	Область = Макет.ПолучитьОбласть("Шапка");
	ПараметрыОбласти = Область.Параметры;
	ПараметрыОбласти.НомерНаборной = ВыборкаШапка.СделкаНомер;	
	ПараметрыОбласти.НомерВозврата = ВыборкаШапка.Номер;
	ПараметрыОбласти.ДатаСоставления = Формат(ВыборкаШапка.Дата, "ДФ=dd.MM.yyyy");	
	Если ЗначениеЗаполнено(ВыборкаШапка.СкладМагазина) Тогда
		ПараметрыОбласти.СкладВозвратаПартнера = "" + ВыборкаШапка.СкладМагазинаНаименование + "  (" + ВыборкаШапка.СкладМагазинаАдрес + ")";		
	Иначе
	КонецЕсли;
	
	ПараметрыОбласти.ДатаВозврата = Формат(ТекущаяДата(), "ДФ=dd.MM.yyyy");	
	ПараметрыОбласти.Договор = "Договор №  от ";
	ПараметрыОбласти.ТипВозврата = "Не системный";
	ПараметрыОбласти.Грузополучатель = ТекстГрузополучатель;
	ПараметрыОбласти.Грузоотправитель = ВыборкаШапка.ДоговорКонтрагентаОрганизацияНаименование;
	
	ТабДок.Вывести(Область);
	
	Область = Макет.ПолучитьОбласть("Строка");
	ПараметрыОбласти = Область.Параметры;
	
	Пока ВыборкаТовары.Следующий() Цикл
		ПараметрыОбласти.Заполнить(ВыборкаТовары);
		ТабДок.Вывести(Область);
	КонецЦикла;
	
	ПустаяОбласть = Макет.ПолучитьОбласть("ПустыеОбласти");
	ТабДок.Вывести(ПустаяОбласть);
	
	Область = Макет.ПолучитьОбласть("СтрокаИтогиОбщие");
	ПараметрыОбласти = Область.Параметры;
	ПараметрыОбласти.КоличествоОбщее = КоличествоОбщее;
	ТабДок.Вывести(Область);
	
	ТабДок.Вывести(ПустаяОбласть);
	
	Область = Макет.ПолучитьОбласть("Подвал");
	ПараметрыОбласти = Область.Параметры;
	ПараметрыОбласти.ИтогиПрописью = "Всего количество переданных заказов/товаров составило " + КоличествоОбщее + " ( " + ФормированиеПечатныхФорм.КоличествоПрописью(КоличествоОбщее) + ")";
	ПараметрыОбласти.КолМестТоваровНадпись = "Количество мест:";
	ПараметрыОбласти.КоличествоМестТоваровПоДокументу = КоличествоОбщее;
	ТабДок.Вывести(Область);
	
КонецПроцедуры
//Асеев 20.05.2022 (Задача № 4809)<<<


