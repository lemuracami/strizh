
#Область СобытияФормы 
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
		
	ТолькоПросмотр = НЕ Объект.Ссылка.Пустая();
		
	Если Параметры.Свойство("Сотрудник") Тогда
		Если Параметры.Сотрудник <> Неопределено Тогда
			Объект.ФизЛицо = Параметры.Сотрудник.ФизЛицо;
		КонецЕсли; 
	КонецЕсли; 
	
	Если Параметры.Свойство("Состояние") Тогда
		Объект.СостояниеОборудования = Параметры.Состояние;
	КонецЕсли; 
	
	Если Параметры.Свойство("ЗаполнитьКомплект") Тогда
		
		Если Параметры.ЗаполнитьКомплект Тогда
			ЗаполнитьТЧТекущийКомплектОборудования();
		КонецЕсли; 		
		
	КонецЕсли;
	
	ИзменитьОтображениеКомплектаОборудования();
	
	Если ТолькоПросмотр Тогда
		
		Для каждого ТекСтрока Из Объект.СписокОборудования Цикл
			
			Если ТекСтрока.Оборудование.МодельОборудования.ТипОборудования = Перечисления.ТипыОборудования.Смартфон Тогда
				Смартфон = ТекСтрока.Оборудование;
			ИначеЕсли ТекСтрока.Оборудование.МодельОборудования.ТипОборудования = Перечисления.ТипыОборудования.ТерминалПриемаОплатыПоКарте Тогда
				Терминал = ТекСтрока.Оборудование;
			ИначеЕсли ТекСтрока.Оборудование.МодельОборудования.ТипОборудования = Перечисления.ТипыОборудования.ПринтерЭтикеток Тогда
				Принтер = ТекСтрока.Оборудование;
			КонецЕсли; 
						
		КонецЦикла; 
	Иначе
		Объект.Пользователь = ПараметрыСеанса.ТекущийПользователь;		
	КонецЕсли; 
	
	//СброситьРазмерыИПоложениеОкна();
	
	//Асеев 10.04.2024 (Задача № 5255)>>>
	Элементы.ДекорацияВыданоНаДлительныйСрок.Видимость = Объект.ВыдачаНаДлительныйСрок;
	//Асеев 10.04.2024 (Задача № 5255)<<<
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Элементы.ДекорацияЭкипаж.Заголовок = ?(Объект.НаемныйЭкипаж, "НАЁМНИК", "ШТАТНЫЙ");
	ИзменитьОтображениеОборудования();
	
КонецПроцедуры

&НаКлиенте
Процедура ВнешнееСобытие(Источник, Событие, Данные)

	Если НЕ Источник = "Сканер штрихкода" Тогда
		Возврат;
	КонецЕсли;		
  
	Если НЕ ЗначениеЗаполнено(Данные) Тогда
		Возврат;
	КонецЕсли;	
	
	НайтиОборудование(Данные);
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)

	Если СмартфонПоврежден Тогда
		ПроверяемыеРеквизиты.Добавить("СмартфонОписаниеПовреждения");	
	КонецЕсли; 	
	
	Если ТерминалПоврежден Тогда
		ПроверяемыеРеквизиты.Добавить("ТерминалОписаниеПовреждения");	
	КонецЕсли; 	
	
	Если ПринтерПоврежден Тогда
		ПроверяемыеРеквизиты.Добавить("ПринтерОписаниеПовреждения");	
	КонецЕсли; 	
	
	Если Смартфон.Пустая() И Терминал.Пустая() И Принтер.Пустая() Тогда
		ПроверяемыеРеквизиты.Добавить("Смартфон");	
		ПроверяемыеРеквизиты.Добавить("Терминал");	
		ПроверяемыеРеквизиты.Добавить("Принтер");	
	КонецЕсли; 
	
КонецПроцедуры

#КонецОбласти 

#Область КомандыФормы

&НаКлиенте
Процедура СостояниеОборудованияПриИзменении(Элемент)

	ОчиститьРеквизитыФормы();
	
	ИзменитьОтображениеОборудования();
	
КонецПроцедуры

&НаКлиенте
Процедура ОборудованиеУстановитьКомплектнымПриИзменении(Элемент)

	ИмяРеквизита = СтрЗаменить(Элемент.Имя, "УстановитьКомплектным", "");	
	УстановитьКомплектным = ЭтаФорма[Элемент.Имя];
	
	Оборудование = ЭтаФорма[ИмяРеквизита];
	
	Если Оборудование.Пустая() Тогда
		Возврат;
	КонецЕсли; 
	
	Для каждого ТекСтрокаОборудование Из Объект.СписокОборудования Цикл
		
		Если ТекСтрокаОборудование.Оборудование = Оборудование Тогда
			ТекСтрокаОборудование.УстановитьКомплектным = УстановитьКомплектным;	
		КонецЕсли;
		
	КонецЦикла; 
		
КонецПроцедуры

&НаКлиенте
Процедура ОборудованиеПоврежденоПриИзменении(Элемент)
	
	ИмяРеквизита = СтрЗаменить(Элемент.Имя, "Поврежден", "") ;
	ОборудованиеПовреждено = ЭтаФорма[Элемент.Имя];
	
	Элементы[ИмяРеквизита+"ОписаниеПовреждения"].Видимость = ОборудованиеПовреждено;
	Элементы["Декорация"+ИмяРеквизита+"Комплект"].Видимость = ОборудованиеПовреждено;
	
	ЭтаФорма[ИмяРеквизита+"ОписаниеПовреждения"] = ?(ОборудованиеПовреждено, ЭтаФорма[ИмяРеквизита+"ОписаниеПовреждения"], "");
	
	Оборудование = ЭтаФорма[ИмяРеквизита];
	
	Если Оборудование.Пустая() Тогда
		Возврат;
	КонецЕсли; 
	
	Для каждого ТекСтрокаОборудование Из Объект.СписокОборудования Цикл
		
		Если ТекСтрокаОборудование.Оборудование = Оборудование Тогда
			ТекСтрокаОборудование.ОбрудованиеПовреждено = ОборудованиеПовреждено;	
		КонецЕсли;
		
	КонецЦикла; 
	
КонецПроцедуры

&НаКлиенте
Процедура ОборудованиеОписаниеПоврежденияПриИзменении(Элемент)

	ИмяРеквизита = СтрЗаменить(Элемент.Имя, "ОписаниеПовреждения", "") ;
	ОборудованиеПовреждено = ЭтаФорма[Элемент.Имя];
	ОписаниеПовреждения = ЭтаФорма[Элемент.Имя];
	
	Оборудование = ЭтаФорма[ИмяРеквизита];
	
	Если Оборудование.Пустая() Тогда
		Возврат;
	КонецЕсли; 
	
	Для каждого ТекСтрокаОборудование Из Объект.СписокОборудования Цикл
		
		Если ТекСтрокаОборудование.Оборудование = Оборудование Тогда
			ТекСтрокаОборудование.ОписаниеПовреждения = ОписаниеПовреждения;	
		КонецЕсли;
		
	КонецЦикла; 
	
КонецПроцедуры
 
&НаКлиенте
Процедура ОборудованиеГиперссылкаНажатие(Элемент)

	ОписаниеОповещения = Новый ОписаниеОповещения("НайтиОборудование", ЭтаФорма);
	ПоказатьВводСтроки(ОписаниеОповещения,,"Введите код оборудования",,);
		
КонецПроцедуры

&НаКлиенте
Процедура Команда1(Команда)
	
	НайтиОборудование("00000000001");
	
КонецПроцедуры
 
#КонецОбласти 

#Область СлужебныеПроцедуры

&НаКлиенте
Процедура ОчиститьРеквизитыФормы()

	Смартфон = Неопределено;
	Терминал = Неопределено;
	Принтер = Неопределено;
	
	СмартфонПоврежден = Ложь;
	ТерминалПоврежден = Ложь;
	ПринтерПоврежден = Ложь;
	
	СмартфонУстановитьКомплектным = Ложь;
	ТерминалУстановитьКомплектным = Ложь;
	ПринтерУстановитьКомплектным = Ложь;
	
	СмартфонОписаниеПовреждения = "";
	ТерминалОписаниеПовреждения = "";
	ПринтерОписаниеПовреждения = "";
	
	Объект.СписокОборудования.Очистить();	

КонецПроцедуры // ()
 
&НаСервере
Процедура ИзменитьОтображениеКомплектаОборудования()
		
	Для каждого ТекСтрока Из Объект.ТекущийКомплектОборудования Цикл
		
		Если ТекСтрока.Оборудование.МодельОборудования.ТипОборудования = Перечисления.ТипыОборудования.Смартфон Тогда
			КомплектСмартфон = ТекСтрока.Оборудование;
		ИначеЕсли ТекСтрока.Оборудование.МодельОборудования.ТипОборудования = Перечисления.ТипыОборудования.ТерминалПриемаОплатыПоКарте Тогда
			КомплектТерминал = ТекСтрока.Оборудование;
		ИначеЕсли ТекСтрока.Оборудование.МодельОборудования.ТипОборудования = Перечисления.ТипыОборудования.ПринтерЭтикеток Тогда
			КомплектПринтер = ТекСтрока.Оборудование;
		КонецЕсли; 
		
	КонецЦикла; 
	
КонецПроцедуры // () 

&НаКлиенте
Процедура ИзменитьОтображениеОборудования()

	ЭтоВыдача = Объект.СостояниеОборудования = ПредопределенноеЗначение("Перечисление.СостоянияЕдиницыОборудования.Выдано");
	
	ПустойЗаголовок = "<<ожидание сканирования>>";
	
	Элементы.Смартфон1.Заголовок = ?(Смартфон.Пустая(), ПустойЗаголовок, Смартфон);
	Элементы.Терминал1.Заголовок = ?(Терминал.Пустая(), ПустойЗаголовок, Терминал);
	Элементы.Принтер1.Заголовок  = ?(Принтер .Пустая(), ПустойЗаголовок, Принтер);

	Элементы.Смартфон2.Заголовок = ?(КомплектСмартфон.Пустая(), "<<не установлено>>", КомплектСмартфон);
	Элементы.Терминал2.Заголовок = ?(КомплектТерминал.Пустая(), "<<не установлено>>", КомплектТерминал);
	Элементы.Принтер2.Заголовок  = ?(КомплектПринтер .Пустая(), "<<не установлено>>", КомплектПринтер);
	
	Элементы.СмартфонПоврежден.Видимость = (НЕ ЭтоВыдача) И (НЕ Смартфон.Пустая());
	Элементы.ТерминалПоврежден.Видимость = (НЕ ЭтоВыдача) И (НЕ Терминал.Пустая());
	Элементы.ПринтерПоврежден.Видимость = (НЕ ЭтоВыдача) И (НЕ Принтер.Пустая());
	
	Элементы.СмартфонОписаниеПовреждения.Видимость = СмартфонПоврежден;
	Элементы.ТерминалОписаниеПовреждения.Видимость = ТерминалПоврежден;
	Элементы.ПринтерОписаниеПовреждения.Видимость = ПринтерПоврежден;
	
	Элементы.ДекорацияСмартфонКомплект.Видимость = СмартфонПоврежден;
	Элементы.ДекорацияТерминалКомплект.Видимость = ТерминалПоврежден;
	Элементы.ДекорацияПринтерКомплект.Видимость = ПринтерПоврежден;
	
	//Асеев 10.04.2024 (Задача № 5255)>>>
	Если Объект.ВыдачаНаДлительныйСрок Тогда
		Элементы.СмартфонУстановитьКомплектным.Видимость = Ложь;
		Элементы.ТерминалУстановитьКомплектным.Видимость  = Ложь;
		Элементы.ПринтерУстановитьКомплектным.Видимость  = Ложь;
	//Асеев 10.04.2024 (Задача № 5255)<<<
	Иначе
		Элементы.СмартфонУстановитьКомплектным.Видимость = (Смартфон <> КомплектСмартфон) И (НЕ Смартфон.Пустая()) И ЭтоВыдача;
		Элементы.ТерминалУстановитьКомплектным.Видимость  = (Терминал <> КомплектТерминал) И (НЕ Терминал.Пустая()) И ЭтоВыдача;
		Элементы.ПринтерУстановитьКомплектным.Видимость  = (Принтер <> КомплектПринтер) И (НЕ Принтер.Пустая()) И ЭтоВыдача;
	КонецЕсли;
	
	//СмартфонУстановитьКомплектным = КомплектСмартфон.Пустая() И Элементы.СмартфонУстановитьКомплектным.Видимость;
	ОборудованиеУстановитьКомплектнымПриИзменении(Элементы.СмартфонУстановитьКомплектным);
	
	//ТерминалУстановитьКомплектным = КомплектТерминал.Пустая() И Элементы.ТерминалУстановитьКомплектным.Видимость;
	ОборудованиеУстановитьКомплектнымПриИзменении(Элементы.ТерминалУстановитьКомплектным);
	
	//ПринтерУстановитьКомплектным = КомплектПринтер.Пустая() И Элементы.ПринтерУстановитьКомплектным.Видимость;
	ОборудованиеУстановитьКомплектнымПриИзменении(Элементы.ПринтерУстановитьКомплектным);
		
	РаскраситьОборудованияПоТипу(Смартфон, КомплектСмартфон, "Смартфон");
	РаскраситьОборудованияПоТипу(Терминал, КомплектТерминал, "Терминал");
	РаскраситьОборудованияПоТипу(Принтер, КомплектПринтер, "Принтер");
	
КонецПроцедуры // ()

&НаКлиенте
Процедура РаскраситьОборудованияПоТипу(Оборудование1, Оборудование2, ИмяПоля)

	ОборудованиеЗаполнено = (НЕ Оборудование1.Пустая()) И (НЕ Оборудование2.Пустая());
	
	Если ОборудованиеЗаполнено И (Оборудование1 <> Оборудование2) Тогда
		УстановитьЦветФонаПолей(Элементы[ИмяПоля+"1"], Элементы[ИмяПоля+"2"], WebЦвета.БледноСиреневый);
	ИначеЕсли ОборудованиеЗаполнено И (Оборудование1 = Оборудование2) Тогда
		УстановитьЦветФонаПолей(Элементы[ИмяПоля+"1"], Элементы[ИмяПоля+"2"], WebЦвета.БледноЗеленый);
	Иначе
		УстановитьЦветФонаПолей(Элементы[ИмяПоля+"1"], Элементы[ИмяПоля+"2"], Новый Цвет);		
	КонецЕсли; 		

КонецПроцедуры // ()
 
&НаКлиенте
Процедура УстановитьЦветФонаПолей(Поле1, Поле2, Цвет)

	Поле1.ЦветФона = Цвет;
	Поле2.ЦветФона = Цвет;

КонецПроцедуры // ()
 

&НаКлиенте
Процедура НайтиОборудование(Код, ДопПараметры = Неопределено) Экспорт

	Если НЕ ЗначениеЗаполнено(Код) Тогда
		Возврат;	
	КонецЕсли; 
	
	СоответствиеОборудования = УчетОборудованияКлиент.НайтиОборудование(Код);
	 
	Для каждого СтрокаОборудование Из СоответствиеОборудования Цикл
		
		ТекстОшибки = ПроверитьОборудование(СтрокаОборудование.Значение, Объект.Рейс, Объект.СостояниеОборудования, Объект.ФизЛицо);
		
		Если ТекстОшибки <> "" Тогда
			
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = ТекстОшибки;
			Сообщение.Поле = Строка(СтрокаОборудование.Ключ);
			Сообщение.Сообщить();
			
			Прервать;
			
		КонецЕсли; 
		
		Если СтрокаОборудование.Ключ = ПредопределенноеЗначение("Перечисление.ТипыОборудования.Смартфон") Тогда
			УдалитьОборудованиеИзТабличнойЧасти(Смартфон);
			Смартфон = СтрокаОборудование.Значение;
			ДобавитьОборудованиеВТабличнуюЧасть(Смартфон);	
		ИначеЕсли СтрокаОборудование.Ключ = ПредопределенноеЗначение("Перечисление.ТипыОборудования.ПринтерЭтикеток") Тогда
			УдалитьОборудованиеИзТабличнойЧасти(Принтер);
			Принтер = СтрокаОборудование.Значение;			
			ДобавитьОборудованиеВТабличнуюЧасть(Принтер);	
		ИначеЕсли СтрокаОборудование.Ключ = ПредопределенноеЗначение("Перечисление.ТипыОборудования.ТерминалПриемаОплатыПоКарте") Тогда
			УдалитьОборудованиеИзТабличнойЧасти(Терминал);
			Терминал = СтрокаОборудование.Значение;
			ДобавитьОборудованиеВТабличнуюЧасть(Терминал);	
		КонецЕсли; 
		
	КонецЦикла; 
	
	ИзменитьОтображениеОборудования();
	
КонецПроцедуры // ()

&НаКлиенте
Процедура ДобавитьОборудованиеВТабличнуюЧасть(Оборудование)
	
	НС = Объект.СписокОборудования.Добавить();
	НС.Оборудование = Оборудование;
	НС.ВремяВводаДанных = ТекущаяДата();
	НС.ТранспортКомплекта = Объект.Транспорт;	

КонецПроцедуры // ()

&НаКлиенте
Процедура УдалитьОборудованиеИзТабличнойЧасти(Оборудование)

	МассивСтрок = Объект.СписокОборудования.НайтиСтроки(Новый Структура("Оборудование", Оборудование));
	
	Для каждого СтрокаТЧ Из МассивСтрок Цикл
		Объект.СписокОборудования.Удалить(СтрокаТЧ.НомерСтроки-1);
	КонецЦикла; 

	Если Оборудование = Смартфон Тогда
		ИмяРеквизита = "Смартфон";
	ИначеЕсли Оборудование = Терминал Тогда
		ИмяРеквизита = "Смартфон";
	ИначеЕсли Оборудование = Принтер Тогда
		ИмяРеквизита = "Принтер";		
	КонецЕсли; 
	
	ЭтаФорма[ИмяРеквизита+"Поврежден"] = Ложь;
	ЭтаФорма[ИмяРеквизита+"УстановитьКомплектным"] = Ложь;
	ЭтаФорма[ИмяРеквизита+"ОписаниеПовреждения"] = "";
	
КонецПроцедуры // ()
 
&НаСервереБезКонтекста
Функция ПроверитьОборудование(Знач Оборудование, Знач Рейс, Знач СостояниеОборудования, Знач ФизЛицо)
	
	ТекстОшибки = "";
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Оборудование", Оборудование);
	Если Рейс.Пустая() Или СостояниеОборудования = Перечисления.СостоянияЕдиницыОборудования.Сдано Тогда
		Запрос.Текст =
		"ВЫБРАТЬ
		|	СтатусыОборудованияСрезПоследних.Оборудование ЕСТЬ NULL КАК Нерабочее,
		|	ЛОЖЬ КАК ТипНаРейсе,
		|	ЕСТЬNULL(УчетОборудованияСрезПоследних.СостояниеЕдиницыОборудования, ЗНАЧЕНИЕ(Перечисление.СостоянияЕдиницыОборудования.Сдано)) КАК ТекущееСостояние,
		|	ЕСТЬNULL(УчетОборудованияСрезПоследних.Рейс, ЗНАЧЕНИЕ(Документ.Рейс.ПустаяСсылка)) КАК ТекущийРейс,
		|	УчетОборудованияСрезПоследних.ФизЛицо КАК ТекущееФизЛицо
		|ИЗ
		|	Справочник.Оборудование КАК Т_Оборудование
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.УчетОборудования.СрезПоследних(, Оборудование = &Оборудование) КАК УчетОборудованияСрезПоследних
		|		ПО (ИСТИНА)
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтатусыОборудования.СрезПоследних(, Оборудование = &Оборудование) КАК СтатусыОборудованияСрезПоследних
		|		ПО (СтатусыОборудованияСрезПоследних.СтатусОборудования = ЗНАЧЕНИЕ(Перечисление.СтатусыЕдиницыОборудования.ВРаботе))
		|ГДЕ
		|	Т_Оборудование.Ссылка = &Оборудование";
	Иначе
		//на рейс, выдача
		Запрос.УстановитьПараметр("Рейс", Рейс);
		Запрос.Текст =
		"ВЫБРАТЬ
		|	СтатусыОборудованияСрезПоследних.Оборудование ЕСТЬ NULL КАК Нерабочее,
		|	НЕ УчетОборудованияСрезПоследних_НаРейсе.Оборудование ЕСТЬ NULL КАК ТипНаРейсе,
		|	УчетОборудованияСрезПоследних_НаРейсе.Оборудование КАК ВыданноеОборудование,
		|	ЕСТЬNULL(УчетОборудованияСрезПоследних.СостояниеЕдиницыОборудования, ЗНАЧЕНИЕ(Перечисление.СостоянияЕдиницыОборудования.Сдано)) КАК ТекущееСостояние,
		|	ЕСТЬNULL(УчетОборудованияСрезПоследних.Рейс, ЗНАЧЕНИЕ(Документ.Рейс.ПустаяСсылка)) КАК ТекущийРейс,
		|	УчетОборудованияСрезПоследних.ФизЛицо КАК ТекущееФизЛицо
		|ИЗ
		|	Справочник.Оборудование КАК Т_Оборудование
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.УчетОборудования.СрезПоследних(, Оборудование = &Оборудование) КАК УчетОборудованияСрезПоследних
		|		ПО (ИСТИНА)
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтатусыОборудования.СрезПоследних(, Оборудование = &Оборудование) КАК СтатусыОборудованияСрезПоследних
		|		ПО (СтатусыОборудованияСрезПоследних.СтатусОборудования = ЗНАЧЕНИЕ(Перечисление.СтатусыЕдиницыОборудования.ВРаботе))
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.УчетОборудования.СрезПоследних КАК УчетОборудованияСрезПоследних_НаРейсе
		|		ПО (УчетОборудованияСрезПоследних_НаРейсе.Рейс = &Рейс)
		|			И (УчетОборудованияСрезПоследних_НаРейсе.СостояниеЕдиницыОборудования = ЗНАЧЕНИЕ(Перечисление.СостоянияЕдиницыОборудования.Выдано))
		|			И Т_Оборудование.МодельОборудования.ТипОборудования = УчетОборудованияСрезПоследних_НаРейсе.Оборудование.МодельОборудования.ТипОборудования
		|ГДЕ
		|	Т_Оборудование.Ссылка = &Оборудование";
	КонецЕсли;
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Если СостояниеОборудования = Перечисления.СостоянияЕдиницыОборудования.Выдано Тогда
			Если СостояниеОборудования = Выборка.ТекущееСостояние Тогда
				//попытка выдать уже выданное
				Если Рейс.Пустая() Тогда
					//попытка выдать на длительный срок
					Если Выборка.ТекущийРейс.Пустая() Тогда
						//уже выдано на длительный срок
						ТекстОшибки = "Оборудование " + Оборудование + " (" + Оборудование.Код + ") выдано на длительный срок " + Выборка.ТекущееФизЛицо;
						Прервать;
					Иначе
						//уже выдано на рейс
						ТекстОшибки = "Оборудование " + Оборудование + " (" + Оборудование.Код + ") выдано на рейс " + Выборка.ТекущийРейс;
						Прервать;
					КонецЕсли;
				Иначе
					//попытка выдать на рейс
					Если Выборка.ТекущийРейс.Пустая() Тогда
						//уже выдано на длительный срок
						ТекстОшибки = "Оборудование " + Оборудование + " (" + Оборудование.Код + ") выдано на длительный срок " + Выборка.ТекущееФизЛицо;
						Прервать;
					Иначе
						//уже выдано на рейс
						ТекстОшибки = "Оборудование " + Оборудование + " (" + Оборудование.Код + ") выдано на рейс " + Выборка.ТекущийРейс;
						Прервать;
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
			Если Выборка.ТипНаРейсе Тогда
				ТекстОшибки = "На рейс " + Рейс + " уже выдано оборудование такого типа " + Выборка.ВыданноеОборудование + "(" + Выборка.ВыданноеОборудование.Код + ")";
				Прервать;	
			КонецЕсли;
			Если Выборка.Нерабочее Тогда
				ТекстОшибки = "Оборудование должно иметь статус Рабочее";
				Прервать;
			КонецЕсли;
		Иначе//Сдано
			Если СостояниеОборудования = Выборка.ТекущееСостояние Тогда
				//уже сдано
				ТекстОшибки = "Оборудование " + Оборудование + " (" + Оборудование.Код + ") сдано!";
				Прервать;
			Иначе
				Если Рейс.Пустая() Тогда
					//с длительного срока
					Если Выборка.ТекущийРейс.Пустая() Тогда
						//проверка сотрудника
						Если ФизЛицо <> Выборка.ТекущееФизЛицо Тогда
							ТекстОшибки = "Оборудование " + Оборудование + " (" + Оборудование.Код + ") выдано на длительный срок " + Выборка.ТекущееФизЛицо;
							Прервать;
						КонецЕсли;
					Иначе
						//было выдано на рейс
						ТекстОшибки = "Оборудование " + Оборудование + " (" + Оборудование.Код + ") выдано на рейс " + Выборка.ТекущийРейс;
						Прервать;
					КонецЕсли;
				Иначе
					//с рейса
					Если Выборка.ТекущийРейс.Пустая() Тогда
						//было выдано на длительный срок
						ТекстОшибки = "Оборудование " + Оборудование + " (" + Оборудование.Код + ") выдано на длительный срок " + Выборка.ТекущееФизЛицо;
						Прервать;
					ИначеЕсли Выборка.ТекущийРейс <> Рейс Тогда
						ТекстОшибки = "Оборудование " + Оборудование + " (" + Оборудование.Код + ") выдано на рейс " + Выборка.ТекущийРейс;
						Прервать;
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
		
	Возврат ТекстОшибки;
	
КонецФункции

&НаСервере
Процедура ЗаполнитьТЧТекущийКомплектОборудования()
		
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	КомплектыОборудованияСрезПоследних.Оборудование КАК Оборудование
		|ИЗ
		|	РегистрСведений.КомплектыОборудования.СрезПоследних(, Транспорт = &Транспорт) КАК КомплектыОборудованияСрезПоследних";
	
	Запрос.УстановитьПараметр("Транспорт", Объект.Транспорт);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Объект.ТекущийКомплектОборудования.Загрузить(РезультатЗапроса.Выгрузить());
			
КонецПроцедуры // ()

&НаСервере
Процедура СброситьРазмерыИПоложениеОкна()
    КлючОбъекта = "Документ.УчетОборудования.Форма.ФормаУчетаОборудованияаписи/Такси/НастройкиОкна";
    ИмяПользователя = ПользователиИнформационнойБазы.ТекущийПользователь().Имя;
    ХранилищеСистемныхНастроек.Удалить(КлючОбъекта,"", ИмяПользователя);
    КлючСохраненияПоложенияОкна = Строка(Новый УникальныйИдентификатор);
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	Если Объект.СостояниеОборудования = ПредопределенноеЗначение("Перечисление.СостоянияЕдиницыОборудования.Выдано") Тогда
		
		Если Объект.ФизЛицо.Пустая() Тогда
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = "Укажите физ.лицо";
			Сообщение.Поле = "ФизЛицо";
			Сообщение.ПутьКДанным = "Объект";
			Сообщение.Сообщить(); 
			
			Отказ = Истина;
		КонецЕсли; 	
		
	КонецЕсли; 
	
КонецПроцедуры

#КонецОбласти 
 