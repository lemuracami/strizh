
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	
	Если Объект.ТерминалОбработки.Пустая() Тогда	
		Объект.ТерминалОбработки = ПараметрыСеанса.ТерминалДоставки;
		Если Объект.ТерминалОбработки.Пустая() Тогда		
			Объект.ТерминалОбработки = ПредопределенноеЗначение("Справочник.РегиональныеТерминалы.МоскваСтриж");			
		КонецЕсли;
	КонецЕсли;
	
	Если Объект.Ответственный.Пустая() Тогда
		Объект.Ответственный = ПараметрыСеанса.ТекущийПользователь;	
	КонецЕсли;
	
	
	ЭтаФорма.ТолькоПросмотр = Объект.Проведен;	

	
КонецПроцедуры


&НаКлиенте
Процедура ВнешнееСобытие(Источник, Событие, Данные)
	
	Если Объект.Проведен Тогда
		#Если Клиент Тогда
			Сообщить("Документ проведен");
		#КонецЕсли		
		Возврат;		
	КонецЕсли;
	
	
	Если НЕ Источник = "Сканер штрихкода" Тогда
		Возврат;
	КонецЕсли;		
  
	Если НЕ ЗначениеЗаполнено(Данные) Тогда
		Возврат;
	КонецЕсли;
	
	ОбработатьШКНаСервере(СокрЛП(Данные));
	
	
КонецПроцедуры


Процедура ОбработатьШКНаСервере(Штрихкод)
	
	
	Если Лев(Штрихкод, 2) = "37" И СтрДлина(Штрихкод) = 13 И СтрНайти(Штрихкод, "-") = 0 Тогда
		Штрихкод = Лев(Штрихкод, СтрДлина(Штрихкод) - 1);	
	Иначе
	КонецЕсли;
	
	Заказ = Неопределено;
	
	Запрос = новый Запрос("ВЫБРАТЬ
	                      |	ШтрихкодыЗаказов.Заказ КАК Заказ,
	                      |	1 КАК Приоритет
	                      |ПОМЕСТИТЬ ВТ
	                      |ИЗ
	                      |	РегистрСведений.ШтрихкодыЗаказов КАК ШтрихкодыЗаказов
	                      |ГДЕ
	                      |	ШтрихкодыЗаказов.Штрихкод = &Штрихкод
	                      |
	                      |ОБЪЕДИНИТЬ ВСЕ
	                      |
	                      |ВЫБРАТЬ
	                      |	РеализацияТоваровУслуг.Ссылка,
	                      |	2
	                      |ИЗ
	                      |	Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
	                      |ГДЕ
	                      |	РеализацияТоваровУслуг.НомерВнешнегоЗаказа = &Штрихкод
	                      |
	                      |ОБЪЕДИНИТЬ ВСЕ
	                      |
	                      |ВЫБРАТЬ
	                      |	РеализацияТоваровУслуг.Ссылка,
	                      |	3
	                      |ИЗ
	                      |	Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
	                      |ГДЕ
	                      |	РеализацияТоваровУслуг.Номер = &Штрихкод
	                      |;
	                      |
	                      |////////////////////////////////////////////////////////////////////////////////
	                      |ВЫБРАТЬ
	                      |	ВТ.Заказ КАК Заказ,
	                      |	ЕСТЬNULL(ПараметрыКонтрагентовСрезПоследних.УчетЗаказовПоМестам, ЛОЖЬ) КАК УчетПоМестам
	                      |ИЗ
	                      |	ВТ КАК ВТ
	                      |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПараметрыКонтрагентов.СрезПоследних КАК ПараметрыКонтрагентовСрезПоследних
	                      |		ПО ВТ.Заказ.ВладелецТовара = ПараметрыКонтрагентовСрезПоследних.Контрагент
	                      |
	                      |УПОРЯДОЧИТЬ ПО
	                      |	ВТ.Приоритет");
	Запрос.УстановитьПараметр("Штрихкод", Штрихкод);
	Рез = Запрос.Выполнить().Выбрать();
	
	Если Рез.Следующий() Тогда
		
		Заказ = Рез.Заказ;
		
		Если Рез.УчетПоМестам Тогда // ------------ ПО МЕСТАМ
			
			Най = Объект.ЗаказыПоМестам.НайтиСтроки(Новый Структура("Заказ", Заказ));
			
			Если Най.Количество() Тогда // заказ есть в таблице
				
				Для каждого Ст Из Най Цикл			
					Если СокрЛП(Ст.Штрихкод) = СокрЛП(Штрихкод) Тогда	
						Ст.Собрано = Истина;					
					КонецЕсли;				
				КонецЦикла;			
			
			Иначе  // заказа нет в таблице
				
				ЗапросДанные = Новый Запрос("ВЫБРАТЬ РАЗЛИЧНЫЕ
				                            |	МестаПоЗаказам.Ссылка КАК Место,
				                            |	МестаПоЗаказам.Заказ КАК Заказ,
				                            |	МестаПоЗаказам.Штрихкод КАК Штрихкод,
				                            |	ВЫБОР
				                            |		КОГДА МестаПоЗаказам.Штрихкод = &Штрихкод
				                            |			ТОГДА ИСТИНА
				                            |		ИНАЧЕ ЛОЖЬ
				                            |	КОНЕЦ КАК Собрано,
				                            |	МестаПоЗаказам.Заказ.ВладелецТовара КАК Контрагент
				                            |ИЗ
				                            |	Справочник.МестаПоЗаказам КАК МестаПоЗаказам
				                            |ГДЕ
				                            |	МестаПоЗаказам.Заказ = &Заказ");
				ЗапросДанные.УстановитьПараметр("Заказ", Заказ);
				ЗапросДанные.УстановитьПараметр("Штрихкод", Штрихкод);
				РезДанныеТЗ = ЗапросДанные.Выполнить().Выгрузить();	
				
				Для каждого Стр Из РезДанныеТЗ Цикл			
					НСтр = Объект.ЗаказыПоМестам.Добавить();
					ЗаполнитьЗначенияСвойств(НСтр, Стр);		
				КонецЦикла;			
			
			КонецЕсли;
												
		Иначе  // ------------ БЕЗ УЧЕТА МЕСТ	
			
			Най = Объект.ЗаказыНеПоМестам.НайтиСтроки(Новый Структура("Заказ", Заказ));
			
			Если Най.Количество() Тогда // заказ есть в таблице
				
				Для каждого Ст Из Най Цикл			
					Ст.МестСобрано = Ст.МестСобрано + 1;					
				КонецЦикла;			
			
			Иначе  // заказа нет в таблице
			
				НСтр = Объект.ЗаказыНеПоМестам.Добавить();	
				НСтр.Заказ = Заказ;
				НСтр.Контрагент = Заказ.ВладелецТовара;
				НСтр.КоличествоМест = Заказ.КоличествоМест;	
				НСтр.МестСобрано = 1;
				
			КонецЕсли;
			
		КонецЕсли;		
		
		Модифицированность = Истина;

	Иначе
		//Сообщить("Штрихкод не распознан");
	КонецЕсли;
				

КонецПроцедуры


&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Элементы.ЗаказыНеПоМестам.ВыделенныеСтроки.Очистить();
	Элементы.ЗаказыПоМестам.ВыделенныеСтроки.Очистить();
	Элементы.ЗаказыНеПоМестам.ТекущийЭлемент = Неопределено;
	Элементы.ЗаказыПоМестам.ТекущийЭлемент = Неопределено;
	
	
	//ПолучитьСерверТО().ПодключитьКлиента(ЭтаФорма);
	
КонецПроцедуры


&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	//ПолучитьСерверТО().ОтключитьКлиента(ЭтаФорма);
	
КонецПроцедуры


&НаКлиенте
Процедура ДобавитьЗаказ(Команда)
		
	Подсказка = "Введите номер заказа";
	Оповещение = Новый ОписаниеОповещения("ПослеВводаСтроки", ЭтотОбъект);
	ПоказатьВводСтроки(Оповещение, "", Подсказка, 0, Ложь);	
	
КонецПроцедуры


&НаКлиенте
Процедура ПослеВводаСтроки(Строка, Параметры) Экспорт
	
    Если НЕ Строка = Неопределено Тогда			
		ОбработатьШКНаСервере(СокрЛП(Строка));				
	КонецЕсли;	
	
КонецПроцедуры



