
Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	ДвиженияПоРСЗаказыУдаленногоЗакрытияДС();
КонецПроцедуры

Процедура ДвиженияПоРСЗаказыУдаленногоЗакрытияДС()
	Движения.СостояниеДСПриУдаленномЗакрытии.Очистить();
	Движения.СостояниеДСПриУдаленномЗакрытии.Записывать = Истина;
	
	
	Зап = Новый Запрос;
	
	Зап.Текст =
	"ВЫБРАТЬ
	|	ПривязкаМашинКРейсам.Транспорт КАК Транспорт,
	|	МАКСИМУМ(ПривязкаМашинКРейсам.Период) КАК Период
	|ПОМЕСТИТЬ ВТ_СрезТранспортПоРейсам
	|ИЗ
	|	РегистрСведений.ПривязкаМашинКРейсам КАК ПривязкаМашинКРейсам
	|ГДЕ
	|	ПривязкаМашинКРейсам.Период МЕЖДУ &ДатаНач И &ДатаКон
	|	И ПривязкаМашинКРейсам.Транспорт = &Транспорт
	|	И ПривязкаМашинКРейсам.Рейс.ДатаРейса = НАЧАЛОПЕРИОДА(&ДатаКон, ДЕНЬ)
	|
	|СГРУППИРОВАТЬ ПО
	|	ПривязкаМашинКРейсам.Транспорт
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПривязкаМашинКРейсамСрезПоследних.Рейс КАК Рейс,
	|	ПривязкаМашинКРейсамСрезПоследних.Транспорт КАК Транспорт,
	|	ПривязкаМашинКРейсамСрезПоследних.Водитель КАК Водитель,
	|	ПривязкаМашинКРейсамСрезПоследних.Период КАК Период
	|ИЗ
	|	ВТ_СрезТранспортПоРейсам КАК ВТ_СрезТранспортПоРейсам
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ПривязкаМашинКРейсам.СрезПоследних(, ) КАК ПривязкаМашинКРейсамСрезПоследних
	|		ПО ВТ_СрезТранспортПоРейсам.Транспорт = ПривязкаМашинКРейсамСрезПоследних.Транспорт
	|			И ВТ_СрезТранспортПоРейсам.Период = ПривязкаМашинКРейсамСрезПоследних.Период";
	Зап.УстановитьПараметр("ДатаНач", НачалоДня(ДатаРейса - 86400));
	Зап.УстановитьПараметр("ДатаКон", КонецДня(ДатаРейса));
	Зап.УстановитьПараметр("Транспорт", Транспорт);
	
	ВыбРейс = Зап.Выполнить().Выбрать();
	ВыбРейс.Следующий();
	
	
	Зап.Текст =
	"ВЫБРАТЬ
	|	ОтчетПоДСМПДС.Заказ КАК Заказ,
	|	ОтчетПоДСМПДС.ТипОплаты КАК ТипОплаты,
	|	СУММА(ВЫБОР
	|			КОГДА ОтчетПоДСМПДС.ТипОплаты В (ЗНАЧЕНИЕ(Справочник.ТипыОплат.Наличные), ЗНАЧЕНИЕ(Справочник.ТипыОплат.Терминал))
	|				ТОГДА ОтчетПоДСМПДС.Сумма
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК Сумма
	|ИЗ
	|	Документ.ОтчетПоДСМП.ДС КАК ОтчетПоДСМПДС
	|ГДЕ
	|	ОтчетПоДСМПДС.Ссылка = &Док
	|	И ОтчетПоДСМПДС.Сумма <> 0
	|
	|СГРУППИРОВАТЬ ПО
	|	ОтчетПоДСМПДС.Заказ,
	|	ОтчетПоДСМПДС.ТипОплаты";
	Зап.УстановитьПараметр("Док", Ссылка);
	
	ВыбЗаказы = Зап.Выполнить().Выбрать();
	
	
	Пока ВыбЗаказы.Следующий() Цикл
		Нов = Движения.СостояниеДСПриУдаленномЗакрытии.Добавить();
		ЗаполнитьЗначенияСвойств(Нов, ВыбРейс, "Рейс, Транспорт, Водитель");
		Нов.Период = Дата;
		Нов.Заказ = ВыбЗаказы.Заказ;
		Нов.ТипОплаты = ВыбЗаказы.ТипОплаты;
		Нов.СуммаДС = ВыбЗаказы.Сумма;
	КонецЦикла;	
КонецПроцедуры	