
&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если Параметры.Ключ.Пустая() Тогда
		Объект.ПериодРегистрации = НачалоМесяца(Объект.Дата);
	КонецЕсли;
	МесяцСтрока = РаботаСДиалогами.ДатаКакМесяцПредставление(Объект.ПериодРегистрации);
	
КонецПроцедуры

&НаКлиенте
Процедура МесяцСтрокаПриИзменении(Элемент)
	РаботаСДиалогами.ДатаКакМесяцПодобратьДатуПоТексту(МесяцСтрока, Объект.ПериодРегистрации);
	МесяцСтрока = РаботаСДиалогами.ДатаКакМесяцПредставление(Объект.ПериодРегистрации);
КонецПроцедуры

&НаКлиенте
Процедура МесяцСтрокаРегулирование(Элемент, Направление, СтандартнаяОбработка)
	Объект.ПериодРегистрации = ДобавитьМесяц(Объект.ПериодРегистрации, Направление);
	МесяцСтрока = РаботаСДиалогами.ДатаКакМесяцПредставление(Объект.ПериодРегистрации);
КонецПроцедуры

&НаКлиенте
Процедура МатериальныйУщербНомерЗаказаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ПараметрыОтбора = Новый Структура("ТерминалДоставки", Объект.Терминал);
	ОткрытьФорму("ОбщаяФорма.мгФормаВыбораЗаказа", ПараметрыОтбора, Элементы.МатериальныйУщерб, Истина,,,, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
КонецПроцедуры

&НаКлиенте
Процедура МатериальныйУщербОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	ТекущиеДанные = Элементы.МатериальныйУщерб.ТекущиеДанные;
	ТекущиеДанные.Заказ = ВыбранноеЗначение;
	ТекущиеДанные.НомерЗаказа = ВыбранноеЗначение.Номер;
	Модифицированность = Истина;
КонецПроцедуры

&НаКлиенте
Процедура МатериальныйУщербСуммаПриИзменении(Элемент)
	ЗаполнитьЕжемесячныйПлатеж();
КонецПроцедуры

&НаКлиенте
Процедура МатериальныйУщербКоличествоМесяцевПриИзменении(Элемент)
	ЗаполнитьЕжемесячныйПлатеж();
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьЕжемесячныйПлатеж()
	ТекущиеДанные = Элементы.МатериальныйУщерб.ТекущиеДанные;
	Попытка
		ТекущиеДанные.ЕжемесячнаяСумма = ТекущиеДанные.Сумма / ТекущиеДанные.КоличествоМесяцев
	Исключение
	    //ОписаниеОшибки()
	КонецПопытки;
	
КонецПроцедуры


&НаКлиенте
Процедура МатериальныйУщербНомерЗаказаОткрытие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ТекущиеДанные = Элементы.МатериальныйУщерб.ТекущиеДанные;
	ОткрытьЗначение(ТекущиеДанные.Заказ);
	
КонецПроцедуры


&НаКлиенте
Процедура МатериальныйУщербФИОСотрудникаПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.МатериальныйУщерб.ТекущиеДанные;
	Если ЗначениеЗаполнено(ТекущиеДанные.ФИОСотрудника) Тогда
		ДанныеСотрудника = мгДоработки.ПолучитьОрганизациюСотрудника(ТекущиеДанные.ФИОСотрудника);
		Если ДанныеСотрудника.Количество() = 0 Тогда
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = "Сотрудник не принят ни в одну организацию";
			Сообщение.Сообщить();
		ИначеЕсли ДанныеСотрудника.Количество() > 1 Тогда
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = "Сотрудник принят в несколько организаций, необходимо выбрать организацию сотрудника";
			Сообщение.Сообщить();
		Иначе
			ТекущиеДанные.Организация = ДанныеСотрудника[0].Организация;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры


&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	Объект.Ответственный = УправлениеПользователями.ПолучитьЗначениеПоУмолчанию(глЗначениеПеременной("глТекущийПользователь"), "ОсновнойОтветственный");
	
КонецПроцедуры

