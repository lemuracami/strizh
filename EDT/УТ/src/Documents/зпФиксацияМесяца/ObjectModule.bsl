
Функция ЗаполнитьИРассчитать(парРегион, парПериодРасчета, парПользователь = Неопределено) Экспорт
	
	Регион = парРегион;
	ПериодРасчета = НачалоМесяца(парПериодРасчета);
	Пользователь = ?(парПользователь = Неопределено, ПараметрыСеанса.ТекущийПользователь, парПользователь);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Регион", Регион);
	Запрос.УстановитьПараметр("Период", ПериодРасчета);
	Запрос.УстановитьПараметр("КонецПериода", КонецМесяца(ПериодРасчета));
	Запрос.УстановитьПараметр("ТарифРасчетаЗП", ТарифРасчетаЗП);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	зпАктивнаяСхемаРасчетаЗПСрезПоследних.СхемаРасчетаЗП КАК СхемаРасчетаЗП
	|ПОМЕСТИТЬ ВТ_АктивнаяСхемаРасчетаЗП
	|ИЗ
	|	РегистрСведений.зпАктивнаяСхемаРасчетаЗП.СрезПоследних(&КонецПериода, Регион = &Регион) КАК зпАктивнаяСхемаРасчетаЗПСрезПоследних
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_АктивнаяСхемаРасчетаЗП.СхемаРасчетаЗП КАК СхемаРасчетаЗП,
	|	ВЫБОР
	|		КОГДА &ТарифРасчетаЗП = ЗНАЧЕНИЕ(Справочник.зпТарифыРасчетаЗП.ПустаяСсылка)
	|			ТОГДА зпАктивныйТарифРасчетаЗПСрезПоследних.ТарифРасчетаЗП
	|		ИНАЧЕ &ТарифРасчетаЗП
	|	КОНЕЦ КАК ТарифРасчетаЗП
	|ПОМЕСТИТЬ ВТ_ТарифРасчетаЗП
	|ИЗ
	|	ВТ_АктивнаяСхемаРасчетаЗП КАК ВТ_АктивнаяСхемаРасчетаЗП
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.зпАктивныйТарифРасчетаЗП.СрезПоследних(
	|				&КонецПериода,
	|				СхемаРасчетаЗП В
	|					(ВЫБРАТЬ
	|						ВТ_АктивнаяСхемаРасчетаЗП.СхемаРасчетаЗП КАК СхемаРасчетаЗП
	|					ИЗ
	|						ВТ_АктивнаяСхемаРасчетаЗП КАК ВТ_АктивнаяСхемаРасчетаЗП)) КАК зпАктивныйТарифРасчетаЗПСрезПоследних
	|		ПО (ИСТИНА)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ТарифРасчетаЗП.СхемаРасчетаЗП КАК СхемаРасчетаЗП,
	|	ВТ_ТарифРасчетаЗП.ТарифРасчетаЗП КАК ТарифРасчетаЗП,
	|	НЕ зпФиксацияМесяца.Регион ЕСТЬ NULL КАК УжеСуществует
	|ИЗ
	|	ВТ_ТарифРасчетаЗП КАК ВТ_ТарифРасчетаЗП
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.зпФиксацияМесяца КАК зпФиксацияМесяца
	|		ПО (зпФиксацияМесяца.Регион = &Регион)
	|			И ВТ_ТарифРасчетаЗП.СхемаРасчетаЗП = зпФиксацияМесяца.СхемаРасчетаЗП
	|			И ВТ_ТарифРасчетаЗП.ТарифРасчетаЗП = зпФиксацияМесяца.ТарифРасчетаЗП
	|			И (зпФиксацияМесяца.ПериодРасчета = &Период)
	|			И (зпФиксацияМесяца.Проведен)";
	
	Результат = Запрос.Выполнить();
	Если Результат.Пустой() Тогда
		СхемаРасчетаЗП = Неопределено;
		ТарифРасчетаЗП = Неопределено;
	Иначе
		Выборка = Результат.Выбрать();
		Выборка.Следующий();
		
		СхемаРасчетаЗП = Выборка.СхемаРасчетаЗП;
		ТарифРасчетаЗП = Выборка.ТарифРасчетаЗП;
	КонецЕсли;
	
	Если СхемаРасчетаЗП.Пустая() Или ТарифРасчетаЗП.Пустая() Тогда
		РасчетМинимальнойЗП.Очистить();
		СводРасчетаМинимальнойЗП.Очистить();
		ПрименяемыеМинимальныеСтавки.Очистить();
		Возврат "Не определены схема/тариф расчета ЗП";
	ИначеЕсли Выборка.УжеСуществует Тогда
		РасчетМинимальнойЗП.Очистить();
		СводРасчетаМинимальнойЗП.Очистить();
		ПрименяемыеМинимальныеСтавки.Очистить();
		Возврат "Уже существует";
	КонецЕсли;
	
	Запрос.УстановитьПараметр("СхемаРасчетаЗП", СхемаРасчетаЗП);
	Запрос.УстановитьПараметр("ТарифРасчетаЗП", ТарифРасчетаЗП);
	Запрос.УстановитьПараметр("НачалоПериода", ПериодРасчета);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Рейс.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ВТ_Рейсы
	|ИЗ
	|	Документ.Рейс КАК Рейс
	|ГДЕ
	|	Рейс.ДатаРейса МЕЖДУ &НачалоПериода И &КонецПериода
	|	И Рейс.ТерминалДоставки = &Регион
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	зпМинимальныеСтавки.ОсновноеНачисление КАК ОсновноеНачисление,
	|	зпМинимальныеСтавки.Роль КАК Роль,
	|	зпМинимальныеСтавки.СоставЭкипажа КАК СоставЭкипажа,
	|	зпМинимальныеСтавки.БрейкОтработанныхДней КАК БрейкОтработанныхДней,
	|	зпМинимальныеСтавки.БрейкМинимальногоПроцентаВыполнения КАК БрейкМинимальногоПроцентаВыполнения,
	|	зпМинимальныеСтавки.МинимальнаяСтавка КАК МинимальнаяСтавка
	|ПОМЕСТИТЬ ВТ_ПрименяемыеМинимальныеСтавки
	|ИЗ
	|	РегистрСведений.зпМинимальныеСтавки КАК зпМинимальныеСтавки
	|ГДЕ
	|	зпМинимальныеСтавки.СхемаРасчетаЗП = &СхемаРасчетаЗП
	|	И зпМинимальныеСтавки.ТарифРасчетаЗП = &ТарифРасчетаЗП
	|	И зпМинимальныеСтавки.ВидМинимальнойСтавки = ЗНАЧЕНИЕ(Перечисление.зпВидыМинимальныхСтавок.ЗаМесяц)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВТ_ПрименяемыеМинимальныеСтавки.ОсновноеНачисление КАК ОсновноеНачисление
	|ПОМЕСТИТЬ ВТ_ОсновныеНачисленияМинимальныхСтавок
	|ИЗ
	|	ВТ_ПрименяемыеМинимальныеСтавки КАК ВТ_ПрименяемыеМинимальныеСтавки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	зпИсходныеДанныеРасчетаЗППоРейсамСрезПоследних.ФизЛицо КАК ФизЛицо,
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ зпИсходныеДанныеРасчетаЗППоРейсамСрезПоследних.ДатаРасчета) КАК КоличествоОтработанныхДней
	|ПОМЕСТИТЬ ВТ_КоличествоОтработанныхДнейОбщее
	|ИЗ
	|	РегистрСведений.зпИсходныеДанныеРасчетаЗППоРейсам.СрезПоследних(
	|			,
	|			СхемаРасчетаЗП = &СхемаРасчетаЗП
	|				И Рейс В
	|					(ВЫБРАТЬ
	|						ВТ_Рейсы.Ссылка КАК Ссылка
	|					ИЗ
	|						ВТ_Рейсы КАК ВТ_Рейсы)) КАК зпИсходныеДанныеРасчетаЗППоРейсамСрезПоследних
	|
	|СГРУППИРОВАТЬ ПО
	|	зпИсходныеДанныеРасчетаЗППоРейсамСрезПоследних.ФизЛицо
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	зпИсходныеДанныеРасчетаМинимальнойЗПСрезПоследних.ФизЛицо КАК ФизЛицо,
	|	зпИсходныеДанныеРасчетаМинимальнойЗПСрезПоследних.ОсновноеНачисление КАК ОсновноеНачисление,
	|	зпИсходныеДанныеРасчетаМинимальнойЗПСрезПоследних.Роль КАК Роль,
	|	зпИсходныеДанныеРасчетаМинимальнойЗПСрезПоследних.ВидЗанятости КАК ВидЗанятости,
	|	зпИсходныеДанныеРасчетаМинимальнойЗПСрезПоследних.СоставЭкипажа КАК СоставЭкипажа,
	|	зпИсходныеДанныеРасчетаМинимальнойЗПСрезПоследних.ДатаРасчета КАК ДатаРасчета,
	|	зпИсходныеДанныеРасчетаМинимальнойЗПСрезПоследних.Рейс КАК Рейс,
	|	зпИсходныеДанныеРасчетаМинимальнойЗПСрезПоследних.СуммаЗаказы КАК СуммаЗаказы,
	|	зпИсходныеДанныеРасчетаМинимальнойЗПСрезПоследних.СуммаПодъем КАК СуммаПодъем,
	|	зпИсходныеДанныеРасчетаМинимальнойЗПСрезПоследних.СуммаПримерка КАК СуммаПримерка,
	|	зпИсходныеДанныеРасчетаМинимальнойЗПСрезПоследних.СуммаИтого КАК СуммаИтого,
	|	зпИсходныеДанныеРасчетаМинимальнойЗПСрезПоследних.ПроцентВыполненияЗаказов КАК ПроцентВыполненияЗаказов,
	|	зпИсходныеДанныеРасчетаМинимальнойЗПСрезПоследних.ВидДня КАК ВидДня,
	|	зпИсходныеДанныеРасчетаМинимальнойЗПСрезПоследних.Пробег КАК Пробег,
	|	зпИсходныеДанныеРасчетаМинимальнойЗПСрезПоследних.БрейкМинимальногоПроцентаВыполнения КАК БрейкМинимальногоПроцентаВыполнения,
	|	зпИсходныеДанныеРасчетаМинимальнойЗПСрезПоследних.БрейкМинимальногоПробегаПоРейсу КАК БрейкМинимальногоПробегаПоРейсу,
	|	зпИсходныеДанныеРасчетаМинимальнойЗПСрезПоследних.ПрименяетсяМинимальнаяСтавка КАК ПрименяетсяМинимальнаяСтавка,
	|	зпИсходныеДанныеРасчетаМинимальнойЗПСрезПоследних.СуммаМинимальнойСтавки КАК СуммаМинимальнойСтавки,
	|	зпИсходныеДанныеРасчетаМинимальнойЗПСрезПоследних.ОтменаНачисленияМинимальнойСтавки КАК ОтменаНачисленияМинимальнойСтавки
	|ПОМЕСТИТЬ ВТ_РасчетМинимальнойЗП
	|ИЗ
	|	РегистрСведений.зпИсходныеДанныеРасчетаМинимальнойЗП.СрезПоследних(
	|			,
	|			СхемаРасчетаЗП = &СхемаРасчетаЗП
	|				И ТарифРасчетаЗП = &ТарифРасчетаЗП
	|				И Рейс В
	|					(ВЫБРАТЬ
	|						ВТ_Рейсы.Ссылка КАК Ссылка
	|					ИЗ
	|						ВТ_Рейсы КАК ВТ_Рейсы)
	|				И ОсновноеНачисление В
	|					(ВЫБРАТЬ
	|						ВТ_ОсновныеНачисленияМинимальныхСтавок.ОсновноеНачисление КАК ОсновноеНачисление
	|					ИЗ
	|						ВТ_ОсновныеНачисленияМинимальныхСтавок КАК ВТ_ОсновныеНачисленияМинимальныхСтавок)) КАК зпИсходныеДанныеРасчетаМинимальнойЗПСрезПоследних
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВЗ_КоличествоОтработанныхДней.ФизЛицо КАК ФизЛицо,
	|	ВЗ_КоличествоОтработанныхДней.ОсновноеНачисление КАК ОсновноеНачисление,
	|	ВЗ_КоличествоОтработанныхДней.Роль КАК Роль,
	|	ВЗ_КоличествоОтработанныхДней.СоставЭкипажа КАК СоставЭкипажа,
	|	ВЗ_КоличествоОтработанныхДней.КоличествоОтработанныхДней КАК КоличествоОтработанныхДней,
	|	ВТ_КоличествоОтработанныхДнейОбщее.КоличествоОтработанныхДней КАК КоличествоОтработанныхДнейОбщее
	|ПОМЕСТИТЬ ВТ_КоличествоОтработанныхДней
	|ИЗ
	|	(ВЫБРАТЬ
	|		ВТ_РасчетМинимальнойЗП.ФизЛицо КАК ФизЛицо,
	|		ВТ_РасчетМинимальнойЗП.ОсновноеНачисление КАК ОсновноеНачисление,
	|		ВТ_РасчетМинимальнойЗП.Роль КАК Роль,
	|		ВТ_РасчетМинимальнойЗП.СоставЭкипажа КАК СоставЭкипажа,
	|		КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ВТ_РасчетМинимальнойЗП.ДатаРасчета) КАК КоличествоОтработанныхДней
	|	ИЗ
	|		ВТ_РасчетМинимальнойЗП КАК ВТ_РасчетМинимальнойЗП
	|	
	|	СГРУППИРОВАТЬ ПО
	|		ВТ_РасчетМинимальнойЗП.ФизЛицо,
	|		ВТ_РасчетМинимальнойЗП.ОсновноеНачисление,
	|		ВТ_РасчетМинимальнойЗП.Роль,
	|		ВТ_РасчетМинимальнойЗП.СоставЭкипажа) КАК ВЗ_КоличествоОтработанныхДней
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_КоличествоОтработанныхДнейОбщее КАК ВТ_КоличествоОтработанныхДнейОбщее
	|		ПО ВЗ_КоличествоОтработанныхДней.ФизЛицо = ВТ_КоличествоОтработанныхДнейОбщее.ФизЛицо
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_КоличествоОтработанныхДней.ФизЛицо КАК ФизЛицо,
	|	ВТ_КоличествоОтработанныхДней.ОсновноеНачисление КАК ОсновноеНачисление,
	|	ВТ_КоличествоОтработанныхДней.Роль КАК Роль,
	|	ВТ_КоличествоОтработанныхДней.СоставЭкипажа КАК СоставЭкипажа,
	|	ВТ_КоличествоОтработанныхДней.КоличествоОтработанныхДней КАК КоличествоОтработанныхДней,
	|	ВТ_КоличествоОтработанныхДней.КоличествоОтработанныхДнейОбщее КАК КоличествоОтработанныхДнейОбщее,
	|	МАКСИМУМ(зпБрейкиГраницыБрейка.Ссылка) КАК БрейкОтработанныхДней
	|ПОМЕСТИТЬ ВТ_БрейкиОтработанныхДней
	|ИЗ
	|	ВТ_КоличествоОтработанныхДней КАК ВТ_КоличествоОтработанныхДней
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.зпБрейки.ГраницыБрейка КАК зпБрейкиГраницыБрейка
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.зпБрейки КАК зпБрейки
	|				ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.зпТарифыРасчетаЗП КАК зпТарифыРасчетаЗП
	|				ПО (зпТарифыРасчетаЗП.Ссылка = &ТарифРасчетаЗП)
	|					И (зпТарифыРасчетаЗП.СхемаБрейковОтработанныхДней = зпБрейки.Владелец)
	|					И (НЕ зпБрейки.ПометкаУдаления)
	|			ПО зпБрейкиГраницыБрейка.Ссылка = зпБрейки.Ссылка
	|		ПО (ВТ_КоличествоОтработанныхДней.КоличествоОтработанныхДнейОбщее МЕЖДУ зпБрейкиГраницыБрейка.ЗначениеОт И зпБрейкиГраницыБрейка.ЗначениеДо)
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_КоличествоОтработанныхДней.ФизЛицо,
	|	ВТ_КоличествоОтработанныхДней.ОсновноеНачисление,
	|	ВТ_КоличествоОтработанныхДней.Роль,
	|	ВТ_КоличествоОтработанныхДней.СоставЭкипажа,
	|	ВТ_КоличествоОтработанныхДней.КоличествоОтработанныхДней,
	|	ВТ_КоличествоОтработанныхДней.КоличествоОтработанныхДнейОбщее
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВЗ_ИзмеренияСтавок.ФизЛицо КАК ФизЛицо,
	|	ВЗ_ИзмеренияСтавок.ОсновноеНачисление КАК ОсновноеНачисление,
	|	ВЗ_ИзмеренияСтавок.Роль КАК Роль,
	|	ВЗ_ИзмеренияСтавок.СоставЭкипажа КАК СоставЭкипажа,
	|	ВЗ_ИзмеренияСтавок.БрейкМинимальногоПроцентаВыполнения КАК БрейкМинимальногоПроцентаВыполнения,
	|	ВЗ_ИзмеренияСтавок.КоличествоОтработанныхДней КАК КоличествоОтработанныхДней,
	|	ВЗ_ИзмеренияСтавок.БрейкОтработанныхДней КАК БрейкОтработанныхДней,
	|	ВЗ_ИзмеренияСтавок.КоличествоОтработанныхДнейОбщее КАК КоличествоОтработанныхДнейОбщее,
	|	ВТ_ПрименяемыеМинимальныеСтавки.МинимальнаяСтавка КАК МинимальнаяСтавкаПоИтогуМесяца,
	|	ВТ_ПрименяемыеМинимальныеСтавки.МинимальнаяСтавка > 0 КАК ПрименяетсяМинимальнаяСтавкаПоИтогуМесяца
	|ПОМЕСТИТЬ ВТ_СводРасчетаМинимальнойЗП
	|ИЗ
	|	(ВЫБРАТЬ
	|		ВТ_БрейкиОтработанныхДней.ФизЛицо КАК ФизЛицо,
	|		ВТ_БрейкиОтработанныхДней.ОсновноеНачисление КАК ОсновноеНачисление,
	|		ВТ_БрейкиОтработанныхДней.Роль КАК Роль,
	|		ВТ_БрейкиОтработанныхДней.СоставЭкипажа КАК СоставЭкипажа,
	|		ВТ_БрейкиОтработанныхДней.БрейкОтработанныхДней КАК БрейкОтработанныхДней,
	|		ВЗ_КоличествоОтработанныхДнейПоБрейкамВыполнения.БрейкМинимальногоПроцентаВыполнения КАК БрейкМинимальногоПроцентаВыполнения,
	|		ВЗ_КоличествоОтработанныхДнейПоБрейкамВыполнения.КоличествоОтработанныхДней КАК КоличествоОтработанныхДней,
	|		ВТ_БрейкиОтработанныхДней.КоличествоОтработанныхДнейОбщее КАК КоличествоОтработанныхДнейОбщее
	|	ИЗ
	|		ВТ_БрейкиОтработанныхДней КАК ВТ_БрейкиОтработанныхДней
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|				ВТ_РасчетМинимальнойЗП.ФизЛицо КАК ФизЛицо,
	|				ВТ_РасчетМинимальнойЗП.ОсновноеНачисление КАК ОсновноеНачисление,
	|				ВТ_РасчетМинимальнойЗП.Роль КАК Роль,
	|				ВТ_РасчетМинимальнойЗП.СоставЭкипажа КАК СоставЭкипажа,
	|				ВТ_РасчетМинимальнойЗП.БрейкМинимальногоПроцентаВыполнения КАК БрейкМинимальногоПроцентаВыполнения,
	|				КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ВТ_РасчетМинимальнойЗП.ДатаРасчета) КАК КоличествоОтработанныхДней
	|			ИЗ
	|				ВТ_РасчетМинимальнойЗП КАК ВТ_РасчетМинимальнойЗП
	|			
	|			СГРУППИРОВАТЬ ПО
	|				ВТ_РасчетМинимальнойЗП.ФизЛицо,
	|				ВТ_РасчетМинимальнойЗП.ОсновноеНачисление,
	|				ВТ_РасчетМинимальнойЗП.Роль,
	|				ВТ_РасчетМинимальнойЗП.СоставЭкипажа,
	|				ВТ_РасчетМинимальнойЗП.БрейкМинимальногоПроцентаВыполнения) КАК ВЗ_КоличествоОтработанныхДнейПоБрейкамВыполнения
	|			ПО (ВЗ_КоличествоОтработанныхДнейПоБрейкамВыполнения.ФизЛицо = ВТ_БрейкиОтработанныхДней.ФизЛицо)
	|				И (ВЗ_КоличествоОтработанныхДнейПоБрейкамВыполнения.ОсновноеНачисление = ВТ_БрейкиОтработанныхДней.ОсновноеНачисление)
	|				И (ВЗ_КоличествоОтработанныхДнейПоБрейкамВыполнения.Роль = ВТ_БрейкиОтработанныхДней.Роль)
	|				И (ВЗ_КоличествоОтработанныхДнейПоБрейкамВыполнения.СоставЭкипажа = ВТ_БрейкиОтработанныхДней.СоставЭкипажа)) КАК ВЗ_ИзмеренияСтавок
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ПрименяемыеМинимальныеСтавки КАК ВТ_ПрименяемыеМинимальныеСтавки
	|		ПО ВЗ_ИзмеренияСтавок.ОсновноеНачисление = ВТ_ПрименяемыеМинимальныеСтавки.ОсновноеНачисление
	|			И ВЗ_ИзмеренияСтавок.Роль = ВТ_ПрименяемыеМинимальныеСтавки.Роль
	|			И ВЗ_ИзмеренияСтавок.СоставЭкипажа = ВТ_ПрименяемыеМинимальныеСтавки.СоставЭкипажа
	|			И ВЗ_ИзмеренияСтавок.БрейкОтработанныхДней = ВТ_ПрименяемыеМинимальныеСтавки.БрейкОтработанныхДней
	|			И ВЗ_ИзмеренияСтавок.БрейкМинимальногоПроцентаВыполнения = ВТ_ПрименяемыеМинимальныеСтавки.БрейкМинимальногоПроцентаВыполнения
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_РасчетМинимальнойЗП.ФизЛицо КАК ФизЛицо,
	|	ВТ_РасчетМинимальнойЗП.ОсновноеНачисление КАК ОсновноеНачисление,
	|	ВТ_РасчетМинимальнойЗП.Роль КАК Роль,
	|	ВТ_РасчетМинимальнойЗП.ВидЗанятости КАК ВидЗанятости,
	|	ВТ_РасчетМинимальнойЗП.СоставЭкипажа КАК СоставЭкипажа,
	|	ВТ_РасчетМинимальнойЗП.ДатаРасчета КАК ДатаРасчета,
	|	ВТ_РасчетМинимальнойЗП.Рейс КАК Рейс,
	|	ВТ_РасчетМинимальнойЗП.СуммаЗаказы КАК СуммаЗаказы,
	|	ВТ_РасчетМинимальнойЗП.СуммаПодъем КАК СуммаПодъем,
	|	ВТ_РасчетМинимальнойЗП.СуммаПримерка КАК СуммаПримерка,
	|	ВТ_РасчетМинимальнойЗП.СуммаИтого КАК СуммаИтого,
	|	ВТ_РасчетМинимальнойЗП.ПроцентВыполненияЗаказов КАК ПроцентВыполненияЗаказов,
	|	ВТ_РасчетМинимальнойЗП.ВидДня КАК ВидДня,
	|	ВТ_РасчетМинимальнойЗП.Пробег КАК Пробег,
	|	ВТ_РасчетМинимальнойЗП.БрейкМинимальногоПроцентаВыполнения КАК БрейкМинимальногоПроцентаВыполнения,
	|	ВТ_РасчетМинимальнойЗП.БрейкМинимальногоПробегаПоРейсу КАК БрейкМинимальногоПробегаПоРейсу,
	|	ВТ_РасчетМинимальнойЗП.ПрименяетсяМинимальнаяСтавка КАК ПрименяетсяМинимальнаяСтавка,
	|	ВТ_РасчетМинимальнойЗП.СуммаМинимальнойСтавки КАК СуммаМинимальнойСтавки,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ВТ_СводРасчетаМинимальнойЗП.ПрименяетсяМинимальнаяСтавкаПоИтогуМесяца, ЛОЖЬ)
	|				И ЕСТЬNULL(ВТ_СводРасчетаМинимальнойЗП.МинимальнаяСтавкаПоИтогуМесяца, 0) > ВТ_РасчетМинимальнойЗП.СуммаИтого
	|			ТОГДА ВТ_СводРасчетаМинимальнойЗП.МинимальнаяСтавкаПоИтогуМесяца
	|	КОНЕЦ КАК СуммаМинимальнойСтавкиПоИтогамМесяца,
	|	ЕСТЬNULL(ВТ_СводРасчетаМинимальнойЗП.ПрименяетсяМинимальнаяСтавкаПоИтогуМесяца, ЛОЖЬ)
	|		И ЕСТЬNULL(ВТ_СводРасчетаМинимальнойЗП.МинимальнаяСтавкаПоИтогуМесяца, 0) > ВТ_РасчетМинимальнойЗП.СуммаИтого
	|		И НЕ ВТ_РасчетМинимальнойЗП.ОтменаНачисленияМинимальнойСтавки КАК ПрименяетсяМинимальнаяСтавкаПоИтогуМесяца,
	|	ВТ_РасчетМинимальнойЗП.ОтменаНачисленияМинимальнойСтавки КАК ОтменаНачисленияМинимальнойСтавки
	|ИЗ
	|	ВТ_РасчетМинимальнойЗП КАК ВТ_РасчетМинимальнойЗП
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_СводРасчетаМинимальнойЗП КАК ВТ_СводРасчетаМинимальнойЗП
	|		ПО ВТ_РасчетМинимальнойЗП.ФизЛицо = ВТ_СводРасчетаМинимальнойЗП.ФизЛицо
	|			И ВТ_РасчетМинимальнойЗП.ОсновноеНачисление = ВТ_СводРасчетаМинимальнойЗП.ОсновноеНачисление
	|			И ВТ_РасчетМинимальнойЗП.Роль = ВТ_СводРасчетаМинимальнойЗП.Роль
	|			И ВТ_РасчетМинимальнойЗП.СоставЭкипажа = ВТ_СводРасчетаМинимальнойЗП.СоставЭкипажа
	|			И ВТ_РасчетМинимальнойЗП.БрейкМинимальногоПроцентаВыполнения = ВТ_СводРасчетаМинимальнойЗП.БрейкМинимальногоПроцентаВыполнения
	|
	|УПОРЯДОЧИТЬ ПО
	|	ВТ_РасчетМинимальнойЗП.ФизЛицо.Наименование,
	|	ВТ_РасчетМинимальнойЗП.ДатаРасчета
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_СводРасчетаМинимальнойЗП.ФизЛицо КАК ФизЛицо,
	|	ВТ_СводРасчетаМинимальнойЗП.ОсновноеНачисление КАК ОсновноеНачисление,
	|	ВТ_СводРасчетаМинимальнойЗП.Роль КАК Роль,
	|	ВТ_СводРасчетаМинимальнойЗП.СоставЭкипажа КАК СоставЭкипажа,
	|	ВТ_СводРасчетаМинимальнойЗП.БрейкМинимальногоПроцентаВыполнения КАК БрейкМинимальногоПроцентаВыполнения,
	|	ВТ_СводРасчетаМинимальнойЗП.КоличествоОтработанныхДней КАК КоличествоОтработанныхДней,
	|	ВТ_СводРасчетаМинимальнойЗП.БрейкОтработанныхДней КАК БрейкОтработанныхДней,
	|	ВТ_СводРасчетаМинимальнойЗП.МинимальнаяСтавкаПоИтогуМесяца КАК МинимальнаяСтавкаПоИтогуМесяца,
	|	ВТ_СводРасчетаМинимальнойЗП.ПрименяетсяМинимальнаяСтавкаПоИтогуМесяца КАК ПрименяетсяМинимальнаяСтавкаПоИтогуМесяца,
	|	ВТ_СводРасчетаМинимальнойЗП.КоличествоОтработанныхДнейОбщее КАК КоличествоОтработанныхДнейОбщее
	|ИЗ
	|	ВТ_СводРасчетаМинимальнойЗП КАК ВТ_СводРасчетаМинимальнойЗП
	|
	|УПОРЯДОЧИТЬ ПО
	|	ВТ_СводРасчетаМинимальнойЗП.ФизЛицо.Наименование
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ПрименяемыеМинимальныеСтавки.ОсновноеНачисление КАК ОсновноеНачисление,
	|	ВТ_ПрименяемыеМинимальныеСтавки.Роль КАК Роль,
	|	ВТ_ПрименяемыеМинимальныеСтавки.СоставЭкипажа КАК СоставЭкипажа,
	|	ВТ_ПрименяемыеМинимальныеСтавки.БрейкОтработанныхДней КАК БрейкОтработанныхДней,
	|	ВТ_ПрименяемыеМинимальныеСтавки.БрейкМинимальногоПроцентаВыполнения КАК БрейкМинимальногоПроцентаВыполнения,
	|	ВТ_ПрименяемыеМинимальныеСтавки.МинимальнаяСтавка КАК МинимальнаяСтавка
	|ИЗ
	|	ВТ_ПрименяемыеМинимальныеСтавки КАК ВТ_ПрименяемыеМинимальныеСтавки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_КоличествоОтработанныхДнейОбщее.ФизЛицо КАК ФизЛицо,
	|	ВТ_КоличествоОтработанныхДнейОбщее.КоличествоОтработанныхДней КАК КоличествоОтработанныхДней
	|ИЗ
	|	ВТ_КоличествоОтработанныхДнейОбщее КАК ВТ_КоличествоОтработанныхДнейОбщее
	|
	|УПОРЯДОЧИТЬ ПО
	|	ВТ_КоличествоОтработанныхДнейОбщее.ФизЛицо.Наименование";
	
	РезультатПакета = Запрос.ВыполнитьПакет();
	
	РасчетМинимальнойЗП.Загрузить(РезультатПакета[8].Выгрузить());
	СводРасчетаМинимальнойЗП.Загрузить(РезультатПакета[9].Выгрузить());
	ПрименяемыеМинимальныеСтавки.Загрузить(РезультатПакета[10].Выгрузить());
	КоличествоОтработанныхДней.Загрузить(РезультатПакета[11].Выгрузить());
	
	Возврат "";
	
КонецФункции

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	Движения.зпИсходныеДанныеРасчетаМинимальнойЗП.Записывать = Истина;
	Движения.зпИсходныеДанныеРасчетаМинимальнойЗП.Очистить();
	
	Для Каждого СтрокаРасчета Из РасчетМинимальнойЗП.НайтиСтроки(Новый Структура("ПрименяетсяМинимальнаяСтавкаПоИтогуМесяца", Истина)) Цикл
		Движение = Движения.зпИсходныеДанныеРасчетаМинимальнойЗП.Добавить();
		ЗаполнитьЗначенияСвойств(Движение, СтрокаРасчета);
		Движение.Период = Дата;
		Движение.Регион = Регион;
		Движение.СхемаРасчетаЗП = СхемаРасчетаЗП;
		Движение.ТарифРасчетаЗП = ТарифРасчетаЗП;
		Движение.ПерерасчетПоИтогуМесяца = Истина;
		Движение.ПрименяетсяМинимальнаяСтавка = Истина;
		Движение.СуммаМинимальнойСтавки = СтрокаРасчета.СуммаМинимальнойСтавкиПоИтогамМесяца;
	КонецЦикла;
	
	//Асеев 27.09.2023 (Задача № 5129)>>>
	Движения.эк_ЗатратыЗаработнаяПлата.Записывать = Истина;
	Движения.эк_ЗатратыЗаработнаяПлата.Очистить();
	
	РегистрыСведений.эк_ЗатратыЗаработнаяПлата.РаспределитьМинимальнуюЗП(Движения.эк_ЗатратыЗаработнаяПлата, Движения.зпИсходныеДанныеРасчетаМинимальнойЗП);
	//Асеев 27.09.2023 (Задача № 5129)<<<
	
КонецПроцедуры





