
Функция unloadGoodsCourier(data)
	
	Попытка
		
		ОбъектВозврата = ФабрикаXDTO.Создать(ФабрикаXDTO.Тип("http://www.strizh-logistic.ru/1CMobile/MobileClosingCrews", "responseGoodsCourier"));
		
		МассивНомеровРеализаций = Новый Массив;
		МассивКодовНоменклатуры = Новый Массив;
		МассивШтрихкодов = Новый Массив;
		
		Для Каждого СтрокаТЧ Из data.goodRows.goodRow Цикл 
			
			МассивНомеровРеализаций.Добавить(СтрокаТЧ.orderId);
			МассивКодовНоменклатуры.Добавить(СтрокаТЧ.goodId);
			МассивШтрихкодов.Добавить(СтрокаТЧ.barcode);
		КонецЦикла;	
		
		
		
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ
		               |	ОтчетПоТоварамМП.Ссылка КАК Ссылка
		               |ИЗ
		               |	Документ.ОтчетПоТоварамМП КАК ОтчетПоТоварамМП
		               |ГДЕ
		               |	ОтчетПоТоварамМП.МП.Код = &id_App
		               |	И ОтчетПоТоварамМП.НомерМП = &number
		               |;
		               |
		               |////////////////////////////////////////////////////////////////////////////////
		               |ВЫБРАТЬ
		               |	ИзмененияДляМП.Ссылка КАК Ссылка
		               |ИЗ
		               |	ПланОбмена.ИзмененияДляМП КАК ИзмененияДляМП
		               |ГДЕ
		               |	ИзмененияДляМП.Код = &id_App
		               |;
		               |
		               |////////////////////////////////////////////////////////////////////////////////
		               |ВЫБРАТЬ
		               |	новаТранспорт.Ссылка КАК Ссылка
		               |ИЗ
		               |	Справочник.новаТранспорт КАК новаТранспорт
		               |ГДЕ
		               |	новаТранспорт.Код = &car
		               |;
		               |
		               |////////////////////////////////////////////////////////////////////////////////
		               |ВЫБРАТЬ РАЗЛИЧНЫЕ
		               |	РеализацияТоваровУслуг.Ссылка КАК Ссылка,
		               |	РеализацияТоваровУслуг.Номер КАК Номер
		               |ИЗ
		               |	Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
		               |ГДЕ
		               |	РеализацияТоваровУслуг.Номер В(&МассивНомеровРеализаций)
		               |;
		               |
		               |////////////////////////////////////////////////////////////////////////////////
		               |ВЫБРАТЬ РАЗЛИЧНЫЕ
		               |	Номенклатура.Код КАК Код,
		               |	Номенклатура.Ссылка КАК Ссылка
		               |ИЗ
		               |	Справочник.Номенклатура КАК Номенклатура
		               |ГДЕ
		               |	Номенклатура.Код В(&МассивКодовНоменклатуры)
		               |;
		               |
		               |////////////////////////////////////////////////////////////////////////////////
		               |ВЫБРАТЬ
		               |	ШтрихкодыТоваров.Ссылка КАК Ссылка,
		               |	ШтрихкодыТоваров.Код КАК Код
		               |ИЗ
		               |	Справочник.ШтрихкодыТоваров КАК ШтрихкодыТоваров
		               |ГДЕ
		               |	ШтрихкодыТоваров.Код В(&МассивШтрихкодов)
		               |	И ШтрихкодыТоваров.Владелец.Код В(&МассивКодовНоменклатуры)";
		
		Запрос.УстановитьПараметр("number", data.number);
		Запрос.УстановитьПараметр("id_App", data.id_App);
		Запрос.УстановитьПараметр("car", data.car);
		Запрос.УстановитьПараметр("МассивНомеровРеализаций", МассивНомеровРеализаций);
		Запрос.УстановитьПараметр("МассивКодовНоменклатуры", МассивКодовНоменклатуры);
		Запрос.УстановитьПараметр("МассивШтрихкодов", МассивШтрихкодов);
		
		РезультатПакета = Запрос.ВыполнитьПакет();
		
		ВыборкаОтчетПоТоварамМП = РезультатПакета[0].Выбрать();
		
		Если ВыборкаОтчетПоТоварамМП.Следующий() Тогда
			
			ВызватьИсключение "Документ уже выгружен"; 
			
		Иначе 
			
			ОтчетПоТоварамМП_Объект = Документы.ОтчетПоТоварамМП.СоздатьДокумент();
			
		КонецЕсли;
		
		ОтчетПоТоварамМП_Объект.Дата = ТекущаяДата();
		ОтчетПоТоварамМП_Объект.ЭкипажСогласенСДанными = data.courierAgreesWithData;
		ОтчетПоТоварамМП_Объект.НомерМП = data.number;
		ОтчетПоТоварамМП_Объект.ДатаРейса = data.dateOfRide;
		
		ВыборкаИзмененияДляМП = РезультатПакета[1].Выбрать();
		
		Если ВыборкаИзмененияДляМП.Следующий() Тогда
			
			ОтчетПоТоварамМП_Объект.МП = ВыборкаИзмененияДляМП.Ссылка;
			
		Иначе 	 
			
			ВызватьИсключение "Не найден узел МП"; 
			
		КонецЕсли;
		
		ВыборкаТранспорт = РезультатПакета[2].Выбрать();
		
		Если ВыборкаТранспорт.Следующий() Тогда
			
			ОтчетПоТоварамМП_Объект.Транспорт = ВыборкаТранспорт.Ссылка;
			
		Иначе 	 
			
			ВызватьИсключение "Не найден транспорт"; 
			
		КонецЕсли;
		
		ТЗ_Реализаций = РезультатПакета[3].Выгрузить();
		ТЗ_Номенклатур = РезультатПакета[4].Выгрузить();
		ТЗ_Штрихкодов = РезультатПакета[5].Выгрузить();
		
		Для Каждого СтрокаТЧ Из data.goodRows.goodRow Цикл 
			
			Реализация = ТЗ_Реализаций.Найти(bao.ДобавитьПробелыПосле(СтрокаТЧ.orderId, 11), "Номер");
			Если Реализация = Неопределено Тогда
				ВызватьИсключение "Не найден заказ: " + СтрокаТЧ.orderId; 
			КонецЕсли;
			
			Номенклатура = ТЗ_Номенклатур.Найти(bao.ДобавитьПробелыПосле(СтрокаТЧ.goodId, 11), "Код");
			Если Номенклатура = Неопределено Тогда
				ВызватьИсключение "Не найден номенклатура: " + СтрокаТЧ.goodId; 
			КонецЕсли;
			
			ШтрихкодТовара = "";
			Если ЗначениеЗаполнено(СтрокаТЧ.barcode) Тогда
				ШтрихкодТовара = ТЗ_Штрихкодов.Найти(СтрокаТЧ.barcode, "Код");
				Если ШтрихкодТовара = Неопределено Тогда
					ВызватьИсключение "Не найден штрихкод: " + СтрокаТЧ.barcode; 
				КонецЕсли;
			КонецеСли;	
			
			
			
			НоваяСтрока = ОтчетПоТоварамМП_Объект.Товары.Добавить();
			НоваяСтрока.Заказ = Реализация.Ссылка;
			НоваяСтрока.Номенклатура = Номенклатура.Ссылка;
			НоваяСтрока.Количество = СтрокаТЧ.count;
			НоваяСтрока.Цена = СтрокаТЧ.price;
			
			Если ЗначениеЗаполнено(СтрокаТЧ.markingCode) Тогда
				НоваяСтрока.КодМаркировки = СтрокаТЧ.markingCode;
			КонецеСли;	
			
			Если ЗначениеЗаполнено(ШтрихкодТовара) Тогда
				НоваяСтрока.ШтрихкодТовара = ШтрихкодТовара.Ссылка;
			КонецеСли;	
			
		КонецЦикла;	
		
		ОтчетПоТоварамМП_Объект.Записать(РежимЗаписиДокумента.Проведение);
		
		ОбъектВозврата.successfully = Истина;
		ОбъектВозврата.idDocument = ОтчетПоТоварамМП_Объект.Номер; 
		
		
	Исключение
		
		Сообщить(ОписаниеОшибки());
		
		ОбъектВозврата.successfully = Ложь;
		ОбъектВозврата.error = 	ИнформацияОбОшибке().Описание;
		
	КонецПопытки;
	
	Возврат ОбъектВозврата;
		
КонецФункции

Функция unloadCashCourier(data)
	
	Попытка
		
		ОбъектВозврата = ФабрикаXDTO.Создать(ФабрикаXDTO.Тип("http://www.strizh-logistic.ru/1CMobile/MobileClosingCrews", "responseCashCourier"));
		
		МассивНомеровРеализаций = Новый Массив;
		МассивКодовТиповОплаты = Новый Массив;
		
		Для Каждого СтрокаТЧ Из data.cashRows.cashRow Цикл 
			
			МассивНомеровРеализаций.Добавить(СтрокаТЧ.orderId);
			МассивКодовТиповОплаты.Добавить(СтрокаТЧ.typeOfPay);
			
		КонецЦикла;	
		
		
		
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ
		               |	ОтчетПоДСМП.Ссылка КАК Ссылка
		               |ИЗ
		               |	Документ.ОтчетПоДСМП КАК ОтчетПоДСМП
		               |ГДЕ
		               |	ОтчетПоДСМП.МП.Код = &id_App
		               |	И ОтчетПоДСМП.НомерМП = &number
		               |;
		               |
		               |////////////////////////////////////////////////////////////////////////////////
		               |ВЫБРАТЬ
		               |	ИзмененияДляМП.Ссылка КАК Ссылка
		               |ИЗ
		               |	ПланОбмена.ИзмененияДляМП КАК ИзмененияДляМП
		               |ГДЕ
		               |	ИзмененияДляМП.Код = &id_App
		               |;
		               |
		               |////////////////////////////////////////////////////////////////////////////////
		               |ВЫБРАТЬ
		               |	новаТранспорт.Ссылка КАК Ссылка
		               |ИЗ
		               |	Справочник.новаТранспорт КАК новаТранспорт
		               |ГДЕ
		               |	новаТранспорт.Код = &car
		               |;
		               |
		               |////////////////////////////////////////////////////////////////////////////////
		               |ВЫБРАТЬ РАЗЛИЧНЫЕ
		               |	РеализацияТоваровУслуг.Ссылка КАК Ссылка,
		               |	РеализацияТоваровУслуг.Номер КАК Номер
		               |ИЗ
		               |	Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
		               |ГДЕ
		               |	РеализацияТоваровУслуг.Номер В(&МассивНомеровРеализаций)
		               |;
		               |
		               |////////////////////////////////////////////////////////////////////////////////
		               |ВЫБРАТЬ РАЗЛИЧНЫЕ
		               |	ТипыОплат.Код КАК Код,
		               |	ТипыОплат.Ссылка КАК Ссылка
		               |ИЗ
		               |	Справочник.ТипыОплат КАК ТипыОплат
		               |ГДЕ
		               |	ТипыОплат.Код В(&МассивКодовТиповОплаты)";
		
		Запрос.УстановитьПараметр("number", data.number);
		Запрос.УстановитьПараметр("id_App", data.id_App);
		Запрос.УстановитьПараметр("car", data.car);
		Запрос.УстановитьПараметр("МассивНомеровРеализаций", МассивНомеровРеализаций);
		Запрос.УстановитьПараметр("МассивКодовТиповОплаты", МассивКодовТиповОплаты);
		
		РезультатПакета = Запрос.ВыполнитьПакет();
		
		ВыборкаОтчетПоДСМП = РезультатПакета[0].Выбрать();
		
		Если ВыборкаОтчетПоДСМП.Следующий() Тогда
			
			ВызватьИсключение "Документ уже выгружен"; 
			
		Иначе 
			
			ОтчетПоДСМП_Объект = Документы.ОтчетПоДСМП.СоздатьДокумент();
			
		КонецЕсли;
		
		ОтчетПоДСМП_Объект.Дата = ТекущаяДата();
		ОтчетПоДСМП_Объект.ЭкипажСогласенСДанными = data.courierAgreesWithData;
		ОтчетПоДСМП_Объект.НомерМП = data.number;
		ОтчетПоДСМП_Объект.ДатаРейса = data.dateOfRide;
		
		ВыборкаИзмененияДляМП = РезультатПакета[1].Выбрать();
		
		Если ВыборкаИзмененияДляМП.Следующий() Тогда
			
			ОтчетПоДСМП_Объект.МП = ВыборкаИзмененияДляМП.Ссылка;
			
		Иначе 	 
			
			ВызватьИсключение "Не найден узел МП"; 
			
		КонецЕсли;
		
		ВыборкаТранспорт = РезультатПакета[2].Выбрать();
		
		Если ВыборкаТранспорт.Следующий() Тогда
			
			ОтчетПоДСМП_Объект.Транспорт = ВыборкаТранспорт.Ссылка;
			
		Иначе 	 
			
			ВызватьИсключение "Не найден транспорт"; 
			
		КонецЕсли;
		
		ТЗ_Реализаций = РезультатПакета[3].Выгрузить();
		ТЗ_КодовТиповОплаты = РезультатПакета[4].Выгрузить();

		
		
		Для Каждого СтрокаТЧ Из data.cashRows.cashRow Цикл 
			
			Реализация = ТЗ_Реализаций.Найти(bao.ДобавитьПробелыПосле(СтрокаТЧ.orderId, 11), "Номер");
			Если Реализация = Неопределено Тогда
				ВызватьИсключение "Не найден заказ: " + СтрокаТЧ.orderId; 
			КонецЕсли;
			
			ТипОплаты = ТЗ_КодовТиповОплаты.Найти(Число(СтрокаТЧ.typeOfPay), "Код");
			Если ТипОплаты = Неопределено Тогда
				ВызватьИсключение "Не найден тип оплаты: " + СтрокаТЧ.typeOfPay; 
			КонецЕсли;
			
			НоваяСтрока = ОтчетПоДСМП_Объект.ДС.Добавить();
			НоваяСтрока.Заказ = Реализация.Ссылка;
			НоваяСтрока.ТипОплаты = ТипОплаты.Ссылка;
			НоваяСтрока.Сумма = СтрокаТЧ.amount;
			
		КонецЦикла;	
		
		ОтчетПоДСМП_Объект.Записать(РежимЗаписиДокумента.Проведение);
		
		ОбъектВозврата.successfully = Истина;
		ОбъектВозврата.idDocument = ОтчетПоДСМП_Объект.Номер; 
		
		
	Исключение
		
		Сообщить(ОписаниеОшибки());
		
		ОбъектВозврата.successfully = Ложь;
		ОбъектВозврата.error = 	ИнформацияОбОшибке().Описание;
		
	КонецПопытки;
	
	Возврат ОбъектВозврата;


КонецФункции
