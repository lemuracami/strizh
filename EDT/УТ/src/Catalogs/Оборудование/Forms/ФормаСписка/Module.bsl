&НаКлиенте
Процедура ВнешнееСобытие(Источник, Событие, Данные)
	
	Если НЕ Источник = "Сканер штрихкода" Тогда
		Возврат;
	КонецЕсли;		
  
	Если НЕ ЗначениеЗаполнено(Данные) Тогда
		Возврат;
	КонецЕсли;	
	
	Если НЕ ВводДоступен() Тогда
		Возврат;
	КонецЕсли; 
	
	СпозиционироватьТекущуюСтроку(Данные);
	
КонецПроцедуры

&НаКлиенте
Процедура СпозиционироватьТекущуюСтроку(Код)
	
	СоответствиеОборудования = УчетОборудованияКлиент.НайтиОборудование(Код);
	 	
	Для каждого СтрокаОборудование Из СоответствиеОборудования Цикл
				
		Элементы.Список.ТекущаяСтрока = СтрокаОборудование.Значение;
				
	КонецЦикла;

КонецПроцедуры // ()

&НаСервереБезКонтекста
Функция ПолучитьДоступностьКоманд(Знач Ссылка)
	
	ДоступностьКоманд = Новый Структура("SIM");
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Оборудование.МодельОборудования.ТипОборудования В (ЗНАЧЕНИЕ(Перечисление.ТипыОборудования.Смартфон), ЗНАЧЕНИЕ(Перечисление.ТипыОборудования.Планшет)) КАК SIM
	|ИЗ
	|	Справочник.Оборудование КАК Оборудование
	|ГДЕ
	|	Оборудование.Ссылка = &Ссылка";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Выборка.Следующий();
	
	ЗаполнитьЗначенияСвойств(ДоступностьКоманд, Выборка);
	
	Возврат ДоступностьКоманд;
	
КонецФункции

&НаКлиенте
Процедура СписокПриАктивизацииСтроки(Элемент)
	
	ТекСтрока = Элемент.ТекущаяСтрока;
	Если ТекСтрока = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	//Асеев 05.04.2024 (Задача № 5251)>>>
	ДоступностьКоманд = ПолучитьДоступностьКоманд(ТекСтрока);
	//Элементы.ФормаSIM.Доступность = ДоступностьКоманд.SIM;
	Элементы.ФормаСправочникОборудованиеУстановитьSIMКарту.Доступность = ДоступностьКоманд.SIM;
	Элементы.ФормаСправочникОборудованиеДемонтироватьSIMКарту.Доступность = ДоступностьКоманд.SIM;
	//Асеев 05.04.2024 (Задача № 5251)<<<

КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Регион = ПараметрыСеанса.ТерминалДоставки;
	//Регион = Неопределено;
	Если Регион.Пустая() Тогда
		Регион = Справочники.РегиональныеТерминалы.МоскваСтриж;
	ИначеЕсли Не РольДоступна("АдминистрированиеОборудования") Тогда
		Элементы.ОтборРегион.ТолькоПросмотр = Истина;
	КонецЕсли;
	
	УстановитьОтборыСписка();
	
КонецПроцедуры
 
&НаСервере
Процедура УстановитьОтборыСписка()
	
	ИмяОтбора = "ОтборРегион";
	
	ОбщегоНазначенияКлиентСервер83.УдалитьЭлементыГруппыОтбора(Список.Отбор,, ИмяОтбора);
	
	Если Не Регион.Пустая() Тогда
		ГруппаЭлементовОтбора = ОбщегоНазначенияКлиентСервер83.СоздатьГруппуЭлементовОтбора(Список.Отбор.Элементы, ИмяОтбора, ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ);
		ОбщегоНазначенияКлиентСервер83.УстановитьЭлементОтбора(ГруппаЭлементовОтбора, "Регион", Регион, ВидСравненияКомпоновкиДанных.Равно,, Истина);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОтборРегионПриИзмененииНаСервере()
	
	УстановитьОтборыСписка();
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборРегионПриИзменении(Элемент)
	ОтборРегионПриИзмененииНаСервере();
КонецПроцедуры
