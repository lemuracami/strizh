&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	//Асеев 25.04.2024 (Задача № 5265)>>>
	Если Объект.Ссылка.Пустая() Тогда
		Элементы.РазукомплектоватьОборудование.Видимость = Ложь;
	Иначе
		ЗаполнитьДанныеОборудования();	
	КонецЕсли;
	//Асеев 25.04.2024 (Задача № 5265)<<<
	
	//Асеев 05.04.2024 (Задача № 5251)>>>
	ТипОборудованияПриИзмененииНаСервере();
	//Асеев 05.04.2024 (Задача № 5251)<<<
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДанныеОборудования()
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", Объект.Ссылка);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	СтатусыОборудованияСрезПервых.СтатусОборудования КАК Статус,
	|	ВЫБОР
	|		КОГДА УчетОборудованияСрезПоследних.ВыдачаНаДлительныйСрок
	|			ТОГДА УчетОборудованияСрезПоследних.ФизЛицо
	|	КОНЕЦ КАК ФизЛицо,
	|	СвязанноеОборудованиеСрезПоследних.ОборудованиеПодчиненное.ПараметрОборудования КАК НомерТелефона,
	|	ВЗ_КомплектыОборудования.Транспорт КАК Транспорт,
	|	ДополнительныеПараметрыТранспортаСрезПоследних.ФорматАвтотранспорта КАК ТипАвто
	|ИЗ
	|	Справочник.Оборудование КАК Т_Оборудование
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.УчетОборудования.СрезПоследних(, Оборудование = &Ссылка) КАК УчетОборудованияСрезПоследних
	|		ПО (ИСТИНА)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтатусыОборудования.СрезПервых(, Оборудование = &Ссылка) КАК СтатусыОборудованияСрезПервых
	|		ПО (ИСТИНА)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СвязанноеОборудование.СрезПоследних(
	|				,
	|				ОборудованиеВладелец = &Ссылка
	|					И ОборудованиеПодчиненное.МодельОборудования = ЗНАЧЕНИЕ(Справочник.МоделиОборудования.SIMКарта)) КАК СвязанноеОборудованиеСрезПоследних
	|		ПО (СвязанноеОборудованиеСрезПоследних.СтатусМонтирования = ЗНАЧЕНИЕ(Перечисление.СтатусыМонтированияОборудования.Смонтировано))
	|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ ПЕРВЫЕ 1
	|			КомплектыОборудованияСрезПоследних.Транспорт КАК Транспорт
	|		ИЗ
	|			РегистрСведений.КомплектыОборудования.СрезПоследних КАК КомплектыОборудованияСрезПоследних
	|		ГДЕ
	|			КомплектыОборудованияСрезПоследних.Оборудование = &Ссылка
	|		
	|		УПОРЯДОЧИТЬ ПО
	|			КомплектыОборудованияСрезПоследних.Период УБЫВ,
	|			КомплектыОборудованияСрезПоследних.Регистратор УБЫВ) КАК ВЗ_КомплектыОборудования
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДополнительныеПараметрыТранспорта.СрезПоследних КАК ДополнительныеПараметрыТранспортаСрезПоследних
	|			ПО ВЗ_КомплектыОборудования.Транспорт = ДополнительныеПараметрыТранспортаСрезПоследних.Транспорт
	|		ПО (ИСТИНА)
	|ГДЕ
	|	Т_Оборудование.Ссылка = &Ссылка";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Выборка.Следующий();
	
	Статус = Выборка.Статус;
	ФизЛицо = Выборка.ФизЛицо;
	НомерТелефона = Выборка.НомерТелефона;
	Транспорт = Выборка.Транспорт;
	ТипАвто = Выборка.ТипАвто;
	
	Если Транспорт.Пустая() Тогда
		Элементы.РазукомплектоватьОборудование.Видимость = Ложь;
	Иначе
		Элементы.РазукомплектоватьОборудование.Видимость = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура МодельОборудованияПриИзменении(Элемент)
	
	МодельОборудованияПриИзмененииНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура МодельОборудованияПриИзмененииНаСервере()
	
	Если Объект.Ссылка.Пустая() Тогда
		Объект.Наименование = Объект.МодельОборудования;
	Иначе
		ЗаполнитьДанныеОборудования();
	КонецЕсли; 
	
	ТипОборудованияПриИзмененииНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ТипОборудованияПриИзмененииНаСервере()
	
	ТипОборудования = Объект.МодельОборудования.ТипОборудования;
	ДоступностьSIM = ТипОборудования = Перечисления.ТипыОборудования.Смартфон Или ТипОборудования = Перечисления.ТипыОборудования.Планшет;
	//Элементы.ФормаSIM.Доступность = ДоступностьSIM;
	Элементы.ФормаСправочникОборудованиеУстановитьSIMКарту.Доступность = ДоступностьSIM;
	Элементы.ФормаСправочникОборудованиеДемонтироватьSIMКарту.Доступность = ДоступностьSIM;
	//Асеев 25.04.2024 (Задача № 5265)>>>
	Элементы.НомерТелефона.Видимость = ДоступностьSIM;
	//Асеев 25.04.2024 (Задача № 5265)<<<
	
КонецПроцедуры

&НаКлиенте
Процедура РазукомплектоватьОборудование(Команда)
	
	//Асеев 10.12.2024 (Задача № 5341)>>>
	ПоказатьВопрос(Новый ОписаниеОповещения("РазукомплектоватьОборудованиеЗавершение", ЭтотОбъект), "Вы действительно хотите разукомплектовать данное оборудование?", РежимДиалогаВопрос.ДаНет);
	//Асеев 10.12.2024 (Задача № 5341)<<<
	
КонецПроцедуры

&НаКлиенте
Процедура РазукомплектоватьОборудованиеЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	//Асеев 10.12.2024 (Задача № 5341)>>>
	Если Результат <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура("Основание", Новый Структура("Транспорт,ТипОборудования,Оборудование,РазукомплектацияОборудования", Транспорт, ТипОборудования, Объект.Ссылка, Истина));
	
	ОткрытьФорму("Документ.СведенияОКомплектеОборудования.ФормаОбъекта", ПараметрыФормы, ЭтаФорма,,,, Новый ОписаниеОповещения("СведенияОКомплектеОборудованияЗавершение", ЭтотОбъект), РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	//Асеев 10.12.2024 (Задача № 5341)<<<
	
КонецПроцедуры

&НаКлиенте
Процедура СведенияОКомплектеОборудованияЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	ЗаполнитьДанныеОборудования();
	
КонецПроцедуры
