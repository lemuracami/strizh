
&НаКлиенте
Функция ПроверитьСтрокуЦифр(СтрокаЦифр)
	
	ДлинаСтроки = СтрДлина(СтрокаЦифр);
	Для НомерСимвола = 1 По ДлинаСтроки Цикл
		Если Не СтрНайти("1234567890", Сред(СтрокаЦифр, НомерСимвола, 1)) Тогда
			Возврат Ложь;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Истина;
	
КонецФункции

&НаСервере
Функция ПолучитьОборудованиеSIM()
	
	ОборудованиеSIM = Неопределено;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("НомерSIM", "%" + НомерSIM + "%");
	
	Если СостояниеОборудования = Перечисления.СостоянияЕдиницыОборудования.Выдано Тогда
		Запрос.Текст =
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	Оборудование.Ссылка КАК Ссылка
		|ПОМЕСТИТЬ ВТ_ОборудованиеSIM
		|ИЗ
		|	Справочник.Оборудование КАК Оборудование
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.МоделиОборудования КАК МоделиОборудования
		|		ПО (МоделиОборудования.ТипОборудования = ЗНАЧЕНИЕ(Перечисление.ТипыОборудования.SIMКарта))
		|			И Оборудование.МодельОборудования = МоделиОборудования.Ссылка
		|			И (Оборудование.ПараметрОборудования ПОДОБНО &НомерSIM)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_ОборудованиеSIM.Ссылка КАК ОборудованиеSIM,
		|	СвязанноеОборудованиеСрезПоследних.ОборудованиеВладелец КАК ОборудованиеВладелец,
		|	СвязанноеОборудованиеСрезПоследних.ОборудованиеВладелец.МодельОборудования.ТипОборудования КАК ТипОборудования,
		|	СвязанноеОборудованиеСрезПоследних.ОборудованиеВладелец.Наименование КАК Наименование,
		|	УчетОборудованияСрезПоследних.ФизЛицо.Наименование КАК ФизЛицоНаименование
		|ИЗ
		|	ВТ_ОборудованиеSIM КАК ВТ_ОборудованиеSIM
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СвязанноеОборудование.СрезПоследних(
		|				,
		|				ОборудованиеПодчиненное В
		|					(ВЫБРАТЬ
		|						ВТ_ОборудованиеSIM.Ссылка КАК Ссылка
		|					ИЗ
		|						ВТ_ОборудованиеSIM КАК ВТ_ОборудованиеSIM)) КАК СвязанноеОборудованиеСрезПоследних
		|		ПО (СвязанноеОборудованиеСрезПоследних.СтатусМонтирования = ЗНАЧЕНИЕ(Перечисление.СтатусыМонтированияОборудования.Смонтировано))
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.УчетОборудования.СрезПоследних(
		|				,
		|				Оборудование В
		|					(ВЫБРАТЬ
		|						ВТ_ОборудованиеSIM.Ссылка КАК Ссылка
		|					ИЗ
		|						ВТ_ОборудованиеSIM КАК ВТ_ОборудованиеSIM)) КАК УчетОборудованияСрезПоследних
		|		ПО (УчетОборудованияСрезПоследних.СостояниеЕдиницыОборудования = ЗНАЧЕНИЕ(Перечисление.СостоянияЕдиницыОборудования.Выдано))";
		
		Результат = Запрос.Выполнить();
		Если Результат.Пустой() Тогда
			
			НачатьТранзакцию();
			Попытка
				Спр = Справочники.Оборудование.СоздатьЭлемент();
				Спр.Наименование = "SIM карта";
				Спр.МодельОборудования = Справочники.МоделиОборудования.SIMКарта;
				Спр.ПараметрОборудования = НомерSIM;
				Спр.Записать();
				
				ОборудованиеSIM = Спр.Ссылка;
				
				Док = Документы.УстановкаСтатусаОборудования.СоздатьДокумент();
				Док.Регион = Регион;
				Док.СтатусОборудования = Перечисления.СтатусыЕдиницыОборудования.ВРаботе;
				Док.Пользователь = ПараметрыСеанса.ТекущийПользователь;
				Док.СписокОборудования.Добавить().Оборудование = ОборудованиеSIM;
				Док.Дата = ТекущаяДата();
				Док.Записать(РежимЗаписиДокумента.Проведение);
				ЗафиксироватьТранзакцию();
			Исключение
				ОборудованиеSIM = ОписаниеОшибки();
				ОтменитьТранзакцию();
			КонецПопытки;
			
		Иначе
			Выборка = Результат.Выбрать();
			Выборка.Следующий();
			Если Выборка.ОборудованиеВладелец = Null Тогда
				Если Выборка.ФизЛицоНаименование = Null Тогда
					ОборудованиеSIM = Выборка.ОборудованиеSIM;
				Иначе
					ОборудованиеSIM = "Данная SIM карта уже выдана " + СокрП(Выборка.ФизЛицоНаименование) + "!";
				КонецЕсли;
			Иначе
				ОборудованиеSIM = "Данная SIM карта уже установлена в оборудование " + Выборка.ТипОборудования + """, наименование " + Выборка.Наименование + ". Сперва удалите SIM карту из указанного оборудования!";
			КонецЕсли;
		КонецЕсли;
		
	Иначе//Сдано
		Запрос.Текст =
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	Оборудование.Ссылка КАК ОборудованиеSIM
		|ИЗ
		|	Справочник.Оборудование КАК Оборудование
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.МоделиОборудования КАК МоделиОборудования
		|		ПО (МоделиОборудования.ТипОборудования = ЗНАЧЕНИЕ(Перечисление.ТипыОборудования.SIMКарта))
		|			И Оборудование.МодельОборудования = МоделиОборудования.Ссылка
		|			И (Оборудование.ПараметрОборудования ПОДОБНО &НомерSIM)";
		
		Результат = Запрос.Выполнить();
		Если Результат.Пустой() Тогда
			ОборудованиеSIM = "SIM карта не найдена!";
		Иначе
			Выборка = Результат.Выбрать();
			Выборка.Следующий();
			ОборудованиеSIM = Выборка.ОборудованиеSIM;
		КонецЕсли;
		
	КонецЕсли;
	Возврат ОборудованиеSIM;
	
КонецФункции

&НаКлиенте
Процедура КомандаОК(Команда)
	
	//СтрокаЦифр = "8(ххх)ххх-хх-хх";
	Если Не ПустаяСтрока(НомерSIM) И ПроверитьСтрокуЦифр(Сред(НомерSIM, 3, 3) + Сред(НомерSIM, 7, 3) + Сред(НомерSIM, 11, 2) + Прав(НомерSIM, 2)) Тогда
		ОборудованиеSIM = ПолучитьОборудованиеSIM();
		Если ТипЗнч(ОборудованиеSIM) = Тип("Строка") Тогда
			ПоказатьПредупреждение(, ОборудованиеSIM);
		ИначеЕсли ОборудованиеSIM <> Неопределено Тогда
			Закрыть(ОборудованиеSIM);
		КонецЕсли;
	Иначе
		ПоказатьПредупреждение(, "Введите номер в формате 8(999)999-99-99");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаОтмена(Команда)
	Закрыть();
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	//Асеев 06.06.2024 (Задача № 5259)>>>
	Если Не Параметры.Свойство("СостояниеОборудования", СостояниеОборудования) Тогда
		СостояниеОборудования = Перечисления.СостоянияЕдиницыОборудования.Выдано;
	КонецЕсли;
	//Асеев 06.06.2024 (Задача № 5259)<<<
	//Асеев 10.07.2024 (Задача № 5259)>>>
	Параметры.Свойство("Регион", Регион);
	//Асеев 10.07.2024 (Задача № 5259)<<<

КонецПроцедуры
