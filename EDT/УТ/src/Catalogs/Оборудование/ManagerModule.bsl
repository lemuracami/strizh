
//CeHbKA 04.04.2019 #2995
Функция ПечатьШК(МассивОборудования) Экспорт

	ТабДок = СформироватьТабДок();
	
	ТабДокЛиния = Новый ТабличныйДокумент;
	ВыводЛинии = Ложь;
	
	Макет = ПолучитьМакет("МакетШК");
	
	Сч = 1;
	
	Для каждого Оборудование Из МассивОборудования Цикл
		
		//Префикс = ПолучитьПрефиксОборудования(Оборудование.МодельОборудования.ТипОборудования);
		
		Макет.Параметры.Код = Оборудование.Код;
		Макет.Параметры.Штрихкод = may.ШтрихкодCode128(Оборудование.Код);
		Макет.Параметры.Наименование = Оборудование.Наименование;
		Макет.Параметры.Модель = Оборудование.МодельОборудования;
		
		Если Сч%5 = 1 Тогда
			ТабДок.Вывести(ТабДокЛиния);					
			ВыводЛинии = Истина;
			
			ТабДокЛиния = Новый ТабличныйДокумент;
		КонецЕсли; 
		
		ТабДокЛиния.Присоединить(Макет);
		ВыводЛинии = Ложь;
		
		Сч = Сч + 1;
		
	КонецЦикла; 
	
	Если НЕ ВыводЛинии Тогда
		ТабДок.Вывести(ТабДокЛиния);
	КонецЕсли; 
	
	Возврат ТабДок;	

КонецФункции // ()

Функция ПолучитьПрефиксОборудования(ТипОборудования) Экспорт

	Если ТипОборудования = Перечисления.ТипыОборудования.Планшет Тогда
		Возврат "TB";	
	ИначеЕсли ТипОборудования = Перечисления.ТипыОборудования.ПринтерЭтикеток Тогда
		Возврат "PL";
	ИначеЕсли ТипОборудования = Перечисления.ТипыОборудования.Смартфон Тогда
		Возврат "SM";	
	ИначеЕсли ТипОборудования = Перечисления.ТипыОборудования.ТерминалПриемаОплатыПоКарте Тогда
		Возврат "PT";	
	КонецЕсли; 	

	Возврат "";
	
КонецФункции // ()

Функция ПечатьQR(МассивОборудования) Экспорт 
	
	ТабДок = СформироватьТабДок();
	
	ТабДокЛиния = Новый ТабличныйДокумент;
	ВыводЛинии = Ложь;
	
	Макет = ПолучитьМакет("МакетQR");
	
	Сч = 1;
	
	Для каждого Оборудование Из МассивОборудования Цикл
		
	  	КартинкаQRКода = Новый Картинка(РаботаСШтрихкодированиемСервер.ПолучитьДвоичныеДанныеКартинкиQR(Оборудование.Код));
	  	Макет.Рисунки.QRКод.Картинка = КартинкаQRКода;
		
		Макет.Параметры.Код = Оборудование.Код;
		Макет.Параметры.Наименование = Оборудование.Наименование;
		Макет.Параметры.Модель = Оборудование.МодельОборудования;
		
		Если Сч%7 = 1 Тогда
			ТабДок.Вывести(ТабДокЛиния);					
			ВыводЛинии = Истина;
			
			ТабДокЛиния = Новый ТабличныйДокумент;
		КонецЕсли; 
		
		ТабДокЛиния.Присоединить(Макет);
		ВыводЛинии = Ложь;
		
		Сч = Сч + 1;
		
		Если Сч = 12 Тогда
			ТабДок.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли; 
		
	КонецЦикла; 
	
	Если НЕ ВыводЛинии Тогда
		ТабДок.Вывести(ТабДокЛиния);
	КонецЕсли; 
	
	Возврат ТабДок;	


КонецФункции // ()

Функция СформироватьТабДок() Экспорт

	ТабДок = Новый ТабличныйДокумент;
	
	ТабДок.АвтоМасштаб			= Ложь;
	ТабДок.МасштабПечати		= 70;
	ТабДок.ПолеСверху			= 7;
	ТабДок.ПолеСнизу			= 7;
	ТабДок.ПолеСправа			= 7;	
	ТабДок.ПолеСлева			= 7;	
	ТабДок.ОтображатьСетку		= Ложь;
	ТабДок.ОтображатьЗаголовки	= Ложь;
	ТабДок.ТолькоПросмотр		= Истина;
	
	Возврат ТабДок;
	
КонецФункции // ()

Процедура ОбработкаПолученияПредставления(Данные, Представление, СтандартнаяОбработка)
	//Сообщить(Данные.Наименование);
	СтандартнаяОбработка = Ложь;
	
	Наименование = Неопределено;
	Если Данные.Свойство("Наименование", Наименование) Тогда
		Представление = "(" + Наименование + ") " + ПолучитьГосНомерТранспорта(Данные.Ссылка);
	Иначе
		Представление = ПолучитьНаименованиеИГосНомер(Данные.Ссылка);
	КонецЕсли;
		
КонецПроцедуры

Процедура ОбработкаПолученияПолейПредставления(Поля, СтандартнаяОбработка)
	// Вставить содержимое обработчика.
//	СтандартнаяОбработка = Ложь;
//	Поля.Добавить("Наименование");
//	Поля.Добавить("Ссылка");
КонецПроцедуры
 
//CeHbKA 04.04.2019 #2995

Функция ПолучитьГосНомерТранспорта(Оборудование) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	КомплектыОборудованияСрезПоследних.Транспорт.НомерГосударственнойРегистрации КАК ГосНомер
	|ИЗ
	|	РегистрСведений.КомплектыОборудования.СрезПоследних(, ) КАК КомплектыОборудованияСрезПоследних
	|ГДЕ
	|	КомплектыОборудованияСрезПоследних.Оборудование = &Оборудование";
	
	Запрос.УстановитьПараметр("Оборудование", Оборудование);
	
	ВыборкаДетальныеЗаписи = Запрос.Выполнить().Выбрать();
	Если ВыборкаДетальныеЗаписи.Следующий() Тогда
		Возврат ВыборкаДетальныеЗаписи.ГосНомер;
	КонецЕсли;
	
	Возврат "<<НК>>";
	
КонецФункции // ()

Функция ПолучитьНаименованиеИГосНомер(Оборудование) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Оборудование.Наименование КАК Наименование,
	|	ВложенныйЗапрос.ГосНомер КАК ГосНомер
	|ИЗ
	|	Справочник.Оборудование КАК Оборудование
	|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ ПЕРВЫЕ 1
	|			КомплектыОборудованияСрезПоследних.Транспорт.НомерГосударственнойРегистрации КАК ГосНомер
	|		ИЗ
	|			РегистрСведений.КомплектыОборудования.СрезПоследних(, ) КАК КомплектыОборудованияСрезПоследних
	|		ГДЕ
	|			КомплектыОборудованияСрезПоследних.Оборудование = &Оборудование) КАК ВложенныйЗапрос
	|		ПО (ИСТИНА)
	|ГДЕ
	|	Оборудование.Ссылка = &Оборудование";
	
	Запрос.УстановитьПараметр("Оборудование", Оборудование);
	
	ВыборкаДетальныеЗаписи = Запрос.Выполнить().Выбрать();
	ВыборкаДетальныеЗаписи.Следующий();
	
	Если ВыборкаДетальныеЗаписи.ГосНомер = Null Тогда
		Возврат "(" + ВыборкаДетальныеЗаписи.Наименование + ") <<НК>>";
	Иначе
		Возврат "(" + ВыборкаДетальныеЗаписи.Наименование + ") " + ВыборкаДетальныеЗаписи.ГосНомер;
	КонецЕсли;
	
КонецФункции // ()



