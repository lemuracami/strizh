
&НаСервере
Функция ПроверитьВозможностьОбработки(Ссылка)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	""Оборудование выдано "" + УчетОборудованияСрезПоследних.ФизЛицо.Наименование + ""! Перед установкой SIM карты примите оборудования от сотрудника!"" КАК Ошибка
	|ИЗ
	|	РегистрСведений.УчетОборудования.СрезПоследних(, Оборудование = &Ссылка) КАК УчетОборудованияСрезПоследних
	|ГДЕ
	|	УчетОборудованияСрезПоследних.СостояниеЕдиницыОборудования = ЗНАЧЕНИЕ(Перечисление.СостоянияЕдиницыОборудования.Выдано)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	""В данном оборудовании уже установлена SIM карта "" + СвязанноеОборудованиеСрезПоследних.ОборудованиеПодчиненное.ПараметрОборудования + "". Сперва демонтируйте текущую установленную SIM карту!""
	|ИЗ
	|	РегистрСведений.СвязанноеОборудование.СрезПоследних(
	|			,
	|			ОборудованиеВладелец = &Ссылка
	|				И ОборудованиеПодчиненное.МодельОборудования = ЗНАЧЕНИЕ(Справочник.МоделиОборудования.SIMКарта)) КАК СвязанноеОборудованиеСрезПоследних
	|ГДЕ
	|	СвязанноеОборудованиеСрезПоследних.СтатусМонтирования = ЗНАЧЕНИЕ(Перечисление.СтатусыМонтированияОборудования.Смонтировано)";
	
	Результат = Запрос.Выполнить();
	Если Результат.Пустой() Тогда
		Возврат Истина;
	КонецЕсли;
	
	Выборка = Результат.Выбрать();
	Пока Выборка.Следующий() Цикл
		Сообщить(Выборка.Ошибка);
	КонецЦикла;
	Возврат Ложь;
		
КонецФункции

&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	//Вставить содержимое обработчика.
	//ПараметрыФормы = Новый Структура("", );
	//ОткрытьФорму("Справочник.Оборудование.ФормаСписка", ПараметрыФормы, ПараметрыВыполненияКоманды.Источник, ПараметрыВыполненияКоманды.Уникальность, ПараметрыВыполненияКоманды.Окно, ПараметрыВыполненияКоманды.НавигационнаяСсылка);
	
	//Асеев 05.04.2024 (Задача № 5251)>>>
	Если Не ПроверитьВозможностьОбработки(ПараметрКоманды) Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура("РежимВыбора", Истина);
	
	ОткрытьФорму("Справочник.Оборудование.Форма.ВводНомераSIM", ПараметрыФормы,,,,, Новый ОписаниеОповещения("ОбработкаКомандыЗавершение", ЭтотОбъект, ПараметрКоманды), РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
	//Асеев 05.04.2024 (Задача № 5251)<<<
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаКомандыЗавершение(РезультатВыбора, Параметры) Экспорт
	
	Если РезультатВыбора = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ОбработкаКомандыНаСервере(РезультатВыбора, Параметры);
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаКомандыНаСервере(ОборудованиеSIM, Оборудование)
	
	Док = Документы.МонтированиеОборудования.СоздатьДокумент();
	Док.РегиональныйТерминал = ПараметрыСеанса.ТерминалДоставки;
	Док.Пользователь = ПараметрыСеанса.ТекущийПользователь;
	СтрокаОборудования = Док.Оборудование.Добавить();
	СтрокаОборудования.ОборудованиеВладелец = Оборудование;
	СтрокаОборудования.ОборудованиеПодчиненное = ОборудованиеSIM;
	СтрокаОборудования.СтатусМонтирования = Перечисления.СтатусыМонтированияОборудования.Смонтировано;
	Док.Дата = ТекущаяДата();
	Док.Записать(РежимЗаписиДокумента.Проведение);
	
КонецПроцедуры
