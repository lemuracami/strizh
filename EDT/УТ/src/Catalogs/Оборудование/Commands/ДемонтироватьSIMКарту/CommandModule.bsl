
&НаСервере
Функция ПроверитьВозможностьОбработки(Ссылка)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	""Оборудование выдано "" + УчетОборудованияСрезПоследних.ФизЛицо.Наименование + ""! Перед демонтажом SIM карты примите оборудования от сотрудника!"" КАК Ошибка
	|ИЗ
	|	РегистрСведений.УчетОборудования.СрезПоследних(, Оборудование = &Ссылка) КАК УчетОборудованияСрезПоследних
	|ГДЕ
	|	УчетОборудованияСрезПоследних.СостояниеЕдиницыОборудования = ЗНАЧЕНИЕ(Перечисление.СостоянияЕдиницыОборудования.Выдано)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СвязанноеОборудованиеСрезПоследних.ОборудованиеПодчиненное КАК ОборудованиеПодчиненное,
	|	СвязанноеОборудованиеСрезПоследних.ОборудованиеВладелец.Наименование КАК ВладелецНаименование,
	|	СвязанноеОборудованиеСрезПоследних.ОборудованиеВладелец.МодельОборудования.ТипОборудования КАК ТипВладельцаНаименование,
	|	СвязанноеОборудованиеСрезПоследних.ОборудованиеПодчиненное.ПараметрОборудования КАК ПодчиненныйПараметрОборудования
	|ИЗ
	|	РегистрСведений.СвязанноеОборудование.СрезПоследних(
	|			,
	|			ОборудованиеВладелец = &Ссылка
	|				И ОборудованиеПодчиненное.МодельОборудования = ЗНАЧЕНИЕ(Справочник.МоделиОборудования.SIMКарта)) КАК СвязанноеОборудованиеСрезПоследних
	|ГДЕ
	|	СвязанноеОборудованиеСрезПоследних.СтатусМонтирования = ЗНАЧЕНИЕ(Перечисление.СтатусыМонтированияОборудования.Смонтировано)";
	
	РезультатПакета = Запрос.ВыполнитьПакет();
	Результат = РезультатПакета[0];
	Если Не Результат.Пустой() Тогда
		Выборка = Результат.Выбрать();
		Выборка.Следующий();
		Сообщить(Выборка.Ошибка);
		Возврат Неопределено;
	КонецЕсли;
	
	Результат = РезультатПакета[1];
	Если Результат.Пустой() Тогда
		Сообщить("SIM карта не установлена");
		Возврат Неопределено;
	КонецЕсли;
	
	Выборка = Результат.Выбрать();
	Выборка.Следующий();
	
	ПодчиненноеОборудование = Новый Структура("ОборудованиеПодчиненное,ВладелецНаименование,ТипВладельцаНаименование,ПодчиненныйПараметрОборудования");
	ЗаполнитьЗначенияСвойств(ПодчиненноеОборудование, Выборка);
	
	Возврат ПодчиненноеОборудование;
		
КонецФункции

&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	//Вставить содержимое обработчика.
	//ПараметрыФормы = Новый Структура("", );
	//ОткрытьФорму("Справочник.Оборудование.ФормаСписка", ПараметрыФормы, ПараметрыВыполненияКоманды.Источник, ПараметрыВыполненияКоманды.Уникальность, ПараметрыВыполненияКоманды.Окно, ПараметрыВыполненияКоманды.НавигационнаяСсылка);
	
	//Асеев 05.04.2024 (Задача № 5251)>>>
	ПодчиненноеОборудование = ПроверитьВозможностьОбработки(ПараметрКоманды);
	Если ПодчиненноеОборудование = Неопределено Тогда
		Возврат;
	КонецЕсли;
	Параметры = Новый Структура("ОборудованиеВладелец,ОборудованиеПодчиненное", ПараметрКоманды, ПодчиненноеОборудование.ОборудованиеПодчиненное);
	
	ПоказатьВопрос(Новый ОписаниеОповещения("ОбработкаКомандыЗавершение", ЭтотОбъект, Параметры), "Вы действительно хотите демонтировать SIM карту " + ПодчиненноеОборудование.ПодчиненныйПараметрОборудования + " от " + ПодчиненноеОборудование.ТипВладельцаНаименование + " " + ПодчиненноеОборудование.ВладелецНаименование + "?", РежимДиалогаВопрос.ДаНет);
	//Асеев 05.04.2024 (Задача № 5251)<<<
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаКомандыЗавершение(РезультатВыбора, Параметры) Экспорт
	
	Если РезультатВыбора <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	
	ОбработкаКомандыНаСервере(Параметры);
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаКомандыНаСервере(Параметры)
	
	Док = Документы.МонтированиеОборудования.СоздатьДокумент();
	Док.РегиональныйТерминал = ПараметрыСеанса.ТерминалДоставки;
	Док.Пользователь = ПараметрыСеанса.ТекущийПользователь;
	СтрокаОборудования = Док.Оборудование.Добавить();
	ЗаполнитьЗначенияСвойств(СтрокаОборудования, Параметры);
	СтрокаОборудования.СтатусМонтирования = Перечисления.СтатусыМонтированияОборудования.Демонтировано;
	Док.Дата = ТекущаяДата();
	Док.Записать(РежимЗаписиДокумента.Проведение);
	
КонецПроцедуры

