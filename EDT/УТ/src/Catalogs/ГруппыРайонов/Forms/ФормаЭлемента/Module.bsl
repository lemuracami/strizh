
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	                                                         //ПараметрыСеанса.ТекущийПользователь = Справочники.Пользователи.НайтиПоКоду("АдминистраторУТЛ")
	Если НЕ ПараметрыСеанса.ТекущийПользователь.Пустая() И РольДоступна("Админы") Тогда
		Элементы.axi_id.Доступность = Истина;
		Элементы.КодТарифа.Доступность = Истина;
	Иначе
		Элементы.axi_id.Доступность = Ложь;
	    Элементы.КодТарифа.Доступность = Ложь;
	КонецЕсли;
	
	//Асеев 10.10.2024 (Задача № 5335)>>>
	КартаТарификацииПартнеровПриИзмененииНаСервере(Ложь);
	//Асеев 10.10.2024 (Задача № 5335)<<<
	
КонецПроцедуры

&НаСервере
Процедура КартаТарификацииПартнеровПриИзмененииНаСервере(Изменение = Истина)
	
	СхемаЗонОпределена = Ложь;
	
	Если Объект.КартаТарификацииПартнеров.Пустая() Тогда
		СхемаЗонТарификацииПартнеров = Неопределено;
	Иначе
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("Карта", Объект.КартаТарификацииПартнеров);
		Запрос.Текст =
		"ВЫБРАТЬ
		|	АктуальныеСхемыЗонКартТарификацииПартнеровСрезПоследних.СхемаЗонТарификацииПартнеров КАК СхемаЗонТарификацииПартнеров
		|ИЗ
		|	РегистрСведений.АктуальныеСхемыЗонКартТарификацииПартнеров.СрезПоследних(, КартаКонструктораКарт = &Карта) КАК АктуальныеСхемыЗонКартТарификацииПартнеровСрезПоследних";
		
		Результат = Запрос.Выполнить();
		Если Результат.Пустой() Тогда
			СхемаЗонТарификацииПартнеров = "<<ДАННЫЕ НЕ НАЙДЕНЫ>>";
		Иначе
			Выборка = Запрос.Выполнить().Выбрать();
			Выборка.Следующий();
			
			СхемаЗонТарификацииПартнеров = Выборка.СхемаЗонТарификацииПартнеров;
			СхемаЗонОпределена = Истина;
			
		КонецЕсли;
	КонецЕсли;
	
	Элементы.ГруппаЗоныТарификацииПартнеров.Видимость = СхемаЗонОпределена;	
	
	Если Изменение Тогда
		Если Не СхемаЗонОпределена Тогда
			Объект.ЗоныТарификацииПартнеров.Очистить();
		КонецЕсли;
	КонецЕсли;
	
	Если Объект.ЗоныТарификацииПартнеров.Количество() Тогда
		//проставить зоны тарификации
		Если СхемаЗонОпределена Тогда
			Запрос = Новый Запрос;
			Запрос.УстановитьПараметр("Владелец", СхемаЗонТарификацииПартнеров);
			Запрос.УстановитьПараметр("НомераЗон", Объект.ЗоныТарификацииПартнеров.Выгрузить(, "НомерЗоныТарификации").ВыгрузитьКолонку("НомерЗоныТарификации"));
			Запрос.Текст =
			"ВЫБРАТЬ
			|	ЗоныТарификацииПартнеров.НомерЗоны КАК НомерЗоны,
			|	ЗоныТарификацииПартнеров.Ссылка КАК ЗонаТарификации
			|ИЗ
			|	Справочник.ЗоныТарификацииПартнеров КАК ЗоныТарификацииПартнеров
			|ГДЕ
			|	ЗоныТарификацииПартнеров.Владелец = &Владелец
			|	И ЗоныТарификацииПартнеров.НомерЗоны В (&НомераЗон)
			|	И ЗоныТарификацииПартнеров.НомерЗоны > 0";
			
			ТаблицаЗон = Запрос.Выполнить().Выгрузить();

			Для Каждого СтрокаЗоны Из Объект.ЗоныТарификацииПартнеров Цикл
				НомерЗоны = СтрокаЗоны.НомерЗоныТарификации;
				Если НомерЗоны Тогда
					СтрокаТаблицы = ТаблицаЗон.Найти(НомерЗоны, "НомерЗоны");
					Если СтрокаТаблицы = Неопределено Тогда
						СтрокаЗоны.ЗонаТарификации = "<<ДАННЫЕ НЕ НАЙДЕНЫ>>";
					Иначе
						СтрокаЗоны.ЗонаТарификации = СтрокаТаблицы.ЗонаТарификации;
					КонецЕсли;
				Иначе
					СтрокаЗоны.ЗонаТарификации = "<<ДАННЫЕ НЕ НАЙДЕНЫ>>";
				КонецЕсли;
			КонецЦикла;
		Иначе
			Для Каждого СтрокаЗоны Из Объект.ЗоныТарификацииПартнеров Цикл
				СтрокаЗоны.ЗонаТарификации = "<<ДАННЫЕ НЕ НАЙДЕНЫ>>";
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КартаТарификацииПартнеровПриИзменении(Элемент)
	
	//Асеев 10.10.2024 (Задача № 5335)>>>
	КартаТарификацииПартнеровПриИзмененииНаСервере();
	//Асеев 10.10.2024 (Задача № 5335)<<<
	
КонецПроцедуры


&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	//Асеев 10.10.2024 (Задача № 5335)>>>
	Если Не Объект.КартаТарификацииПартнеров.Пустая() Тогда
		Если Не ЗначениеЗаполнено(СхемаЗонТарификацииПартнеров) Или СхемаЗонТарификацииПартнеров = "<<ДАННЫЕ НЕ НАЙДЕНЫ>>" Тогда
			Отказ = Истина;
			Сообщить("Схема зон тарификации партнеров не определена!");
			Возврат;
		КонецЕсли;
		Если Объект.ЗоныТарификацииПартнеров.Количество() Тогда
			Для Каждого СтрокаЗоны Из Объект.ЗоныТарификацииПартнеров Цикл
				Если Не ЗначениеЗаполнено(СтрокаЗоны.ЗонаТарификации) Или СтрокаЗоны.ЗонаТарификации = "<<ДАННЫЕ НЕ НАЙДЕНЫ>>" Тогда
					Отказ = Истина;
					Сообщить("Не все зоны тарификации определены!");
					Возврат;
				КонецЕсли;
			КонецЦикла;
		Иначе
			Отказ = Истина;
			Сообщить("Не внесены зоны тарификации!");
			Возврат;
		КонецЕсли;
	КонецЕсли;
	//Асеев 10.10.2024 (Задача № 5335)<<<
	
КонецПроцедуры


&НаСервереБезКонтекста
Функция ПолучитьЗонуТарификацииНаСервере(Знач СхемаЗоны, Знач НомерЗоны)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Владелец", СхемаЗоны);
	Запрос.УстановитьПараметр("НомерЗоны", НомерЗоны);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ЗоныТарификацииПартнеров.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.ЗоныТарификацииПартнеров КАК ЗоныТарификацииПартнеров
	|ГДЕ
	|	ЗоныТарификацииПартнеров.Владелец = &Владелец
	|	И ЗоныТарификацииПартнеров.НомерЗоны = &НомерЗоны";
	
	Результат = Запрос.Выполнить();
	Если Результат.Пустой() Тогда
		Возврат "<<ДАННЫЕ НЕ НАЙДЕНЫ>>";
	Иначе
		Выборка = Результат.Выбрать();
		Выборка.Следующий();
		Возврат Выборка.Ссылка; 
	КонецЕсли;
	
КонецФункции

&НаКлиенте
Процедура ЗоныТарификацииПартнеровПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	ДанныеСтроки = Элементы.ЗоныТарификацииПартнеров.ТекущиеДанные;
	НомерЗоны = ДанныеСтроки.НомерЗоныТарификации;
	Если НомерЗоны Тогда
		ДанныеСтроки.ЗонаТарификации = ПолучитьЗонуТарификацииНаСервере(СхемаЗонТарификацииПартнеров, НомерЗоны);
	Иначе
		ДанныеСтроки.ЗонаТарификации = "<<ДАННЫЕ НЕ НАЙДЕНЫ>>";
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	КартаТарификацииПартнеровПриИзмененииНаСервере(Ложь);
	
КонецПроцедуры

