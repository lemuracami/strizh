
// Рейс, для которого рассчитывается значение начисления.
Перем Рейс Экспорт; // <БизнесПроцессСсылка.новаРейсМестнойДоставки>

// Местная доставка, для которой рассчитывается значение начисления.
Перем ЗаданиеРейса Экспорт; // <БизнесПроцессСсылка.новаМестнаяДоставка>

// Установленные категории местной доставки, для которых рассчитывается значение начисления.
Перем КатегорииЗадания Экспорт; // <Соответствие(<СправочникСсылка.новаКатегорииГрузов>)>

// Дата, на которую рассчитывается начисление.
Перем ДатаРасчета Экспорт; // <Дата>

Перем мсЗначенияПоказателей;
Перем МакросРасчета;

// Подготавливает расчет начисления. Вызывается один раз при создании объекта.
//
Процедура ПодготовитьКРасчету() Экспорт
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	ЕСТЬNULL(ЗначенияПоказателей.Значение, 0) КАК Значение,
	|	Показатели.Псевдоним КАК Псевдоним
	|ИЗ
	|	Справочник.новаНачисленияУдержания.Показатели КАК Показатели
	|
	|ЛЕВОЕ СОЕДИНЕНИЕ
	|	РегистрСведений.новаЗначенияПоказателейРасчетаНачислений.СрезПоследних(&ДатаРасчета, Показатель В (&Показатели)) КАК ЗначенияПоказателей
	|ПО
	|	ЗначенияПоказателей.Показатель = Показатели.Показатель
	|
	|ГДЕ
	|	Показатели.Ссылка = &Начисление";
	
	Запрос.УстановитьПараметр("ДатаРасчета", КонецДня(ДатаРасчета));
	Запрос.УстановитьПараметр("Начисление", Ссылка);
	Запрос.УстановитьПараметр("Показатели", Показатели.ВыгрузитьКолонку("Показатель"));
	
	тзЗначенияПоказателей = Запрос.Выполнить().Выгрузить();
	
	мсЗначенияПоказателей = Новый Массив;
	МакросРасчета = ФормулаРасчета;
	Для Каждого стрПоказатель Из тзЗначенияПоказателей Цикл
		мсЗначенияПоказателей.Добавить(стрПоказатель.Значение);
		
		Псевдоним = СокрЛП(стрПоказатель.Псевдоним);
		Если Псевдоним = "" Тогда Продолжить; КонецЕсли;
		
		МакросРасчета = СтрЗаменить(МакросРасчета, Псевдоним, "мсЗначенияПоказателей[" + СтрЗаменить(Строка(тзЗначенияПоказателей.Индекс(стрПоказатель)), Символы.НПП, "") + "]");
	КонецЦикла;
КонецПроцедуры

// Вычисляет значение начисления по заданным параметрам.
//
// Возвращаемое значение:
//  <Число>
//
Функция Рассчитать() Экспорт
	Результат = 0;
	Выполнить(МакросРасчета);
	Возврат Результат;
КонецФункции

КатегорииЗадания = Новый Соответствие;