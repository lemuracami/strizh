
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Элементы.СписокКонтекстноеМенюНачислениеНеАктуально.Доступность = РольДоступна("зпИспользованиеРабочегоМестаУправлениеСхемойИТарификациейЗП");
	
	УстановитьОтборСписка();
	
КонецПроцедуры


&НаКлиенте
Процедура НачислениеНеАктуально(Команда)
	
	//Асеев 01.08.2024 (Задача № 5299)>>>
	Если Элементы.Список.ТекущаяСтрока = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	НачислениеНеАктуальноНаСервере();
	ИзменитьСостояниеКнопки();
	//Асеев 01.08.2024 (Задача № 5299)<<<
	
КонецПроцедуры

&НаСервере
Процедура НачислениеНеАктуальноНаСервере()
	
	Начисление = Элементы.Список.ТекущаяСтрока;
	НачислениеНеАктуально = Не Элементы.СписокКонтекстноеМенюНачислениеНеАктуально.Пометка;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", Начисление);
	Запрос.УстановитьПараметр("НачислениеНеАктуально", НачислениеНеАктуально);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ИСТИНА КАК Поле1
	|ИЗ
	|	Справочник.зпНачисления КАК зпНачисления
	|ГДЕ
	|	зпНачисления.Ссылка = &Ссылка
	|	И зпНачисления.НачислениеНеАктуально <> &НачислениеНеАктуально";
	
	Если Не Запрос.Выполнить().Пустой() Тогда
		Спр = Начисление.ПолучитьОбъект();
		Спр.НачислениеНеАктуально = НачислениеНеАктуально;
		Спр.Записать();
	КонецЕсли;
	
	Элементы.Список.Обновить();
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПриАктивизацииСтроки(Элемент)
	
	//Асеев 01.08.2024 (Задача № 5299)>>>
	ИзменитьСостояниеКнопки();
	//Асеев 01.08.2024 (Задача № 5299)<<<
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьСостояниеКнопки()
	
	Если Элементы.Список.ТекущаяСтрока = Неопределено Тогда
		Элементы.СписокКонтекстноеМенюНачислениеНеАктуально.Пометка = Ложь;
	Иначе
		СостояниеКнопки = ПолучитьСостояниеКнопки(Элементы.Список.ТекущаяСтрока);
		Элементы.СписокКонтекстноеМенюНачислениеНеАктуально.Пометка = СостояниеКнопки.НачислениеНеАктуально;
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьСостояниеКнопки(Знач Начисление)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", Начисление);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	зпНачисления.НачислениеНеАктуально КАК НачислениеНеАктуально
	|ИЗ
	|	Справочник.зпНачисления КАК зпНачисления
	|ГДЕ
	|	зпНачисления.Ссылка = &Ссылка";

	Выборка = Запрос.Выполнить().Выбрать();
	Выборка.Следующий();
	
	Возврат Новый Структура("НачислениеНеАктуально", Выборка.НачислениеНеАктуально);
	
КонецФункции

&НаСервере
Процедура УстановитьОтборСписка();
	
	ИмяОтбора = "ОтборНеАктуально";
	
	ОбщегоНазначенияКлиентСервер83.УдалитьЭлементыГруппыОтбора(Список.Отбор,, ИмяОтбора);
	
	Если Не Элементы.ФормаОтобразитьНеактуальныеНачисления.Пометка Тогда
		ГруппаЭлементовОтбора = ОбщегоНазначенияКлиентСервер83.СоздатьГруппуЭлементовОтбора(Список.Отбор.Элементы, ИмяОтбора, ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ);
		ОбщегоНазначенияКлиентСервер83.УстановитьЭлементОтбора(ГруппаЭлементовОтбора, "НачислениеНеАктуально", Ложь, ВидСравненияКомпоновкиДанных.Равно,, Истина);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОтобразитьНеактуальныеНачисленияНаСервере()
	
	Элементы.ФормаОтобразитьНеактуальныеНачисления.Пометка = Не Элементы.ФормаОтобразитьНеактуальныеНачисления.Пометка;
	
	УстановитьОтборСписка();
	
КонецПроцедуры


&НаКлиенте
Процедура ОтобразитьНеактуальныеНачисления(Команда)
	ОтобразитьНеактуальныеНачисленияНаСервере();
КонецПроцедуры

