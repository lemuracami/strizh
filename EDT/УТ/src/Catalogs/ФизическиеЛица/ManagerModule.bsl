
//CeHbKA 09.04.2019 #2989

//Параметры:
//	СтароеФИО - Структура("Фамилия, Имя, Отчество")
//	ФизЛицо - СправочникСсылка.ФизическиеЛица
//	ТерминалДоставки - ПеречислениеСсылка.РегиональныеТерминалы
//	Фамилия, Имя, Отчество - Строка
//	ТипСправочника = СправочникСсылка.новаВодители или СправочникСсылка.новаЭкспедиторы

//Геннадий 10.06.2020 
//добавлены параметры НомерЛичногоТелефона, Комментарий 
Функция ОбработатьФизЛицо(ФизЛицо, ТерминалДоставки, Фамилия, Имя, Отчество, ДатаРождения, ТипСправочника, НомерЛичногоТелефона = Неопределено, Комментарий = Неопределено) Экспорт
		
	Если ФизЛицо.Пустая() Тогда
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ФизическиеЛица.Ссылка КАК ФизЛицо
		|ПОМЕСТИТЬ ВТ_ФизЛица
		|ИЗ
		|	Справочник.ФизическиеЛица КАК ФизическиеЛица
		|ГДЕ
		|	ФизическиеЛица.Ссылка В ИЕРАРХИИ (ЗНАЧЕНИЕ(Справочник.ФизическиеЛица.Сотрудники))
		|	И ФизическиеЛица.Терминал = &Терминал
		|	И ФизическиеЛица.Имя = &Имя
		|	И ФизическиеЛица.Фамилия = &Фамилия
		|	И ФизическиеЛица.Отчество = &Отчество
		|	И ФизическиеЛица.ДатаРождения = &ДатаРождения
		|	И ФизическиеЛица.ПометкаУдаления = ЛОЖЬ
		|	И ФизическиеЛица.ЭтоГруппа = ЛОЖЬ
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	новаВодители.Ссылка КАК Ссылка,
		|	новаВодители.ФизЛицо КАК ФизЛицо
		|ИЗ
		|	Справочник.новаВодители КАК новаВодители
		|ГДЕ
		|	новаВодители.ФизЛицо В
		|			(ВЫБРАТЬ
		|				ВТ_ФизЛица.ФизЛицо КАК ФизЛицо
		|			ИЗ
		|				ВТ_ФизЛица КАК ВТ_ФизЛица)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	новаЭкспедиторы.Ссылка,
		|	новаЭкспедиторы.ФизЛицо
		|ИЗ
		|	Справочник.новаЭкспедиторы КАК новаЭкспедиторы
		|ГДЕ
		|	новаЭкспедиторы.ФизЛицо В
		|			(ВЫБРАТЬ
		|				ВТ_ФизЛица.ФизЛицо КАК ФизЛицо
		|			ИЗ
		|				ВТ_ФизЛица КАК ВТ_ФизЛица)
		|
		//Переварюха В.В. 2019.09.24 Задача № 3441
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	Сотрудники.Ссылка,
		|	Сотрудники.ФизЛицо
		|ИЗ
		|	Справочник.Сотрудники КАК Сотрудники
		|ГДЕ
		|	Сотрудники.ФизЛицо В
		|			(ВЫБРАТЬ
		|				ВТ_ФизЛица.ФизЛицо КАК ФизЛицо
		|			ИЗ
		|				ВТ_ФизЛица КАК ВТ_ФизЛица)";
		//Переварюха В.В. 2019.09.24 Задача № 3441
		Запрос.УстановитьПараметр("Фамилия", 	Фамилия);
		Запрос.УстановитьПараметр("Имя", 		Имя);
		Запрос.УстановитьПараметр("Отчество", 	Отчество);
		Запрос.УстановитьПараметр("ДатаРождения", ДатаРождения);
		Запрос.УстановитьПараметр("Терминал", 	ТерминалДоставки);
		
		РезультатЗапроса = Запрос.Выполнить();
		
		//Если НЕ РезультатЗапроса.Пустой() Тогда
		//	Возврат Справочники.ФизическиеЛица.ПустаяСсылка();
		//КонецЕсли;
		
		Выборка = РезультатЗапроса.Выбрать();
				
		Пока Выборка.Следующий() Цикл
			
			Если ТипЗнч(Выборка.Ссылка) = ТипСправочника Тогда
				Возврат Справочники.ФизическиеЛица.ПустаяСсылка();
			КонецЕсли; 
			
			ФизЛицо = Выборка.ФизЛицо;	
			
		КонецЦикла; 
		
		Если НЕ ЗначениеЗаполнено(ФизЛицо) Тогда
			ФизЛицоОбъект = Справочники.ФизическиеЛица.СоздатьЭлемент();
		Иначе	
			ФизЛицоОбъект = ФизЛицо.ПолучитьОбъект();
		КонецЕсли; 
	Иначе
		ФизЛицоОбъект = ФизЛицо.ПолучитьОбъект();
	КонецЕсли;		
			
	ФизЛицоОбъект.Наименование 	= Фамилия + " " + Имя + " " + Отчество;
	ФизЛицоОбъект.Фамилия 		= Фамилия;
	ФизЛицоОбъект.Имя			= Имя;		
	ФизЛицоОбъект.Отчество		= Отчество;
	ФизЛицоОбъект.Терминал		= ТерминалДоставки;
	ФизЛицоОбъект.Родитель      = Справочники.ФизическиеЛица.Сотрудники;
	ФизЛицоОбъект.ДатаРождения  = ДатаРождения;	
	//Геннадий 10.06.2020 ->
	ФизЛицоОбъект.НомерЛичногоТелефона 	= НомерЛичногоТелефона;
	ФизЛицоОбъект.Комментарий 			= Комментарий;
	//Геннадий 10.06.2020 <-
	ФизЛицоОбъект.Записать();
	
	ФизЛицо = ФизЛицоОбъект.Ссылка;
	
	Возврат ФизЛицо;

КонецФункции

Функция ПроверитьОднофамильцев(ФизЛицо) Экспорт
	
	МассивОднофамильцев = Новый Массив;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	новаВодители.Ссылка КАК Ссылка,
		|	новаВодители.ФизЛицо.ДатаРождения КАК ДатаРождения,
		|	новаВодители.ФизЛицо.Фамилия КАК Фамилия,
		|	новаВодители.ФизЛицо.Имя КАК Имя,
		|	новаВодители.ФизЛицо.Отчество КАК Отчество
		|ИЗ
		|	Справочник.новаВодители КАК новаВодители
		|ГДЕ
		|	новаВодители.ФизЛицо.Фамилия = &Фамилия
		|	И новаВодители.ФизЛицо.Имя = &Имя
		|	И новаВодители.ФизЛицо.Отчество = &Отчество
		|	И новаВодители.ФизЛицо.ДатаРождения <> &ДатаРождения
		|	И новаВодители.ФизЛицо.Терминал = &Терминал
		|	И НЕ новаВодители.ПометкаУдаления
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	новаЭкспедиторы.Ссылка,
		|	новаЭкспедиторы.ФизЛицо.ДатаРождения,
		|	новаЭкспедиторы.ФизЛицо.Фамилия,
		|	новаЭкспедиторы.ФизЛицо.Имя,
		|	новаЭкспедиторы.ФизЛицо.Отчество
		|ИЗ
		|	Справочник.новаЭкспедиторы КАК новаЭкспедиторы
		|ГДЕ
		|	новаЭкспедиторы.ФизЛицо.Фамилия = &Фамилия
		|	И новаЭкспедиторы.ФизЛицо.Имя = &Имя
		|	И новаЭкспедиторы.ФизЛицо.Отчество = &Отчество
		|	И новаЭкспедиторы.ФизЛицо.ДатаРождения <> &ДатаРождения
		|	И новаЭкспедиторы.ФизЛицо.Терминал = &Терминал
		|	И НЕ новаЭкспедиторы.ПометкаУдаления
		|
		//Переварюха В.В. 2019.09.24 Задача № 3441
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	Сотрудники.Ссылка,
		|	Сотрудники.ФизЛицо.ДатаРождения,
		|	Сотрудники.ФизЛицо.Фамилия,
		|	Сотрудники.ФизЛицо.Имя,
		|	Сотрудники.ФизЛицо.Отчество
		|ИЗ
		|	Справочник.Сотрудники КАК Сотрудники
		|ГДЕ
		|	Сотрудники.ФизЛицо.Фамилия = &Фамилия
		|	И Сотрудники.ФизЛицо.Имя = &Имя
		|	И Сотрудники.ФизЛицо.Отчество = &Отчество
		|	И Сотрудники.ФизЛицо.ДатаРождения <> &ДатаРождения
		|	И Сотрудники.ФизЛицо.Терминал = &Терминал
		|	И НЕ Сотрудники.ПометкаУдаления";
	    //Переварюха В.В. 2019.09.24 Задача № 3441
	Запрос.УстановитьПараметр("Фамилия", ФизЛицо.Фамилия);
	Запрос.УстановитьПараметр("Имя", ФизЛицо.Имя);
	Запрос.УстановитьПараметр("Отчество", ФизЛицо.Отчество);
	Запрос.УстановитьПараметр("ДатаРождения", ФизЛицо.ДатаРождения);
	Запрос.УстановитьПараметр("Терминал", ФизЛицо.Терминал);

	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		
		Если ВыборкаДетальныеЗаписи.Ссылка.НаименованиеИзменено Тогда
			Продолжить;
		КонецЕсли;
		
		СотрудникОбъект = ВыборкаДетальныеЗаписи.Ссылка.ПолучитьОбъект();
		
		СотрудникОбъект.Наименование = ВыборкаДетальныеЗаписи.Фамилия + " " + ВыборкаДетальныеЗаписи.Имя + " " + ВыборкаДетальныеЗаписи.Отчество + " (" + Формат(ВыборкаДетальныеЗаписи.ДатаРождения, "ДЛФ=Д") + ")";
		
		СотрудникОбъект.Записать();
		
		МассивОднофамильцев.Добавить(ВыборкаДетальныеЗаписи.Ссылка);
		
	КонецЦикла;
	
	Возврат МассивОднофамильцев;
	
КонецФункции
//CeHbKA 09.04.2019 #2989
