
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Параметры.Свойство("РежимСписка", РежимСписка);
	
	Если РежимСписка = "Исключения" Тогда
		
		КлючОбъекта = "НастройкиИсключенийАдресовПочтовыхРассылок";
		ОписаниеНастроек = "Настройки исключений адресов почтовых рассылок";
		
		Элементы.ДекорацияРасшифровка.Заголовок = "Перечисленные в списке адреса будут исключены при отправке почты.";
		
	ИначеЕсли РежимСписка = "ДоступныеДомены" Тогда

		КлючОбъекта = "НастройкиДоступныхДоменовПочтовыхРассылок";
		ОписаниеНастроек = "Настройки доступных доменов почтовых рассылок";	
		
		Элементы.ДекорацияРасшифровка.Заголовок = "Только на перечисленные в списке домены будет проведена рассылка, остальные адреса будут исключены.";

	КонецЕсли;

	СохраненныйСписокИсключений = ХранилищеОбщихНастроек.Загрузить(
	КлючОбъекта, 
	"НастройкиПочтовыхРассылок", 
	ОписаниеНастроек,
	"Стриж");

	
	Если Не СохраненныйСписокИсключений = Неопределено 
		И ТипЗНЧ(СохраненныйСписокИсключений) = Тип("Массив") Тогда
		СписокИсключений.ЗагрузитьЗначения(СохраненныйСписокИсключений);
	КонецЕсли;	
	
	ЭтотОбъект.Заголовок = ОписаниеНастроек;
	Элементы.ДекорацияИнформация.Заголовок = ОписаниеНастроек;
	
КонецПроцедуры

&НаСервере
Функция ЗавершитьРедактированиеНаСервере()
	
	МассивЗначенийНастроек = СписокИсключений.ВыгрузитьЗначения();
	
	Для Каждого СтрокаНастройки Из МассивЗначенийНастроек Цикл
		Если  Не ЗначениеЗаполнено(СтрокаНастройки) Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Нельза задавать пустое исключение! Настройки не сохранены.");
			Возврат Ложь;
		КонецЕсли;	
	КонецЦикла;
	
	ХранилищеОбщихНастроек.Сохранить(
	КлючОбъекта,
	"НастройкиПочтовыхРассылок", 
	МассивЗначенийНастроек, 
	ОписаниеНастроек,
	"Стриж");
	ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Настройки сохранены.");
	
	Возврат Истина;
	
КонецФункции

&НаКлиенте
Процедура ЗавершитьРедактирование(Команда)
	Если ЗавершитьРедактированиеНаСервере() тогда
		Закрыть();
	КонецЕсли;	
КонецПроцедуры
