
&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	// Михушкин --->>
	//Если РольДоступна("РегиональныйЛогист") Тогда	
	//	Элементы.ФормаКоманднаяПанель.Доступность = Ложь;	
	//	ЭтаФорма.ТолькоПросмотр = Истина;
	//КонецЕсли;
	// <<--- Михушкин
	
КонецПроцедуры

//CeHbKA #3733 27.01.2020
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	ПользовательИБ = ПользователиИнформационнойБазы.ТекущийПользователь();
	Элементы.ФормаСтатус.Доступность = ПользовательИБ.Роли.Содержит(Метаданные.Роли.УправлениеСостояниемАвтотранспорта);
	
	Элементы.ФормаСтатус.ТолькоПросмотр = НЕ ПользовательИБ.Роли.Содержит(Метаданные.Роли.УправлениеСостояниемАвтотранспорта);
	
	Для каждого ЭлементЗначение Из Метаданные.Перечисления.СостоянияАвтотранспорта.ЗначенияПеречисления Цикл
		
		// создадим команду
		НоваяКоманда = Команды.Добавить("Статус"+ЭлементЗначение.Имя);
		НоваяКоманда.Заголовок  = ЭлементЗначение.Синоним;
		НоваяКоманда.Действие   = "ВыполнитьДействиеКомандыЗаписатьСостояние";
		
		// создадим кнопку
		НовыйЭлемент        = Элементы.Добавить("Статус"+ЭлементЗначение.Имя, Тип("КнопкаФормы"), Элементы.ФормаСтатус);
		НовыйЭлемент.Вид    = ВидКнопкиФормы.КнопкаКоманднойПанели;
		НовыйЭлемент.Заголовок  = ЭлементЗначение.Синоним;
		НовыйЭлемент.ИмяКоманды = "Статус"+ЭлементЗначение.Имя;		
		
		НовыйЭлемент.Доступность = Элементы.ФормаСтатус.Доступность;
		
	КонецЦикла; 	
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьДействиеКомандыЗаписатьСостояние(Команда)

	Если Элементы.Список.ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли; 
	
	ИмяСостояния = СтрЗаменить(Команда.Имя, "Статус", "");
	СостояниеТранспорта = ПредопределенноеЗначение("Перечисление.СостоянияАвтотранспорта."+ИмяСостояния);
	
	РезультатЗаписи = СделатьЗаписьВРегистрСведенийСостоянияАвтотранспорта(Элементы.Список.ТекущаяСтрока, СостояниеТранспорта);
	
	Сообщение = Новый СообщениеПользователю;
	Сообщение.Текст = РезультатЗаписи;
	Сообщение.Поле = "Список";
	Сообщение.Сообщить(); 
	
	Элементы.Список.Обновить();
	
КонецПроцедуры // ()
 
&НаСервере
Функция СделатьЗаписьВРегистрСведенийСостоянияАвтотранспорта(Транспорт, СостояниеТранспорта)
	
	Если Транспорт.ЭтоГруппа Тогда
		Возврат "Для группы транспорта нельзя устанавливать статус!";
	КонецЕсли; 
	
	Запрос = Новый Запрос;
	
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	новаТранспорт.Ссылка КАК Транспорт,
		|	ЕСТЬNULL(СформированныеЭкипажиСрезПоследних.Водитель, ЗНАЧЕНИЕ(Справочник.новаВодители.ПустаяСсылка)) КАК Водитель,
		|	ЕСТЬNULL(СформированныеЭкипажиСрезПоследних.Экспедитор, ЗНАЧЕНИЕ(Справочник.новаЭкспедиторы.ПустаяСсылка)) КАК Экспедитор
		|ИЗ
		|	Справочник.новаТранспорт КАК новаТранспорт
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СформированныеЭкипажи.СрезПоследних КАК СформированныеЭкипажиСрезПоследних
		|		ПО СформированныеЭкипажиСрезПоследних.Транспорт = новаТранспорт.Ссылка
		|ГДЕ
		|	новаТранспорт.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", Транспорт);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Набор = РегистрыСведений.СостоянияАвтотранспорта.СоздатьНаборЗаписей();
	Набор.Отбор.Транспорт.Установить(Транспорт);

	Набор.Прочитать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл

		Запись = Набор.Добавить();
		Запись.Период = ТекущаяДата();
		Запись.Транспорт = Транспорт;
		Запись.СостояниеТранспорта = СостояниеТранспорта;
		Запись.Водитель = ВыборкаДетальныеЗаписи.Водитель;
		Запись.Экспедитор = ВыборкаДетальныеЗаписи.Экспедитор;
		Запись.Пользователь = ПараметрыСеанса.ТекущийПользователь;
		
	КонецЦикла;
	
	Набор.Записать();
	
	Возврат "Для транспорта "+Транспорт+" изменено состояние на "+СостояниеТранспорта;;
	
КонецФункции // ()
//CeHbKA #3733 27.01.2020
 