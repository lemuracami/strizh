
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Элементы.Список.РежимВыбора = Параметры.РежимВыбора;
	
	СписокВыбора = Элементы.ОтборТипУдержания.СписокВыбора;
	
	ЗначениеПеречисления = Перечисления.зпТипыУдержаний.ПустаяСсылка();
	СписокВыбора.Добавить(ЗначениеПеречисления, "Все");
	
	МетаданныеПеречисления = ЗначениеПеречисления.Метаданные();
	Для Каждого МетаданныеЗначения Из МетаданныеПеречисления.ЗначенияПеречисления Цикл
		ЗначениеПеречисления = Перечисления.зпТипыУдержаний[МетаданныеЗначения.Имя];
		СписокВыбора.Добавить(ЗначениеПеречисления);
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ОтборТипУдержанияПриИзмененииНаСервере()
	
	ИмяОтбора = "БыстрыйОтбор";
	
	ОбщегоНазначенияКлиентСервер83.УдалитьЭлементыГруппыОтбора(Список.Отбор,, ИмяОтбора);
	
	Если Не ОтборТипУдержания.Пустая() Тогда
		ГруппаЭлементовОтбора = ОбщегоНазначенияКлиентСервер83.СоздатьГруппуЭлементовОтбора(Список.Отбор.Элементы, ИмяОтбора, ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ);
		ОбщегоНазначенияКлиентСервер83.УстановитьЭлементОтбора(ГруппаЭлементовОтбора, "ТипУдержания", ОтборТипУдержания, ВидСравненияКомпоновкиДанных.Равно,, Истина);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборТипУдержанияПриИзменении(Элемент)
	ОтборТипУдержанияПриИзмененииНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура СписокПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	Если Копирование Или ОтборТипУдержания.Пустая() Тогда
		Возврат;
	КонецЕсли;
	
	Отказ = Истина;
	
	ПараметрыФормы = Новый Структура("Основание", Новый Структура("ТипУдержания", ОтборТипУдержания));
	ОткрытьФорму("Справочник.зпУдержания.ФормаОбъекта", ПараметрыФормы);
	
КонецПроцедуры
