
//////////////////////////////////////////////////////////////////////////////// 
// ОБРАБОТЧИКИ СОБЫТИЙ 
// 

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если Параметры.Ключ.Пустая() Тогда
		Если НЕ Параметры.ЗначениеКопирования.Пустая() Тогда
			// при копировании очищаем имя файла, чтобы не возникало иллюзии, что содержимое файла тоже скопируется
			Объект.ИмяФайла = "";
		КонецЕсли;
		Объект.Владелец = Справочники.Контрагенты.ПустаяСсылка();
	КонецЕсли;
	Если ТипЗнч(Объект.Владелец)= Тип("СправочникСсылка.Товары") Тогда
		// Товары в этом приложении не редактируются
		ТолькоПросмотр = Истина;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
#Если НЕ МобильноеПриложениеКлиент Тогда
	
	Элементы.СделатьАудиозаписьИЗаписать.Видимость = Ложь;
	Элементы.СделатьВидеозаписьИЗаписать.Видимость = Ложь;
	Элементы.СделатьФотоснимокИЗаписать.Видимость = Ложь;
	
#Иначе
	Если ТолькоПросмотр = Ложь Тогда
		Элементы.СделатьАудиозаписьИЗаписать.Доступность = СредстваМультимедиа.ПоддерживаетсяАудиозапись();
		Элементы.СделатьВидеозаписьИЗаписать.Доступность = СредстваМультимедиа.ПоддерживаетсяВидеозапись();
		Элементы.СделатьФотоснимокИЗаписать.Доступность = СредстваМультимедиа.ПоддерживаетсяФотоснимок();
	Иначе
		Элементы.СделатьАудиозаписьИЗаписать.Доступность = Ложь;
		Элементы.СделатьВидеозаписьИЗаписать.Доступность = Ложь;
		Элементы.СделатьФотоснимокИЗаписать.Доступность = Ложь;
	КонецЕсли;
	Элементы.ВыбратьФайлСДискаИЗаписать.Видимость = Ложь;
	
#КонецЕсли
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	Если Объект.ИмяФайла = "" Тогда
		Предупреждение("Не выбран файл!");
		Отказ = Истина;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьФайлСДискаИЗаписать()
	Перем ВыбранноеИмя;
	Перем АдресВременногоХранилища;
	Если ПоместитьФайл(АдресВременногоХранилища, "", ВыбранноеИмя, Истина) Тогда
		Объект.ИмяФайла = ВыбранноеИмя;
		ПоместитьФайлОбъекта(АдресВременногоХранилища);
	КонецЕсли;	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФайл()
	
	Если Объект.Ссылка.Пустая() Тогда
		Предупреждение(НСтр("ru = 'Данные не записаны'", "ru"));
		Возврат;
	КонецЕсли;
	
	Если ПустаяСтрока(Объект.ИмяФайла) Тогда
		Предупреждение(НСтр("ru = 'Имя не задано'", "ru"));
		Возврат;
	КонецЕсли;
	
	Файл = Новый Файл(Объект.ИмяФайла);
	ИмяФайла = ПолучитьИмяВременногоФайла(Файл.Расширение);
	Адрес = ПолучитьНавигационнуюСсылку(Объект.Ссылка, "ДанныеФайла");
	ПолучитьФайл(Адрес, ИмяФайла, Ложь);
	ЗапуститьПриложение(ИмяФайла);
КонецПроцедуры

#Если МобильноеПриложениеКлиент Тогда
	
&НаКлиенте
Процедура ПоместитьМультимедиа(ДанныеМультимедиа)
	
	Если ДанныеМультимедиа <> Неопределено Тогда
		АдресВременногоХранилища = ПоместитьВоВременноеХранилище(ДанныеМультимедиа.ПолучитьДвоичныеДанные(), УникальныйИдентификатор);
		ТипСодержимого = ДанныеМультимедиа.ТипСодержимого;
		Номер = Найти(ТипСодержимого, "/");
		Если Номер > 0 Тогда
			ТипСодержимого = Лев(ТипСодержимого, Номер - 1);
		КонецЕсли;
		Объект.Наименование = ТипСодержимого + " " + Строка(ТекущаяДата());
		Объект.ИмяФайла = СтрЗаменить(Строка(ТекущаяДата()), ":", "_") + "." + ДанныеМультимедиа.РасширениеФайла;
		ПоместитьФайлОбъекта(АдресВременногоХранилища);
	КонецЕсли;
	
КонецПроцедуры

#КонецЕсли
	
&НаКлиенте
Процедура СделатьАудиозапись(Команда)
	
#Если МобильноеПриложениеКлиент Тогда
	ДанныеМультимедиа = СредстваМультимедиа.СделатьАудиозапись();
	ПоместитьМультимедиа(ДанныеМультимедиа);
#КонецЕсли
	
КонецПроцедуры

&НаКлиенте
Процедура СделатьВидеозапись(Команда)
	
#Если МобильноеПриложениеКлиент Тогда
	ДанныеМультимедиа = СредстваМультимедиа.СделатьВидеозапись();
	ПоместитьМультимедиа(ДанныеМультимедиа);
#КонецЕсли
	
КонецПроцедуры

&НаКлиенте
Процедура СделатьФотоснимок(Команда)
	
#Если МобильноеПриложениеКлиент Тогда
	ДанныеМультимедиа = СредстваМультимедиа.СделатьФотоснимок();
	ПоместитьМультимедиа(ДанныеМультимедиа);
#КонецЕсли
	
КонецПроцедуры


//////////////////////////////////////////////////////////////////////////////// 
// ПРОЦЕДУРЫ И ФУНКЦИИ 
// 

// процедура сохраняет на сервере файл, и, при наличии, файлы ЭЦП
&НаСервере
Процедура ЗаписатьДанныеФайла(ФайлДвоичныеДанные)
	ЭлементСправочника = РеквизитФормыВЗначение("Объект");
	// ДанныеФайла меняем, только если переданы двоичные ланные
	Если ТипЗнч(ФайлДвоичныеДанные) = Тип("ДвоичныеДанные") Тогда
		ЭлементСправочника.ДанныеФайла = Новый ХранилищеЗначения(ФайлДвоичныеДанные, Новый СжатиеДанных());
	КонецЕсли;
	
	ЭлементСправочника.Записать();
	Модифицированность = Ложь;
	ЗначениеВРеквизитФормы(ЭлементСправочника, "Объект");     
	
КонецПроцедуры	

// Процедура извлекает данные объекта из временного хранилища, 
// производит модификацию элемента справочника и записывает его.
// 
// Параметры: 
//  АдресВременногоХранилища – Строка – адрес временного хранилища. 
// 
// Возвращаемое значение: 
//  Нет.
&НаСервере
Процедура ПоместитьФайлОбъекта(АдресВременногоХранилища)
	ЭлементСправочника = РеквизитФормыВЗначение("Объект");
	ДвоичныеДанные = ПолучитьИзВременногоХранилища(АдресВременногоХранилища);
	ЭлементСправочника.ДанныеФайла = Новый ХранилищеЗначения(ДвоичныеДанные, Новый СжатиеДанных());
	Файл = Новый Файл(ЭлементСправочника.ИмяФайла);
	ЭлементСправочника.ИмяФайла = Файл.Имя;
	ЭлементСправочника.Подпись = Новый ХранилищеЗначения(Неопределено, Новый СжатиеДанных());
	ЭлементСправочника.Зашифрован = Ложь;
	ЭлементСправочника.Подписан = Ложь;
	ЭлементСправочника.Записать();
	Модифицированность = Ложь;
	УдалитьИзВременногоХранилища(АдресВременногоХранилища);
	ЗначениеВРеквизитФормы(ЭлементСправочника, "Объект");     
КонецПроцедуры

